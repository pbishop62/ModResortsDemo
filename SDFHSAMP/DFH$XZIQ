         TITLE 'CUSTOMER INFORMATION CONTROL - SAMPLE XZIQUE           *
                GLOBAL USER EXIT PROGRAM'
***********************************************************************
*                                                                     *
* MODULE NAME = DFH$XZIQ                                              *
*                                                                     *
* DESCRIPTIVE NAME = C.I.C.S./VS Sample XZIQUE  global exit.          *
*                                                                     *
*                                                                     *
*                                                                     *
*     Licensed Materials - Property of IBM                            *
*                                                                     *
*     "Restricted Materials of IBM"                                   *
*                                                                     *
*     5655-Y04                                                        *
*                                                                     *
*     (C) Copyright IBM Corp. 1993"                                   *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
* STATUS = 7.2.0                                                      *
*                                                                     *
* FUNCTION =                                                          *
*        DFH$XZIQ acts as an exit program for the exit point XZIQUE.  *
*        The exit deals with the problems encountered when 'sickness' *
*        in a remote system causes allocates to queue in the sending  *
*        system.This system develops 'sympathy sickness' as the       *
*        allocate queue of tasks becomes too large.                   *
*                                                                     *
*        The exit allows the following return codes:                  *
*                                                                     *
*        UERCAQUE - queue the allocate                                *
*        UERCAPUR - reject the allocate - return SYSIDERR             *
*        UERCAKLL - reject allocate & purge the allocate queue        *
*                                - return SYSIDERR                    *
*        UERCAKLM - reject allocate & purge the modegrp allocate      *
*                                  queue                              *
*                                - return SYSIDERR                    *
*        UERCNORM - allow the allocate following purge - may be queued*
*                                                                     *
*        This sample program takes the queue limit specified on the   *
*        connection definition (QUEUELIMIT) as the maximum queue      *
*        length and issues RC 4 when this limit is reached.           *
*                                                                     *
*        Once this queue limit has been reached the process time      *
*        of the queue is considered & when this becomes too large     *
*        a request to purge the queue can be issued.This can be       *
*        either a return code 8 for the whole connection or a         *
*        return code 12 if mode group analysis is required.           *
*                                                                     *
*        The process time of the queue is specified on the            *
*        connection definition (MAXQTIME).                            *
*                                                                     *
* NOTES :                                                             *
*    DEPENDENCIES = S/370                                             *
*        DFH$XZIQ  must be defined on the CSD as a PROGRAM.           *
*    RESTRICTIONS =                                                   *
*        None                                                         *
*    REGISTER CONVENTIONS =                                           *
*        See the code                                                 *
*    MODULE TYPE = Executable                                         *
*    PROCESSOR = Assembler                                            *
*    ATTRIBUTES = Read only, Re-entrant                               *
*                                                                     *
*------------------------------------------------------------------ *
*                                                                     *
* ENTRY POINT = DFH$XZIQ                                              *
*                                                                     *
*     PURPOSE = see above                                             *
*                                                                     *
*     LINKAGE =                                                       *
*        Called by the User Exit Handler.                             *
*                                                                     *
*     INPUT =                                                         *
*        On entry register 1 addresses a parameter list described     *
*        by the DFHUEPAR DSECT.                                       *
*                                                                     *
*     OUTPUT =                                                        *
*        A return code is placed in register 15.                      *
*                                                                     *
*     EXIT-NORMAL =                                                   *
*        DFH$XZIQ returns to the address that was in register 14      *
*        when it was called. Possible return codes in register 15     *
*        are:-                                                        *
*          0 - queue the allocate.                                    *
*          4 - purge the allocate                                     *
*          8 - Purge the queue & issue msg DFHZC2300                  *
*         12 - Purge the queue for the modegroup & issue DFHZC2309    *
*          0 - after previous RC=8 or 12, allow the allocate, queuing *
*              if necessary. Issue DFHZC2301 after Rc=8, DFHZC2310    *
*              after RC12.                                            *
*     EXIT-ERROR = none                                               *
*                                                                     *
*------------------------------------------------------------------ *
*                                                                     *
* EXTERNAL REFERENCES = none                                          *
*                                                                     *
*------------------------------------------------------------------ *
*                                                                     *
* DESCRIPTION - see above                                             *
*                                                                     *
*------------------------------------------------------------------ *
*                                                                     *
* CHANGE ACTIVITY :                                                   *
*                                                                     *
*     $MOD(DFH$XZIQ),COMP(ISC),PROD(CICS TS ):                        *
*                                                                     *
*     PN= REASON REL YYMMDD HDXIII : REMARKS                          *
*    $L1= 614    410 930601 HD6NPW : Created.                         *
*                                                                     *
***********************************************************************
*
* Dsect for parameters passed to exit
*
         COPY DFHXZIDS
*
* Dsect for connection statistics record
*
         COPY DFHA14DS
*
* Dsect for mode entry statistics record
*
         COPY DFHA20DS
*
XSTOR    DSECT
TIMENOW  DS    D
ALLOCS_PROCESSED DS F
QUEUE_LEN DS F
*
         DFHUEXIT TYPE=EP,ID=XZIQUE
R1              EQU  1        Parameter list address
R2              EQU  2        Not used
R3              EQU  3        Program base register
R4              EQU  4        Address XZIQUE  parameter list
R5              EQU  5        Address connection or modegrp stats
R6              EQU  6        General
R7              EQU  7        General
R8              EQU  8        General
R9              EQU  9        General
R10             EQU  10       General
R11             EQU  11       General
R12             EQU  12       General
R13             EQU  13       General
R14             EQU  14       Return address
R15             EQU  15       Entry point address,return code
*
         EJECT ,
DFH$XZIQ CSECT
DFH$XZIQ AMODE 31
DFH$XZIQ RMODE ANY
         SAVE (14,12)                  Save current registers
         LR    R3,R15                  Set-up base register
         USING DFH$XZIQ,R3
         B     *+12                    Branch around module ID
         DC    CL8'DFH$XZIQ'
*
         USING DFHUEPAR,R1             Set addressability to plist
*
         L     R4,UEPZDATA             Set up addressability to
         USING DFHXZIDS,R4             XZIQUE plist
*
         L     R2,UEPXSTOR             Address local variables
         USING XSTOR,R2
*
         STCK  TIMENOW                 Current time
         ICM   R11,B'1111',TIMENOW     Store meaningful part
*
         ICM   R5,B'1111',UEPMODST     Set up addressability to
         BZ    NOMODST                 Mode stats.
*--------------------------------------------------------------------*
*        Modegrp stats - specific allocate                           *
*--------------------------------------------------------------------*
         TM    UEPFLAG,UEPRC8          Return code 8  issued?
         BNZ   RC8                     Yes......
         TM    UEPFLAG,UEPRC12         Return code 12 issued?
         BZ    NOTRC12                 No.......
         SR    R15,R15                 Prepare for ICM
         ICM   R15,B'0011',UEPMAR12    Number of allocates since RC12
         BZ    NOQUEUE                 Mgroup still bad if zero
         B     NORMAL                  Link OK - allow allocate
*
NOTRC12  DS    0H
         USING DFHA20DS,R5             Modegrp stats
         L     R6,A20ESTAQ             Number in queue for modegrp
*
         CLM   R6,B'0011',UEPQUELM     OK to queue?
         BL    QUEUE                   Yes.........
*--------------------------------------------------------------------*
*        Consider process time                                       *
*--------------------------------------------------------------------*
         ST    R6,QUEUE_LEN            Store queue length
         ICM   R12,B'1111',UEPMAQTS    Time queue began
         SR    R11,R12                 Time difference
         SR    R7,R7                   Prepare for ICM
         ICM   R7,B'0011',UEPMACNT     Allocates processed on this Q
         BZ    TESTMOD                 Kill queue if none processed
         ST    R7,ALLOCS_PROCESSED     Store for further calculation
*
         SR    R10,R10                 Clear for divide
         D     R10,ALLOCS_PROCESSED    Time for each allocate
         M     R10,QUEUE_LEN           Time to process current queue
TESTMOD  DS    0H
         CH    R11,UEPEMXQT            Link slow ?
         BH    MODKILL                 Yes..........
         B     NOQUEUE                 Queue limit reached so reject
*--------------------------------------------------------------------*
*        Specific allocate following RC8                             *
*--------------------------------------------------------------------*
RC8      SR    R15,R15                 Prepare for ICM
         ICM   R15,B'0011',UEPSARC8    Number of allocates since RC8
         BZ    NOQUEUE                 Link still bad if zero
         B     NORMAL                  Link OK - allow allocate
*--------------------------------------------------------------------*
*        Connection stats - non-specific allocate                    *
*--------------------------------------------------------------------*
NOMODST  DS    0H
         TM    UEPFLAG,UEPRC8          Return code 8 issued?
         BZ    NOTRC8                  No.......
         SR    R15,R15                 Prepare for ICM
         ICM   R15,B'0011',UEPSARC8    Number of allocates since RC8
         BZ    NOQUEUE                 Link still bad if zero
         B     NORMAL                  Link OK - allow allocate
NOTRC8   DS    0H
         ICM   R5,B'1111',UEPCONST     Set up addressability
         BZ    NOSTATS                 Zero?
*
         USING DFHA14DS,R5             Connection stats
         L     R6,A14ESTAQ             Number in queue
*
         CLM   R6,B'0011',UEPQUELM     OK to queue?
         BL    QUEUE                   Yes..........
*--------------------------------------------------------------------*
*        Consider process time                                       *
*--------------------------------------------------------------------*
         ST    R6,QUEUE_LEN            Store queue length
         ICM   R12,15,UEPSAQTS         Time queue began
         SR    R11,R12                 Time difference
         SR    R7,R7                   Prepare for ICM
         ICM   R7,B'0011',UEPSACNT     Allocates processed on this Q
         BZ    TESTIME                 Kill queue if none processed
         ST    R7,ALLOCS_PROCESSED     Store for further calculation
*
         SR    R10,R10                 Clear for divide
         D     R10,ALLOCS_PROCESSED    Time for each allocate
         M     R10,QUEUE_LEN           Time to process current queue
TESTIME  DS    0H
         CH    R11,UEPEMXQT            Link slow ?
         BH    KILLQ                   Yes..........
         B     NOQUEUE                 Queue limit reached so reject
*--------------------------------------------------------------------*
*        Return codes                                                *
*--------------------------------------------------------------------*
NORMAL   LA    R15,UERCNORM            Allow the request
         B     EXIT                    return
QUEUE    LA    R15,UERCAQUE            Queue the request
         B     EXIT                    return
NOQUEUE  LA    R15,UERCAPUR            No queue
         B     EXIT                    return
KILLQ    LA    R15,UERCAKLL            Purge queue
         B     EXIT                    return
MODKILL  LA    R15,UERCAKLM            Purge modegrp queue
         B     EXIT
*
NOSTATS  WTO   ('DFH$XZIQ - NO CONNECTION STATS RECORD RECEIVED')
         LA    R15,UERCAPUR            No connection stats record
         B     EXIT                    Purge transaction
*
EXIT     L     R13,UEPEPSA
         RETURN (14,12),RC=(15)        Return
         LTORG
*
         END   DFH$XZIQ
