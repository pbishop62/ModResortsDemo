PROCESS MAP,XREF,TRUNC(BIN)
      ******************************************************************
      *                                                                *
      * MODULE NAME = DFH0STXD                                         *
      *                                                                *
      * DESCRIPTIVE NAME = CICS TS  (SAMPLES) Statistics Extract Sample*
      *                                                                *
      *                                                                *
      *                                                                *
      *                                                                *
      *      Licensed Materials - Property of IBM                      *
      *                                                                *
      *      "Restricted Materials of IBM"                             *
      *                                                                *
      *      5655-Y04                                                  *
      *                                                                *
      *      (C) Copyright IBM Corp. 2007, 2016"                       *
      *                                                                *
      *                                                                *
      *                                                                *
      *                                                                *
      *                                                                *
      * STATUS = 7.2.0                                                 *
      *                                                                *
      *----------------------------------------------------------------*
      *                                                                *
      * LANGUAGE: COBOL II                                             *
      *                                                                *
      * FUNCTION =                                                     *
      *                                                                *
      * DFH0STXD is designed to run as a DFHSTUP EXTRACT exit.         *
      *                                                                *
      * DFH0STXD produces an 'Resource Definition' type of statistics  *
      * report which shows in a single print line each type of         *
      * resource defined to your CICS systems. It is designed to       *
      * provide a simple method of analyzing from the CICS statistics  *
      * records                                                        *
      *                                                                *
      *                                                                *
      *                                                                *
      *                                                                *
      *                                                                *
      *                                                                *
      *  Following the prologue code, Register 1 contains the address  *
      *  of a parameter list (mapped by structure extract-plist)       *
      *  passed from DFHSTUP as follows:                               *
      *                                                                *
      * -> Exit-function-code-ptr --> one byte indicating call type    *
      *    Exit-work-area-ptr     --> fullword anchor area             *
      *    Smf-record-ptr         --> A copy of CICS SMF 110 record    *
      *    Statistics-record-ptr  --> A copy of CICS stats record      *
      *    parm-data-ptr          --> report date mmddyyyy             *
      *                               report time hhmmss               *
      *                               lines per page default = 60      *
      *                               CICS release number n.n.n        *
      *                               case setting M = Mixed (default) *
      *                                            U = upper           *
      *                                                                *
      *                                                                *
      *  The exit_work_area_ptr addresses a fullword into which the    *
      *  exit may store the address of any user defined workarea       *
      *  getmained. The value is returned on subsequent invocations    *
      *  of the exit for the same extract command.                     *
      *                                                                *
      *  The parm data addressed by parm_data_ptr is mapped by the     *
      *  structure EXTRACT-PARM-DATA listed below.                     *
      *                                                                *
      *  The exit may set a return code to be returned to DFHSTUP. Any *
      *  non zero return code will cause the current extract command   *
      *  to be terminated. No further exit invocations will occur for  *
      *  for this command. DFHSTUP will issue message DFHST0234        *
      *  showing the return code set in the exit.                      *
      *                                                                *
      *                                                                *
      *                                                                *
      *----------------------------------------------------------------*
      *                                                                *
      * N.B. DFH0STXD MUST be linked AMODE(31) RMODE(24)               *
      *                                                                *
      *----------------------------------------------------------------*
      *                                                                *
      * CHANGE ACTIVITY :                                              *
      *      $SEG(DFH0STXD),COMP(SAMPLES),PROD(CICS TS ):              *
      *                                                                *
      *     PN= REASON REL YYMMDD HDXIII : REMARKS                     *
      *    $L0= 907    660 071206 HD1VCJB: EXTRACT Sample creation     *
      *    $L1= 907    660 080303 HD1VCJB: RDO audit for PROGRAM       *
      *    $L2= 907    660 080320 HDITLJG: Part 4 RDO audit for IPCONN *
      *    $L4= 907    660 080414 HD1VCJB: RDO audit for TCPIPSERVICE  *
      *    $L5= 907    660 080423 HD1VCJB: Part 4 RDO Audit for TRANS  *
      *    $L6= 907    660 080624 HDITLJG: Part 4 RDO Audit for DB2CONN*
      *    $L7= 907    660 080708 HD1VCJB: Bundle Statistics Support   *
      *    $L8= 923    660 080718 HD1VCJB: Atomservice statistics      *
      *    $L9= 937    660 080812 HD1VCJB: JVMSERVER Statistics Support*
      *    $LA= 907    660 080813 HDITLJG: RDO audit for DOCTEMPLATE   *
      *    $LB= 907    660 081010 HD1VCJB: Resource Sigs for connection*
      *    $LC= 907    660 081014 HD1VCJB: Resource Signatures for File*
      *    $LD= 907    660 081024 HD1VCJB: Add Resource signature      *
      *    $LE= 907    660 081028 HDITLJG: Part 4 Stats for CORBAS     *
      *    $LG= 907    660 081113 HDITLJG: LID 907 TDQ Stats           *
      *    $LH= 918    660 081117 HD2GJST: CICS-MQ Group attach changes*
      *    $LI= 907    660 081119 HDITLJG: Part 4 Requestmodel stats   *
      *    $LJ= 936    660 081130 HD1VCJB: XMLTRANSFORM Statistics Supp*
      *    $LK= 907    660 081216 HD1VCJB: Add bundle install agent    *
      *    $LL= 907    660 090127 HD1VCJB: CPSM resources SYSTEM create*
      *    $LM= R00559 670 091024 HDHCPC : ATOMSERVICE from BUNDLE     *
      *    $LN= R00256 670 100723 HD4IAEC: EPADAPTER statistics        *
      *         R38170 680 111205 HD1VCJB: Remove EJB Statistics       *
      *         R38307 680 111221 HDIGPHG: Add BUNDLE installagent     *
      *         R38347 680 120501 HDHCPMC: Add BUNDLE installagent     *
      *         R46548 680 120510 HDLVDNM: Add MGMTPART installagent   *
      *         R38309 680 120607 HDLLGJH: Add BUNDLE installagent     *
      *         R49927 680 120813 HDHCPMC: Change MGMTPART to CLOUD    *
      *         R62949 690 130113 HDLPLGQ: TCPIPService Bundle enable  *
      *         R63328 690 130104 HDLLGJH: JVMServer Bundle enable     *
      *    $P1= D21810 660 080820 HD1VCJB: Change Install Agent Reporte*
      *    $P2= D24060 660 090120 HD4PALS: autoinstall agent missing   *
      *    $P3= D22911 660 090123 HDITLJG: Add res_sig for TDQ CXRF    *
      *    $P4= D24610 660 090220 HD4PALS: add createspi changeagent   *
      *    $P5= D25905 660 090409 HD1VCJB: Correct line count page skip*
      *         D70189 690 130425 HD3BADW: Add BUNDLE installagent     *
      *         R70934 690 130708 HDLULGJ: Add BUNDLE installagent     *
      *        R120820 700 160817 HDHCPMC: Add DYNAMIC installagent    *
      *        R110014 710 160825 HDFVGMB: Add MQMONITOR statistics    *
      *        R148770 720 180919 HDLHJJH: Add NODEJSAPP statistics    *
      *        D150786 720 181023 HDFVGMB: Add TRUNC(BIN) to PROCESS   *
      *                                                                *
      ******************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID. DFH0STXD.
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT DATA-OUT
                  ASSIGN TO SYS002-DA-3380-S-STXDRPT
                  ORGANIZATION IS SEQUENTIAL
                  FILE STATUS IS OUTPUT-FILE-STATUS
                  ACCESS IS SEQUENTIAL.

       DATA DIVISION.
       FILE SECTION.
       FD  DATA-OUT
           RECORD CONTAINS 132 CHARACTERS
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F
           LABEL RECORDS ARE OMITTED.

       01  PRINT-RECORD                PIC X(132).

       WORKING-STORAGE SECTION.
       77  OUTPUT-FILE-FLAG            PIC X.
           88  OUTPUT-FILE-OPEN           VALUE IS '1'.
       77  FILE-OPEN                   PIC X VALUE '1'.
       77  OUTPUT-FILE-STATUS          PIC XX.
           88 ERROR-ON-OUTPUT-FILE         VALUES ARE '01' THRU '99'.
       77  OUTPUT-FILE-OPEN-ERROR      PIC S9(4)   COMP VALUE IS 5.
       77  OUTPUT-FILE-CLOSE-ERROR     PIC S9(4)   COMP VALUE IS 6.
       77  OUTPUT-FILE-WRITE-ERROR     PIC S9(4)   COMP VALUE IS 7.

       77  WF-SPACING                  PIC 9            VALUE 1.
       77  PAGE-NUMBER                 PIC S9(7)   COMP-3  VALUE +0.
       77  LINE-COUNT                  PIC S9(3)   COMP-3  VALUE +0.

       77  CURRENT-APPLID              PIC X(8)        VALUE SPACES.

       77  REPORT-DATE                 PIC 99B99B9999.
       77  REPORT-TIME                 PIC 99B99B99.
       77  LINES-PER-PAGE              PIC S9(3)   COMP-3  VALUE +0.
       77  RELEASE-NO                  PIC X(5)        VALUE SPACES.

       77  STATISTICS-RECORD-PTR       USAGE IS POINTER.

       77  REPORT-COMPLETION-MSG       PIC X(50)
           VALUE 'Statistics Resource Report Processing Complete.   '.

       77  TOTAL-STATISTICS-RECORD-MSG PIC X(50)
           VALUE 'Total Statistics Records Processed . . . . . . . :'.
       77  STATISTICS-RECORD-MSG       PIC X(50)
           VALUE 'Statistics Records Processed . . . . . . . . . . :'.
       77  RESOURCE-DEFINITIONS-MSG    PIC X(50)
           VALUE 'Total Resource Definitions Reported. . . . . . . :'.

       01  CLOCK-FIELDS.
           03  CLOCK-OUTPUT-DATE       PIC X(10).
           03  CLOCK-OUTPUT-TIME.
               05  CLOCK-OUTPUT-HHMMSS PIC X(8).
               05  FILLER              PIC X(7).

       01  RESOURCE-DESCRIPTION.
           03  RESOURCE-TYPE           PIC X(12)       VALUE SPACES.
           03  FILLER                  PIC X           VALUE SPACES.
           03  RESOURCE-ID             PIC X(32)       VALUE SPACES.
           03  FILLER                  PIC X           VALUE SPACES.
           03  RESOURCE-DEFINE-SOURCE  PIC X(8)        VALUE SPACES.
           03  FILLER                  PIC XX          VALUE SPACES.
           03  RESOURCE-INSTALL-AGENT  PIC X(12)       VALUE SPACES.
           03  FILLER                  PIC X           VALUE SPACES.
           03  RESOURCE-INSTALL-DATE   PIC X(10)       VALUE SPACES.
           03  FILLER                  PIC X           VALUE SPACES.
           03  RESOURCE-INSTALL-TIME   PIC X(8)        VALUE SPACES.
           03  FILLER                  PIC XX          VALUE SPACES.
           03  RESOURCE-INSTALL-USERID PIC X(8)        VALUE SPACES.

       01  RECORD-TOTALS.
           03  TOTAL-STATS-RECORDS     PIC S9(9)   COMP-3  VALUE +0.
           03  STATISTICS-RECORD-TOTAL PIC S9(9)   COMP-3  VALUE +0.
           03  ATOMSERVICE-RECORDS     PIC S9(9)   COMP-3  VALUE +0.
           03  BUNDLE-RECORDS          PIC S9(9)   COMP-3  VALUE +0.
           03  CONNECTION-RECORDS      PIC S9(9)   COMP-3  VALUE +0.
           03  DB2CONN-RECORDS         PIC S9(9)   COMP-3  VALUE +0.
           03  DB2ENTRY-RECORDS        PIC S9(9)   COMP-3  VALUE +0.
           03  DOCTEMPLATE-RECORDS     PIC S9(9)   COMP-3  VALUE +0.
           03  EPADAPTER-RECORDS       PIC S9(9)   COMP-3  VALUE +0.
           03  EVENTBINDING-RECORDS    PIC S9(9)   COMP-3  VALUE +0.
           03  FILE-RECORDS            PIC S9(9)   COMP-3  VALUE +0.
           03  IPCONN-RECORDS          PIC S9(9)   COMP-3  VALUE +0.
           03  JVMSERVER-RECORDS       PIC S9(9)   COMP-3  VALUE +0.
           03  LIBRARY-RECORDS         PIC S9(9)   COMP-3  VALUE +0.
           03  MQCONN-RECORDS          PIC S9(9)   COMP-3  VALUE +0.
           03  MQMONITOR-RECORDS       PIC S9(9)   COMP-3  VALUE +0.
           03  NODEJSAPP-RECORDS       PIC S9(9)   COMP-3  VALUE +0.
           03  PIPELINE-RECORDS        PIC S9(9)   COMP-3  VALUE +0.
           03  PROGRAMDEF-RECORDS      PIC S9(9)   COMP-3  VALUE +0.
           03  TCPIPSERVICE-RECORDS    PIC S9(9)   COMP-3  VALUE +0.
           03  TDQUEUE-RECORDS         PIC S9(9)   COMP-3  VALUE +0.
           03  TRANCLASS-RECORDS       PIC S9(9)   COMP-3  VALUE +0.
           03  TRANSACTION-RECORDS     PIC S9(9)   COMP-3  VALUE +0.
           03  URIMAP-RECORDS          PIC S9(9)   COMP-3  VALUE +0.
           03  WEBSERVICE-RECORDS      PIC S9(9)   COMP-3  VALUE +0.
           03  XMLTRANSFORM-RECORDS    PIC S9(9)   COMP-3  VALUE +0.

       01  REPORT-TOTALS.
           03  RESOURCE-DEFS-TOTAL     PIC S9(7)   COMP-3  VALUE +0.

       01  PRINT-HEADING-LINE.
           03  FILLER                  PIC X(4)    VALUE 'CICS'.
           03  FILLER                  PIC X       VALUE SPACE.
           03  HEADING-RELEASE-NO      PIC X(5).
           03  FILLER                  PIC X       VALUE SPACE.
           03  HEADING-TITLE           PIC X(39)
                   VALUE 'Statistics Resource Definition Reporter'.
           03  FILLER                  PIC XX      VALUE SPACES.
           03  FILLER                  PIC X(16)   VALUE SPACES.
           03  FILLER                  PIC X(12)
                                           VALUE 'Report Date:'.
           03  FILLER                  PIC X       VALUE SPACE.
           03  HEADING-REPORT-DATE     PIC X(10).
           03  FILLER                  PIC XXX     VALUE SPACES.
           03  FILLER                  PIC X(12)
                                           VALUE 'Report Time:'.
           03  FILLER                  PIC X       VALUE SPACE.
           03  HEADING-REPORT-TIME     PIC X(8).
           03  FILLER                  PIC X(6)    VALUE SPACES.
           03  FILLER                  PIC X(4)    VALUE 'Page'.
           03  FILLER                  PIC X       VALUE SPACE.
           03  HEADING-PAGENO          PIC ZZ,ZZ9.

       01  PRINT-HEADING-UNDERLINE.
           03  FILLER                  PIC X(132)  VALUE ALL '_'.

       01  PRINT-SUB-HEADING-LINE1.
           03  FILLER                  PIC X(33)
                       VALUE '<- Statistics Record  ->         '.
           03  FILLER                  PIC X(33)
                       VALUE ' Resource                        '.
           03  FILLER                  PIC X(33)
                       VALUE '              Define    <--------'.
           03  FILLER                  PIC X(33)
                       VALUE '-- Install Signature ----------->'.

       01  PRINT-SUB-HEADING-LINE2.
           03  FILLER                  PIC X(33)
                       VALUE '   Date      Time   Type Applid  '.
           03  FILLER                  PIC X(33)
                       VALUE '   Type       Resource ID        '.
           03  FILLER                  PIC X(33)
                       VALUE '              Source    Agent    '.
           03  FILLER                  PIC X(33)
                       VALUE '       Date      Time     Userid '.

       01  PRINT-SUB-HEADING-UNDERLINE.
           03  FILLER                  PIC X(132)  VALUE ALL '_'.

       01  PRINT-LINE.
           03  PP1-132.
               05  PP1-10              PIC 99B99B9999.
               05  FILLER              PIC X.
               05  PP12-19             PIC 99B99B99.
               05  FILLER              PIC X.
               05  FILLER              PIC X.
               05  PP22-24             PIC XXX.
               05  FILLER              PIC X.
               05  PP26-33             PIC X(8).
               05  FILLER              PIC X.
               05  PP35-132            PIC X(98).
           03  FILLER REDEFINES PP1-132.
               05  PP1-50              PIC X(50).
               05  FILLER              PIC XX.
               05  PP53-62             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X(70).
           03  FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(34).
               05  PP35-91             PIC X(57).
               05  PP92-99             PIC X(8).
               05  FILLER              PIC X(33).
           03 FILLER REDEFINES PP1-132.
               05  PP1-33             PIC X(33).
               05  PP34-66            PIC X(33).
               05  PP67-99            PIC X(33).
               05  PP100-132          PIC X(33).
           EJECT
       LINKAGE SECTION.
      *****************************************************************
      * ************************************************************* *
      * Define the linkage between this program and the statistics    *
      * off-line utility program, DFHSTUP. The addressability to the  *
      * values addressed by the parameter list pass from DFHSTUP is   *
      * established automatically by the COBOL compiler so all we     *
      * need to define here are the actual formats of the values      *
      * themselves.                                                   *
      * ************************************************************* *
      *****************************************************************

       01  EXIT-FUNCTION-CODE          PIC 9(4) COMP.
             88 INITIAL-CALL               VALUE IS 0.
             88 APPLID-START-CALL          VALUE IS 4.
             88 STATISTICS-RECORD          VALUE IS 8.
             88 APPLID-END-CALL            VALUE IS 12.
             88 FINAL-CALL                 VALUE IS 16.

       01  EXIT-WORK-AREA-PTR          POINTER.
           EJECT
      *****************************************************************
      *** Data structures for CICS SMF 110 Records                  ***
      *** (See the CICS Data Areas manual)                          ***
      *** Note that the exit is passed a copy of the CICS SMF 110   ***
      *** record, the SMF-HEADER section is set to nulls, but all   ***
      *** data starting at the SMF-PRODUCT-SECTION is available.    ***
      *****************************************************************
           COPY DFHSMFDS.
           EJECT
      *****************************************************************
      *** Data structures for Statistics Record Ids                 ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHSTIDS.
           EJECT
      *****************************************************************
      ***                                                           ***
      *****************************************************************
       01  EXTRACT-PARM-DATA.
           03  EXTRACT-REPORT-DATE     PIC X(8).
           03  EXTRACT-REPORT-TIME     PIC X(6).
           03  EXTRACT-LINES-PER-PAGE  PIC 9(4) COMP.
           03  EXTRACT-RELEASE-NO      PIC X(5).
           03  EXTRACT-CASE-SETTING    PIC X.
           03  FILLER                  PIC XX.
           EJECT
      *****************************************************************
      *** Data structures below for CICS statistics records. Note   ***
      *** The exit is passed a copy of the CICS statistics record.  ***
      *****************************************************************
      *****************************************************************
      *** Data structures for Connection Resource statistics        ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHA14DS.
           EJECT
      *****************************************************************
      *** Data structures for File Resource statistics              ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHA17DS.
           EJECT
      *****************************************************************
      *** Data structures for DOCTEMPLATE Resource statistics       ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHDHDDS.
           EJECT
      *****************************************************************
      *** Data structures for DB2CONN Global statistics             ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHD2GDS.
           EJECT
      *****************************************************************
      *** Data structures for DB2ENTRY Resource statistics          ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHD2RDS.
           EJECT
      *****************************************************************
      *** Data structures for EPADAPTER Resource statistics         ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHEPRDS.
           EJECT
      *****************************************************************
      *** Data structures for EVENTBINDING Resource statistics      ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHECRDS.
           EJECT
      *****************************************************************
      *** Data structures for IPCONN Resource statistics            ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHISRDS.
           EJECT
      *****************************************************************
      *** Data structures for Loader LIBRARY Resource statistics    ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHLDBDS.
           EJECT
      *****************************************************************
      *** Data structures for MQCONN Global statistics              ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHMQGDS.
           EJECT
      *****************************************************************
      *** Data structures for MQMONITOR Resource statistics         ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHMQRDS.
           EJECT
      *****************************************************************
      *** Data structures for XMLTRANSFORM Resource statistics      ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHMLRDS.
           EJECT
      *****************************************************************
      *** Data structures for Program Manager Resource statistics   ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHPGDDS.
           EJECT
      *****************************************************************
      *** Data structures for PIPELINE Resource statistics          ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHPIRDS.
           EJECT
      *****************************************************************
      *** Data structures for WEBSERVICE Resource statistics        ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHPIWDS.
           EJECT
      *****************************************************************
      *** Data structures for BUNDLE Resource statistics            ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHRLRDS.
           EJECT
      *****************************************************************
      *** Data structures for JVMSERVER Resource statistics         ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHSJSDS.
           EJECT
      *****************************************************************
      *** Data structures for NODEJSAPP Resource statistics         ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHSJNDS.
           EJECT
      *****************************************************************
      *** Data structures for TCPIPSERVICE Resource statistics      ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHSORDS.
           EJECT
      *****************************************************************
      *** Data structures for TDQUEUE Resource statistics           ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHTQRDS.
           EJECT
      *****************************************************************
      *** Data structures for URIMAP Resource statistics            ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHWBRDS.
           EJECT
      *****************************************************************
      *** Data structures for ATOMSERVICE Resource statistics       ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHW2RDS.
           EJECT
      *****************************************************************
      *** Data structures for TRANCLASS Resource statistics         ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHXMCDS.
           EJECT
      *****************************************************************
      *** Data structures for TRANSACTION Resource statistics       ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHXMRDS.
           EJECT
       PROCEDURE DIVISION USING EXIT-FUNCTION-CODE
                                EXIT-WORK-AREA-PTR
                                DFHSMFDS
                                DFHSTIDS
                                EXTRACT-PARM-DATA.

      *****************************************************************
      ***                                                           ***
      *****************************************************************
       MAIN-PROCESSING SECTION.

           MOVE 0 TO RETURN-CODE.

           EVALUATE TRUE
               WHEN INITIAL-CALL
                   PERFORM PROCESS-INITIAL-CALL
               WHEN APPLID-START-CALL
                   PERFORM PROCESS-APPLID-START-CALL
               WHEN STATISTICS-RECORD
                   PERFORM PROCESS-STATS-RECORD-CALL
               WHEN APPLID-END-CALL
                   PERFORM PROCESS-APPLID-END-CALL
               WHEN FINAL-CALL
                   PERFORM PROCESS-FINAL-CALL
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.

      * ************************************************************** *
      * If the value in the 'RETURN-CODE' special register at this     *
      * point is non-zero then the current EXTRACT command will        *
      * be terminated. Return must be made via a GOBACK in order to    *
      * prevent STOP RUN processing.                                   *
      * ************************************************************** *
           PERFORM CHECK-RETURN-CODE.

       MAIN-PROCESSING-EXIT.
           GOBACK.
           EJECT
      *****************************************************************
      *** INITIAL-CALL                                              ***
      ***                                                           ***
      *****************************************************************

       PROCESS-INITIAL-CALL SECTION.

      *****************************************************************
      *    Initialise the various report default values.              *
      *****************************************************************
           MOVE 0      TO PAGE-NUMBER.
           MOVE 0      TO LINE-COUNT.
           MOVE 58     TO LINES-PER-PAGE.

           MOVE SPACES TO PRINT-LINE.

      *****************************************************************
      *    Setup the report heading fields.                           *
      *****************************************************************
           MOVE EXTRACT-REPORT-DATE    TO REPORT-DATE.
           INSPECT REPORT-DATE REPLACING ALL ' ' BY '/'.
           MOVE EXTRACT-REPORT-TIME    TO REPORT-TIME.
           INSPECT REPORT-TIME REPLACING ALL ' ' BY ':'.
           MOVE EXTRACT-LINES-PER-PAGE TO LINES-PER-PAGE.
           MOVE EXTRACT-RELEASE-NO     TO RELEASE-NO.

      *****************************************************************
      ***  Open the STXDRPT output file.                            ***
      *****************************************************************
           PERFORM OPEN-FILES.

       PROCESS-INITIAL-CALL-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** PROCESS-APPLID-START-CALL                                 ***
      ***                                                           ***
      *****************************************************************
       PROCESS-APPLID-START-CALL SECTION.

           MOVE SMF-GENERIC-APPLID TO CURRENT-APPLID.

           PERFORM INITIALISE-RECORD-TOTALS.

           MOVE 99 TO LINE-COUNT.

       PROCESS-APPLID-START-CALL-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** PROCESS-STATS-RECORD-CALL                                 ***
      ***                                                           ***
      *****************************************************************
       PROCESS-STATS-RECORD-CALL SECTION.

           ADD 1 TO TOTAL-STATS-RECORDS STATISTICS-RECORD-TOTAL.

           SET STATISTICS-RECORD-PTR TO ADDRESS OF DFHSTIDS.

      *****************************************************************
      ***   Evaluate the statistics id (STATID) and perform the     ***
      *** statistics event analysis for each type of statistics     ***
      *** record.                                                   ***
      *****************************************************************
           EVALUATE TRUE
               WHEN STIXMC
                   PERFORM TRANCLASS-DEFINITIONS
               WHEN STIXMR
                   PERFORM TRANSACTION-DEFINITIONS
               WHEN STIDHD
                   PERFORM DOCTEMPLATE-DEFINITIONS
               WHEN STIFCR
                   PERFORM FILE-DEFINITIONS
               WHEN STILDB
                   PERFORM LIBRARY-DEFINITIONS
               WHEN STICONSR
                   PERFORM CONNECTION-DEFINITIONS
               WHEN STISOR
                   PERFORM TCPIPSERVICE-DEFINITIONS
               WHEN STITDQR
                   PERFORM TDQUEUE-DEFINITIONS
               WHEN STIRLR
                   PERFORM BUNDLE-DEFINITIONS
               WHEN STIISR
                   PERFORM IPCONN-DEFINITIONS
               WHEN STISJS
                   PERFORM JVMSERVER-DEFINITIONS
               WHEN STIPIR
                   PERFORM PIPELINE-DEFINITIONS
               WHEN STIPGD
                   PERFORM PROGRAM-DEFINITIONS
               WHEN STIWBR
                   PERFORM URIMAP-DEFINITIONS
               WHEN STIW2R
                   PERFORM ATOMSERVICE-DEFINITIONS
               WHEN STIPIW
                   PERFORM WEBSERVICE-DEFINITIONS
               WHEN STIMLR
                   PERFORM XMLTRANSFORM-DEFINITIONS
               WHEN STID2G
                   PERFORM DB2CONN-DEFINITIONS
               WHEN STID2R
                   PERFORM DB2ENTRY-DEFINITIONS
               WHEN STIMQG
                   PERFORM MQCONN-DEFINITIONS
               WHEN STIMQR
                   PERFORM MQMONITOR-DEFINITIONS
               WHEN STIECR
                   PERFORM EVENTBINDING-DEFINITIONS
               WHEN STIEPR
                   PERFORM EPADAPTER-DEFINITIONS
               WHEN STINDJ
                   PERFORM NODEJSAPP-DEFINITIONS
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.

       PROCESS-STATS-RECORD-CALL-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  ATOMSERVICE Definitions                                  ***
      *****************************************************************
       ATOMSERVICE-DEFINITIONS SECTION.

           ADD 1 TO ATOMSERVICE-RECORDS.
           SET ADDRESS OF DFHW2RDS TO STATISTICS-RECORD-PTR.

      *****************************************************************

           MOVE 'ATOMSERVICE '             TO RESOURCE-TYPE.
           MOVE W2R-ATOMSERV-NAME          TO RESOURCE-ID.
      *    Group installed from
           MOVE W2R-ATOMSERV-DEFINE-SOURCE TO RESOURCE-DEFINE-SOURCE.
      *    Install agent
           EVALUATE TRUE
               WHEN W2R-CSDAPI-INSTALL
                   MOVE 'CSDAPI      '     TO RESOURCE-INSTALL-AGENT
               WHEN W2R-CREATE-INSTALL
                   MOVE 'CREATESPI   '     TO RESOURCE-INSTALL-AGENT
               WHEN W2R-GRPLIST-INSTALL
                   MOVE 'GRPLIST     '     TO RESOURCE-INSTALL-AGENT
               WHEN W2R-BUNDLE-INSTALL
                   MOVE 'BUNDLE      '     TO RESOURCE-INSTALL-AGENT
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
      *    Install/Create date and time
           CALL 'DFH$STXK' USING W2R-ATOMSERV-INSTALL-TIME,
                                 CLOCK-OUTPUT-DATE,
                                 CLOCK-OUTPUT-TIME.
           MOVE CLOCK-OUTPUT-DATE   TO RESOURCE-INSTALL-DATE.
           MOVE CLOCK-OUTPUT-HHMMSS TO RESOURCE-INSTALL-TIME.
      *    Install userid
           MOVE W2R-ATOMSERV-INSTALL-USERID TO RESOURCE-INSTALL-USERID.

           PERFORM PRINT-RESOURCE-DEFINITION.

       ATOMSERVICE-DEFINITIONS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  BUNDLE Definitions                                       ***
      *****************************************************************
       BUNDLE-DEFINITIONS SECTION.

           ADD 1 TO BUNDLE-RECORDS.
           SET ADDRESS OF DFHRLRDS TO STATISTICS-RECORD-PTR.

      *****************************************************************

           MOVE 'BUNDLE      '    TO RESOURCE-TYPE.
           MOVE RLR-BUNDLE-NAME   TO RESOURCE-ID.
      *    Group installed from
           MOVE RLR-BUNDLE-DEFINE-SOURCE   TO RESOURCE-DEFINE-SOURCE.
      *    Install agent
           EVALUATE TRUE
               WHEN RLR-BUNDLE-CSDAPI-INSTALL
                   MOVE 'CSDAPI      '     TO RESOURCE-INSTALL-AGENT
               WHEN RLR-BUNDLE-CREATE-INSTALL
                   MOVE 'CREATESPI   '     TO RESOURCE-INSTALL-AGENT
               WHEN RLR-BUNDLE-GRPLIST-INSTALL
                   MOVE 'GRPLIST     '     TO RESOURCE-INSTALL-AGENT
               WHEN RLR-BUNDLE-CLOUD-INSTALL
                   MOVE 'CLOUD       '     TO RESOURCE-INSTALL-AGENT
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
      *    Install/Create date and time
           CALL 'DFH$STXK' USING RLR-BUNDLE-INSTALL-TIME,
                                 CLOCK-OUTPUT-DATE,
                                 CLOCK-OUTPUT-TIME.
           MOVE CLOCK-OUTPUT-DATE   TO RESOURCE-INSTALL-DATE.
           MOVE CLOCK-OUTPUT-HHMMSS TO RESOURCE-INSTALL-TIME.
      *    Install userid
           MOVE RLR-BUNDLE-INSTALL-USERID TO RESOURCE-INSTALL-USERID.

           PERFORM PRINT-RESOURCE-DEFINITION.

       BUNDLE-DEFINITIONS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  CONNECTION Definitions                                   ***
      *****************************************************************
       CONNECTION-DEFINITIONS SECTION.

           ADD 1 TO CONNECTION-RECORDS.
           SET ADDRESS OF DFHA14DS TO STATISTICS-RECORD-PTR.

      *****************************************************************

           MOVE 'CONNECTION  '             TO RESOURCE-TYPE.
           MOVE A14CNTN                    TO RESOURCE-ID.
      *    Group installed from
           MOVE A14-DEFINE-SOURCE          TO RESOURCE-DEFINE-SOURCE.
      *    Install agent
           EVALUATE TRUE
               WHEN A14-CSDAPI-INSTALL
                   MOVE 'CSDAPI      '     TO RESOURCE-INSTALL-AGENT
               WHEN A14-CREATE-INSTALL
                   MOVE 'CREATESPI   '     TO RESOURCE-INSTALL-AGENT
               WHEN A14-GRPLIST-INSTALL
                   MOVE 'GRPLIST     '     TO RESOURCE-INSTALL-AGENT
               WHEN A14-AUTOINSTALL-INSTALL
                   MOVE 'AUTOINSTALL '     TO RESOURCE-INSTALL-AGENT
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
      *    Install/Create date and time
           CALL 'DFH$STXK' USING A14-INSTALL-TIME,
                                 CLOCK-OUTPUT-DATE,
                                 CLOCK-OUTPUT-TIME.
           MOVE CLOCK-OUTPUT-DATE   TO RESOURCE-INSTALL-DATE.
           MOVE CLOCK-OUTPUT-HHMMSS TO RESOURCE-INSTALL-TIME.
      *    Install userid
           MOVE A14-INSTALL-USERID  TO RESOURCE-INSTALL-USERID.

           PERFORM PRINT-RESOURCE-DEFINITION.

       CONNECTION-DEFINITIONS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  DB2CONN Definitions                                      ***
      *****************************************************************
       DB2CONN-DEFINITIONS SECTION.

           ADD 1 TO DB2CONN-RECORDS.
           SET ADDRESS OF DFHD2GDS TO STATISTICS-RECORD-PTR.

      *****************************************************************

           MOVE 'DB2CONN     '             TO RESOURCE-TYPE.
           MOVE D2G-DB2CONN-NAME           TO RESOURCE-ID.
      *    Group installed from
           MOVE D2G-DB2CONN-DEFINE-SOURCE  TO RESOURCE-DEFINE-SOURCE.
      *    Install agent
           EVALUATE TRUE
               WHEN D2G-DB2CONN-CSDAPI-INSTALL
                   MOVE 'CSDAPI      '     TO RESOURCE-INSTALL-AGENT
               WHEN D2G-DB2CONN-CREATE-INSTALL
                   MOVE 'CREATESPI   '     TO RESOURCE-INSTALL-AGENT
               WHEN D2G-DB2CONN-GRPLIST-INSTALL
                   MOVE 'GRPLIST     '     TO RESOURCE-INSTALL-AGENT
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
      *    Install/Create date and time
           CALL 'DFH$STXK' USING D2G-DB2CONN-INSTALL-TIME,
                                 CLOCK-OUTPUT-DATE,
                                 CLOCK-OUTPUT-TIME.
           MOVE CLOCK-OUTPUT-DATE   TO RESOURCE-INSTALL-DATE.
           MOVE CLOCK-OUTPUT-HHMMSS TO RESOURCE-INSTALL-TIME.
      *    Install userid
           MOVE D2G-DB2CONN-INSTALL-USERID TO RESOURCE-INSTALL-USERID.

           PERFORM PRINT-RESOURCE-DEFINITION.

       DB2CONN-DEFINITIONS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  DB2ENTRY Definitions                                     ***
      *****************************************************************
       DB2ENTRY-DEFINITIONS SECTION.

           ADD 1 TO DB2ENTRY-RECORDS.
           SET ADDRESS OF DFHD2RDS TO STATISTICS-RECORD-PTR.

      *****************************************************************

           MOVE 'DB2ENTRY    '             TO RESOURCE-TYPE.
           MOVE D2R-DB2ENTRY-NAME          TO RESOURCE-ID.
      *    Group installed from
           MOVE D2R-DEFINE-SOURCE  TO RESOURCE-DEFINE-SOURCE.
      *    Install agent
           EVALUATE TRUE
               WHEN D2R-CSDAPI-INSTALL
                   MOVE 'CSDAPI      '     TO RESOURCE-INSTALL-AGENT
               WHEN D2R-CREATE-INSTALL
                   MOVE 'CREATESPI   '     TO RESOURCE-INSTALL-AGENT
               WHEN D2R-GRPLIST-INSTALL
                   MOVE 'GRPLIST     '     TO RESOURCE-INSTALL-AGENT
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
      *    Install/Create date and time
           CALL 'DFH$STXK' USING D2R-INSTALL-TIME,
                                 CLOCK-OUTPUT-DATE,
                                 CLOCK-OUTPUT-TIME.
           MOVE CLOCK-OUTPUT-DATE   TO RESOURCE-INSTALL-DATE.
           MOVE CLOCK-OUTPUT-HHMMSS TO RESOURCE-INSTALL-TIME.
      *    Install userid
           MOVE D2R-INSTALL-USERID TO RESOURCE-INSTALL-USERID.

           PERFORM PRINT-RESOURCE-DEFINITION.

       DB2ENTRY-DEFINITIONS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  DOCTEMPLATE Definitions                                  ***
      *****************************************************************
       DOCTEMPLATE-DEFINITIONS SECTION.

           ADD 1 TO DOCTEMPLATE-RECORDS.
           SET ADDRESS OF DFHDHDDS TO STATISTICS-RECORD-PTR.

      *****************************************************************

           MOVE 'DOCTEMPLATE '    TO RESOURCE-TYPE.
           MOVE DHD-DOCTEMPLATE-NAME   TO RESOURCE-ID.
      *    Group installed from
           MOVE DHD-TEMPLATE-DEFINE-SOURCE TO RESOURCE-DEFINE-SOURCE.
      *    Install agent
           EVALUATE TRUE
               WHEN DHD-CSDAPI-INSTALL
                   MOVE 'CSDAPI      '     TO RESOURCE-INSTALL-AGENT
               WHEN DHD-CREATE-INSTALL
                   MOVE 'CREATESPI   '     TO RESOURCE-INSTALL-AGENT
               WHEN DHD-GRPLIST-INSTALL
                   MOVE 'GRPLIST     '     TO RESOURCE-INSTALL-AGENT
               WHEN DHD-DYNAMIC-INSTALL
                   MOVE 'DYNAMIC     '     TO RESOURCE-INSTALL-AGENT
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
      *    Install/Create date and time
           CALL 'DFH$STXK' USING DHD-TEMPLATE-INSTALL-TIME,
                                 CLOCK-OUTPUT-DATE,
                                 CLOCK-OUTPUT-TIME.
           MOVE CLOCK-OUTPUT-DATE   TO RESOURCE-INSTALL-DATE.
           MOVE CLOCK-OUTPUT-HHMMSS TO RESOURCE-INSTALL-TIME.
      *    Install userid
           MOVE DHD-TEMPLATE-INSTALL-USERID TO RESOURCE-INSTALL-USERID.

           PERFORM PRINT-RESOURCE-DEFINITION.

       DOCTEMPLATE-DEFINITIONS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  EPADAPTER Definitions                                    ***
      *****************************************************************
       EPADAPTER-DEFINITIONS SECTION.

           ADD 1 TO EPADAPTER-RECORDS.
           SET ADDRESS OF DFHEPRDS TO STATISTICS-RECORD-PTR.

      *****************************************************************

           MOVE 'EPADAPTER'            TO RESOURCE-TYPE.
           MOVE EPR-ADAPTER-NAME       TO RESOURCE-ID.
      *    Group installed from
           MOVE EPR-ADA-DEFINE-SOURCE  TO RESOURCE-DEFINE-SOURCE.
      *    Install agent
           EVALUATE TRUE
               WHEN EPR-BUNDLE-INSTALL
                   MOVE 'BUNDLE      ' TO RESOURCE-INSTALL-AGENT
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
      *    Install/Create date and time
           CALL 'DFH$STXK' USING EPR-ADA-INSTALL-TIME,
                                 CLOCK-OUTPUT-DATE,
                                 CLOCK-OUTPUT-TIME.
           MOVE CLOCK-OUTPUT-DATE   TO RESOURCE-INSTALL-DATE.
           MOVE CLOCK-OUTPUT-HHMMSS TO RESOURCE-INSTALL-TIME.
      *    Install userid
           MOVE EPR-ADA-INSTALL-USERID TO RESOURCE-INSTALL-USERID.

           PERFORM PRINT-RESOURCE-DEFINITION.

       EPADAPTER-DEFINITIONS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  EVENTBINDING Definitions                                 ***
      *****************************************************************
       EVENTBINDING-DEFINITIONS SECTION.

           ADD 1 TO EVENTBINDING-RECORDS.
           SET ADDRESS OF DFHECRDS TO STATISTICS-RECORD-PTR.

      *****************************************************************

           MOVE 'EVENTBINDING'         TO RESOURCE-TYPE.
           MOVE ECR-EVENTBINDING-NAME  TO RESOURCE-ID.
      *    Group installed from
           MOVE ECR-EB-DEFINE-SOURCE   TO RESOURCE-DEFINE-SOURCE.
      *    Install agent
           EVALUATE TRUE
               WHEN ECR-BUNDLE-INSTALL
                   MOVE 'BUNDLE      ' TO RESOURCE-INSTALL-AGENT
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
      *    Install/Create date and time
           CALL 'DFH$STXK' USING ECR-EB-INSTALL-TIME,
                                 CLOCK-OUTPUT-DATE,
                                 CLOCK-OUTPUT-TIME.
           MOVE CLOCK-OUTPUT-DATE   TO RESOURCE-INSTALL-DATE.
           MOVE CLOCK-OUTPUT-HHMMSS TO RESOURCE-INSTALL-TIME.
      *    Install userid
           MOVE ECR-EB-INSTALL-USERID TO RESOURCE-INSTALL-USERID.

           PERFORM PRINT-RESOURCE-DEFINITION.

       EVENTBINDING-DEFINITIONS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  FILE Definitions                                         ***
      *****************************************************************
       FILE-DEFINITIONS SECTION.

           ADD 1 TO FILE-RECORDS.
           SET ADDRESS OF DFHA17DS TO STATISTICS-RECORD-PTR.

           MOVE 'FILE        '           TO RESOURCE-TYPE.
           MOVE A17FNAM                  TO RESOURCE-ID.
      *    Group installed from
           MOVE A17-FILE-DEFINE-SOURCE   TO RESOURCE-DEFINE-SOURCE.
      *    Install agent
           EVALUATE TRUE
               WHEN A17-FILE-CSDAPI-INSTALL
                   MOVE 'CSDAPI      '   TO RESOURCE-INSTALL-AGENT
               WHEN A17-FILE-CREATE-INSTALL
                   MOVE 'CREATESPI   '   TO RESOURCE-INSTALL-AGENT
               WHEN A17-FILE-GRPLIST-INSTALL
                   MOVE 'GRPLIST     '   TO RESOURCE-INSTALL-AGENT
               WHEN A17-FILE-SYSTEM-INSTALL
                   MOVE 'SYSTEM      '   TO RESOURCE-INSTALL-AGENT
               WHEN A17-FILE-TABLE-INSTALL
                   MOVE 'TABLE       '   TO RESOURCE-INSTALL-AGENT
               WHEN A17-FILE-BUNDLE-INSTALL
                   MOVE 'BUNDLE      '   TO RESOURCE-INSTALL-AGENT
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
      *    Install/Create date and time
           CALL 'DFH$STXK' USING A17-FILE-INSTALL-TIME,
                                 CLOCK-OUTPUT-DATE,
                                 CLOCK-OUTPUT-TIME.
           MOVE CLOCK-OUTPUT-DATE   TO RESOURCE-INSTALL-DATE.
           MOVE CLOCK-OUTPUT-HHMMSS TO RESOURCE-INSTALL-TIME.
      *    Install userid
           MOVE A17-FILE-INSTALL-USERID  TO RESOURCE-INSTALL-USERID.

           PERFORM PRINT-RESOURCE-DEFINITION.

       FILE-DEFINITIONS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  IPCONN Definitions                                       ***
      *****************************************************************
       IPCONN-DEFINITIONS SECTION.

           ADD 1 TO IPCONN-RECORDS.
           SET ADDRESS OF DFHISRDS TO STATISTICS-RECORD-PTR.

      *****************************************************************

           MOVE 'IPCONN      '    TO RESOURCE-TYPE.
           MOVE ISR-IPCONN-NAME   TO RESOURCE-ID.
      *    Group installed from
           MOVE ISR-IPCONN-DEFINE-SOURCE   TO RESOURCE-DEFINE-SOURCE.
      *    Install agent
           EVALUATE TRUE
               WHEN ISR-CSDAPI-INSTALL
                   MOVE 'CSDAPI      '     TO RESOURCE-INSTALL-AGENT
               WHEN ISR-CREATE-INSTALL
                   MOVE 'CREATESPI   '     TO RESOURCE-INSTALL-AGENT
               WHEN ISR-GRPLIST-INSTALL
                   MOVE 'GRPLIST     '     TO RESOURCE-INSTALL-AGENT
               WHEN ISR-AUTOINSTALL-INSTALL
                   MOVE 'AUTOINSTALL '     TO RESOURCE-INSTALL-AGENT
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
      *    Install/Create date and time
           CALL 'DFH$STXK' USING ISR-IPCONN-INSTALL-TIME,
                                 CLOCK-OUTPUT-DATE,
                                 CLOCK-OUTPUT-TIME.
           MOVE CLOCK-OUTPUT-DATE   TO RESOURCE-INSTALL-DATE.
           MOVE CLOCK-OUTPUT-HHMMSS TO RESOURCE-INSTALL-TIME.
      *    Install userid
           MOVE ISR-IPCONN-INSTALL-USERID TO RESOURCE-INSTALL-USERID.

           PERFORM PRINT-RESOURCE-DEFINITION.

       IPCONN-DEFINITIONS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  JVMSERVER Definitions                                    ***
      *****************************************************************
       JVMSERVER-DEFINITIONS SECTION.

           ADD 1 TO JVMSERVER-RECORDS.
           SET ADDRESS OF DFHSJSDS TO STATISTICS-RECORD-PTR.

           MOVE 'JVMSERVER   '              TO RESOURCE-TYPE.
           MOVE SJS-JVMSERVER-NAME          TO RESOURCE-ID.
      *    Group installed from
           MOVE SJS-JVMSERVER-DEFINE-SOURCE TO RESOURCE-DEFINE-SOURCE.
      *    Install agent
           EVALUATE TRUE
               WHEN SJS-CSDAPI-INSTALL
                   MOVE 'CSDAPI      '      TO RESOURCE-INSTALL-AGENT
               WHEN SJS-CREATE-INSTALL
                   MOVE 'CREATESPI   '      TO RESOURCE-INSTALL-AGENT
               WHEN SJS-GRPLIST-INSTALL
                   MOVE 'GRPLIST     '      TO RESOURCE-INSTALL-AGENT
               WHEN SJS-BUNDLE-INSTALL
                   MOVE 'BUNDLE      '      TO RESOURCE-INSTALL-AGENT
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
      *    Install/Create date and time
           CALL 'DFH$STXK' USING SJS-JVMSERVER-INSTALL-TIME,
                                 CLOCK-OUTPUT-DATE,
                                 CLOCK-OUTPUT-TIME.
           MOVE CLOCK-OUTPUT-DATE   TO RESOURCE-INSTALL-DATE.
           MOVE CLOCK-OUTPUT-HHMMSS TO RESOURCE-INSTALL-TIME.
      *    Install userid
           MOVE SJS-JVMSERVER-INSTALL-USERID
                                    TO RESOURCE-INSTALL-USERID.

           PERFORM PRINT-RESOURCE-DEFINITION.

       JVMSERVER-DEFINITIONS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  NODEJSAPP Definitions                                    ***
      *****************************************************************
       NODEJSAPP-DEFINITIONS SECTION.

           ADD 1 TO NODEJSAPP-RECORDS.
           SET ADDRESS OF DFHSJNDS TO STATISTICS-RECORD-PTR.

           MOVE 'NODEJSAPP   '              TO RESOURCE-TYPE.
           MOVE SJN-NODEJSAPP-NAME          TO RESOURCE-ID.
      *    Group installed from
           MOVE SJN-NODEJSAPP-DEFINE-SOURCE TO RESOURCE-DEFINE-SOURCE.
      *    Install agent
           EVALUATE TRUE
               WHEN SJN-BUNDLE-INSTALL
                   MOVE 'BUNDLE      '      TO RESOURCE-INSTALL-AGENT
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
      *    Install/Create date and time
           CALL 'DFH$STXK' USING SJN-NODEJSAPP-INSTALL-TIME,
                                 CLOCK-OUTPUT-DATE,
                                 CLOCK-OUTPUT-TIME.
           MOVE CLOCK-OUTPUT-DATE   TO RESOURCE-INSTALL-DATE.
           MOVE CLOCK-OUTPUT-HHMMSS TO RESOURCE-INSTALL-TIME.
      *    Install userid
           MOVE SJN-NODEJSAPP-INSTALL-USERID
                                    TO RESOURCE-INSTALL-USERID.

           PERFORM PRINT-RESOURCE-DEFINITION.

       NODEJSAPP-DEFINITIONS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  LIBRARY Definitions                                      ***
      *****************************************************************
       LIBRARY-DEFINITIONS SECTION.

           ADD 1 TO LIBRARY-RECORDS.
           SET ADDRESS OF DFHLDBDS TO STATISTICS-RECORD-PTR.

      *****************************************************************

           MOVE 'LIBRARY     '             TO RESOURCE-TYPE.
           MOVE LDB-LIBRARY-NAME           TO RESOURCE-ID.
      *    Group installed from
           MOVE LDB-LIBRARY-DEFINE-SOURCE  TO RESOURCE-DEFINE-SOURCE.
      *    Install agent
           EVALUATE TRUE
               WHEN LDB-LIBRARY-CSDAPI-INSTALL
                   MOVE 'CSDAPI      '     TO RESOURCE-INSTALL-AGENT
               WHEN LDB-LIBRARY-CREATE-INSTALL
                   MOVE 'CREATESPI   '     TO RESOURCE-INSTALL-AGENT
               WHEN LDB-LIBRARY-GRPLIST-INSTALL
                   MOVE 'GRPLIST     '     TO RESOURCE-INSTALL-AGENT
               WHEN LDB-LIBRARY-SYSTEM-INSTALL
                   MOVE 'SYSTEM      '     TO RESOURCE-INSTALL-AGENT
               WHEN LDB-LIBRARY-BUNDLE-INSTALL
                   MOVE 'BUNDLE      '     TO RESOURCE-INSTALL-AGENT
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
      *    Install/Create date and time
           CALL 'DFH$STXK' USING LDB-LIBRARY-INSTALL-TIME,
                                 CLOCK-OUTPUT-DATE,
                                 CLOCK-OUTPUT-TIME.
           MOVE CLOCK-OUTPUT-DATE   TO RESOURCE-INSTALL-DATE.
           MOVE CLOCK-OUTPUT-HHMMSS TO RESOURCE-INSTALL-TIME.
      *    Install userid
           MOVE LDB-LIBRARY-INSTALL-USERID TO RESOURCE-INSTALL-USERID.

           PERFORM PRINT-RESOURCE-DEFINITION.

       LIBRARY-DEFINITIONS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  MQCONN Definitions                                       ***
      *****************************************************************
       MQCONN-DEFINITIONS SECTION.

           ADD 1 TO MQCONN-RECORDS.
           SET ADDRESS OF DFHMQGDS TO STATISTICS-RECORD-PTR.

      *****************************************************************

           MOVE 'MQCONN      '             TO RESOURCE-TYPE.
           MOVE MQG-MQCONN-NAME            TO RESOURCE-ID.
      *    Group installed from
           MOVE MQG-MQCONN-DEFINE-SOURCE   TO RESOURCE-DEFINE-SOURCE.
      *    Install agent
           EVALUATE TRUE
               WHEN MQG-MQCONN-CSDAPI-INSTALL
                   MOVE 'CSDAPI      '     TO RESOURCE-INSTALL-AGENT
               WHEN MQG-MQCONN-CREATE-INSTALL
                   MOVE 'CREATESPI   '     TO RESOURCE-INSTALL-AGENT
               WHEN MQG-MQCONN-GRPLIST-INSTALL
                   MOVE 'GRPLIST     '     TO RESOURCE-INSTALL-AGENT
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
      *    Install/Create date and time
           CALL 'DFH$STXK' USING MQG-MQCONN-INSTALL-TIME,
                                 CLOCK-OUTPUT-DATE,
                                 CLOCK-OUTPUT-TIME.
           MOVE CLOCK-OUTPUT-DATE   TO RESOURCE-INSTALL-DATE.
           MOVE CLOCK-OUTPUT-HHMMSS TO RESOURCE-INSTALL-TIME.
      *    Install userid
           MOVE MQG-MQCONN-INSTALL-USERID TO RESOURCE-INSTALL-USERID.

           PERFORM PRINT-RESOURCE-DEFINITION.

       MQCONN-DEFINITIONS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  MQMONITOR Definitions                                    ***
      *****************************************************************
       MQMONITOR-DEFINITIONS SECTION.

           ADD 1 TO MQMONITOR-RECORDS.
           SET ADDRESS OF DFHMQRDS TO STATISTICS-RECORD-PTR.

      *****************************************************************

           MOVE 'MQMONITOR   '             TO RESOURCE-TYPE.
           MOVE MQR-NAME                   TO RESOURCE-ID.
      *    Group installed from
           MOVE MQR-MQMON-DEFINE-SOURCE   TO RESOURCE-DEFINE-SOURCE.
      *    Install agent
           EVALUATE TRUE
               WHEN MQR-MQMON-CSDAPI-INSTALL
                   MOVE 'CSDAPI      '     TO RESOURCE-INSTALL-AGENT
               WHEN MQR-MQMON-CREATE-INSTALL
                   MOVE 'CREATESPI   '     TO RESOURCE-INSTALL-AGENT
               WHEN MQR-MQMON-GRPLIST-INSTALL
                   MOVE 'GRPLIST     '     TO RESOURCE-INSTALL-AGENT
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
      *    Install/Create date and time
           CALL 'DFH$STXK' USING MQR-MQMON-INSTALL-TIME,
                                 CLOCK-OUTPUT-DATE,
                                 CLOCK-OUTPUT-TIME.
           MOVE CLOCK-OUTPUT-DATE   TO RESOURCE-INSTALL-DATE.
           MOVE CLOCK-OUTPUT-HHMMSS TO RESOURCE-INSTALL-TIME.
      *    Install userid
           MOVE MQR-MQMON-INSTALL-USERID TO RESOURCE-INSTALL-USERID.

           PERFORM PRINT-RESOURCE-DEFINITION.

       MQMONITOR-DEFINITIONS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  PIPELINE Definitions                                     ***
      *****************************************************************
       PIPELINE-DEFINITIONS SECTION.

           ADD 1 TO PIPELINE-RECORDS.
           SET ADDRESS OF DFHPIRDS TO STATISTICS-RECORD-PTR.

      *****************************************************************

           MOVE 'PIPELINE    '             TO RESOURCE-TYPE.
           MOVE PIR-PIPELINE-NAME          TO RESOURCE-ID.
      *    Group installed from
           MOVE PIR-PIPELINE-DEFINE-SOURCE TO RESOURCE-DEFINE-SOURCE.
      *    Install agent
           EVALUATE TRUE
               WHEN PIR-CSDAPI-INSTALL
                   MOVE 'CSDAPI      '     TO RESOURCE-INSTALL-AGENT
               WHEN PIR-CREATE-INSTALL
                   MOVE 'CREATESPI   '     TO RESOURCE-INSTALL-AGENT
               WHEN PIR-GRPLIST-INSTALL
                   MOVE 'GRPLIST     '     TO RESOURCE-INSTALL-AGENT
      *    @R70934A
               WHEN PIR-BUNDLE-INSTALL
                   MOVE 'BUNDLE      '     TO RESOURCE-INSTALL-AGENT
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
      *    Install/Create date and time
           CALL 'DFH$STXK' USING PIR-PIPELINE-INSTALL-TIME,
                                 CLOCK-OUTPUT-DATE,
                                 CLOCK-OUTPUT-TIME.
           MOVE CLOCK-OUTPUT-DATE   TO RESOURCE-INSTALL-DATE.
           MOVE CLOCK-OUTPUT-HHMMSS TO RESOURCE-INSTALL-TIME.
      *    Install userid
           MOVE PIR-PIPELINE-INSTALL-USERID TO RESOURCE-INSTALL-USERID.

           PERFORM PRINT-RESOURCE-DEFINITION.

       PIPELINE-DEFINITIONS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  PROGRAM Definitions                                      ***
      *****************************************************************
       PROGRAM-DEFINITIONS SECTION.

           ADD 1 TO PROGRAMDEF-RECORDS.
           SET ADDRESS OF DFHPGDDS TO STATISTICS-RECORD-PTR.

      *****************************************************************

           MOVE 'PROGRAM     '             TO RESOURCE-TYPE.
           MOVE PGD-PROGRAM-NAME           TO RESOURCE-ID.
      *    Group installed from
           MOVE PGD-PROGRAM-DEFINE-SOURCE  TO RESOURCE-DEFINE-SOURCE.
      *    Install agent
           EVALUATE TRUE
               WHEN PGD-CSDAPI-INSTALL
                   MOVE 'CSDAPI      '     TO RESOURCE-INSTALL-AGENT
               WHEN PGD-CREATE-INSTALL
                   MOVE 'CREATESPI   '     TO RESOURCE-INSTALL-AGENT
               WHEN PGD-GRPLIST-INSTALL
                   MOVE 'GRPLIST     '     TO RESOURCE-INSTALL-AGENT
               WHEN PGD-AUTOINSTALL-INSTALL
                   MOVE 'AUTOINSTALL '     TO RESOURCE-INSTALL-AGENT
               WHEN PGD-SYSTEM-INSTALL
                   MOVE 'SYSTEM      '     TO RESOURCE-INSTALL-AGENT
               WHEN PGD-DYNAMIC-INSTALL
                   MOVE 'DYNAMIC     '     TO RESOURCE-INSTALL-AGENT
               WHEN PGD-BUNDLE-INSTALL
                   MOVE 'BUNDLE      '     TO RESOURCE-INSTALL-AGENT
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
      *    Install/Create date and time
           CALL 'DFH$STXK' USING PGD-PROGRAM-INSTALL-TIME,
                                 CLOCK-OUTPUT-DATE,
                                 CLOCK-OUTPUT-TIME.
           MOVE CLOCK-OUTPUT-DATE   TO RESOURCE-INSTALL-DATE.
           MOVE CLOCK-OUTPUT-HHMMSS TO RESOURCE-INSTALL-TIME.
      *    Install userid
           MOVE PGD-PROGRAM-INSTALL-USERID TO RESOURCE-INSTALL-USERID.

           PERFORM PRINT-RESOURCE-DEFINITION.

       PROGRAM-DEFINITIONS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  TCPIPSERVICE Definitions                                 ***
      *****************************************************************
       TCPIPSERVICE-DEFINITIONS SECTION.

           ADD 1 TO TCPIPSERVICE-RECORDS.
           SET ADDRESS OF DFHSORDS TO STATISTICS-RECORD-PTR.

      *****************************************************************

           MOVE 'TCPIPSERVICE'             TO RESOURCE-TYPE.
           MOVE SOR-SERVICE-NAME           TO RESOURCE-ID.
      *    Group installed from
           MOVE SOR-DEFINE-SOURCE          TO RESOURCE-DEFINE-SOURCE.
      *    Install agent
           EVALUATE TRUE
               WHEN SOR-CSDAPI-INSTALL
                   MOVE 'CSDAPI      '     TO RESOURCE-INSTALL-AGENT
               WHEN SOR-CREATE-INSTALL
                   MOVE 'CREATESPI   '     TO RESOURCE-INSTALL-AGENT
               WHEN SOR-GRPLIST-INSTALL
                   MOVE 'GRPLIST     '     TO RESOURCE-INSTALL-AGENT
               WHEN SOR-BUNDLE-INSTALL
                   MOVE 'BUNDLE      '     TO RESOURCE-INSTALL-AGENT
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
      *    Install/Create date and time
           CALL 'DFH$STXK' USING SOR-INSTALL-TIME,
                                 CLOCK-OUTPUT-DATE,
                                 CLOCK-OUTPUT-TIME.
           MOVE CLOCK-OUTPUT-DATE   TO RESOURCE-INSTALL-DATE.
           MOVE CLOCK-OUTPUT-HHMMSS TO RESOURCE-INSTALL-TIME.
      *    Install userid
           MOVE SOR-INSTALL-USERID  TO RESOURCE-INSTALL-USERID.

           PERFORM PRINT-RESOURCE-DEFINITION.

       TCPIPSERVICE-DEFINITIONS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  TDQUEUE Definitions                                      ***
      *****************************************************************
       TDQUEUE-DEFINITIONS SECTION.

           ADD 1 TO TDQUEUE-RECORDS.
           SET ADDRESS OF DFHTQRDS TO STATISTICS-RECORD-PTR.

      *****************************************************************

           MOVE 'TDQUEUE     '             TO RESOURCE-TYPE.
           MOVE TQRQID                     TO RESOURCE-ID.
      *    Group installed from
           MOVE TQR-DEFINE-SOURCE          TO RESOURCE-DEFINE-SOURCE.
      *    Install agent
           EVALUATE TRUE
               WHEN TQR-CSDAPI-INSTALL
                   MOVE 'CSDAPI      '     TO RESOURCE-INSTALL-AGENT
               WHEN TQR-CREATE-INSTALL
                   MOVE 'CREATESPI   '     TO RESOURCE-INSTALL-AGENT
               WHEN TQR-GRPLIST-INSTALL
                   MOVE 'GRPLIST     '     TO RESOURCE-INSTALL-AGENT
               WHEN TQR-SYSTEM-INSTALL
                   MOVE 'SYSTEM      '     TO RESOURCE-INSTALL-AGENT
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
      *    Install/Create date and time
           CALL 'DFH$STXK' USING TQR-INSTALL-TIME,
                                 CLOCK-OUTPUT-DATE,
                                 CLOCK-OUTPUT-TIME.
           MOVE CLOCK-OUTPUT-DATE   TO RESOURCE-INSTALL-DATE.
           MOVE CLOCK-OUTPUT-HHMMSS TO RESOURCE-INSTALL-TIME.
      *    Install userid
           MOVE TQR-INSTALL-USERID  TO RESOURCE-INSTALL-USERID.

           PERFORM PRINT-RESOURCE-DEFINITION.

       TDQUEUE-DEFINITIONS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  TRANCLASS Definitions                                    ***
      *****************************************************************
       TRANCLASS-DEFINITIONS SECTION.

           ADD 1 TO TRANCLASS-RECORDS.
           SET ADDRESS OF DFHXMCDS TO STATISTICS-RECORD-PTR.

      *****************************************************************

           MOVE 'TRANCLASS   '             TO RESOURCE-TYPE.
           MOVE XMCTCL                     TO RESOURCE-ID.
      *    Group installed from
           MOVE XMC-TCLASS-DEFINE-SOURCE   TO RESOURCE-DEFINE-SOURCE.
      *    Install agent
           EVALUATE TRUE
               WHEN XMC-CSDAPI-INSTALL
                   MOVE 'CSDAPI      '     TO RESOURCE-INSTALL-AGENT
               WHEN XMC-CREATE-INSTALL
                   MOVE 'CREATESPI   '     TO RESOURCE-INSTALL-AGENT
               WHEN XMC-GRPLIST-INSTALL
                   MOVE 'GRPLIST     '     TO RESOURCE-INSTALL-AGENT
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
      *    Install/Create date and time
           CALL 'DFH$STXK' USING XMC-TCLASS-INSTALL-TIME,
                                 CLOCK-OUTPUT-DATE,
                                 CLOCK-OUTPUT-TIME.
           MOVE CLOCK-OUTPUT-DATE   TO RESOURCE-INSTALL-DATE.
           MOVE CLOCK-OUTPUT-HHMMSS TO RESOURCE-INSTALL-TIME.
      *    Install userid
           MOVE XMC-TCLASS-INSTALL-USERID TO RESOURCE-INSTALL-USERID.

           PERFORM PRINT-RESOURCE-DEFINITION.

       TRANCLASS-DEFINITIONS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  TRANSACTION Definitions                                  ***
      *****************************************************************
       TRANSACTION-DEFINITIONS SECTION.

           ADD 1 TO TRANSACTION-RECORDS.
           SET ADDRESS OF DFHXMRDS TO STATISTICS-RECORD-PTR.

      *****************************************************************

           MOVE 'TRANSACTION '             TO RESOURCE-TYPE.
           MOVE XMRTI                      TO RESOURCE-ID.
      *    Group installed from
           MOVE XMR-TRAN-DEFINE-SOURCE     TO RESOURCE-DEFINE-SOURCE.
      *    Install agent
           EVALUATE TRUE
               WHEN XMR-CSDAPI-INSTALL
                   MOVE 'CSDAPI      '     TO RESOURCE-INSTALL-AGENT
               WHEN XMR-CREATE-INSTALL
                   MOVE 'CREATESPI   '     TO RESOURCE-INSTALL-AGENT
               WHEN XMR-GRPLIST-INSTALL
                   MOVE 'GRPLIST     '     TO RESOURCE-INSTALL-AGENT
               WHEN XMR-SYSTEM-INSTALL
                   MOVE 'SYSTEM      '     TO RESOURCE-INSTALL-AGENT
               WHEN XMR-BUNDLE-INSTALL
                   MOVE 'BUNDLE      '     TO RESOURCE-INSTALL-AGENT
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
      *    Install/Create date and time
           CALL 'DFH$STXK' USING XMR-TRAN-INSTALL-TIME,
                                 CLOCK-OUTPUT-DATE,
                                 CLOCK-OUTPUT-TIME.
           MOVE CLOCK-OUTPUT-DATE   TO RESOURCE-INSTALL-DATE.
           MOVE CLOCK-OUTPUT-HHMMSS TO RESOURCE-INSTALL-TIME.
      *    Install userid
           MOVE XMR-TRAN-INSTALL-USERID    TO RESOURCE-INSTALL-USERID.

           PERFORM PRINT-RESOURCE-DEFINITION.

       TRANSACTION-DEFINITIONS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  URIMAP Definitions                                       ***
      *****************************************************************
       URIMAP-DEFINITIONS SECTION.

           ADD 1 TO URIMAP-RECORDS.
           SET ADDRESS OF DFHWBRDS TO STATISTICS-RECORD-PTR.

      *****************************************************************

           MOVE 'URIMAP      '             TO RESOURCE-TYPE.
           MOVE WBR-URIMAP-NAME            TO RESOURCE-ID.
      *    Group installed from
           MOVE WBR-URIMAP-DEFINE-SOURCE   TO RESOURCE-DEFINE-SOURCE.
      *    Install agent
           EVALUATE TRUE
               WHEN WBR-CSDAPI-INSTALL
                   MOVE 'CSDAPI      '     TO RESOURCE-INSTALL-AGENT
               WHEN WBR-CREATE-INSTALL
                   MOVE 'CREATESPI   '     TO RESOURCE-INSTALL-AGENT
               WHEN WBR-GRPLIST-INSTALL
                   MOVE 'GRPLIST     '     TO RESOURCE-INSTALL-AGENT
               WHEN WBR-DYNAMIC-INSTALL
                   MOVE 'DYNAMIC     '     TO RESOURCE-INSTALL-AGENT
               WHEN WBR-BUNDLE-INSTALL
                   MOVE 'BUNDLE      '     TO RESOURCE-INSTALL-AGENT
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
      *    Install/Create date and time
           CALL 'DFH$STXK' USING WBR-URIMAP-INSTALL-TIME,
                                 CLOCK-OUTPUT-DATE,
                                 CLOCK-OUTPUT-TIME.
           MOVE CLOCK-OUTPUT-DATE   TO RESOURCE-INSTALL-DATE.
           MOVE CLOCK-OUTPUT-HHMMSS TO RESOURCE-INSTALL-TIME.
      *    Install userid
           MOVE WBR-URIMAP-INSTALL-USERID TO RESOURCE-INSTALL-USERID.

           PERFORM PRINT-RESOURCE-DEFINITION.

       URIMAP-DEFINITIONS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  WEBSERVICE Definitions                                   ***
      *****************************************************************
       WEBSERVICE-DEFINITIONS SECTION.

           ADD 1 TO WEBSERVICE-RECORDS.
           SET ADDRESS OF DFHPIWDS TO STATISTICS-RECORD-PTR.

      *****************************************************************

           MOVE 'WEBSERVICE  '             TO RESOURCE-TYPE.
           MOVE PIW-WEBSERVICE-NAME        TO RESOURCE-ID.
      *    Group installed from
           MOVE PIW-WEBSERVICE-DEFINE-SOURCE TO RESOURCE-DEFINE-SOURCE.
      *    Install agent
           EVALUATE TRUE
               WHEN PIW-CSDAPI-INSTALL
                   MOVE 'CSDAPI      '     TO RESOURCE-INSTALL-AGENT
               WHEN PIW-CREATE-INSTALL
                   MOVE 'CREATESPI   '     TO RESOURCE-INSTALL-AGENT
               WHEN PIW-GRPLIST-INSTALL
                   MOVE 'GRPLIST     '     TO RESOURCE-INSTALL-AGENT
               WHEN PIW-DYNAMIC-INSTALL
                   MOVE 'DYNAMIC     '     TO RESOURCE-INSTALL-AGENT
               WHEN PIW-BUNDLE-INSTALL
                   MOVE 'BUNDLE      '     TO RESOURCE-INSTALL-AGENT
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
      *    Install/Create date and time
           CALL 'DFH$STXK' USING PIW-WEBSERVICE-INSTALL-TIME,
                                 CLOCK-OUTPUT-DATE,
                                 CLOCK-OUTPUT-TIME.
           MOVE CLOCK-OUTPUT-DATE   TO RESOURCE-INSTALL-DATE.
           MOVE CLOCK-OUTPUT-HHMMSS TO RESOURCE-INSTALL-TIME.
      *    Install userid
           MOVE PIW-WEBSERVICE-INSTALL-USERID TO
                                       RESOURCE-INSTALL-USERID.

           PERFORM PRINT-RESOURCE-DEFINITION.

       WEBSERVICE-DEFINITIONS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  XMLTRANSFORM Definitions                                 ***
      *****************************************************************
       XMLTRANSFORM-DEFINITIONS SECTION.

           ADD 1 TO XMLTRANSFORM-RECORDS.
           SET ADDRESS OF DFHMLRDS TO STATISTICS-RECORD-PTR.

      *****************************************************************

           MOVE 'XMLTRANSFORM'             TO RESOURCE-TYPE.
           MOVE MLR-XMLTRANSFORM-NAME      TO RESOURCE-ID.
      *    Group installed from
           MOVE MLR-XMLTRNFM-DEFINE-SOURCE TO RESOURCE-DEFINE-SOURCE.
      *    Install agent
           EVALUATE TRUE
               WHEN MLR-BUNDLE-INSTALL
                   MOVE 'BUNDLE      '     TO RESOURCE-INSTALL-AGENT
               WHEN MLR-DYNAMIC-INSTALL
                   MOVE 'DYNAMIC     '     TO RESOURCE-INSTALL-AGENT
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
      *    Install/Create date and time
           CALL 'DFH$STXK' USING MLR-XMLTRNFM-INSTALL-TIME,
                                 CLOCK-OUTPUT-DATE,
                                 CLOCK-OUTPUT-TIME.
           MOVE CLOCK-OUTPUT-DATE   TO RESOURCE-INSTALL-DATE.
           MOVE CLOCK-OUTPUT-HHMMSS TO RESOURCE-INSTALL-TIME.
      *    Install userid
           MOVE MLR-XMLTRNFM-INSTALL-USERID
                                    TO RESOURCE-INSTALL-USERID.

           PERFORM PRINT-RESOURCE-DEFINITION.

       XMLTRANSFORM-DEFINITIONS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** PROCESS-APPLID-END-CALL                                   ***
      ***                                                           ***
      *****************************************************************
       PROCESS-APPLID-END-CALL SECTION.

           PERFORM APPLID-REPORT-TOTALS.

       PROCESS-APPLID-END-CALL-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** PROCESS-FINAL-CALL                                        ***
      ***     Print the report complete message and close the files.***
      *****************************************************************
       PROCESS-FINAL-CALL SECTION.

           MOVE 99 TO LINE-COUNT.

           MOVE 3 TO WF-SPACING.
           MOVE TOTAL-STATISTICS-RECORD-MSG TO PP1-50.
           MOVE TOTAL-STATS-RECORDS         TO PP53-62.
           PERFORM WRITE-A-LINE.

           MOVE 3 TO WF-SPACING.
           MOVE REPORT-COMPLETION-MSG TO PP1-50.
           PERFORM WRITE-A-LINE.

           PERFORM CLOSE-FILES.

       PROCESS-FINAL-CALL-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** PRINT-RESOURCE-DEFINTION                                  ***
      ***     Print a Resource Defintion.                           ***
      *****************************************************************
       PRINT-RESOURCE-DEFINITION SECTION.

           ADD 1 TO RESOURCE-DEFS-TOTAL.

      *    Statistics collection date - mmddyyyy
           MOVE SMF-COLLECTION-DATE  TO PP1-10.
           INSPECT PP1-10 REPLACING ALL ' ' BY '/'.

      *    Statistics collection time - hhmmss
           MOVE SMF-COLLECTION-TIME  TO PP12-19.
           INSPECT PP12-19 REPLACING ALL ' ' BY ':'.

      *    Statistics type --  USS/EOD/REQ/INT/RRT
           MOVE SMF-STATISTICS-TYPE  TO PP22-24.

      *    Applid
           MOVE CURRENT-APPLID       TO PP26-33.

      *    Resource Description
           MOVE RESOURCE-DESCRIPTION TO PP35-132.

           PERFORM WRITE-A-LINE.

           MOVE SPACES TO RESOURCE-DESCRIPTION.

       PRINT-RESOURCE-DEFINITION-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** Write a print line to the statistics report.              ***
      *****************************************************************
       WRITE-A-LINE SECTION.

           IF LINE-COUNT > LINES-PER-PAGE THEN
               PERFORM PAGE-HEADINGS
           END-IF.

           WRITE PRINT-RECORD FROM PRINT-LINE
               AFTER ADVANCING WF-SPACING
           END-WRITE.

           EVALUATE WF-SPACING
               WHEN 0
                   ADD 0 TO LINE-COUNT
               WHEN 1
                   ADD 1 TO LINE-COUNT
               WHEN 2
                   ADD 2 TO LINE-COUNT
               WHEN 3
                   ADD 3 TO LINE-COUNT
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.

           MOVE SPACES TO PRINT-LINE.
           MOVE 1      TO WF-SPACING.

       WRITE-A-LINE-EXIT.
           EXIT.

      *****************************************************************
      ***  Print the STXDRPT page headings.                         ***
      *****************************************************************
       PAGE-HEADINGS SECTION.

           ADD  1 TO PAGE-NUMBER.

           MOVE RELEASE-NO  TO HEADING-RELEASE-NO.
           MOVE REPORT-DATE TO HEADING-REPORT-DATE.
           MOVE REPORT-TIME TO HEADING-REPORT-TIME.
           MOVE PAGE-NUMBER TO HEADING-PAGENO.

           WRITE PRINT-RECORD FROM PRINT-HEADING-LINE
               AFTER ADVANCING PAGE
           END-WRITE.

           WRITE PRINT-RECORD FROM PRINT-HEADING-UNDERLINE
               AFTER ADVANCING 0
           END-WRITE.

           MOVE 1 TO LINE-COUNT.

           PERFORM PAGE-SUB-HEADINGS.

       PAGE-HEADINGS-EXIT.
           EXIT.

      *****************************************************************
      ***  Print the STXDRPT page sub-headings.                     ***
      *****************************************************************
       PAGE-SUB-HEADINGS SECTION.
=
           WRITE PRINT-RECORD FROM PRINT-SUB-HEADING-LINE1
               AFTER ADVANCING 2
           END-WRITE.

           WRITE PRINT-RECORD FROM PRINT-SUB-HEADING-LINE2
               AFTER ADVANCING 1
           END-WRITE.

           WRITE PRINT-RECORD FROM PRINT-SUB-HEADING-UNDERLINE
               AFTER ADVANCING 0
           END-WRITE.

           ADD  3 TO LINE-COUNT.
           MOVE 2 TO WF-SPACING.

       PAGE-SUB-HEADINGS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***                                                           ***
      *****************************************************************
       INITIALISE-RECORD-TOTALS SECTION.

           MOVE 0 TO STATISTICS-RECORD-TOTAL
                     RESOURCE-DEFS-TOTAL.

           MOVE 0 TO ATOMSERVICE-RECORDS BUNDLE-RECORDS
                     CONNECTION-RECORDS
                     DB2CONN-RECORDS DB2ENTRY-RECORDS
                     DOCTEMPLATE-RECORDS EPADAPTER-RECORDS
                     EVENTBINDING-RECORDS FILE-RECORDS
                     IPCONN-RECORDS JVMSERVER-RECORDS
                     LIBRARY-RECORDS MQCONN-RECORDS
                     MQMONITOR-RECORDS NODEJSAPP-RECORDS
                     PIPELINE-RECORDS
                     PROGRAMDEF-RECORDS
                     TCPIPSERVICE-RECORDS TDQUEUE-RECORDS
                     TRANCLASS-RECORDS TRANSACTION-RECORDS
                     URIMAP-RECORDS WEBSERVICE-RECORDS
                     XMLTRANSFORM-RECORDS.

       INITIALISE-RECORD-TOTALS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  Print the event report totals for an applid.             ***
      *****************************************************************
       APPLID-REPORT-TOTALS SECTION.

      ***-----------------------------------------------------------***
      ***   If we have reported some resources for this applid then ***
      *** skip to a new page to print the totals. If we have not    ***
      *** reported any events then we have already printed the page ***
      *** headings at the start of this applid's processing.        ***
      ***-----------------------------------------------------------***

           MOVE 99 TO LINE-COUNT.

           MOVE 3 TO WF-SPACING.
           MOVE 'Statistics Resource Definition Report Totals for Applid
      -         ': ' TO PP35-91.
           MOVE CURRENT-APPLID TO PP92-99.
           PERFORM WRITE-A-LINE.

           MOVE 0       TO WF-SPACING.
           MOVE ALL '_' TO PP35-91.
           MOVE ALL '_' TO PP92-99.
           PERFORM WRITE-A-LINE.

           MOVE 3 TO WF-SPACING.
           MOVE STATISTICS-RECORD-MSG   TO PP1-50.
           MOVE STATISTICS-RECORD-TOTAL TO PP53-62.
           PERFORM WRITE-A-LINE.

           MOVE 3 TO WF-SPACING.
           MOVE RESOURCE-DEFINITIONS-MSG TO PP1-50.
           MOVE RESOURCE-DEFS-TOTAL      TO PP53-62.
           PERFORM WRITE-A-LINE.

       APPLID-REPORT-TOTALS-EXIT.
           EXIT.
           EJECT
      * ************************************************************** *
      *                                                                *
      *                 O P E N  T H E  F I L E S                      *
      *                                                                *
      *   This subroutine opens the output file to be used by this     *
      *   program. A open failure will result in the setting of        *
      *   the appropriate file status flag and consequently the        *
      *   'RETURN-CODE' special register will be set to identify       *
      *   the error.                                                   *
      *                                                                *
      * ************************************************************** *

       OPEN-FILES SECTION.

           OPEN OUTPUT DATA-OUT.
           IF ERROR-ON-OUTPUT-FILE THEN
               MOVE OUTPUT-FILE-OPEN-ERROR TO RETURN-CODE
           ELSE
               MOVE FILE-OPEN TO OUTPUT-FILE-FLAG
           END-IF.

           MOVE 0 TO TOTAL-STATS-RECORDS.

       OPEN-FILES-EXIT.
           EXIT.
           EJECT
      * ************************************************************** *
      *                                                                *
      *           C L O S E  T H E  F I L E S                          *
      *                                                                *
      *   This subroutine closes the output file used by this          *
      *   program. A close failure will result in the setting of       *
      *   the appropriate file status flag and consequently the        *
      *   'RETURN-CODE' special register will be set to identify       *
      *   the error.                                                   *
      *                                                                *
      * ************************************************************** *

       CLOSE-FILES SECTION.

           CLOSE DATA-OUT.
           IF ERROR-ON-OUTPUT-FILE THEN
               MOVE OUTPUT-FILE-CLOSE-ERROR TO RETURN-CODE
           END-IF.

       CLOSE-FILES-EXIT.
           EXIT.
           EJECT
      * ************************************************************** *
      *                                                                *
      *          C H E C K  R E T U R N  C O D E                       *
      *                                                                *
      *  If return code is non zero and any file is still open then    *
      *  we must close that file to avoid any C03 abends.              *
      *  Any errors on a CLOSE are ignored as we already have a error. *
      * ************************************************************** *
       CHECK-RETURN-CODE SECTION.

           IF RETURN-CODE IS NOT ZERO AND OUTPUT-FILE-OPEN THEN
               CLOSE  DATA-OUT
           END-IF.

       CHECK-RETURN-CODE-EXIT.
           EXIT.
