***********************************************************************
*                                                                     *
*   MODULE NAME = DFH$AGCB                                            *
*                                                                     *
* DESCRIPTIVE NAME = CICS/MVS ....                                    *
**       SAMPLE CICS XRF OVERSEER ROUTINE TO SET UP                   *
**       RPL ACB AND OTHER PARAMETER LISTS                            *
*                                                                     *
* TRANSACTION NAME = Cxxx                                             *
**       THIS IS NOT A CICS TRANSACTION                               *
*                                                                     *
*                                                                     *
*                                                                     *
*        Licensed Materials - Property of IBM                         *
*                                                                     *
*        "Restricted Materials of IBM"                                *
*                                                                     *
*        5655-Y04                                                     *
*                                                                     *
*        (C) Copyright IBM Corp. 1987, 2014"                          *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
* STATUS = 7.2.0                                                      *
*                                                                     *
* FUNCTION =                                                          *
**       DFH$AGCB CSECT                                               *
**                                                                    *
**       DFHGCBNA IS THE ENTRY POINT.                                 *
**       CONTROL IS PASSED TO THIS ROUTINE TO OBTAIN                  *
**       STORAGE FOR THE RPL/ACB FOR THE DFHOSD DATA SET, AND ALSO    *
**       THE SNAP DCB AND VARIOUS ASSOCIATED PARAMETER LISTS. THESE   *
**       CONTROL BLOCKS AND PARAMETER LISTS ARE COPIED INTO THE       *
**       GETMAINED AREA AND POINTERS SET UP IN THE RABDS CONTOL BLOCK *
**       BEFORE CONTROL IS RETURNED TO THE CALLER (DFH$AXRO).         *
**       THE VSAM EODAD EXIT IS ALSO INCLUDED IN THIS MODULE.         *
**       THE ABOVE SCHEME ALLOWS THE OVERSEER SAMPLE TO BE READ ONLY. *
**       THE CODE IS CALLED ONCE ONLY DURING THE OVERSEER SAMPLE      *
**       INITIALIZATION.                                              *
* NOTES :                                                             *
*    DEPENDENCIES = S/370                                             *
**         MVS/XA ONLY                                                *
*    RESTRICTIONS =                                                   *
**         NONE                                                       *
*    REGISTER CONVENTIONS =                                           *
**         SEE THE MODULE REGISTER EQUATES FOR A FULL DESCRIPTION     *
**         R9 IS USED (PASSED) TO ADDRESS THE RABDS CONTOL BLOCK      *
**         R12 IS USED AS A BASE REGISTER                             *
*    MODULE TYPE = EXECUTABLE                                         *
*    PROCESSOR = Assembler                                            *
*    ATTRIBUTES = READ ONLY                                           *
*                                                                     *
*------------------------------------------------------------------ *
*                                                                     *
* ENTRY POINT = DFHGCBNA                                              *
*                                                                     *
*     PURPOSE =                                                       *
**        THIS IS THE ONLY ENTRY POINT                                *
*                                                                     *
*     LINKAGE =                                                       *
**        CALLED FROM DFH$AXRO INITIALAIZATION                        *
*     INPUT =                                                         *
**        R9 ADDRESSES THE RABDS                                      *
*        NO SPECIFIC PARAMETERS PASSED                                *
*                                                                     *
*     OUTPUT =                                                        *
**        R15 IS SET TO A RETURN CODE DEPENDING ON THE OPERATION      *
**        OF THIS ROUTINE                                             *
*                                                                     *
*     EXIT-NORMAL =                                                   *
**        RETURN TO CALLER (DFH$AXRO) WITH R15 SET TO ZERO            *
*                                                                     *
*     EXIT-ERROR =                                                    *
**        RETURN TO CALLER (DFH$AXRO) WITH R15 SET TO RETURN CODE     *
*                                                                     *
*------------------------------------------------------------------ *
*                                                                     *
* EXTERNAL REFERENCES =                                               *
**        NONE                                                        *
*                                                                     *
*     ROUTINES =                                                      *
**        NONE                                                        *
*                                                                     *
*     DATA AREAS =                                                    *
**        NO SYSTEM DATA AREAS ALTERED                                *
*                                                                     *
*     CONTROL BLOCKS =                                                *
*                                                                     *
*        THE FOLLOWING SYSTEM MACROS/PARAMETER LIST ARE USED TO       *
*        SET UP THE NECESSARY CONTROL BLOCKS.                         *
*        APART FROM INITIALIZING SOME RPL AND ACB FIELDS, NO OTHER    *
*        CONTROL BLOCKS ARE USED HERE. EACH IS USED FROM THE MAIN     *
*        ROUTINE DFH$AXRO                                             *
*                                                                     *
**        ACB     ACB FOR DFHOSD                                      *
**        RPL     RPL FOR DFHOSD                                      *
**        DCB     SNAP DCB FOR SNAPOUT DD                             *
**        SNAP    SNAP PARAMETER LIST                                 *
**        EXTRACT MVS MODIFY PARAMETER LIST                           *
**        STIMERM TIMER PARAMETER LIST                                *
**        RABDS  POINTERS SET UP (INTERNAL CONTROL BLOCK SEE DFH$XRDS)*
*                                                                     *
*     GLOBAL VARIABLES =                                              *
**        NONE                                                        *
*                                                                     *
* TABLES =                                                            *
**       NONE                                                         *
*                                                                     *
* MACROS =                                                            *
**        NONE                                                        *
*                                                                     *
*------------------------------------------------------------------ *
*                                                                     *
* DESCRIPTION                                                         *
**        This section should identify the major code elements in     *
**        the module, and describe their purpose. If there is a bug   *
**        in part of the module, this section should enable you to    *
**        quickly find the piece of code responsible for the          *
**        erroneous function.                                         *
**        Major code elements are identified by:                      *
**          a) The names and functions of all internal procedures     *
**          b) Each major block of in line code should be 'labeled'   *
**             with a 'label' in the block comment preceding that    *
**             code element.                                          *
**       DFH$AGCB CSECT                                               *
**                                                                    *
**       DFHGCBNA IS THE ENTRY POINT. THIS CODE IS COPIED AS PART     *
**       OF DFH$AXRO. CONTROL IS PASSED TO THIS ROUTINE TO OBTAIN     *
**       STORAGE FOR THE RPL/ACB FOR THE DFHOSD DATA SET, AND ALSO    *
**       THE SNAP DCB AND VARIOUS ASSOCIATED PARAMETER LISTS. THESE   *
**       CONTROL BLOCKS AND PARAMETER LISTS ARE COPIED INTO THE       *
**       GETMAINED AREA AND POINTERS SET UP IN THE RABDS CONTOL BLOCK *
**       BEFORE CONTROL IS RETURNED TO THE CALLER (DFH$AXRO).         *
**       THE VSAM EODAD EXIT IS ALSO INCLUDED IN THIS MODULE.         *
**       THE ABOVE SCHEME ALLOWS THE OVERSEER SAMPLE TO BE READ ONLY. *
**       THE CODE IS CALLED ONCE ONLY DURING THE OVERSEER SAMPLE      *
**       INITIALIZATION.                                              *
*                                                                     *
*------------------------------------------------------------------ *
*                                                                     *
* CHANGE ACTIVITY :                                                   *
**                                                                    *
*        $MOD(DFH$AGCB),COMP(XRF),PROD(CICS TS ):                     *
*                                                                     *
*     PN= REASON REL YYMMDD HDXIII : REMARKS                          *
*    $L0= Base   210 90     HDZZUNK: Base                             *
*    $P1= M51001 310 8708   HD1HSS : SEPARATE OVERSEER CODE           *
*                                                                     *
***********************************************************************
*        COPY DSECTS
         COPY DFH$XRDS                  CICS/XRF OVERSEER SAMPLE
         IFGRPL AM=VSAM                 VSAM RPL
         IFGACB AM=VSAM                 VSAM ACB
R0       EQU   0
R1       EQU   1
R2       EQU   2                   WORK REG
R3       EQU   3                   WORK REG
R4       EQU   4                   WORK REG
R5       EQU   5                   WORK REG
R6       EQU   6                   WORK REG
R7       EQU   7
OSDBAR   EQU   7                   BASE REG FOR OSD ENTRY
GENBAR   EQU   8                   BASE REG FOR GEN ENTRY
RABBAR   EQU   9                   BASE REG FOR RAB
R10      EQU   10                  CODE BASE REG1 FOR MAIN ROUTINE
R11      EQU   11                  CODE BASE REG2 FOR MAIN ROUTINE
R12      EQU   12                  BASE REG FOR THIS (SUB) ROUTINE
R13      EQU   13
R14      EQU   14
R15      EQU   15
DFH$AGCB CSECT
***********************************************************************
*
*        CREATE THE RPL ACB ETC FOR THE DFHOSD DATA SET
*
*        THIS CODE WILL GETMAIN STORAGE FROM MVS AND COPY IN THE
*
*        ACB RPL AND BUFFER FOR THE OSD DATA SET
*        SNAP DCB AND PARMLIST
*        MODIFY PARMLIST
*        TIMER  PARMLIST
*
*        THE PARMLISTS AND RPL AND ACB ETC ARE SET UP READY FOR
*        THE MAIN CODE IN DFH$AXRO TO OPEN ETC
*
***********************************************************************
         ENTRY DFHGCBNA
         DFHVM $AGCB,ENTRY=DFHGCBNA
DFHGCBNA DS    0H
         SAVE  (14,12)
         BALR  R12,0
         USING *,R12
         USING RABDS,RABBAR     ADDRESS THE RAB
         LA    R1,RABSAV2          ADDRESS SAVE AREA FOR THIS CODE
         ST    R1,8(,R13)          FWD PTR
         ST    R13,4(,R1)          BKWD POINTER
         LR    R13,R1              POINT AT OUR SAVE AREA
*
*        NOW MOVE THE CONTROL BLOCKS TO A GETMAINED AREA
*        OF LENGTH CBLEN
*
         GETMAIN RC,LV=CBLEN
         LTR   R15,R15
         BNZ   CBGMFL
*        GETMAIN ADDR IN R1
*        NOW SET UP FOR MOVE LONG
         LR    R2,R1               TO ADDR
         LA    R4,CBSTOR           FROM ADDR
         LA    R5,CBLEN(0,0)       FROM LENGTH
         LR    R3,R5               TO LENGTH
         MVCL  R2,R4               MOVE THE CONTROL BLOCKS
*
         LA    R3,SNAPOUT-CBSTOR(0,R1)  ADDR THE SNAP DCB (IN GETM STG)
         ST    R3,RABSDCBA         SAVE IT
         OI    RABSDCBA,X'80'      SHOW ITS THE ONLY ONE IN THE LIST
*
         LA    R3,SNAPLST-CBSTOR(0,R1)  ADDR THE SNAP PARMLIST
         ST    R3,RABSNAPA         SAVE IT
*
         LA    R3,MODLST-CBSTOR(0,R1)  ADDR THE MODIFY PARMLIST
         ST    R3,RABMODA          SAVE IT
*
         LA    R3,TIMERLST-CBSTOR(0,R1)  ADDR THE TIMER PARMLIST
         ST    R3,RABSTIMA          SAVE IT
*
         LA    R3,INPAREA-CBSTOR(0,R1)  ADDR THE DFHOSD INPUT AREA
         ST    R3,RABINPA          SAVE IT
*
         LA    R3,OSDACB-CBSTOR(0,R1)  ADDR THE DFHOSD ACB
         ST    R3,RABACBA          SAVE IT
         OI    RABACBA,X'80'       SHOW ITS THE ONLY ONE IN THE LIST
*
         USING IFGACB,R3
         LA    R2,EXLST1           POINT AT THE EXIT LIST
         ST    R2,ACBEXLST         SET UP THE ADDRESS IN THE ACB
         DROP  R3
         LR    R2,R3               REMEMBER ACB ADDR
*
         LA    R3,OSDRPL-CBSTOR(0,R1)  ADDR THE DFHOSD RPL
         ST    R3,RABRPLA          SAVE IT
         USING IFGRPL,R3
         ST    R2,RPLDACB          SET UP THE ACB POINTER
         L     R2,RABINPA
         ST    R2,RPLAREA          SET UP THE BUFFER POINTER
         LA    R2,GNKEY            POINT AT THE GEN NAME KEY
         ST    R2,RPLARG           SET UP THE ARGUMENT KEY
         DROP  R3
CBGRET   DS    0H                  RETURN TO THE MAIN CODE
         SR    R15,R15             SET A GOOD RETURN CODE
CBGMFL   DS    0H                  R15 SET IF WE BRANCH HERE
         L     R13,RABSAV2+4       RESTORE CALLERS SAVE AREA
         RETURN (14,12),RC=(15)
GNKEY    DC    CL4'GN  '           GENERIC NAME KEY FOR DFHOSD RECORD
EXLST1   EXLST EODAD=EOD
******************************
*
*         EODAD EXIT
*
******************************
EOD      DS    0H
         OI    RABSTAT1,RABEOD     INDICATE EOD
         BR    R14
*
*
*
CBSTOR   DS    0F
********************************************************************
*
*        CONTROL BLOCKS FOR  DFH$AXRO. THESE ARE COPIED TO A
*        GETMAINED AREA AND THE RELEVANT ADDRESSES KEPT IN THE RAB
*
********************************************************************
*
************************
*
*        SNAP DCB
*
************************
SNAPOUT  DCB   DSORG=PS,                                               X
               RECFM=VBA,                                              X
               MACRF=(W),                                              X
               BLKSIZE=882,                                            X
               LRECL=125,                                              X
               DDNAME=SNAPOUT
SNAPDCBL EQU   *-SNAPOUT
*
************************
*
*        SNAP PARMLIST
*
************************
SNAPLST  SNAP PDATA=ALL,MF=L
SNAPLEN  EQU   *-SNAPLST           LENGTH OF LIST
*
************************
*
*   MVS MODIFY PARMLIST
*
************************
MODLST   EXTRACT ,,FIELDS=COMM,MF=L
MODLEN   EQU   *-MODLST            LENGTH OF LIST
************************
*
*   TIMER PARMLIST
*
************************
TIMERLST STIMERM SET,MF=L          REMOTE TIMER LIST
TIMELEN  EQU   *-TIMERLST          LENGTH OF LIST
*
************************
*
*      ACB FOR DFHOSD DATA SET
*
************************
OSDACB   ACB   AM=VSAM,                                                *
               BUFND=1,                                                *
               BUFNI=2,                                                *
               DDNAME=DFHOSD,                                          *
               MACRF=(KEY,DIR,SEQ),                                    *
               EXLST=EXLST1
ACBLEN   EQU   *-OSDACB            LENGTH OF THE ACB
*
************************
*
*      RPL FOR DFHOSD DATA SET
*
************************
OSDRPL   RPL   AREALEN=80,                                             *
               OPTCD=(KEY,SEQ,SYN,NUP,GEN),                            *
               KEYLEN=4,                                               *
               ARG=GNKEY
RRLEN    EQU   *-OSDRPL            LENGTH OF THIS RPL
*
INPAREA  DS    CL80                INPUT AREA FOR DFHOSD DATA
*
CBLEN    EQU   *-CBSTOR            LENGTH OF STORAGE
         END
