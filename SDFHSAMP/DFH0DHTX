CBL CICS('COBOL3') APOST
      *****************************************************************
      *
      *  MODULE NAME = DFH0DHTX
      *
      *  DESCRIPTIVE NAME = CICS TS  Sample EXITPGM Template
      *
      *
      *
      *       Licensed Materials - Property of IBM
      *
      *       "Restricted Materials of IBM"
      *
      *       5655-Y04
      *
      *       (C) Copyright IBM Corp. 1998, 2016"
      *
      *
      *
      *
      *  STATUS = 7.2.0
      *
      *  FUNCTION =
      *    This module produces a sample template when a DOCTEMPLATE
      *    of type EXITPGM is defined with an exit program of DFH0DHTX.
      *
      *  NOTES :
      *   DEPENDENCIES = S/390
      *   RESTRICTIONS = None
      *   MODULE TYPE = Executable
      *   PROCESSOR = COBOL
      *   ATTRIBUTES = Read only, Serially Reusable
      *
      ******************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    DFH0DHTX.
       DATE-WRITTEN.  January 1999.
       DATE-COMPILED. January 1999.
      ******************************************************************
      ****                                                          ****
      ****        E X I T P G M ( D F H 0 D H T X )                 ****
      ****                                                          ****
      ****                                                          ****
      ****    This program demonstrates the use of the CICS API     ****
      ****    in a DOCTEMPLATE EXITPGM to create a variable string  ****
      ****    that can be inserted in a CICS document as a template ****
      ****                                                          ****
      ******************************************************************

       ENVIRONMENT DIVISION.

       DATA DIVISION.

       WORKING-STORAGE SECTION.

        77 ABSTIME-TOKEN      PIC X(8).
        77 STRING-SIZE        PIC S9(8) COMP SYNCHRONIZED.

        01 THIS-TEMPLATE-STRING.
           02 FILLER  PIC X(02) VALUE X'0D25'.
           02 FILLER  PIC X(29) VALUE '<p>This page was produced at '.
           02 DOCTIME PIC X(08).
           02 FILLER  PIC X(04) VALUE ' on '.
           02 DOCDATE PIC X(10).
           02 FILLER  PIC X(16) VALUE ' by CICS system '.
           02 DOCAPPL PIC X(08).
           02 FILLER  PIC X(03) VALUE X'4B0D25'.

       LINKAGE SECTION.
           COPY DFHDHTXO.

        01 DHTX-TEMPLATE-RETURN-AREA.
           02 TEMPLATE-STRING PIC X OCCURS 1 TO 256
                                    DEPENDING ON STRING-SIZE.

       PROCEDURE DIVISION.

      * Format the time and date and according to local conventions
           EXEC CICS ASKTIME ABSTIME(ABSTIME-TOKEN) END-EXEC.
           EXEC CICS FORMATTIME
                     ABSTIME (ABSTIME-TOKEN)
                     FULLDATE(DOCDATE) DATESEP
                     TIME    (DOCTIME) TIMESEP
           END-EXEC.

      * Fill in the current CICS applid
           EXEC CICS ASSIGN APPLID(DOCAPPL) END-EXEC.

      * Set response into CICS-provided buffer
           SET ADDRESS OF DHTX-PLIST TO ADDRESS OF DFHCOMMAREA.

           SET ADDRESS OF DHTX-TEMPLATE-RETURN-AREA
                       TO DHTX-BUFFER-PTR.

           MOVE LENGTH OF THIS-TEMPLATE-STRING TO DHTX-TEMPLATE-LEN.
           MOVE LENGTH OF THIS-TEMPLATE-STRING TO STRING-SIZE.
           IF DHTX-BUFFER-LEN IS LESS THAN STRING-SIZE THEN
              MOVE DHTX-BUFFER-LEN TO STRING-SIZE.

           MOVE THIS-TEMPLATE-STRING TO DHTX-TEMPLATE-RETURN-AREA.
           MOVE ZERO TO DHTX-RETURN-CODE.

           EXEC CICS RETURN END-EXEC.
