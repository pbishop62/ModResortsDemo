CBL CICS('COBOL3,SP') APOST
PROCESS MAP,XREF,TRUNC(BIN)
      *****************************************************************
      *                                                               *
      * MODULE NAME = DFH0STEJ                                        *
      *                                                               *
      * DESCRIPTIVE NAME = Collect and Print Statistics Sample        *
      *                                                               *
      *                                                               *
      *                                                               *
      *     Licensed Materials - Property of IBM                      *
      *                                                               *
      *     "Restricted Materials of IBM"                             *
      *                                                               *
      *     5655-Y04                                                  *
      *                                                               *
      *     (C) Copyright IBM Corp. 2001, 2018"                       *
      *                                                               *
      *                                                               *
      *                                                               *
      *                                                               *
      * STATUS = 7.2.0                                                *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      * DESCRIPTION :                                                 *
      *                                                               *
      *     This set of programs illustrate ways of using the EXEC    *
      *     CICS INQUIRE, COLLECT and EXTRACT STATISTICS commands of  *
      *     CICS to produce information about a CICS system.          *
      *     The reports include a CICS and MVS storage analysis.      *
      *     Statistics obtained using these commands can be used to   *
      *     aid the specification of the DSA Size parameters.         *
      *     (See the CICS Performance Guide and CICS Data Areas       *
      *      manuals for more information, and the CICS System        *
      *      Programming Reference for use of commands)               *
      *                                                               *
      *     These programs can be invoked from the PLT at PLTSD or    *
      *     as a CICS transaction (conversationally).                 *
      *     The output is sent via the CICS JES SPOOL interface for   *
      *     which a number of default parameters can be changed by    *
      *     the user to specify the distribution of the report(s).    *
      *     These defaults are defined in the working-storage         *
      *     section of the DFH0STAT program under the 01 level        *
      *     "OUTPUT-DEFAULTS".                                        *
      *                                                               *
      *     Installation instructions:-                               *
      *     Require SIT parameters:-  SPOOL=YES                       *
      *     Required CICS definitions:-                               *
      *         Transactions:-  STAT -> DFH0STAT                      *
      *         Programs:-    DFH0STAT   IBM COBOL                    *
      *                       DFH0STDB       for OS/390 & VM          *
      *                       DFH0STEJ           2.1.1 or later       *
      *                       DFH0STEP       "   "   "                *
      *                       DFH0STGN       "   "   "                *
      *                       DFH0STLK       "   "   "                *
      *                       DFH0STPR       "   "   "                *
      *                       DFH0STSA       "   "   "                *
      *                       DFH0STSY       "   "   "                *
      *                       DFH0STTP       "   "   "                *
      *                       DFH0STTS       "   "   "                *
      *                       DFH0STWB       "   "   "                *
      *                       DFH$STCN   Assembler                    *
      *                       DFH$STTB   Assembler                    *
      *         Mapsets:-     DFH0STM                                 *
      *                       DFH0STS                                 *
      *                                                               *
      *     The CICS-supplied RDO group, DFH$STAT, contains all       *
      *     the necessary resource definitions for this sample.       *
      *                                                               *
      *     All programs are command level and run above the          *
      *     16Mb line. Note: that DFH0STAT, DFH0STDB, DFH0STEJ,       *
      *     DFH0STEP, DFH0STGN, DFH0STLK, DFH0STPR, DFH0STSA,         *
      *     DFH0STSY, DFH0STTP, DFH0STTS, and DFH0STWB must be        *
      *     translated with the Translator options "COBOL3 and SP".   *
      *                                                               *
      *   SPOOL errors.  If an EXEC CICS SPOOL .. command fails       *
      *   when the program is run as a transaction, an error message  *
      *   is displayed on the users screen and the transaction will   *
      *   continue.  If the program is not being run from a terminal  *
      *   then a message will be sent to the console using EXEC       *
      *   CICS WRITE OPERATOR commands and the transaction will be    *
      *   terminated normally.                                        *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      * CHANGE ACTIVITY :                                             *
      *  $SEG(DFH0STEJ),COMP(SAMPLES),PROD(CICS):                     *
      *                                                               *
      *  PN= REASON REL YYMMDD HDXIII  : REMARKS                      *
      * $D0= I07099 620 010207 HD1VCJB : Module Creation              *
      * $D1= I07404 630 030724 HD4PALS : Add REUSEST to INQUIREs      *
      * $D2= I07610 650 050506 HD1VCJB : Clock/Timestamp Formatting   *
      * $D3= I07921 660 080708 HDIKTG  : Tidy up JVM profiles         *
      * $L1= 819    620 010323 HD1VCJB : EJ Bean Statistics           *
      * $L2= 813    620 010411 HD4PALS : Lid 813 SPI changes for corb *
      * $L3= 819    620 010614 HD1VCJB : 32 character DJAR name       *
      * $L4= 819    620 010619 HD4PALS : Remove non-2.2 RDO items     *
      * $L5= 813    620 010815 HD1VCJB : EJB Updates                  *
      * $L6= 832    630 020219 HD1VCJB : Support for Jvmprofiles      *
      * $L7= 832    630 020423 HD1VCJB : Statistics Sample Program Up *
      * $L8= 832    630 020802 HD4PALS : Provision for 64-bit numbers *
      * $L9= 813    630 020806 HD2JPEH : Reinstate Asserted Identity  *
      * $LA= 813    630 020830 HD4PALS : Outbound privacy on corbaserv*
      * $LB= 832    630 020905 HD1VCJB : Initial Support for JVM Prog *
      * $LC= 832    630 020910 HD4PALS : JVM Program stats            *
      * $LD= 852    640 040524 HD1VCJB : Statistics Updates for 3.1   *
      * $LE= 892    650 050915 HDIKTG  : Removal of Resettable Mode of*
      * $LF= 923    660 080515 HD1VCJB : Atomservice Statistics Suppor*
      * $LG= 927    660 080604 HD1VCJB : Event Processing Support     *
      * $LH= 937    660 080724 HD1VCJB : JVMSERVER Statistics Support *
      * $LI= 830    660 080909 HD1VCJB : Statistics Support for IPV6  *
      * $LJ= R00528 670 090902 HD4PALT : JVMPROFILEDIR SPI on JVMPOOL *
      * $LK= R00279 670 100309 HDIRSX  : Add JVM server to JVM Program*
      * $LL= R12678 670 100630 HDHBIMS : Support 1-255 lsrpools       *
      * $LM= R01188 670 101007 HDJCIDH : JVMSERVER stats              *
      * $LN= R01188 670 101101 HDJCIDH : Change GCMODEL to GCPOLICY   *
      * $LO= R00114 670 110106 HD1VCJB : 64-bit Storage Manager Statis*
      * $LM= R01188 670 100902 HDJCIDH : JVMSERVER STATS              *
      *      R38170 680 111205 HD1VCJB : Remove EJB Statistics        *
      *      R38170 680 111220 HD1VCJB : Remove JVM Statistics        *
      *      R38042 690 130404 HDAFDRB : Use EXTRACT STATISTICS       *
      *      R38042 690 130422 HDIDNCS : Use EXTRACT STATISTICS       *
      * $P1= D02818 620 010919 HD1VCJB : Stats for TCB Attach Failure *
      * $P2= D03309 620 011026 HD1VCJB : Correct DS TCB Mismatch Stat *
      * $P3= D08547 630 030725 HD4PALS : Change report to say JVM pool*
      * $P4= D09060 630 030910 HD4PALS : Fix DFH0STAT JVM reuse status*
      * $P5= D09099 630 030923 HD4PALS : Change descript of jvm heap s*
      * $P6= D09215 630 030930 HD4PALS : Include mismatch in report   *
      * $P7= D09223 630 031008 HD4PALS : Change wording of heap stoage*
      * $P8= D09460 630 031022 HD4PALS : corbaserver enablestatus wron*
      * $P9= D15023 650 060118 HD1VCJB : JVM Report Descriptions      *
      * $PA= D16105 650 060814 HDIKTG  : TASKIDERR in DFH0STAT.       *
      * $PB= D16089 650 060815 HDIKTG  : JVM Garbage Collection stats *
      * $PC= D20276 660 080115 HD1VCJB : Correct Class cache reports  *
      * $PD= D17194 670 100719 HD2GJST : LSRpoolid > 8 for files      *
      * $PE= D22049 670 101123 HDJCIDH : Format large byte fields     *
      * $PF= D23970 670 110310 HDJCIDH : Fix JVM created in 1900      *
      * $PG= D27819 670 110318 HDJCIDH : JVMSERVER Stats formatting   *
      *      R48697 680 120329 HD1VCJB: JVMPOOL/JVMPROFILE Removal    *
      *      D50318 680 120508 HDHCPMC : JVMProfile removal           *
      *      D53912 680 120621 HDHCPMC : Width of Times Used field    *
      *      D58782 680 120919 HCHCPMC : Suppress DFHSJTHHP           *
      *      D98593 700 150106 HD3BADW : Fix comments                 *
      *     R148775 720 180718 HDLHJJH : New NODEJSAPP statistics     *
      *     R148770 720 180918 HDLHJJH : CPU heap in NODEJSAPP stats  *
      *     R151691 720 180926 HDLHJJH : Invoke requests in NODEJSAPP *
      *     D152592 720 181022 HDHCPMC : LOG for NODEJSAPP            *
      *     D150786 720 181023 HDFVGMB : Add TRUNC(BIN) to PROCESS    *
      *                                                               *
      *****************************************************************
       ID DIVISION.
       PROGRAM-ID. DFH0STEJ.

       ENVIRONMENT DIVISION.
           EJECT
       DATA DIVISION.
       WORKING-STORAGE SECTION.
       77  STCN-COMMAREA               PIC S9(4)  COMP.
       77  INQUIRE-RESP                PIC S9(8)  COMP VALUE +0.

       77  BYTE-DATA                   PIC S9(18) COMP.
       77  RESULT                      PIC 9(8)   COMP VALUE 0.
       77  BYTE-DATA-SIZE              PIC S9(8)  COMP VALUE +0.
       77  BYTE-DATA-UNITS             PIC X           VALUE 'M'.

       77  JVMPROGRAM-NAME             PIC X(8)        VALUE SPACES.
       77  JVMPROGRAM-JVMSERVER        PIC X(8)        VALUE SPACES.
       77  JVMPROGRAM-EXECKEY          PIC S9(8)  COMP VALUE +0.
       77  JVMPROGRAM-RUNTIME          PIC S9(8)  COMP VALUE +0.
       77  JVMPROGRAM-JVMCLASS         PIC X(255)      VALUE SPACES.

       77  JVMSERVER-NAME              PIC X(8)        VALUE SPACES.
       77  JVMSERVER-ENABLE-STATUS     PIC S9(8)  COMP VALUE +0.

      *****************************************************************
      ***  Total fields                                             ***
      *****************************************************************
       77  JVMPROGRAM-TOTAL            PIC S9(9)  COMP-3 VALUE +0.
       77  JVMSERVER-TOTAL             PIC S9(9)  COMP-3 VALUE +0.
       77  NODEJSAPP-TOTAL             PIC S9(9)  COMP-3 VALUE +0.

       77  NODEJSAPP-NAME              PIC X(32)      VALUE SPACES.
       77  NODEJSAPP-ENABLE-STATUS     PIC S9(8)  COMP VALUE +0.
           EJECT
      *****************************************************************
      ***  Report print line definitions                            ***
      *****************************************************************
       01  PRINT-LINE.
           03  PRINT-CONTROL           PIC X.
           03  PP1-132.
               05  PP1-12.
                   07  PP1-10          PIC X(10).
                   07  FILLER          PIC XX.
               05  FILLER              PIC X(120).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(50).
               05  PP51-64             PIC X(14).
               05  FILLER              PIC X(68).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC XX.
               05  PP3-132.
                   07  PP3-102.
                       09  PP3-72.
                           11  PP3-37  PIC X(35).
                           11  PP38-72 PIC X(35).
                       09  PP73-102    PIC X(30).
                   07  PP103-132       PIC X(30).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(39).
               05  PP40-53.
                   07  PP40-49N        PIC ZZ,ZZZ,ZZ9.
                   07  FILLER          PIC X(4).
               05  FILLER REDEFINES PP40-53.
                   07  PP40-49.
                       09  PP40-47     PIC X(8).
                       09  FILLER      PIC XX.
                   07  FILLER          PIC X(4).
               05  FILLER              PIC X(79).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC XX.
               05  PP3-10              PIC X(8).
               05  FILLER              PIC XX.
               05  PP13-20             PIC X(8).
               05  FILLER              PIC X.
               05  PP22-31             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X.
               05  PP33-36             PIC X(4).
               05  FILLER              PIC XX.
               05  PP39-123            PIC X(85).
               05  FILLER              PIC X(9).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(39).
               05  PP40-71.
                   07  PP40-52         PIC Z,ZZZ,ZZZ,ZZ9.
                   07  PP53            PIC X.
                   07  FILLER          PIC X(18).
               05  FILLER              PIC X(61).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC XX.
               05  PP3-34.
                   07  FILLER          PIC X(31).
                   07  PP34            PIC X.
               05  FILLER              PIC XX.
               05  PP37-121.
                   07  PP37-68         PIC X(32).
                   07  FILLER          PIC X(53).
               05  FILLER REDEFINES PP37-121.
                   07  PP37-44         PIC X(8).
                   07  FILLER          PIC X(77).
               05  FILLER REDEFINES PP37-121.
                   07  PP37-46         PIC ZZ,ZZZ,ZZ9.
                   07  FILLER          PIC X(75).
               05  FILLER REDEFINES PP37-121.
                   07  PP37-49         PIC Z,ZZZ,ZZZ,ZZ9.
                   07  PP50            PIC X.
                   07  FILLER          PIC X(70).
               05  FILLER REDEFINES PP37-121.
                   07  PP37-46L.
                       09  PP37-44L    PIC X(8).
                       09  FILLER      PIC X(2).
                   07  FILLER          PIC X(2).
                   07  PP49-63         PIC X(14).
                   07  FILLER          PIC X(58).
               05  FILLER              PIC X(11).
           EJECT
       LINKAGE SECTION.
           EJECT
      *****************************************************************
      ***  Statistics Sample Programs Commarea.                     ***
      *****************************************************************
       01  DFHCOMMAREA.
           COPY DFH0STCM.
           EJECT
      *****************************************************************
      *** Data structures for JVM Program Resource statistics       ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHPGRDS.
           EJECT
      *****************************************************************
      *** Data structures for JVM Domain Resource statistics        ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHSJSDS.
           EJECT
      *****************************************************************
      *** Data structures for NODEJSAPP Resource statistics         ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHSJNDS.
           EJECT
      *****************************************************************
      *** Data structures for Storage Manager DSA                   ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHSMSDS.
           EJECT
      *****************************************************************
      ***                                                           ***
      ***           CICS Collect and print statistics               ***
      ***                                                           ***
      *****************************************************************
       PROCEDURE DIVISION.

      *****************************************************************
      *** MAIN-PROCESSING                                           ***
      ***     Initialise the totals fields                          ***
      ***     Print the statistics reports                          ***
      ***     Return to DFH0STLK                                    ***
      *****************************************************************

       MAIN-PROCESSING SECTION.

           PERFORM TOTALS-INITIALISE.

           PERFORM PRINT-STATISTICS.

           EXEC CICS RETURN
           END-EXEC.

       MAIN-PROCESSING-EXIT.
           GOBACK.
           EJECT
      *****************************************************************
      *****************************************************************
      ***         Collect and Print the CICS Statistics             ***
      *****************************************************************
      *****************************************************************
      ***                                                           ***
      *** 1. Print the statistics in the following order:-          ***
      ***    - JVM Programs                                         ***
      ***    - JVMSERVERs                                           ***
      ***                                                           ***
      *****************************************************************
      *****************************************************************

       PRINT-STATISTICS SECTION.

           IF SELECT-JVM-PROGRAMS = 'Y' OR 'y' THEN
               PERFORM JVM-PROGRAM-STATISTICS
           END-IF.

           IF SELECT-JVMSERVERS = 'Y' OR 'y' THEN
               PERFORM JVMSERVER-STATISTICS
           END-IF.

           IF SELECT-NODEJSAPP = 'Y' OR 'y' THEN
               PERFORM NODEJSAPP-STATISTICS
           END-IF.

       PRINT-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** JVM-PROGRAM-STATISTICS                                    ***
      ***   Print headings and JVM Program statistics.              ***
      *****************************************************************
       JVM-PROGRAM-STATISTICS SECTION.

      *****************************************************************
      ***   Print the JVM Programs statistics heading.              ***
      *****************************************************************
           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO JVM-PROGRAMS-PAGENO.

           MOVE '-' TO PRINT-CONTROL.
           MOVE 'JVM Programs' TO PP1-12.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-12.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      ***   Print the JVM Programs.                                 ***
      *****************************************************************

           MOVE '0' TO PRINT-CONTROL.

           EXEC CICS INQUIRE PROGRAM START
           END-EXEC.

           EXEC CICS INQUIRE PROGRAM(JVMPROGRAM-NAME) NEXT
               EXECKEY(JVMPROGRAM-EXECKEY)
               JVMCLASS(JVMPROGRAM-JVMCLASS)
               JVMSERVER(JVMPROGRAM-JVMSERVER)
               RUNTIME(JVMPROGRAM-RUNTIME)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

               IF LINE-COUNT > LINES-PER-PAGE
                   PERFORM PAGE-HEADINGS
               END-IF

               IF SUB-HEADINGS = 'Y' THEN
                   PERFORM JVM-PROGRAM-SUB-HEADINGS
               END-IF

               IF JVMPROGRAM-NAME NOT = 'DFHSJTHP' THEN

                   IF JVMPROGRAM-RUNTIME = DFHVALUE(JVM) THEN
                       EXEC CICS EXTRACT STATISTICS JVMPROGRAM
                         RESID(JVMPROGRAM-NAME)
                         SET(ADDRESS OF DFHPGRDS)
                         NOHANDLE
                       END-EXEC

                       ADD 1 TO JVMPROGRAM-TOTAL

                       MOVE '0' TO PRINT-CONTROL

                       MOVE JVMPROGRAM-NAME         TO PP3-10
                       MOVE JVMPROGRAM-JVMSERVER    TO PP13-20
                       MOVE PGR-JVMPROGRAM-USECOUNT TO PP22-31

                       EVALUATE JVMPROGRAM-EXECKEY
                           WHEN DFHVALUE(CICSEXECKEY)
                              MOVE 'CICS'         TO PP33-36
                           WHEN DFHVALUE(USEREXECKEY)
                              MOVE 'User'         TO PP33-36
                           WHEN OTHER
                              CONTINUE
                       END-EVALUATE

                       MOVE JVMPROGRAM-JVMCLASS(1:85) TO PP39-123

                       PERFORM WRITE-OUTPUT

                       IF JVMPROGRAM-JVMCLASS(86:85) NOT = SPACES THEN
                           MOVE JVMPROGRAM-JVMCLASS(86:85) TO PP39-123
                           PERFORM WRITE-OUTPUT
                           IF JVMPROGRAM-JVMCLASS(171:85) NOT = SPACES
                           THEN
                               MOVE JVMPROGRAM-JVMCLASS(171:85)
                                    TO PP39-123
                               PERFORM WRITE-OUTPUT
                           END-IF
                       END-IF
                   END-IF
               END-IF

               EXEC CICS INQUIRE PROGRAM(JVMPROGRAM-NAME) NEXT
                   EXECKEY(JVMPROGRAM-EXECKEY)
                   JVMCLASS(JVMPROGRAM-JVMCLASS)
                   JVMSERVER(JVMPROGRAM-JVMSERVER)
                   RUNTIME(JVMPROGRAM-RUNTIME)
                   RESP(INQUIRE-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE PROGRAM END
           END-EXEC.

       JVM-PROGRAM-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** JVM-PROGRAM-SUB-HEADINGS                                  ***
      *****************************************************************
       JVM-PROGRAM-SUB-HEADINGS SECTION.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Program    JVM         Times  EXEC ' TO PP3-37.
           MOVE '  JVM                              ' TO PP38-72.
           PERFORM WRITE-OUTPUT.
           MOVE ' Name     server        Used  Key  ' TO PP3-37.
           MOVE ' Class                             ' TO PP38-72.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP3-37
                           PP38-72
                           PP73-102.
           MOVE '______________                '      TO PP103-132.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.
           MOVE 'N'     TO SUB-HEADINGS.

       JVM-PROGRAM-SUB-HEADINGS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** JVMSERVER-STATISTICS                                      ***
      ***   Print headings and JVMSERVER statistics.                ***
      *****************************************************************
       JVMSERVER-STATISTICS SECTION.

      *****************************************************************
      *** Get the Storage Manager DSA statistics.                   ***
      *****************************************************************
           EXEC CICS EXTRACT STATISTICS STORAGE
               SET(ADDRESS OF DFHSMSDS)
           END-EXEC.

      *****************************************************************
      ***   Print the JVMSERVERs statistics heading.                ***
      *****************************************************************
           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO JVMSERVERS-PAGENO.

           MOVE '-' TO PRINT-CONTROL.
           MOVE 'JVMSERVERs' TO PP1-10.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-10.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      ***   Print the JVMSERVERs.                                   ***
      *****************************************************************

           MOVE '0' TO PRINT-CONTROL.

           EXEC CICS INQUIRE JVMSERVER START
           END-EXEC.

           EXEC CICS INQUIRE JVMSERVER(JVMSERVER-NAME) NEXT
               ENABLESTATUS(JVMSERVER-ENABLE-STATUS)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

               IF LINE-COUNT > (LINES-PER-PAGE - 40) THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               EXEC CICS EXTRACT STATISTICS JVMSERVER
                   RESID(JVMSERVER-NAME)
                   SET(ADDRESS OF DFHSJSDS)
                   NOHANDLE
               END-EXEC

               ADD 1 TO JVMSERVER-TOTAL

               MOVE '0' TO PRINT-CONTROL
               MOVE 'JVMSERVER Name. . . . . . . . . . :' TO PP3-37
               MOVE JVMSERVER-NAME                        TO PP40-47
               PERFORM WRITE-OUTPUT

               MOVE 'JVMSERVER Enable Status . . . . . :' TO PP3-37
               EVALUATE JVMSERVER-ENABLE-STATUS
                   WHEN DFHVALUE(ENABLING)
                       MOVE 'Enabling  ' TO PP40-49
                   WHEN DFHVALUE(ENABLED)
                       MOVE 'Enabled   ' TO PP40-49
                   WHEN DFHVALUE(DISABLING)
                       MOVE 'Disabling ' TO PP40-49
                   WHEN DFHVALUE(DISABLED)
                       MOVE 'Disabled  ' TO PP40-49
                   WHEN DFHVALUE(DISCARDING)
                       MOVE 'Discarding' TO PP40-49
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'JVMSERVER JVM profile name. . . . :' TO PP3-37
               MOVE SJS-JVMSERVER-JVMPROFILE              TO PP40-47
               PERFORM WRITE-OUTPUT

               MOVE 'JVMSERVER LE runtime options. . . :' TO PP3-37
               MOVE SJS-JVMSERVER-LE-RUNOPTS              TO PP40-47
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'JVMSERVER use count . . . . . . . :' TO PP3-37
               MOVE SJS-JVMSERVER-USE-COUNT               TO PP40-49N
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'JVMSERVER thread limit. . . . . . :' TO PP3-37
               MOVE SJS-JVMSERVER-THREAD-LIMIT            TO PP40-49N
               PERFORM WRITE-OUTPUT

               MOVE 'JVMSERVER current threads . . . . :' TO PP3-37
               MOVE SJS-JVMSERVER-THREAD-CURRENT          TO PP40-49N
               PERFORM WRITE-OUTPUT

               MOVE 'JVMSERVER peak threads. . . . . . :' TO PP3-37
               MOVE SJS-JVMSERVER-THREAD-HWM              TO PP40-49N
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'JVMSERVER thread limit waits. . . :' TO PP3-37
               MOVE SJS-JVMSERVER-THREAD-WAITS            TO PP40-49N
               PERFORM WRITE-OUTPUT

               MOVE 'JVMSERVER thread limit wait time. :' TO PP3-37
               MOVE SJS-JVMSERVER-THREAD-WAIT-TIME TO CLOCK-FIELD
               PERFORM STOPWATCH-CONVERT
               MOVE STOPWATCH                      TO PP40-53
               PERFORM WRITE-OUTPUT

               MOVE 'JVMSERVER current thread waits. . :' TO PP3-37
               MOVE SJS-JVMSERVER-THREAD-WAIT-CUR         TO PP40-49N
               PERFORM WRITE-OUTPUT

               MOVE 'JVMSERVER peak thread waits . . . :' TO PP3-37
               MOVE SJS-JVMSERVER-THREAD-WAIT-HWM         TO PP40-49N
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'JVMSERVER system thread use count :' TO PP3-37
               MOVE SJS-JVMSERVER-SYS-USE-COUNT           TO PP40-49N
               PERFORM WRITE-OUTPUT

               MOVE 'JVMSERVER system thread waits . . :' TO PP3-37
               MOVE SJS-JVMSERVER-SYS-WAITED              TO PP40-49N
               PERFORM WRITE-OUTPUT

               MOVE 'JVMSERVER system thread wait time :' TO PP3-37
               MOVE SJS-JVMSERVER-SYS-WAITED-TIME TO CLOCK-FIELD
               PERFORM STOPWATCH-CONVERT
               MOVE STOPWATCH      TO PP40-53
               PERFORM WRITE-OUTPUT

               MOVE 'JVMSERVER current sys thread waits:' TO PP3-37
               MOVE SJS-JVMSERVER-SYS-WAIT-CUR            TO PP40-49N
               PERFORM WRITE-OUTPUT

               MOVE 'JVMSERVER peak system thread waits:' TO PP3-37
               MOVE SJS-JVMSERVER-SYS-WAIT-HWM            TO PP40-49N
               PERFORM WRITE-OUTPUT

               MOVE 'JVMSERVER JVM creation time . . . :' TO PP3-37
               IF SJS-JVMSERVER-JVM-CREATION-LCL = LOW-VALUES THEN
                 MOVE 'N/A'        TO PP40-49
               ELSE
                 MOVE SJS-JVMSERVER-JVM-CREATION-LCL TO CLOCK-FIELD
                 PERFORM TIMESTAMP-CONVERT
                 MOVE MMDDYYYY       TO PP40-49
                 MOVE STOPWATCH      TO PP51-64
               END-IF
               PERFORM WRITE-OUTPUT
      * Heap related stats
               MOVE '0' TO PRINT-CONTROL
               MOVE 'JVMSERVER current heap size . . . :' TO PP3-37
               MOVE SJS-JVMSERVER-CURRENT-HEAP            TO BYTE-DATA
               PERFORM CALCULATE-BYTES
               MOVE BYTE-DATA-SIZE                        TO PP40-52
               MOVE BYTE-DATA-UNITS                       TO PP53
               PERFORM WRITE-OUTPUT

               MOVE 'JVMSERVER initial heap size . . . :' TO PP3-37
               MOVE SJS-JVMSERVER-INITIAL-HEAP            TO BYTE-DATA
               PERFORM CALCULATE-BYTES
               MOVE BYTE-DATA-SIZE                        TO PP40-52
               MOVE BYTE-DATA-UNITS                       TO PP53
               PERFORM WRITE-OUTPUT

               MOVE 'JVMSERVER maximum heap size . . . :' TO PP3-37
               MOVE SJS-JVMSERVER-MAX-HEAP                TO BYTE-DATA
               PERFORM CALCULATE-BYTES
               MOVE BYTE-DATA-SIZE                        TO PP40-52
               MOVE BYTE-DATA-UNITS                       TO PP53
               PERFORM WRITE-OUTPUT

               MOVE 'JVMSERVER peak heap size. . . . . :' TO PP3-37
               MOVE SJS-JVMSERVER-PEAK-HEAP               TO BYTE-DATA
               PERFORM CALCULATE-BYTES
               MOVE BYTE-DATA-SIZE                        TO PP40-52
               MOVE BYTE-DATA-UNITS                       TO PP53
               PERFORM WRITE-OUTPUT

               MOVE 'JVMSERVER heap occupancy. . . . . :' TO PP3-37
               MOVE SJS-JVMSERVER-OCCUPANCY               TO BYTE-DATA
               PERFORM CALCULATE-BYTES
               MOVE BYTE-DATA-SIZE                        TO PP40-52
               MOVE BYTE-DATA-UNITS                       TO PP53
               PERFORM WRITE-OUTPUT

      * GC related stats
               MOVE '0' TO PRINT-CONTROL
               MOVE 'JVMSERVER Garbage Collection (GC) :' TO PP3-37
               if SJS-JVMSERVER-GC-POLICY = SPACES THEN
                 MOVE 'N/A'                               TO PP40-71
               ELSE
                 MOVE SJS-JVMSERVER-GC-POLICY             TO PP40-71
               END-IF
               PERFORM WRITE-OUTPUT

      * Major Garbage collections
               MOVE 'JVMSERVER no. of major GC events. :' TO PP3-37
               MOVE SJS-JVMSERVER-MJR-GC-EVENTS           TO PP40-52
               PERFORM WRITE-OUTPUT

               MOVE 'JVMSERVER elapsed time in major GC:' TO PP3-37
               MOVE SJS-JVMSERVER-MJR-GC-CPU              TO PP40-52
               PERFORM WRITE-OUTPUT

               MOVE 'JVMSERVER heap freed by major GC. :' TO PP3-37
               MOVE SJS-JVMSERVER-MJR-HEAP-FREED          TO BYTE-DATA
               PERFORM CALCULATE-BYTES
               MOVE BYTE-DATA-SIZE                        TO PP40-52
               MOVE BYTE-DATA-UNITS                       TO PP53
               PERFORM WRITE-OUTPUT

      * Minor Garbage collections
               MOVE 'JVMSERVER no. of minor GC events. :' TO PP3-37
               MOVE SJS-JVMSERVER-MNR-GC-EVENTS           TO PP40-52
               PERFORM WRITE-OUTPUT

               MOVE 'JVMSERVER elapsed time in minor GC:' TO PP3-37
               MOVE SJS-JVMSERVER-MNR-GC-CPU              TO PP40-52
               PERFORM WRITE-OUTPUT

               MOVE 'JVMSERVER heap freed by minor GC. :' TO PP3-37
               MOVE SJS-JVMSERVER-MNR-HEAP-FREED          TO BYTE-DATA
               PERFORM CALCULATE-BYTES
               MOVE BYTE-DATA-SIZE                        TO PP40-52
               MOVE BYTE-DATA-UNITS                       TO PP53
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE ALL '-' TO PP3-132
               PERFORM WRITE-OUTPUT

               EXEC CICS INQUIRE JVMSERVER(JVMSERVER-NAME) NEXT
                   ENABLESTATUS(JVMSERVER-ENABLE-STATUS)
                   RESP(INQUIRE-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE JVMSERVER END
           END-EXEC.

       JVMSERVER-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** NODEJSAPP-STATISTICS                                      ***
      ***   Print headings and NODEJSAPP statistics.                ***
      *****************************************************************
       NODEJSAPP-STATISTICS SECTION.

      *****************************************************************
      *** Get the Storage Manager DSA statistics.                   ***
      *****************************************************************
           EXEC CICS EXTRACT STATISTICS STORAGE
               SET(ADDRESS OF DFHSMSDS)
           END-EXEC.

      *****************************************************************
      ***   Print the NODEJSAPPs statistics heading.                ***
      *****************************************************************
           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO NODEJSAPP-PAGENO.

           MOVE '-' TO PRINT-CONTROL.
           MOVE 'NODEJSAPPs' TO PP1-10.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-10.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      ***   Print the NODEJSAPPs.                                   ***
      *****************************************************************

           MOVE '0' TO PRINT-CONTROL.

           EXEC CICS INQUIRE NODEJSAPP START
           END-EXEC.

           EXEC CICS INQUIRE NODEJSAPP(NODEJSAPP-NAME) NEXT
               ENABLESTATUS(NODEJSAPP-ENABLE-STATUS)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

               IF LINE-COUNT > (LINES-PER-PAGE - 40) THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               EXEC CICS EXTRACT STATISTICS NODEJSAPP
                   RESID(NODEJSAPP-NAME)
                   SET(ADDRESS OF DFHSJNDS)
                   NOHANDLE
               END-EXEC

               ADD 1 TO NODEJSAPP-TOTAL

               MOVE 'NODEJSAPP name . . . . . . . . :' TO PP3-34
               MOVE NODEJSAPP-NAME                     TO PP37-68
               PERFORM WRITE-OUTPUT

               MOVE 'NODEJSAPP status . . . . . . . :' TO PP3-34
               EVALUATE NODEJSAPP-ENABLE-STATUS
                   WHEN DFHVALUE(ENABLING)
                       MOVE 'Enabling  ' TO PP37-44
                   WHEN DFHVALUE(ENABLED)
                       MOVE 'Enabled   ' TO PP37-44
                   WHEN DFHVALUE(DISABLING)
                       MOVE 'Disabling ' TO PP37-44
                   WHEN DFHVALUE(DISABLED)
                       MOVE 'Disabled  ' TO PP37-44
                   WHEN DFHVALUE(FAILED)
                       MOVE 'Failed    ' TO PP37-44
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               MOVE 'NODEJSAPP creation time. . . . :' TO PP3-34
               IF SJN-NODEJSAPP-CREATION-LCL = LOW-VALUES THEN
                 MOVE 'N/A'        TO PP37-46L
               ELSE
                 MOVE SJN-NODEJSAPP-CREATION-LCL TO CLOCK-FIELD
                 PERFORM TIMESTAMP-CONVERT
                 MOVE MMDDYYYY       TO PP37-46L
                 MOVE STOPWATCH      TO PP49-63
               END-IF
               PERFORM WRITE-OUTPUT

               MOVE 'NODEJSAPP process ID . . . . . :' TO PP3-34
               MOVE SJN-NODEJSAPP-PID                  TO PP37-46
               PERFORM WRITE-OUTPUT

               MOVE 'NODEJSAPP LE runtime options . :' TO PP3-34
               MOVE SJN-NODEJSAPP-LE-RUNOPTS           TO PP37-44
               PERFORM WRITE-OUTPUT

               MOVE 'NODEJSAPP bundle name. . . . . :' TO PP3-34
               MOVE SJN-NODEJSAPP-BUNDLE-NAME          TO PP37-44
               PERFORM WRITE-OUTPUT

               MOVE 'NODEJSAPP CPU time . . . . . . :' TO PP3-34
               MOVE SJN-NODEJSAPP-CPU                  TO PP37-49
               PERFORM WRITE-OUTPUT

               MOVE 'NODEJSAPP current heap size. . :' TO PP3-34
               MOVE SJN-NODEJSAPP-HEAP-CURRENT         TO BYTE-DATA
               PERFORM CALCULATE-BYTES
               MOVE BYTE-DATA-SIZE                     TO PP37-49
               MOVE BYTE-DATA-UNITS                    TO PP50
               PERFORM WRITE-OUTPUT

               MOVE 'NODEJSAPP heap used by runtime :' TO PP3-34
               MOVE SJN-NODEJSAPP-HEAP-RUNTIME         TO BYTE-DATA
               PERFORM CALCULATE-BYTES
               MOVE BYTE-DATA-SIZE                     TO PP37-49
               MOVE BYTE-DATA-UNITS                    TO PP50
               PERFORM WRITE-OUTPUT

               MOVE 'NODEJSAPP heap used on data. . :' TO PP3-34
               MOVE SJN-NODEJSAPP-HEAP-APP-DATA        TO BYTE-DATA
               PERFORM CALCULATE-BYTES
               MOVE BYTE-DATA-SIZE                     TO PP37-49
               MOVE BYTE-DATA-UNITS                    TO PP50
               PERFORM WRITE-OUTPUT

               MOVE 'NODEJSAPP maximum heap size. . :' TO PP3-34
               MOVE SJN-NODEJSAPP-HEAP-MAX             TO BYTE-DATA
               PERFORM CALCULATE-BYTES
               MOVE BYTE-DATA-SIZE                     TO PP37-49
               MOVE BYTE-DATA-UNITS                    TO PP50
               PERFORM WRITE-OUTPUT

               MOVE 'NODEJSAPP completed invokes. . :' TO PP3-34
               MOVE SJN-NODEJSAPP-INVK                 TO PP37-46
               PERFORM WRITE-OUTPUT

               MOVE 'NODEJSAPP completed invokes err:' TO PP3-34
               MOVE SJN-NODEJSAPP-INVK-ERR             TO PP37-46
               PERFORM WRITE-OUTPUT

               MOVE 'NODEJSAPP invokes in progress. :' TO PP3-34
               MOVE SJN-NODEJSAPP-INVK-CUR             TO PP37-46
               PERFORM WRITE-OUTPUT

               MOVE 'NODEJSAPP peak invokes in prog :' TO PP3-34
               MOVE SJN-NODEJSAPP-INVK-PEAK            TO PP37-46
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'NODEJSAPP profile. . . . . . . :' TO PP3-34
               MOVE SJN-NODEJSAPP-PROFILE(1:85)        TO PP37-121
               PERFORM WRITE-OUTPUT
               IF SJN-NODEJSAPP-PROFILE(86:85) NOT = SPACES THEN
                   MOVE ':' TO PP34
                   MOVE SJN-NODEJSAPP-PROFILE(86:85)   TO PP37-121
                   PERFORM WRITE-OUTPUT
                   IF SJN-NODEJSAPP-PROFILE(171:85) NOT = SPACES THEN
                       MOVE ':' TO PP34
                       MOVE SJN-NODEJSAPP-PROFILE(171:85) TO PP37-121
                       PERFORM WRITE-OUTPUT
                   END-IF
               END-IF

               MOVE 'NODEJSAPP nodehome . . . . . . :' TO PP3-34
               MOVE SJN-NODEJSAPP-NODEHOME(1:85)       TO PP37-121
               PERFORM WRITE-OUTPUT
               IF SJN-NODEJSAPP-NODEHOME(86:85) NOT = SPACES THEN
                   MOVE ':' TO PP34
                   MOVE SJN-NODEJSAPP-NODEHOME(86:85)  TO PP37-121
                   PERFORM WRITE-OUTPUT
                   IF SJN-NODEJSAPP-NODEHOME(171:85) NOT = SPACES THEN
                       MOVE ':' TO PP34
                       MOVE SJN-NODEJSAPP-NODEHOME(171:85) TO PP37-121
                       PERFORM WRITE-OUTPUT
                   END-IF
               END-IF

               MOVE 'NODEJSAPP start script . . . . :' TO PP3-34
               MOVE SJN-NODEJSAPP-START-SCRIPT(1:85)   TO PP37-121
               PERFORM WRITE-OUTPUT
               IF SJN-NODEJSAPP-START-SCRIPT(86:85) NOT = SPACES THEN
                 MOVE ':' TO PP34
                 MOVE SJN-NODEJSAPP-START-SCRIPT(86:85) TO PP37-121
                 PERFORM WRITE-OUTPUT
                 IF SJN-NODEJSAPP-START-SCRIPT(171:85) NOT = SPACES THEN
                    MOVE ':' TO PP34
                    MOVE SJN-NODEJSAPP-START-SCRIPT(171:85) TO PP37-121
                    PERFORM WRITE-OUTPUT
                 END-IF
               END-IF

               MOVE 'NODEJSAPP stdout . . . . . . . :' TO PP3-34
               MOVE SJN-NODEJSAPP-STDOUT(1:85)         TO  PP37-121
               PERFORM WRITE-OUTPUT
               IF SJN-NODEJSAPP-STDOUT(86:85) NOT = SPACES THEN
                   MOVE ':' TO PP34
                   MOVE SJN-NODEJSAPP-STDOUT(86:85)   TO  PP37-121
                   PERFORM WRITE-OUTPUT
                   IF SJN-NODEJSAPP-STDOUT(171:85) NOT = SPACES THEN
                       MOVE ':' TO PP34
                       MOVE SJN-NODEJSAPP-STDOUT(171:85) TO PP37-121
                       PERFORM WRITE-OUTPUT
                   END-IF
               END-IF

               MOVE 'NODEJSAPP stderr . . . . . . . :' TO PP3-34
               MOVE SJN-NODEJSAPP-STDERR(1:85)         TO  PP37-121
               PERFORM WRITE-OUTPUT
               IF SJN-NODEJSAPP-STDERR(86:85) NOT = SPACES THEN
                   MOVE ':' TO PP34
                   MOVE SJN-NODEJSAPP-STDERR(86:85)   TO  PP37-121
                   PERFORM WRITE-OUTPUT
                   IF SJN-NODEJSAPP-STDERR(171:85) NOT = SPACES THEN
                       MOVE ':' TO PP34
                       MOVE SJN-NODEJSAPP-STDERR(171:85) TO PP37-121
                       PERFORM WRITE-OUTPUT
                   END-IF
               END-IF

               MOVE 'NODEJSAPP trace. . . . . . . . :' TO PP3-34
               MOVE SJN-NODEJSAPP-TRACE(1:85)          TO  PP37-121
               PERFORM WRITE-OUTPUT
               IF SJN-NODEJSAPP-TRACE(86:85) NOT = SPACES THEN
                   MOVE ':' TO PP34
                   MOVE SJN-NODEJSAPP-TRACE(86:85)   TO  PP37-121
                   PERFORM WRITE-OUTPUT
                   IF SJN-NODEJSAPP-TRACE(171:85) NOT = SPACES THEN
                       MOVE ':' TO PP34
                       MOVE SJN-NODEJSAPP-TRACE(171:85) TO PP37-121
                       PERFORM WRITE-OUTPUT
                   END-IF
               END-IF

               MOVE 'NODEJSAPP log. . . . . . . . . :' TO PP3-34
               MOVE SJN-NODEJSAPP-LOG(1:85)            TO  PP37-121
               PERFORM WRITE-OUTPUT
               IF SJN-NODEJSAPP-LOG(86:85) NOT = SPACES THEN
                   MOVE ':' TO PP34
                   MOVE SJN-NODEJSAPP-LOG(86:85)   TO  PP37-121
                   PERFORM WRITE-OUTPUT
                   IF SJN-NODEJSAPP-LOG(171:85) NOT = SPACES THEN
                       MOVE ':' TO PP34
                       MOVE SJN-NODEJSAPP-LOG(171:85) TO PP37-121
                       PERFORM WRITE-OUTPUT
                   END-IF
               END-IF

               MOVE '0' TO PRINT-CONTROL
               MOVE ALL '-' TO PP3-132
               PERFORM WRITE-OUTPUT

               EXEC CICS INQUIRE NODEJSAPP(NODEJSAPP-NAME) NEXT
               ENABLESTATUS(NODEJSAPP-ENABLE-STATUS)
               RESP(INQUIRE-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE NODEJSAPP END
           END-EXEC.

       NODEJSAPP-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      * CALCULATE BYTES                                               *
      *                                                               *
      *    FIELD=nnnnnK                                               *
      *    FIELD=nnnnnM
      *    FIELD=nnnnnG                                               *
      *    FIELD=nnnnnT                                               *
      *    FIELD=nnnnnP                                               *
      *                                                               *
      *****************************************************************
       CALCULATE-BYTES SECTION.

      *    if BYTE-DATA > 9999 bytes, convert to Kilobytes
           IF BYTE-DATA > 9999 THEN
               DIVIDE BYTE-DATA BY 1024
                   GIVING BYTE-DATA
               END-DIVIDE
      *        if BYTE-DATA > 9999 Kilobytes, convert
      *                                   to Megabytes
               IF BYTE-DATA > 9999 THEN
                   DIVIDE BYTE-DATA BY 1024
                       GIVING BYTE-DATA
                   END-DIVIDE
      *            if BYTE-DATA > 9999 Megabytes, convert
      *                                       to Gigabytes.
                   IF BYTE-DATA > 9999 THEN
                       DIVIDE BYTE-DATA BY 1024
                           GIVING BYTE-DATA
                       END-DIVIDE
      *                if BYTE-DATA > 9999 Gigabytes, convert
      *                                          to Terabytes.
                       IF BYTE-DATA > 9999 THEN
                           DIVIDE BYTE-DATA BY 1024
                               GIVING BYTE-DATA
                           END-DIVIDE
      *                    if BYTE-DATA > 9999 Terabytes, convert
      *                                              to Petabytes.
                           IF BYTE-DATA > 9999 THEN
                               DIVIDE BYTE-DATA BY 1024
                                   GIVING BYTE-DATA
                               END-DIVIDE
                               MOVE BYTE-DATA TO BYTE-DATA-SIZE
                               MOVE 'P' TO BYTE-DATA-UNITS
      *                    else BYTE-DATA is in Terabytes.
                           ELSE
                               MOVE BYTE-DATA TO BYTE-DATA-SIZE
                               MOVE 'T'         TO BYTE-DATA-UNITS
                           END-IF
      *                else BYTE-DATA is in Gigabytes.
                       ELSE
                           MOVE BYTE-DATA TO BYTE-DATA-SIZE
                           MOVE 'G' TO BYTE-DATA-UNITS
                       END-IF
      *            else BYTE-DATA is in Megabytes.
                   ELSE
                       MOVE BYTE-DATA TO BYTE-DATA-SIZE
                       MOVE 'M'         TO BYTE-DATA-UNITS
                   END-IF
      *        else BYTE-DATA is in Kilbytes
               ELSE
                   MOVE BYTE-DATA TO BYTE-DATA-SIZE
                   MOVE 'K'         TO BYTE-DATA-UNITS
               END-IF
      *    else BYTE-DATA is in bytes
           ELSE
               MOVE BYTE-DATA TO BYTE-DATA-SIZE
               MOVE ' '       TO BYTE-DATA-UNITS
           END-IF.

       CALCULATE-BYTES-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** TOTALS-INITIALISE                                         ***
      *****************************************************************
       TOTALS-INITIALISE SECTION.

           MOVE 0 TO JVMPROGRAM-TOTAL JVMSERVER-TOTAL NODEJSAPP-TOTAL.

       TOTALS-INITIALISE-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** PAGE-HEADINGS                                             ***
      ***   Increment page                                          ***
      ***   Write new heading line to spool                         ***
      *****************************************************************
       PAGE-HEADINGS SECTION.

           ADD 1       TO PAGENO.
           MOVE PAGENO TO PAGE-NUMBER.
           MOVE 1      TO LINE-COUNT.

           MOVE '1'          TO PRINT-CONTROL.
           MOVE HEADING-LINE TO PP1-132.
           PERFORM WRITE-OUTPUT.
           MOVE '+'          TO PRINT-CONTROL.
           MOVE ALL '_'      TO PP1-132.
           PERFORM WRITE-OUTPUT.

           MOVE 'Y' TO SUB-HEADINGS.

       PAGE-HEADINGS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** STOPWATCH-CONVERT                                         ***
      *** Call program DFH$STCN to convert S/370 format TOD to      ***
      ***   EBCDIC date and time                                    ***
      *****************************************************************
       STOPWATCH-CONVERT SECTION.

           MOVE 'S'    TO CLOCK-TYPE.
           MOVE SPACES TO CLOCK-OUTPUT.

           MOVE LENGTH OF CLOCK-FIELDS TO STCN-COMMAREA.

           EXEC CICS LINK PROGRAM('DFH$STCN')
               COMMAREA(CLOCK-FIELDS)
               LENGTH(STCN-COMMAREA)
           END-EXEC.

           MOVE LOW-VALUES TO CLOCK-FIELD.

       STOPWATCH-CONVERT-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** TIMESTAMP-CONVERT                                         ***
      *** Call program DFH$STCN to convert S/370 format TOD to      ***
      ***   EBCDIC date and time                                    ***
      *****************************************************************
       TIMESTAMP-CONVERT SECTION.

           MOVE 'T'    TO CLOCK-TYPE.
           MOVE SPACES TO CLOCK-OUTPUT.

           MOVE LENGTH OF CLOCK-FIELDS TO STCN-COMMAREA.

           EXEC CICS LINK PROGRAM('DFH$STCN')
               COMMAREA(CLOCK-FIELDS)
               LENGTH(STCN-COMMAREA)
           END-EXEC.

           MOVE LOW-VALUES TO CLOCK-FIELD.

       TIMESTAMP-CONVERT-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** WRITE-OUTPUT                                              ***
      ***   Write current print line to report output destination.  ***
      *****************************************************************
       WRITE-OUTPUT SECTION.

           PERFORM WRITE-SPOOL.

       WRITE-OUTPUT-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** WRITE-SPOOL                                               ***
      *** Write current print line to spool                         ***
      *** Increment line-count                                      ***
      *** IF CICS exceptional condition is raised THEN              ***
      ***    call error routine                                     ***
      *****************************************************************
       WRITE-SPOOL SECTION.

           EXEC CICS SPOOLWRITE TOKEN(OUTPUT-TOKEN)
               FROM(PRINT-LINE) FLENGTH(LINE-LENGTH)
               LINE
               NOHANDLE
           END-EXEC.

           EVALUATE PRINT-CONTROL
               WHEN ' '
                   ADD 1 TO LINE-COUNT
               WHEN '0'
                   ADD 2 TO LINE-COUNT
               WHEN '-'
                   ADD 3 TO LINE-COUNT
               WHEN '+'
                   ADD 0 TO LINE-COUNT
               WHEN '1'
                   MOVE 1 TO LINE-COUNT
           END-EVALUATE.

      *                                       Check return code
           IF EIBRESP NOT = DFHRESP(NORMAL) THEN
               MOVE 'SPOOLWRITE' TO CICS-COMMAND
               PERFORM ERROR-ROUTINE
           ELSE
               MOVE ' '    TO PRINT-CONTROL
               MOVE SPACES TO PP1-132
           END-IF.

       WRITE-SPOOL-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  ERROR ROUTINE                                            ***
      ***  Error routine produces appropriate responses to CICS     ***
      ***  spool commands. To ensure this the resp and resp2 values ***
      ***  are returned to the main program DFH0STAT for appropriate***
      ***  outputing of the messages.                               ***
      *****************************************************************
       ERROR-ROUTINE SECTION.

      *  If an error occurs which is not due to the users input or if
      *  the program is not being run from a terminal then a general
      *  message is sent to the screen/console and the RESP and RESP2
      *  fields are given along with the spool command which failed.
           MOVE 'Y'      TO ERROR-FOUND.
           MOVE EIBRESP  TO STCM-RESP.
           MOVE EIBRESP2 TO STCM-RESP2.

      *  Whatever happens ensure we return to DFH0STAT quickly.
           EXEC CICS RETURN
           END-EXEC.

       ERROR-ROUTINE-EXIT.
           EXIT.
