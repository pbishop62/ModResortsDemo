* ASM CICS ( NOPROLOG NOEPILOG)
99KR     TITLE 'DFH99KR - CICS DYNAMIC ALLOCATION SAMPLE PROGRAM (MVS) *
               - CONVERTS A RETURNED VALUE TO A KEYWORD'
***********************************************************************
*                                                                     *
* MODULE NAME = DFH99KR                                               *
*                                                                     *
* DESCRIPTIVE NAME = Dynamic Allocation Sample Program                *
*                    Converts a Returned Value to a Keyword           *
*                                                                     *
*                                                                     *
*                                                                     *
*     Licensed Materials - Property of IBM                            *
*                                                                     *
*     "Restricted Materials of IBM"                                   *
*                                                                     *
*     5655-Y04                                                        *
*                                                                     *
*     (C) Copyright IBM Corp. 1980, 1991"                             *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
* STATUS = 7.2.0                                                      *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
* GIVEN A RETURNED VALUE AND A DESCRIPTION FOR A KEYWORD VALUE,       *
* DETERMINES WHICH KEYWORD HAS BEEN RETURNED, ISSUES MESSAGE.         *
*                                                                     *
* INPUT:      REG1 -> A(OPERAND KEYWORD TOKEN)                        *
*                     A(DESCRIPTION)                                  *
*                     A(RETURNED VALUE)                               *
* OUTPUT:     MESSAGE ISSUED OF SAME FORMAT USED BY RETPROC.          *
*                                                                     *
*      INVOKED BY RETPROC.                                            *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
* CHANGE ACTIVITY :                                                   *
* $MOD(DFH99KR),COMP(SAMPLES),PROD(CICS TS ):                         *
*                                                                     *
*     PN= REASON REL YYMMDD HDXIII : REMARKS                          *
*    $P0= Mnnnnn 170 800925 HDZZUNK: Created.                         *
*    $P1= M90474 330 910807 HDBWSH : Prologue fixed.                  *
*                                                                     *
***********************************************************************
DFH99KR  CSECT
         EJECT
DFH99KR  DFH99M PROLOG,PARMS=(KEYOP,DESC,VALUE),REGS=(KS,K,D,V,L,W)
* SET UP FOR SCAN OF KEYSET IN DESCRIPTION
         L     K,KEYOP             K -> KEYWORD OPERAND TOKEN
         L     D,DESC              D -> DESCRIPTION
         USING DESCRIPT,D
         L     V,VALUE             V -> VALUE
         LA    KS,DSKEYSET         KS -> FIRST KEYWORD IN SET
         USING KEYSET,KS
* WORK OUT LENGTH OF VALUE
         LA    L,0                 SET FOR LENGTH 1
         CLI   DSVTYPE,TBIN2
         BNE   LEN1
         LA    L,1                 SET FOR LENGTH 2
LEN1     DS    0H
* LOOP FOR EACH ENTRY IN KEYSET
LOOP     DS    0H
         CLI   KSTOK,TEND          IF END OF KEYSET
         BE    NOMATCH             THEN NO MATCH.
         EX    L,CLC               IS IT RIGHT VALUE?
         BE    MATCH
         LA    KS,KSNEXT
         B     LOOP                LOOP FOR NEXT KEYWORD
CLC      CLC   0(0,V),KSCKW        COMPARE VALUES
* MATCH, PUT OUT SUITABLE MESSAGE
MATCH    DS    0H
         DFH99M ID,1999
         DFH99M INSERT,(K),FORMAT=TOKEN
         DFH99M TEXT,'('
         DFH99M INSERT,(KS),FORMAT=TOKEN
         DFH99M TEXT,(')',RETURNED)
         DFH99M FILE
         B     RET
* NO MATCH FOUND AT END, PUT OUT VALUE AS HEX.
NOMATCH  DS    0H
HEX      DFH99M AUTO,CL5           HEX TOKEN
         STCM  V,15,HEX+1          SET ADDR OF VALUE IN TOKEN
         MVI   HEX,THEX1           SET FOR HEX1
         BCT   L,HEX1              BR IF HEX1
         MVI   HEX,THEX2           SET FOR HEX2
HEX1     DS    0H
         DFH99M ID,1999
         DFH99M INSERT,(K),FORMAT=TOKEN
         DFH99M TEXT,'('
         DFH99M INSERT,HEX,FORMAT=TOKEN
         DFH99M TEXT,(')',RETURNED)
         DFH99M FILE
RET      DFH99M EPILOG
         END
