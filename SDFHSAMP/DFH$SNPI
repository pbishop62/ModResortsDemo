*ASM XOPTS(CICS,SP,NOEDF,NOPROLOG,NOEPILOG)
***********************************************************************
*                                                                     *
*   MODULE NAME = DFH$SNPI                                            *
*                                                                     *
* DESCRIPTIVE NAME =                                                  *
*        PLTPI program for the sample user exit program DFH$SNEX.     *
*                                                                     *
*                                                                     *
*                                                                     *
*        Licensed Materials - Property of IBM                         *
*                                                                     *
*        "Restricted Materials of IBM"                                *
*                                                                     *
*        5655-Y04                                                     *
*                                                                     *
*        (C) Copyright IBM Corp. 2001, 2014"                          *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
* STATUS = 7.2.0                                                      *
*                                                                     *
* FUNCTION =                                                          *
*        This program enables the sample user exit programs DFH$SNEX. *
*                                                                     *
*        The sample program DFH$SNEX causes the behavior of           *
*        SIGNON requests to revert to that which existed prior        *
*        to the changes which were introduced in CICS TS 2.1.         *
*                                                                     *
*        By assembling this program in the phase 2 section of         *
*        the PLT and including the PLT in the SIT or as a SIT         *
*        override, the previous behavior will automatically be        *
*        restored.                                                    *
*                                                                     *
* NOTES :                                                             *
*    DEPENDENCIES = S/370                                             *
*    RESTRICTIONS = None                                              *
*    MODULE TYPE = Executable                                         *
*    PROCESSOR = Assembler                                            *
*    ATTRIBUTES = Read only, Serially Reusable                        *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
* ENTRY POINT = DFHEAIx                                               *
*     PURPOSE = All Functions                                         *
*     LINKAGE = Can be invoked from PLTPI processing (second phase)   *
*               or via a user application using EXEC CICS LINK.       *
*     INPUT = N/A                                                     *
*     OUTPUT = N/A                                                    *
*     EXIT-NORMAL = EXEC CICS RETURN                                  *
*         The following messages can be issued by the module if a     *
*         response of other than NORMAL is returned from various      *
*         EXEC CICS xxxxxxxx commands:                                *
*                                                                     *
*             DFH$SNPI - ERROR ENABLING EXIT PROGRAM DFH$SNEX         *
*             DFH$SNPI - ERROR STARTING EXIT PROGRAM DFH$SNEX         *
*                                                                     *
*     EXIT-ERROR = None                                               *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
* EXTERNAL REFERENCES = None                                          *
*     ROUTINES = EXEC CICS ENABLE PROGRAM, EXEC CICS WRITE OPERATOR.  *
*     DATA AREAS = None                                               *
*     CONTROL BLOCKS =                                                *
*         DFH$SNGA - Global Work Area mapping for DFH$SNEX sample     *
*                    user exit program                                *
*     GLOBAL VARIABLES = None                                         *
* TABLES = None                                                       *
* MACROS = None                                                       *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
* CHANGE ACTIVITY :                                                   *
*        $MOD(DFH$SNPI),COMP(SAMPLES),PROD(CICS TS ):                 *
*                                                                     *
*     PN= REASON REL YYMMDD HDXIII : REMARKS                          *
*    $D0= I05404 610 010112 HD0FJTC: Module creation                  *
*                                                                     *
***********************************************************************
         SPACE
R0       EQU   0                   WORK
R1       EQU   1                   WORK
R2       EQU   2                   WORK
R3       EQU   3                   PROGRAM BASE
R4       EQU   4                   NOT USED
R5       EQU   5                   NOT USED
R6       EQU   6                   NOT USED
R7       EQU   7                   NOT USED
R8       EQU   8                   NOT USED
R9       EQU   9                   NOT USED
R10      EQU   10                  NOT USED
R11      EQU   11                  DFHEIBR
R12      EQU   12                  NOT USED
R13      EQU   13                  DFHEISTG
R14      EQU   14                  WORK
R15      EQU   15                  DFHEIPLR
         EJECT
DFHEISTG DSECT
         EJECT
ERRMSG   DS    CL60                WRITE OPERATOR ERROR MESSAGE
         EJECT
DFH$SNPI AMODE 31
DFH$SNPI RMODE ANY
DFH$SNPI DFHEIENT
         SPACE
***********************************************************************
*** Enable the DFH$SNEX global user exit program at the XSNEX       ***
*** exit point.                                                     ***
***********************************************************************
         SPACE
         EXEC CICS ENABLE PROGRAM('DFH$SNEX') EXIT('XSNEX')            C
               NOHANDLE
         CLC   EIBRESP,DFHRESP(NORMAL) RESPONSE NORMAL?
         BNE   ERRRTN1                 NO, GO TO ERROR ROUTINE
         SPACE
***********************************************************************
*** Start the DFH$SNEX Global User Exit program.                    ***
***********************************************************************
         SPACE
         EXEC CICS ENABLE PROGRAM('DFH$SNEX') START                    C
               NOHANDLE
         CLC   EIBRESP,DFHRESP(NORMAL) RESPONSE NORMAL?
         BNE   ERRRTN2                 NO, GO TO ERROR ROUTINE
         SPACE
***********************************************************************
*** Return to CICS.                                                 ***
***********************************************************************
         SPACE
RETURN   DS    0H                      RETURN TO CICS
         EXEC CICS RETURN
         SPACE
***********************************************************************
*** Error message proceesing routines.                              ***
***********************************************************************
         SPACE
ERRRTN1  DS    0H
         MVC   ERRMSG,ERRMSG1      SETUP ERROR MESSAGE
         CLI   EIBRESP+1,X'80'
         BNE   NEXT1
         MVC   ERRMSG,ERRMSG1A     SETUP ERROR MESSAGE
         B     SENDMSG
NEXT1    DS    0H
         CLI   EIBRESP+1,X'40'
         BNE   NEXT2
         MVC   ERRMSG,ERRMSG1B     SETUP ERROR MESSAGE
         B     SENDMSG
NEXT2    DS    0H
         CLI   EIBRESP+1,X'20'
         BNE   NEXT3
         MVC   ERRMSG,ERRMSG1C     SETUP ERROR MESSAGE
         B     SENDMSG
NEXT3    DS    0H
         CLI   EIBRESP+1,X'10'
         BNE   NEXT4
         MVC   ERRMSG,ERRMSG1D     SETUP ERROR MESSAGE
         B     SENDMSG
NEXT4    DS    0H
         CLI   EIBRESP+1,X'08'
         BNE   NEXT5
         MVC   ERRMSG,ERRMSG1E     SETUP ERROR MESSAGE
         B     SENDMSG
NEXT5    DS    0H
         CLI   EIBRESP+1,X'04'
         BNE   NEXT6
         MVC   ERRMSG,ERRMSG1F     SETUP ERROR MESSAGE
         B     SENDMSG             AND SEND MESSAGE TO OPERATOR
NEXT6    DS    0H
         MVC   ERRMSG,ERRMSG1G     SETUP ERROR MESSAGE
         B     SENDMSG             AND SEND MESSAGE TO OPERATOR
ERRRTN2  DS    0H
         MVC   ERRMSG,ERRMSG2      SETUP ERROR MESSAGE
         B     SENDMSG             AND SEND MESSAGE TO OPERATOR
         SPACE
SENDMSG  DS    0H
         EXEC CICS WRITE OPERATOR TEXT(ERRMSG) TEXTLENGTH(OPMSGLEN)    C
               NOHANDLE
         B     RETURN              AND RETURN TO CICS
         EJECT
***********************************************************************
*** Constants                                                       ***
***********************************************************************
         SPACE
ERRMSG1  DC    CL60'DFH$SNPI - ERROR ENABLING EXIT PROGRAM DFH$SNEX  '
ERRMSG1A DC    CL60'DFH$SNPI - ERROR ENABLING EXIT PROGRAM DFH$SNEX 1'
ERRMSG1B DC    CL60'DFH$SNPI - ERROR ENABLING EXIT PROGRAM DFH$SNEX 2'
ERRMSG1C DC    CL60'DFH$SNPI - ERROR ENABLING EXIT PROGRAM DFH$SNEX 3'
ERRMSG1D DC    CL60'DFH$SNPI - ERROR ENABLING EXIT PROGRAM DFH$SNEX 4'
ERRMSG1E DC    CL60'DFH$SNPI - ERROR ENABLING EXIT PROGRAM DFH$SNEX 5'
ERRMSG1F DC    CL60'DFH$SNPI - ERROR ENABLING EXIT PROGRAM DFH$SNEX 6'
ERRMSG1G DC    CL60'DFH$SNPI - ERROR ENABLING EXIT PROGRAM DFH$SNEX 7'
ERRMSG2  DC    CL60'DFH$SNPI - ERROR STARTING EXIT PROGRAM DFH$SNEX'
         SPACE
OPMSGLEN DC    F'60'               WRITE OPERATOR MESSAGE LENGTH
         SPACE
         LTORG
         SPACE
         DFHEISTG
         DFHEIEND
         END
