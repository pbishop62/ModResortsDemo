      CBL CICS('COBOL3,SP') APOST
PROCESS MAP,XREF,TRUNC(BIN)
      *****************************************************************
      *                                                               *
      * MODULE NAME = DFH0STPR                                        *
      *                                                               *
      * DESCRIPTIVE NAME = Collect and Print Statistics Sample        *
      *                                                               *
      *                                                               *
      *                                                               *
      *     Licensed Materials - Property of IBM                      *
      *                                                               *
      *     "Restricted Materials of IBM"                             *
      *                                                               *
      *     5655-Y04                                                  *
      *                                                               *
      *     (C) Copyright IBM Corp. 1999, 2016"                       *
      *                                                               *
      *                                                               *
      *                                                               *
      *                                                               *
      * STATUS = 7.2.0                                                *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      * DESCRIPTION :                                                 *
      *                                                               *
      *     This set of programs illustrate ways of using the EXEC    *
      *     CICS INQUIRE, COLLECT and EXTRACT STATISTICS commands of  *
      *     CICS to produce information about a CICS system.          *
      *     The reports include a CICS and MVS storage analysis.      *
      *     Statistics obtained using these commands can be used to   *
      *     aid the specification of the DSA Size parameters.         *
      *     (See the CICS Performance Guide and CICS Data Areas       *
      *      manuals for more information, and the CICS System        *
      *      Programming Reference for use of commands)               *
      *                                                               *
      *     These programs can be invoked from the PLT at PLTSD or    *
      *     as a CICS transaction (conversationally).                 *
      *     The output is sent via the CICS JES SPOOL interface for   *
      *     which a number of default parameters can be changed by    *
      *     the user to specify the distribution of the report(s).    *
      *     These defaults are defined in the working-storage         *
      *     section of the DFH0STAT program under the 01 level        *
      *     "OUTPUT-DEFAULTS".                                        *
      *                                                               *
      *     Installation instructions:-                               *
      *     Require SIT parameters:-  SPOOL=YES                       *
      *     Required CICS definitions:-                               *
      *         Transactions:-  STAT -> DFH0STAT                      *
      *         Programs:-    DFH0STAT   IBM COBOL                    *
      *                       DFH0STDB       for OS/390 & VM          *
      *                       DFH0STEJ           2.1.1 or later       *
      *                       DFH0STEP       "   "   "                *
      *                       DFH0STGN       "   "   "                *
      *                       DFH0STLK       "   "   "                *
      *                       DFH0STPR       "   "   "                *
      *                       DFH0STSA       "   "   "                *
      *                       DFH0STSY       "   "   "                *
      *                       DFH0STTP       "   "   "                *
      *                       DFH0STTS       "   "   "                *
      *                       DFH0STWB       "   "   "                *
      *                       DFH$STCN   Assembler                    *
      *                       DFH$STTB   Assembler                    *
      *         Mapsets:-     DFH0STM                                 *
      *                       DFH0STS                                 *
      *                                                               *
      *     The CICS-supplied RDO group, DFH$STAT, contains all       *
      *     the necessary resource definitions for this sample.       *
      *                                                               *
      *     All programs are command level and run above the          *
      *     16Mb line. Note: that DFH0STAT, DFH0STDB, DFH0STEJ,       *
      *     DFH0STEP, DFH0STGN, DFH0STLK, DFH0STPR, DFH0STSA,         *
      *     DFH0STSY, DFH0STTP, DFH0STTS, and DFH0STWB must be        *
      *     translated with the Translator options "COBOL3 and SP".   *
      *                                                               *
      *   SPOOL errors.  If an EXEC CICS SPOOL .. command fails       *
      *   when the program is run as a transaction, an error message  *
      *   is displayed on the users screen and the transaction will   *
      *   continue.  If the program is not being run from a terminal  *
      *   then a message will be sent to the console using EXEC       *
      *   CICS WRITE OPERATOR commands and the transaction will be    *
      *   terminated normally.                                        *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      * CHANGE ACTIVITY :                                             *
      *  $SEG(DFH0STPR),COMP(SAMPLES),PROD(CICS):                     *
      *                                                               *
      *  PN= REASON REL YYMMDD HDXXIII : REMARKS                      *
      * $D1= I07083 610 000508 HD1VCJB : Requestmodel migration       *
      * $D4= I07099 620 001013 HD1VCJB : Enhanced Statistics - Additi *
      * $01= A30143 650 070115 HDBGNRB : Migrate PK30143 from SPA R640*
      * $D5= I07001 620 010614 HD1VCJB : Tidy up Statistics Reports   *
      * $D6= I07546 640 040618 HD4PALS : Support for EXTRACT STATISTIC*
      * $D7= I07593 640 041021 HD1VCJB : Add redirect locate for URIMA*
      * $D8= I07610 650 050506 HD1VCJB : Clock/Timestamp Formatting   *
      * $D9= I07799 650 070104 HD1VCJB : change use of isc to ipic    *
      * $DA= I07861 650 070321 HD1VCJB : Add linkauth to IPCONN       *
      * $DB= I07767 660 080111 HDJNKM  : VTAM Persistency Configurable*
      * $L0= 776    610 991027 HD1VCJB : MODULE CREATED FROM DFH0STAT *
      * $L1= 795    610 991202 HD1VCJB : EJ Related Resources support *
      * $L2= 773    620 000627 HD1VCJB : Statistics Support for ECI   *
      * $L3= 795    610 001103 HD4PALS : Install/Discard CorbaServer  *
      * $L4= 832    620 001218 HD1VCJB : Dispatcher Statistics Update *
      * $L5= 813    620 010411 HD4PALS : Lid 813 SPI changes for tcpi *
      * $L6= 819    620 010510 HD1VCJB : Discard STATS for corbaserve *
      * $L7= 819    620 010619 HD1VCJB : Update 2.2 Statistics items  *
      * $L8= 813    630 020806 HD2JPEH : Reinstate Asserted Identity  *
      * $L9= 813    630 020902 HD4PALS : Statistics for tcpips privac *
      * $LA= 781    640 040226 HD1VCJB : Initial Updates for 3.1      *
      * $LB= 781    640 040316 HD1VCJB : DFH0STAT Updates for URIMAP  *
      * $LC= 852    640 040408 HD1VCJB : Pipeline/Webservice Statistic*
      * $LD= 831    640 040413 HD1VCJB : Add SSLCACHE to SO Global Sta*
      * $LE= 840    640 040429 HD1VCJB : Channel request connection st*
      * $LF= 855    640 040802 HD1VCJB : Add updating cvda for webserv*
      * $LG= 710    650 060103 HD4PALS : Add isc protocol to tcpipserv*
      * $LH= 873    650 060104 HD1VCJB : 3 - Doctemplate Statistics   *
      * $LI= 710    650 060112 HD1VCJB : 3 - IPCONN statistics        *
      * $LJ= 875    650 060307 HD1VCJB : 3 - Additional PIPELINE stats*
      * $LK= 884    650 060627 HD1VCJB : Add Policy Directories       *
      * $LL= 710    650 061024 HD1VCJB : security attributes for IPCON*
      * $LN= 710    650 070105 HD1VCJB : ipconn userauth security     *
      * $LO= 710    650 070228 HD4PALS : IPCONN portnumber 'no' format*
      * $LP= 897    650 070307 HD1VCJB : DYNAMIC LIBRARY SUPPORT      *
      * $LQ= 830    660 080214 HD1VCJB : Statistics Support for IPV6  *
      * $LR= 830    660 080225 HD1VCJB : Statistics Support for IPv6  *
      * $LS= 830    660 080421 HD1VCJB : Statistics Support for IPv6  *
      * $LT= 923    660 080513 HD1VCJB : Atomservice Statistics Suppor*
      * $LU= 927    660 080604 HD1VCJB : Event Processing Support     *
      * $LW= 711    660 081118 HDFXAB  : IPCONN Statistics Update     *
      * $LX= 711    660 090102 HDFXAB  : More ipconn stats            *
      * $LY= R00368 670 100112 HD4PALT : Externals for Mirror life tim*
      * $LZ= R00368 670 100210 HDIYAJL : IPIC FS FC Stats changes     *
      * $M1= R00367 670 100622 HDIYAJL : IPIC FS TD and TS Statistics *
      * $M2= R00367 670 100705 HDIYAJL : IPIC stats unsupported reques*
      * $M3= R18828 670 101110 HDILMWC : IP throttling Stats          *
      * $M4= R00552 670 101126 HDJCIDH : HTTP Outbound pooling        *
      *      R38042 690 130319 HDAFDRB : Use EXTRACT STATISTICS       *
      *      R38042 690 130422 HDIDNCS : Use EXTRACT STATISTICS       *
      *      R38042 690 130404 HDAFDRB : More use of EXTRACT STATISTIC*
      *      R75407 690 130920 HDLHJJH : Remove attri. from TCPIPS    *
      *      R91618 700 141203 HDIYAJL : SO SOTUNING stats            *
      * $P1= M15421 610 000503 HD1VCJB : Inq/Set Corba/DJAR functions *
      * $P2= M15441 610 000505 HD1VCJB : Statistics Consistency for T *
      * $P3= M16021 610 000814 HD1VCJB : Correct EJB Formatting Error *
      * $P5= M16770 610 001116 HD1VCJB : Remove demarcation/xcoordina *
      * $P8= D02825 620 011101 HD1VCJB : Correct elapsed time calcula *
      * $P9= D03414 620 011105 HD1VCJB : Correct Autoinstall Report   *
      * $PA= D09616 640 040108 HD1VCJB : Correct DFH0STPR for Dummy Lo*
      * $PB= D10505 640 040927 HD4PALS : hfspath becomes hfsfile      *
      * $PC= D11655 640 041018 HD1VCJB : Change ENDPOINT to mixed case*
      * $PD= D11637 640 041122 HD4PALS : TCPIP sslcache for stats     *
      * $PE= D12889 640 050112 HD1VCJB : Clarify URIMAP Statistics Rep*
      * $PF= D25227 660 090309 HD1VCJB : Correct VTAM PS Stats Fields *
      *      D98593 700 150106 HD3BADW : Fix comments                 *
      *      R96039 700 150224 HDLHJJH : Add ATTLSAWARE statics       *
      *     D104029 700 150602 HDIYAJL : Tweak SOTUNING stats         *
      *     R111500 710 160322 HDIYAJL : Enhance statistics           *
      *     R120476 710 161017 HDDLCRP : BMS3270 Serviceability       *
      *     D150786 720 181023 HDFVGMB : Add TRUNC(BIN) to PROCESS    *
      *                                                               *
      *****************************************************************
       ID DIVISION.
       PROGRAM-ID. DFH0STPR.

       ENVIRONMENT DIVISION.
           EJECT
       DATA DIVISION.
       WORKING-STORAGE SECTION.
       77  STCN-COMMAREA               PIC S9(4)  COMP.
       77  INQUIRE-RESP                PIC S9(8)  COMP VALUE +0.
       77  INQUIRE-FILE-RESP           PIC S9(8)  COMP VALUE +0.
       77  INQUIRE-MODENAME-RESP       PIC S9(8)  COMP VALUE +0.
       77  INQUIRE-PROGRAM-RESP        PIC S9(8)  COMP VALUE +0.
       77  INQUIRE-TDQUEUE-RESP        PIC S9(8)  COMP VALUE +0.
       77  INQUIRE-TSQNAME-RESP        PIC S9(8)  COMP VALUE +0.

       77  SUB1                        PIC S9(4)  COMP VALUE +0.
       77  SUB2                        PIC S9(4)  COMP VALUE +0.

       77  RESULT                      PIC S9(8)  COMP VALUE +0.
       77  RESULT2                     PIC S9(8)  COMP VALUE +0.
       77  RESULT-WORK                 PIC 9(18)  COMP VALUE 0.

       77  CURRENT-SECONDS             PIC S9(8)  COMP VALUE +0.

       77  LOGSTREAM-WRITE-RATE        PIC 9(7)V99     VALUE 0.
       77  ALLOCATE-RATE               PIC 9(4)V99     VALUE 0.

       77  TIME-DAYS                   PIC S9(8)  COMP VALUE +0.
       77  TIME-HOURS                  PIC S9(8)  COMP VALUE +0.
       77  TIME-MINUTES                PIC S9(8)  COMP VALUE +0.

       77  KEYPOINT-FREQUENCY          PIC S9(8)  COMP VALUE +0.
       77  LOGDEFER-INTERVAL           PIC S9(8)  COMP VALUE +0.

       77  JOURNAL-NAME                PIC X(8)        VALUE SPACES.
       77  JOURNAL-STATUS              PIC S9(8)  COMP VALUE +0.
       77  JOURNAL-TYPE                PIC S9(8)  COMP VALUE +0.
       77  JOURNAL-LOGSTREAM-NAME      PIC X(26)       VALUE SPACES.

       77  LOGSTREAM-NAME              PIC X(26)       VALUE SPACES.
       77  LOGSTREAM-USE-COUNT         PIC S9(8)  COMP VALUE +0.
       77  LOGSTREAM-STATUS            PIC S9(8)  COMP VALUE +0.
       77  SYSTEM-LOG                  PIC S9(8)  COMP VALUE +0.

       77  PROGRAM-AUTOINSTALL         PIC S9(8)  COMP VALUE +0.
       77  PROGAUTO-CATLG              PIC S9(8)  COMP VALUE +0.
       77  VTAM-OPEN-STATUS            PIC S9(8)  COMP VALUE +0.
       77  GENERIC-RESOURCE-NAME       PIC X(8)        VALUE SPACES.
       77  GENERIC-RESOURCE-STATUS     PIC S9(8)  COMP VALUE +0.
       77  VTAM-PSTYPE                 PIC S9(8)  COMP VALUE +0.
       77  VTAM-PSDINTERVAL            PIC S9(7)  COMP-3 VALUE +0.

       77  CONNECTION-NAME             PIC X(4)        VALUE SPACES.
       77  ACCESS-METHOD               PIC S9(8)  COMP VALUE +0.
       77  PROTOCOL                    PIC S9(8)  COMP VALUE +0.
       77  CONNECTION-NETNAME          PIC X(8)        VALUE SPACES.
       77  CONNECTION-TYPE             PIC S9(8)  COMP VALUE +0.
       77  SEND-COUNT                  PIC S9(8)  COMP VALUE +0.
       77  RECEIVE-COUNT               PIC S9(8)  COMP VALUE +0.

       77  MODE-NAME                   PIC X(8)        VALUE SPACES.
       77  MODENAME-CONNECTION         PIC X(4)        VALUE SPACES.
       77  ACTIVE-SESSIONS             PIC S9(4)  COMP VALUE +0.
       77  MAXIMUM-SESSIONS            PIC S9(4)  COMP VALUE +0.
       77  AVAILABLE-SESSIONS          PIC S9(4)  COMP VALUE +0.
       77  MAXIMUM-WINNERS             PIC S9(4)  COMP VALUE +0.

       77  TOTAL-SOCKETS-CREATED       PIC S9(18) COMP VALUE +0.
       77  CURR-PERS-INB-SOCKETS       PIC S9(8)  COMP VALUE +0.
       77  TOTAL-PERS-INB-SOCKETS      PIC S9(8)  COMP VALUE +0.
       77  CURR-OUTB-SOCKETS           PIC S9(8)  COMP VALUE +0.

       77  AUTOINSTALL-PROGRAM         PIC X(8)        VALUE SPACES.
       77  CURRENT-REQUESTS            PIC S9(8)  COMP VALUE +0.
       77  MAXIMUM-REQUESTS            PIC S9(8)  COMP VALUE +0.
       77  AUTOINSTALL-STATUS          PIC S9(8)  COMP VALUE +0.
       77  AUTOINSTALL-CONSOLES        PIC S9(8)  COMP VALUE +0.
       77  AUTOINSTALL-BRIDGE          PIC S9(8)  COMP VALUE +0.

       77  GARBAGE-COLLECTION-INT      PIC S9(8)  COMP VALUE +0.
       77  TERMINAL-INPUT-TIMEOUT-INT  PIC S9(8)  COMP VALUE +0.

       77  TCPIP-OPEN-STATUS           PIC S9(8)  COMP VALUE +0.
       77  ACTIVE-SOCKETS              PIC S9(8)  COMP VALUE +0.
       77  MAX-SOCKETS                 PIC S9(8)  COMP VALUE +0.

       77  ACTIVE-SSL-TCBS             PIC S9(8)  COMP VALUE +0.
       77  MAX-SSL-TCBS                PIC S9(8)  COMP VALUE +0.

       77  SSL-CACHE                   PIC S9(8)  COMP VALUE +0.
           EJECT
       77  TCPIPSERVICE-NAME           PIC X(8)        VALUE SPACES.
       77  TCPIPSERVICE-BACKLOG        PIC S9(8)  COMP VALUE +0.
       77  TCPIPSERVICE-PORT           PIC S9(8)  COMP VALUE +0.
       77  TCPIPSERVICE-SSLTYPE        PIC S9(8)  COMP VALUE +0.
       77  TCPIPSERVICE-OPENSTATUS     PIC S9(8)  COMP VALUE +0.
       77  TCPIPSERVICE-PRIVACY        PIC S9(8)  COMP VALUE +0.
       77  TCPIPSERVICE-PROTOCOL       PIC S9(8)  COMP VALUE +0.
       77  TCPIPSERVICE-TRANSID        PIC X(4)        VALUE SPACES.
       77  TCPIPSERVICE-URM            PIC X(8)        VALUE SPACES.
       77  TCPIPSERVICE-AUTHENTICATE   PIC S9(8)  COMP VALUE +0.
       77  TCPIPSERVICE-ATTACHSEC      PIC S9(8)  COMP VALUE +0.
       77  TCPIPSERVICE-CERTIFICATE    PIC X(56)       VALUE SPACES.
       77  TCPIPSERVICE-MAXDATA        PIC S9(8)  COMP VALUE +0.

       77  IPCONN-NAME                 PIC X(8)        VALUE SPACES.
       77  IPCONN-APPLID               PIC X(8)        VALUE SPACES.
       77  IPCONN-NETWORK-ID           PIC X(8)        VALUE SPACES.
       77  IPCONN-CONNECTION-STATUS    PIC S9(8)  COMP VALUE +0.
       77  IPCONN-HOST-NAME            PIC X(116)      VALUE SPACES.
       77  IPCONN-PORT-NUMBER          PIC S9(8)  COMP VALUE +0.
       77  IPCONN-MAX-QUEUE-TIME       PIC S9(8)  COMP VALUE +0.
       77  IPCONN-QUEUE-LIMIT          PIC S9(8)  COMP VALUE +0.
       77  IPCONN-SERVICE-STATUS       PIC S9(8)  COMP VALUE +0.
       77  IPCONN-SSLTYPE              PIC S9(8)  COMP VALUE +0.
       77  IPCONN-TCPIPSERVICE         PIC X(8)        VALUE SPACES.
       77  IPCONN-USERAUTH             PIC S9(8)  COMP VALUE +0.
       77  IPCONN-LINKAUTH             PIC S9(8)  COMP VALUE +0.
       77  IPCONN-MIRRORLIFE           PIC S9(8)  COMP VALUE +0.

      *****************************************************************
      ***  Total fields                                             ***
      *****************************************************************
       77  TCPIPSERVICE-TOTAL          PIC S9(9)  COMP-3 VALUE +0.

       77  IPCONN-TOTAL                PIC S9(9)  COMP-3 VALUE +0.

       77  TOTAL-FUNCSHIP-REQS         PIC 9(8)   COMP   VALUE 0.

      *****************************************************************
      *** System Log Names                                          ***
      *****************************************************************
       01  SYSTEM-LOGS.
           03  SYSTEM-LOG-NAMES.
               05  FILLER          PIC X(8)    VALUE 'DFHLOG  '.
               05  FILLER          PIC X(8)    VALUE 'DFHSHUNT'.
           03  FILLER REDEFINES SYSTEM-LOG-NAMES.
               05  SYSTEM-LOG-NAME PIC X(8)    OCCURS 2.
           EJECT

       01  NULL-FIELD-CONSTANTS.
           03  NULL-DATE-FIELD     PIC X(10)   VALUE '--/--/----'.
           03  NULL-TIME-FIELD     PIC X(15)   VALUE '--:--:--.------'.

      *****************************************************************
      ***  Report print line definitions                            ***
      *****************************************************************
       01  PRINT-LINE.
           03  PRINT-CONTROL           PIC X.
           03  PP1-132.
               05  FILLER              PIC XX.
               05  PP3-49.
                   07  FILLER          PIC X(37).
                   07  PP40-49         PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X(61).
               05  PP111-120           PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X(12).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(39).
               05  PP40-50.
                   07  PP40-49X.
                       09  PP40-48.
                           11  PP40-47 PIC X(8).
                           11  FILLER  PIC X.
                       09  FILLER      PIC X.
                   07  FILLER          PIC X.
               05  FILLER              PIC X(82).
           03 FILLER REDEFINES PP1-132.
               05  PP1-40.
                   07  PP1-38.
                       09  PP1-7.
                           11  PP1-6   PIC X(6).
                           11  FILLER  PIC X.
                       09  FILLER      PIC X(31).
                   07  FILLER          PIC XX.
               05  FILLER              PIC X(6).
               05  PP47-54             PIC 99B99B99.
               05  FILLER              PIC X(78).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(41).
               05  PP42-49             PIC 99B99B99.
               05  FILLER              PIC X(83).
           03 FILLER REDEFINES PP1-132.
               05  PP1-15.
                   07  PP1-12.
                       09  PP1-11      PIC X(11).
                       09  FILLER      PIC X.
                   07  FILLER          PIC XXX.
               05  FILLER              PIC X(117).
           03 FILLER REDEFINES PP1-132.
               05  PP1-23.
                   07  PP1-21.
                       09  PP1-13      PIC X(13).
                       09  PP14-21     PIC X(8).
                   07  FILLER          PIC XX.
               05  FILLER              PIC X(7).
               05  PP31-40             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X(6).
               05  PP47-62             PIC X(16).
               05  FILLER              PIC X(4).
               05  PP67-69             PIC XXX.
               05  FILLER              PIC X(9).
               05  FILLER              PIC XXX.
               05  FILLER              PIC X(9).
               05  PP91-93             PIC XXX.
               05  FILLER              PIC X(9).
               05  PP103-105           PIC XXX.
               05  FILLER              PIC X(8).
               05  PP114-120N          PIC ZZZ9.99.
               05  FILLER              PIC X(12).
           03 FILLER REDEFINES PP1-132.
               05  PP1-4               PIC X(4).
               05  FILLER              PIC X(8).
               05  PP13-20             PIC X(8).
               05  FILLER              PIC X(31).
               05  PP52-67             PIC X(16).
               05  FILLER              PIC XX.
               05  PP70-79             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP82-84             PIC XXX.
               05  FILLER              PIC XXX.
               05  PP88-97             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC XXX.
               05  PP101-103           PIC XXX.
               05  FILLER              PIC X(15).
               05  PP119-120           PIC XX.
               05  FILLER              PIC X(12).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(23).
               05  PP24-28             PIC X(5).
               05  FILLER              PIC XXX.
               05  PP32-57             PIC X(26).
               05  FILLER              PIC XX.
               05  PP60-69             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP72-81             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP84-93             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP96-105            PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X(27).
           03 FILLER REDEFINES PP1-132.
               05  PP1-19.
                   07  PP1-18          PIC X(18).
                   07  FILLER          PIC X.
               05  FILLER              PIC X(27).
               05  PP47-72.
                   07  PP47-54X.
                       09  PP47-51.
                           11  PP47-49 PIC XXX.
                           11  FILLER  PIC XX.
                       09  FILLER      PIC XXX.
                   07  FILLER          PIC X(18).
               05  FILLER              PIC X(42).
               05  PP115               PIC X.
               05  FILLER              PIC X(17).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(42).
               05  PP43-57             PIC ZZZ,ZZZ,ZZZ,ZZ9.
               05  FILLER              PIC X(38).
               05  PP96-102            PIC ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP105-111           PIC ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP114-120           PIC ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP123-129           PIC ZZZ,ZZ9.
               05  PP130-132           PIC XXX.
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC XX.
               05  PP3-30.
                   07  PP3-28          PIC X(26).
                   07  FILLER          PIC XX.
               05  PP31-36             PIC Z,ZZZ9.
               05  FILLER              PIC XX.
               05  PP39-44             PIC X(6).
               05  FILLER              PIC XXX.
               05  PP48-56             PIC ZZ,ZZ9.99.
               05  FILLER              PIC X(51).
               05  PP108-114N          PIC ZZZ,ZZ9.
               05  FILLER REDEFINES PP108-114N.
                   07  FILLER          PIC X(4).
                   07  PP112-114       PIC XXX.
               05  FILLER              PIC XX.
               05  PP117-123           PIC ZZZ,ZZ9.
               05  FILLER REDEFINES PP117-123.
                   07  FILLER          PIC X(4).
                   07  PP121-123       PIC XXX.
               05  FILLER              PIC XX.
               05  PP126-132           PIC ZZZ,ZZ9.
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC XX.
               05  PP3-10              PIC X(8).
               05  FILLER              PIC X(36).
               05  PP47-60             PIC X(14).
               05  FILLER              PIC X(44).
               05  PP105-114X          PIC X(10).
               05  FILLER              PIC XX.
               05  PP117-130           PIC X(14).
               05  FILLER              PIC XX.
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(39).
               05  PP40-51.
                   07  PP40-45.
                       09  PP40-42     PIC XXX.
                       09  FILLER      PIC XXX.
                   07  FILLER          PIC X(6).
               05  FILLER              PIC X(27).
               05  PP79-120            PIC X(42).
               05  FILLER              PIC XX.
               05  PP123-132           PIC ZZ,ZZZ,ZZ9.
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(39).
               05  PP40-78.
                   07  PP40-44         PIC ZZZZ9.
                   07  FILLER          PIC X(34).
               05  FILLER              PIC X(26).
               05  PP105-122.
                   07  PP105-116.
                       09  PP105-107   PIC XXX.
                       09  FILLER      PIC X(9).
                   07  FILLER          PIC X(6).
               05  FILLER              PIC X(10).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC XX.
               05  PP3-44              PIC X(42).
               05  FILLER              PIC XX.
               05  PP47-56             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X(22).
               05  PP79-81             PIC XXX.
               05  FILLER              PIC X(23).
               05  PP105-118           PIC ZZ,ZZZ,ZZZ,ZZ9.
               05  FILLER              PIC X(8).
               05  PP127-132           PIC X(6).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC XX.
               05  PP3-60.
                   07  PP3-50.
                       09  PP3-46      PIC X(44).
                       09  FILLER      PIC X(4).
                   07  FILLER          PIC X(10).
               05  FILLER              PIC XX.
               05  PP63-76.
                   07  FILLER          PIC X(4).
                   07  PP67-76         PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X(56).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(52).
               05  PP53-62             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X(4).
               05  PP67-76X.
                   07  FILLER          PIC XXX.
                   07  PP70-76         PIC X(7).
               05  FILLER              PIC X(56).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(36).
               05  PP37                PIC X.
               05  FILLER              PIC XX.
               05  PP40-124.
                   07  PP40-70         PIC X(31).
                   07  FILLER          PIC X(54).
               05  FILLER              PIC X(8).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(39).
               05  PP40-43             PIC X(4).
               05  PP44                PIC X.
               05  PP45-52.
                   09  PP45-49         PIC ZZZZ9.
                   09  FILLER          PIC XXX.
               05  FILLER              PIC X(15).
               05  PP68-108.
                   07  PP68-104.
                       09  PP68-102    PIC X(35).
                       09  FILLER      PIC XX.
                   07  FILLER          PIC X(4).
               05  FILLER              PIC XX.
               05  PP111-124.
                   07  PP111-121       PIC X(11).
                   07  FILLER REDEFINES PP111-121.
                       09  PP111-119   PIC X(9).
                       09  FILLER      PIC XX.
                   07  FILLER          PIC X(3).
               05  FILLER              PIC X(8).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(104).
               05  PP105-114           PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X.
               05  PP116-119           PIC X(4).
               05  FILLER              PIC X(13).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(4).
               05  PP5-13              PIC X(9).
               05  FILLER              PIC X(30).
               05  PP44-53             PIC ZZ,ZZZ,ZZ9.
               05  FILLER REDEFINES PP44-53.
                   07  PP44-51.
                       09  PP44-47     PIC X(4).
                       09  FILLER      PIC X(4).
                   07  FILLER          PIC XX.
               05  FILLER              PIC X(9).
               05  PP63-64             PIC XX.
               05  PP65-93             PIC X(29).
               05  FILLER              PIC X(17).
               05  PP111-118.
                   07  PP111-114       PIC X(4).
                   07  FILLER          PIC X(4).
               05  FILLER              PIC X(14).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(54).
               05  PP55-62             PIC 99B99B99.
               05  FILLER              PIC X(70).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC XX.
               05  PP3-132.
                   07  PP3-102.
                       09  PP3-72.
                           11  PP3-37  PIC X(35).
                           11  PP38-72 PIC X(35).
                       09  PP73-102    PIC X(30).
                   07  PP103-132       PIC X(30).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(6).
               05  PP7-41              PIC X(35).
               05  FILLER              PIC X(7).
               05  PP49-62             PIC ZZ,ZZZ,ZZZ,ZZ9.
               05  FILLER              PIC X(44).
               05  PP107-116           PIC X(10).
               05  FILLER              PIC XX.
               05  PP119-132           PIC X(14).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(67).
               05  PP68-99             PIC X(32).
               05  FILLER              PIC X(3).
               05  PP103-116           PIC ZZ,ZZZ,ZZZ,ZZ9.
               05  PP117-119           PIC X(3).
               05  PP120-129           PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X(3).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC XX.
               05  PP3-60X.
                   07  PP3-50X.
                       09  PP3-46X     PIC X(44).
                       09  FILLER      PIC X(4).
                   07  FILLER          PIC X(10).
               05  FILLER              PIC XX.
               05  PP63-72             PIC X(10).
               05  FILLER              PIC XX.
               05  PP75-88             PIC X(14).
               05  FILLER              PIC X(44).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(52).
               05  PP53-64             PIC X(12).
               05  FILLER              PIC X(4).
               05  PP69-104            PIC X(36).
               05  FILLER              PIC X(2).
               05  PP107-118           PIC X(12).
               05  FILLER              PIC X(14).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(54).
               05  PP55-64             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X(44).
               05  PP109-118           PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X(14).
           EJECT
       LINKAGE SECTION.
           EJECT
      *****************************************************************
      ***  Statistics Sample Programs Commarea.                     ***
      *****************************************************************
       01  DFHCOMMAREA.
           COPY DFH0STCM.
           EJECT
      *****************************************************************
      *** Data structures for VTAM Global statistics                ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHA03DS.
           EJECT
      *****************************************************************
      *** Data structures for Terminal Autoinstall statstics        ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHA04DS.
           EJECT
      *****************************************************************
      *** Data structures for Connection Resource statistics        ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHA14DS.
           EJECT
      *****************************************************************
      *** Data structures for IPCONN Resource statistics            ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHISRDS.
           EJECT
      *****************************************************************
      *** Data structures for Dispatcher Global statistics          ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHDSGDS.
           EJECT
      *****************************************************************
      *** Data structures for Logstream global statistics           ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHLGGDS.
           EJECT
      *****************************************************************
      *** Data structures for Journalname statistics                ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHLGRDS.
           EJECT
      *****************************************************************
      *** Data structures for Logstream statistics                  ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHLGSDS.
           EJECT
      *****************************************************************
      *** Data structures for Program Manager global statistics     ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHPGGDS.
           EJECT
      *****************************************************************
      *** Data structures for Recovery Manager global statistics    ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHRMGDS.
           EJECT
      *****************************************************************
      *** Data structures for Storage Manager Domain Subpools       ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHSMDDS.
           EJECT
      *****************************************************************
      *** Data structures for Socket Domain Global statistics       ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHSOGDS.
           EJECT
      *****************************************************************
      *** Data structures for Socket Domain Resource statistics     ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHSORDS.
           EJECT
      *****************************************************************
      *** Data structures for Transaction statistics                ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHXMRDS.
           EJECT
      *****************************************************************
      ***                                                           ***
      ***           CICS Collect and print statistics               ***
      ***                                                           ***
      *****************************************************************
       PROCEDURE DIVISION.

      *****************************************************************
      *** MAIN-PROCESSING                                           ***
      ***     Initialise the totals fields                          ***
      ***     Print the statistics reports                          ***
      ***     Return to DFH0STLK                                    ***
      *****************************************************************

       MAIN-PROCESSING SECTION.

           PERFORM TOTALS-INITIALISE.

           PERFORM PRINT-STATISTICS.

           EXEC CICS RETURN
           END-EXEC.

       MAIN-PROCESSING-EXIT.
           GOBACK.
           EJECT
      *****************************************************************
      *****************************************************************
      ***         Collect and Print the CICS Statistics             ***
      *****************************************************************
      *****************************************************************
      ***                                                           ***
      *** 1. Print the statistics in the following order:-          ***
      ***    - Journalnames                                         ***
      ***    - Logstream Global - DFHLOG and DFHSHUNT               ***
      ***    - Logstreams                                           ***
      ***    - Program and Terminal Autoinstall and VTAM            ***
      ***    - Connections and Modenames                            ***
      ***    - TCP/IP                                               ***
      ***    - TCPIPSERVICE Resources                               ***
      ***    - IPCONN Resources                                     ***
      ***                                                           ***
      *****************************************************************
      *****************************************************************

       PRINT-STATISTICS SECTION.

           IF SELECT-JOURNALS = 'Y' OR 'y' THEN
               PERFORM JOURNAL-STATISTICS
           END-IF.

           IF SELECT-LOGSTREAM-GLOBAL = 'Y' OR 'y' THEN
               PERFORM LOGSTREAM-GBL-STATISTICS
           END-IF.

           IF SELECT-LOGSTREAMS = 'Y' OR 'y' THEN
               PERFORM LOGSTREAM-STATISTICS
           END-IF.

           IF SELECT-PGM-AUTOINST = 'Y' OR 'y' THEN
               PERFORM PGM-AUTOINSTALL-STATS
           END-IF.

           IF SELECT-AUTO-VTAM = 'Y' OR 'y' THEN
               PERFORM AUTOINSTALL-VTAM-STATISTICS
           END-IF.

           IF SELECT-IRC-ISC = 'Y' OR 'y' THEN
               PERFORM CONNECTION-STATISTICS
           END-IF.

           IF SELECT-TCPIP = 'Y' OR 'y' THEN
               PERFORM TCPIP-STATISTICS
           END-IF.

           IF SELECT-TCPIPSERVICES = 'Y' OR 'y' THEN
               PERFORM TCPIPSERVICE-STATISTICS
           END-IF.

           IF SELECT-IPCONNS = 'Y' OR 'y' THEN
               PERFORM IPCONN-STATISTICS
           END-IF.

       PRINT-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** JOURNAL-STATISTICS                                        ***
      ***   Print heading, Journalname and Logstream statistics.    ***
      *****************************************************************
       JOURNAL-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO JOURNALS-PAGENO.

           MOVE '-' TO PRINT-CONTROL.
           MOVE 'Journalnames' TO PP1-12.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-12.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      ***   Print the journalname and logstream statistics.         ***
      *****************************************************************

           MOVE '0'      TO PRINT-CONTROL.

           EXEC CICS INQUIRE JOURNALNAME START
           END-EXEC.

           EXEC CICS INQUIRE JOURNALNAME(JOURNAL-NAME) NEXT
               TYPE(JOURNAL-TYPE)
               STATUS(JOURNAL-STATUS)
               STREAMNAME(JOURNAL-LOGSTREAM-NAME)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

               IF LINE-COUNT > LINES-PER-PAGE THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               IF SUB-HEADINGS = 'Y' THEN
                   PERFORM JOURNAL-SUB-HEADINGS
               END-IF

               EXEC CICS EXTRACT STATISTICS
                   JOURNALNAME RESID(JOURNAL-NAME)
                   SET(ADDRESS OF DFHLGRDS)
                   NOHANDLE
               END-EXEC

               MOVE JOURNAL-NAME TO PP3-10
               EVALUATE JOURNAL-STATUS
                   WHEN DFHVALUE(ENABLED)
                       MOVE 'Enabled ' TO PP13-20
                   WHEN DFHVALUE(DISABLED)
                       MOVE 'Disabled' TO PP13-20
                   WHEN DFHVALUE(FAILED)
                       MOVE 'Failed  ' TO PP13-20
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               IF LGRJTYPEMVS
                   MOVE 'MVS  '  TO PP24-28
               END-IF
               IF LGRJTYPESMF
                   MOVE 'SMF  '  TO PP24-28
               END-IF
               IF LGRJTYPEDMY
                   MOVE 'Dummy'  TO PP24-28
               END-IF
               MOVE LGRSTREAM    TO PP32-57
               IF JOURNAL-NAME = 'DFHLOG  ' OR
                 JOURNAL-NAME = 'DFHSHUNT' THEN
                   MOVE 'N/A' TO PP67-69
                   MOVE 'N/A' TO PP79-81
                   MOVE 'N/A' TO PP91-93
                   MOVE 'N/A' TO PP103-105
               ELSE
                   MOVE LGRWRITES    TO PP60-69
                   MOVE LGRBYTES     TO PP72-81
                   IF LGRWRITES > 0 THEN
                       COMPUTE RESULT = (LGRBYTES / LGRWRITES)
                   ELSE
                       MOVE 0 TO RESULT
                   END-IF
                   MOVE RESULT       TO PP84-93
                   MOVE LGRBUFLSH    TO PP96-105
               END-IF
               PERFORM WRITE-OUTPUT

               EXEC CICS INQUIRE JOURNALNAME(JOURNAL-NAME) NEXT
                   TYPE(JOURNAL-TYPE)
                   STATUS(JOURNAL-STATUS)
                   STREAMNAME(JOURNAL-LOGSTREAM-NAME)
                   RESP(INQUIRE-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE JOURNALNAME END
           END-EXEC.

       JOURNAL-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** LOGSTREAM-GBL-STATISTICS                                  ***
      ***   Print heading and Logstream Global Statistics.          ***
      *****************************************************************
       LOGSTREAM-GBL-STATISTICS SECTION.

      *****************************************************************
      ***   Print the page headings.                                ***
      *****************************************************************
           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO LOGSTREAM-GLOBAL-PAGENO.

      *****************************************************************
      *** Collect the Recovery Manager Global statistics, the       ***
      *** Logger system parameter settings and the Logger Global    ***
      *** statistics.                                               ***
      *****************************************************************
           EXEC CICS EXTRACT STATISTICS RECOVERY
               SET(ADDRESS OF DFHRMGDS)
           END-EXEC.

           EXEC CICS INQUIRE SYSTEM
               AKP(KEYPOINT-FREQUENCY)
               LOGDEFER(LOGDEFER-INTERVAL)
           END-EXEC.

           EXEC CICS EXTRACT STATISTICS STREAMNAME
               SET(ADDRESS OF DFHLGGDS)
               LASTRESET(LAST-RESET-TIME)
               NOHANDLE
           END-EXEC.

           MOVE EIBTIME TO TIME-WORK.
           COMPUTE CURRENT-SECONDS = ((TIME-WORK-HH * 3600) +
               (TIME-WORK-MM * 60) + TIME-WORK-SS)
           END-COMPUTE.
           MOVE LAST-RESET-TIME TO TIME-WORK.
           COMPUTE LAST-RESET-SECONDS = ((TIME-WORK-HH * 3600) +
               (TIME-WORK-MM * 60) + TIME-WORK-SS)
           END-COMPUTE.

      *****************************************************************
      *    Calculate the elapsed time (in seconds) since statistics   *
      *    last reset. If the elapsed time is less than zero we've    *
      *    crossed a 24 hour period (the maximum statistics interval) *
      *    and we must add a day's worth of seconds to restore the    *
      *    correct positive value.                                    *
      *****************************************************************
           COMPUTE ELAPSED-SECONDS =
               (CURRENT-SECONDS - LAST-RESET-SECONDS)
           END-COMPUTE.

           IF ELAPSED-SECONDS < 0 THEN
               COMPUTE ELAPSED-SECONDS = (ELAPSED-SECONDS + 86400)
               END-COMPUTE
           END-IF.

      *****************************************************************
      ***   Print heading and Global Logstream statistics.          ***
      *****************************************************************
           MOVE '-'      TO PRINT-CONTROL.
           MOVE 'Logstream - Global' TO PP1-18.
           PERFORM WRITE-OUTPUT.
           MOVE '+'      TO PRINT-CONTROL.
           MOVE ALL '_'  TO PP1-18.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Activity Keypoint Frequency (AKPFREQ). . . . . :'
                                    TO PP3-50.
           MOVE LGG-AKP-FREQUENCY   TO PP53-62.
           PERFORM WRITE-OUTPUT.
           MOVE 'Activity Keypoints Taken . . . . . . . . . . . :'
                                    TO PP3-50.
           MOVE LGG-KEYPOINTS-TAKEN TO PP53-62.
           PERFORM WRITE-OUTPUT.
           MOVE 'Average time between Activity Keypoints. . . . :'
                                    TO PP3-50.
           IF LGG-KEYPOINTS-TAKEN > 0 THEN
               COMPUTE RESULT = (ELAPSED-SECONDS / LGG-KEYPOINTS-TAKEN)
               END-COMPUTE
           ELSE
               MOVE 0 TO RESULT
           END-IF.
           DIVIDE RESULT BY 60 GIVING RESULT2
               REMAINDER TIME-WORK-SS
           END-DIVIDE.
           DIVIDE RESULT2 BY 60 GIVING TIME-WORK-HH
               REMAINDER TIME-WORK-MM
           END-DIVIDE.
           MOVE TIME-WORK TO PP55-62.
           INSPECT PP55-62 REPLACING ALL ' ' BY ':'.
           MOVE '(Since Last Statistics Reset)' TO PP65-93.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Logstream Deferred Force Interval (LGDFINT). . :'
                                      TO PP3-50.
           MOVE LGG-LOGDEFER-INTERVAL TO PP53-62.
           MOVE 'ms'                  TO PP63-64.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.
           MOVE ALL '-' TO PP1-132.
           PERFORM WRITE-OUTPUT.

           PERFORM PAGE-HEADINGS.
           MOVE '-'      TO PRINT-CONTROL.
           MOVE 'Logstream - System Logs' TO PP1-23.
           PERFORM WRITE-OUTPUT.
           MOVE '+'      TO PRINT-CONTROL.
           MOVE ALL '_'  TO PP1-23.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      ***  System Logs (DFHLOG and DFHSHUNT) Statistics.            ***
      *****************************************************************

           MOVE 1 TO SUB1.

           PERFORM WITH TEST BEFORE
               UNTIL SUB1 > 2

                   PERFORM SYSTEM-LOG-STATISTICS

                   MOVE '0'     TO PRINT-CONTROL
                   MOVE ALL '-' TO PP1-132
                   PERFORM WRITE-OUTPUT

                   ADD 1 TO SUB1

           END-PERFORM.

       LOGSTREAM-GBL-STATISTICS-EXIT.
           EXIT.
           EJECT
       SYSTEM-LOG-STATISTICS SECTION.

           MOVE '0'                   TO PRINT-CONTROL.
           MOVE 'System log - '       TO PP1-13.
           MOVE SYSTEM-LOG-NAME(SUB1) TO PP14-21.
           PERFORM WRITE-OUTPUT.
           MOVE '+'      TO PRINT-CONTROL.
           MOVE ALL '_'  TO PP1-21.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      ***  Use inquire journalname to find out the type, name and   ***
      *** status of the logstream for the system log.               ***
      *****************************************************************
           EXEC CICS INQUIRE JOURNALNAME(SYSTEM-LOG-NAME(SUB1))
               TYPE(JOURNAL-TYPE)
               STATUS(JOURNAL-STATUS)
               STREAMNAME(LOGSTREAM-NAME)
               RESP(INQUIRE-RESP)
           END-EXEC.

           IF INQUIRE-RESP NOT = DFHRESP(NORMAL) THEN
               GO TO SYSTEM-LOG-STATISTICS-EXIT
           END-IF.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'System Log Type. . . . . . . . . . . . . :' TO PP3-44.
           EVALUATE JOURNAL-TYPE
               WHEN DFHVALUE(MVS)
                   MOVE 'MVS  ' TO PP47-51
               WHEN DFHVALUE(DUMMY)
                   MOVE 'DUMMY' TO PP47-51
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           PERFORM WRITE-OUTPUT.

           MOVE 'System Log Status. . . . . . . . . . . . :' TO PP3-44.
           EVALUATE JOURNAL-STATUS
               WHEN DFHVALUE(DISABLED)
                   MOVE 'DISABLED' TO PP47-54X
               WHEN DFHVALUE(ENABLED)
                   MOVE 'ENABLED ' TO PP47-54X
               WHEN DFHVALUE(FAILED)
                   MOVE 'FAILED  ' TO PP47-54X
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           PERFORM WRITE-OUTPUT.

           IF JOURNAL-TYPE NOT = DFHVALUE(MVS) THEN
               GO TO SYSTEM-LOG-STATISTICS-EXIT
           END-IF.

      *****************************************************************
      ***  System Log Logstream                                     ***
      *****************************************************************

           EXEC CICS INQUIRE STREAMNAME(LOGSTREAM-NAME)
               STATUS(LOGSTREAM-STATUS)
           END-EXEC.

           EXEC CICS EXTRACT STATISTICS
               STREAMNAME RESID(LOGSTREAM-NAME)
               SET(ADDRESS OF DFHLGSDS)
               NOHANDLE
           END-EXEC

      *****************************************************************

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Logstream Name . . . . . . . . . . . . . :' TO PP3-44.
           MOVE LOGSTREAM-NAME   TO PP47-72.
           MOVE 'Logstream Status . . . . . . . . . . . . :'
                                 TO PP79-120.
           EVALUATE LOGSTREAM-STATUS
               WHEN DFHVALUE(FAILED)
                   MOVE 'FAILED' TO PP127-132
               WHEN DFHVALUE(OK)
                   MOVE '    OK' TO PP127-132
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'DASD Only. . . . . . . . . . . . . . . . :' TO PP3-44.
           EVALUATE TRUE
               WHEN LGSDOYES
                   MOVE 'YES' TO PP47-49
               WHEN LGSDONO
                   MOVE 'NO ' TO PP47-49
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           MOVE 'Retention Period (days). . . . . . . . . :'
                              TO PP79-120.
           MOVE LGSRETPD      TO PP123-132.
           PERFORM WRITE-OUTPUT.
           MOVE 'Coupling Facility Structure Name . . . . :' TO PP3-44.
           IF LGSSTRUC NOT = LOW-VALUES THEN
               MOVE LGSSTRUC  TO PP47-62
           END-IF.
           MOVE 'Auto Delete. . . . . . . . . . . . . . . :'
                              TO PP79-120.
           EVALUATE TRUE
               WHEN LGSADYES
                   MOVE 'YES' TO PP130-132
               WHEN LGSADNO
                   MOVE ' NO' TO PP130-132
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Logstream Writes . . . . . . . . . . . . :' TO PP3-44.
           MOVE LGSWRITES            TO PP47-56.
           MOVE 'Maximum Block Length . . . . . . . . . . :'
                                     TO PP79-120.
           MOVE LGSMAXBL             TO PP123-132.
           PERFORM WRITE-OUTPUT.
           MOVE 'Logstream Writes per second. . . . . . . :' TO PP3-44.
           IF ELAPSED-SECONDS > 0 THEN
               COMPUTE LOGSTREAM-WRITE-RATE =
                           (LGSWRITES / ELAPSED-SECONDS)
               END-COMPUTE
           ELSE
               MOVE LGSWRITES TO LOGSTREAM-WRITE-RATE
           END-IF.
           MOVE LOGSTREAM-WRITE-RATE TO PP48-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Average Bytes per Logstream Write. . . . :' TO PP3-44.
           IF LGSWRITES > 0 THEN
               COMPUTE RESULT = (LGSBYTES / LGSWRITES)
           ELSE
               MOVE 0 TO RESULT
           END-IF.
           MOVE RESULT               TO PP47-56.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Logstream Deletes (Tail Trims) . . . . . :' TO PP3-44.
           MOVE LGSDELETES           TO PP47-56.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Logstream Query Requests . . . . . . . . :' TO PP3-44.
           MOVE LGSQUERIES           TO PP47-56.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Logstream Browse Starts. . . . . . . . . :' TO PP3-44.
           MOVE LGSBRWSTRT           TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Logstream Browse Reads . . . . . . . . . :' TO PP3-44.
           MOVE LGSBRWREAD           TO PP47-56.
           PERFORM WRITE-OUTPUT.

           MOVE 'Logstream Buffer Appends . . . . . . . . :' TO PP3-44.
           MOVE LGSBUFAPP            TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Logstream Buffer Full Waits. . . . . . . :' TO PP3-44.
           MOVE LGSBUFWAIT           TO PP47-56.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.
           MOVE 'Logstream Force Waits. . . . . . . . . . :' TO PP3-44.
           MOVE LGSTFCWAIT           TO PP47-56.
           MOVE 'Logstream Current Force Waiters. . . . . :'
                                     TO PP79-120.
           MOVE LGSCUFWTRS           TO PP123-132.
           PERFORM WRITE-OUTPUT.
           MOVE 'Logstream Retry Errors . . . . . . . . . :' TO PP3-44.
           MOVE LGSRTYERRS           TO PP47-56.
           MOVE 'Logstream Peak Force Waiters . . . . . . :'
                                     TO PP79-120.
           MOVE LGSPKFWTRS           TO PP123-132.
           PERFORM WRITE-OUTPUT.

       SYSTEM-LOG-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** LOGSTREAM-STATISTICS                                      ***
      ***   Print heading and Logstream statistics.                 ***
      *****************************************************************
       LOGSTREAM-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO LOGSTREAMS-PAGENO.

           MOVE '-' TO PRINT-CONTROL.
           MOVE 'Logstreams - Resource' TO PP1-21.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-21.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      ***   Print the logstream statistics - Pass 1.                ***
      *****************************************************************

           MOVE '0'      TO PRINT-CONTROL.

           EXEC CICS INQUIRE STREAMNAME START
           END-EXEC.

           EXEC CICS INQUIRE STREAMNAME(LOGSTREAM-NAME) NEXT
               USECOUNT(LOGSTREAM-USE-COUNT)
               STATUS(LOGSTREAM-STATUS)
               SYSTEMLOG(SYSTEM-LOG)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

               IF LINE-COUNT > LINES-PER-PAGE THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               IF SUB-HEADINGS = 'Y' THEN
                   PERFORM LOGSTREAM-SUB-HEADINGS-1
               END-IF

               EXEC CICS EXTRACT STATISTICS
                   STREAMNAME RESID(LOGSTREAM-NAME)
                   SET(ADDRESS OF DFHLGSDS)
                   NOHANDLE
               END-EXEC

               MOVE LOGSTREAM-NAME      TO PP3-28
               MOVE LOGSTREAM-USE-COUNT TO PP31-36
               EVALUATE LOGSTREAM-STATUS
                   WHEN DFHVALUE(FAILED)
                       MOVE 'FAILED' TO PP39-44
                   WHEN DFHVALUE(OK)
                       MOVE '  OK  ' TO PP39-44
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               EVALUATE SYSTEM-LOG
                   WHEN DFHVALUE(NOSYSLOG)
                       MOVE 'NO ' TO PP47-49
                   WHEN DFHVALUE(SYSLOG)
                       MOVE 'YES' TO PP47-49
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               IF LGSSTRUC NOT = LOW-VALUES THEN
                   MOVE LGSSTRUC  TO PP52-67
               END-IF
               MOVE LGSMAXBL      TO PP70-79
               EVALUATE TRUE
                   WHEN LGSDOYES
                       MOVE 'YES' TO PP82-84
                   WHEN LGSDONO
                       MOVE 'NO ' TO PP82-84
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               MOVE LGSRETPD      TO PP88-97
               EVALUATE TRUE
                   WHEN LGSADYES
                       MOVE 'YES' TO PP101-103
                   WHEN LGSADNO
                       MOVE 'NO ' TO PP101-103
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               IF SYSTEM-LOG = DFHVALUE(SYSLOG) THEN
                   MOVE LGSDELETES      TO PP108-114N
                   MOVE LGSBRWSTRT      TO PP117-123
                   MOVE LGSBRWREAD      TO PP126-132
               ELSE
                   MOVE 'N/A'           TO PP112-114
                   MOVE 'N/A'           TO PP121-123
                   MOVE 'N/A'           TO PP130-132
               END-IF
               PERFORM WRITE-OUTPUT

               EXEC CICS INQUIRE STREAMNAME(LOGSTREAM-NAME) NEXT
                   USECOUNT(LOGSTREAM-USE-COUNT)
                   STATUS(LOGSTREAM-STATUS)
                   SYSTEMLOG(SYSTEM-LOG)
                   RESP(INQUIRE-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE STREAMNAME END
           END-EXEC.

      *****************************************************************
      ***   Print heading and Logstream statistics.                 ***
      *****************************************************************
           PERFORM PAGE-HEADINGS.

           MOVE '-' TO PRINT-CONTROL.
           MOVE 'Logstreams - Requests' TO PP1-21.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-21.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      ***   Print the logstream statistics - Pass 2.                ***
      *****************************************************************

           MOVE '0'      TO PRINT-CONTROL.

           EXEC CICS INQUIRE STREAMNAME START
           END-EXEC.

           EXEC CICS INQUIRE STREAMNAME(LOGSTREAM-NAME) NEXT
               SYSTEMLOG(SYSTEM-LOG)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

               IF LINE-COUNT > LINES-PER-PAGE THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               IF SUB-HEADINGS = 'Y' THEN
                   PERFORM LOGSTREAM-SUB-HEADINGS-2
               END-IF

               EXEC CICS EXTRACT STATISTICS
                   STREAMNAME RESID(LOGSTREAM-NAME)
                   SET(ADDRESS OF DFHLGSDS)
                   NOHANDLE
               END-EXEC

               MOVE LOGSTREAM-NAME      TO PP3-28
               MOVE LGSWRITES           TO PP31-40
               MOVE LGSBYTES            TO PP43-57
               IF LGSWRITES > 0 THEN
                   COMPUTE RESULT = (LGSBYTES / LGSWRITES)
               ELSE
                   MOVE 0 TO RESULT
               END-IF
               MOVE RESULT              TO PP60-69
               MOVE LGSBUFAPP           TO PP72-81
               MOVE LGSBUFWAIT          TO PP84-93
               MOVE LGSTFCWAIT          TO PP96-102
               MOVE LGSCUFWTRS          TO PP105-111
               MOVE LGSPKFWTRS          TO PP114-120
               MOVE LGSRTYERRS          TO PP123-129
               PERFORM WRITE-OUTPUT

               EXEC CICS INQUIRE STREAMNAME(LOGSTREAM-NAME) NEXT
                   SYSTEMLOG(SYSTEM-LOG)
                   RESP(INQUIRE-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE STREAMNAME END
           END-EXEC.

       JOURNAL-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** JOURNAL-SUB-HEADINGS                                      ***
      *****************************************************************
       JOURNAL-SUB-HEADINGS SECTION.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Journal   Journal   Journal        ' TO PP3-37.
           MOVE '                        Write      ' TO PP38-72.
           MOVE '  Bytes      Average     Buffe'      TO PP73-102.
           MOVE 'r                             '      TO PP103-132.
           PERFORM WRITE-OUTPUT.
           MOVE ' Name     Status     Type    Logstr' TO PP3-37.
           MOVE 'eam Name               Requests    ' TO PP38-72.
           MOVE ' Written      Bytes      Flush'      TO PP73-102.
           MOVE 'es                            '      TO PP103-132.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP3-37
                           PP38-72
                           PP73-102.
           MOVE '___                           '      TO PP103-132.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.
           MOVE 'N'     TO SUB-HEADINGS.

       JOURNAL-SUB-HEADINGS-EXIT.
           EXIT.

      *****************************************************************
      *** LOGSTREAM-SUB-HEADINGS-1                                  ***
      *****************************************************************
       LOGSTREAM-SUB-HEADINGS-1 SECTION.

           MOVE '0' TO PRINT-CONTROL.
           MOVE '                              Use  ' TO PP3-37.
           MOVE '         Sys                     Ma' TO PP38-72.
           MOVE 'x Block  DASD   Retention   Au'      TO PP73-102.
           MOVE 'to   Stream   Browse   Browse '      TO PP103-132.
           PERFORM WRITE-OUTPUT.
           MOVE 'Logstream Name               Count ' TO PP3-37.
           MOVE ' Status  Log  Structure Name      L' TO PP38-72.
           MOVE 'ength    Only    Period    Del'      TO PP73-102.
           MOVE 'ete  Deletes  Starts    Reads '      TO PP103-132.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP3-37
                           PP38-72
                           PP73-102
                           PP103-132.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.
           MOVE 'N'     TO SUB-HEADINGS.

       LOGSTREAM-SUB-HEADINGS-1-EXIT.
           EXIT.

      *****************************************************************
      *** LOGSTREAM-SUB-HEADINGS-2                                  ***
      *****************************************************************
       LOGSTREAM-SUB-HEADINGS-2 SECTION.

           MOVE '0' TO PRINT-CONTROL.
           MOVE '                              Write' TO PP3-37.
           MOVE '                        Average    ' TO PP38-72.
           MOVE ' Buffer      Buffer     Force '      TO PP73-102.
           MOVE '  Current    Peak   Retry     '      TO PP103-132.
           PERFORM WRITE-OUTPUT.
           MOVE 'Logstream Name               Reques' TO PP3-37.
           MOVE 'ts    Bytes Written      Bytes     ' TO PP38-72.
           MOVE ' Appends   Full Waits   Waits '      TO PP73-102.
           MOVE '  Waiters  Waiters  Errors    '      TO PP103-132.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP3-37
                           PP38-72
                           PP73-102.
           MOVE '___________________________   '      TO PP103-132.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.
           MOVE 'N'     TO SUB-HEADINGS.

       LOGSTREAM-SUB-HEADINGS-2-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** CONNECTION-STATISTICS                                     ***
      ***   Print heading and Connection statistics.                ***
      *****************************************************************
       CONNECTION-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO CONNECTIONS-PAGENO.

      *****************************************************************
      ***   Print the connection and modename statistics.           ***
      *****************************************************************

           MOVE '0'      TO PRINT-CONTROL.

           EXEC CICS INQUIRE CONNECTION START
           END-EXEC.

           EXEC CICS INQUIRE CONNECTION(CONNECTION-NAME) NEXT
               ACCESSMETHOD(ACCESS-METHOD)
               CONNTYPE(CONNECTION-TYPE)
               NETNAME(CONNECTION-NETNAME)
               PROTOCOL(PROTOCOL)
               RECEIVECOUNT(RECEIVE-COUNT)
               SENDCOUNT(SEND-COUNT)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

               IF LINE-COUNT > (LINES-PER-PAGE - 41) THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               IF SUB-HEADINGS = 'Y' THEN
                   MOVE '-' TO PRINT-CONTROL
                   MOVE 'Connections' TO PP1-11
                   PERFORM WRITE-OUTPUT
                   MOVE '+' TO PRINT-CONTROL
                   MOVE ALL '_' TO PP1-11
                   PERFORM WRITE-OUTPUT

                   MOVE 'N' TO SUB-HEADINGS
                   MOVE '0' TO PRINT-CONTROL
               ELSE
                   MOVE '-' TO PRINT-CONTROL
               END-IF

               EXEC CICS COLLECT STATISTICS CONNECTION(CONNECTION-NAME)
                   SET(ADDRESS OF DFHA14DS)
                   NOHANDLE
               END-EXEC

               MOVE 'Connection Name/Netname . . . . . :' TO PP3-37
               MOVE CONNECTION-NAME                       TO PP40-43
               MOVE '/'                                   TO PP44
               MOVE CONNECTION-NETNAME                    TO PP45-52
               MOVE 'Access Method/Protocol. . . . . . . . . :'
                                                          TO PP68-108
               EVALUATE ACCESS-METHOD
                   WHEN DFHVALUE(INDIRECT)
                       MOVE 'INDIRECT' TO PP111-118
                   WHEN DFHVALUE(IRC)
                       MOVE ' IRC'     TO PP111-114
                   WHEN DFHVALUE(VTAM)
                       MOVE 'VTAM'     TO PP111-114
                   WHEN DFHVALUE(XCF)
                       MOVE ' XCF'     TO PP111-114
                   WHEN DFHVALUE(XM)
                       MOVE '  XM'     TO PP111-114
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               EVALUATE PROTOCOL
                   WHEN DFHVALUE(APPC)
                       MOVE '/'    TO PP115
                       MOVE 'APPC' TO PP116-119
                   WHEN DFHVALUE(EXCI)
                       MOVE '/'    TO PP115
                       MOVE 'EXCI' TO PP116-119
                   WHEN DFHVALUE(LU61)
                       MOVE '/'    TO PP115
                       MOVE 'LU61' TO PP116-119
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               MOVE 'Autoinstalled Connection Create Time. . :'
                                                          TO PP68-108
               MOVE A14AICT   TO CLOCK-FIELD
               PERFORM STOPWATCH-CONVERT
               MOVE STOPWATCH TO PP111-124
               PERFORM WRITE-OUTPUT

               MOVE 'Peak Contention Losers. . . . . . :' TO PP3-37
               MOVE A14E1HWM                              TO PP40-49
               PERFORM WRITE-OUTPUT
               MOVE 'ATIs satisfied by Losers. . . . . :' TO PP3-37
               MOVE A14ES1                                TO PP40-49
               MOVE 'Receive Session Count . . . . . . . . . :'
                                                          TO PP68-108
               IF RECEIVE-COUNT NOT = -1 THEN
                   MOVE RECEIVE-COUNT TO PP111-120
               ELSE
                   MOVE 0             TO PP111-120
               END-IF
               PERFORM WRITE-OUTPUT
               MOVE 'Peak Contention Winners . . . . . :' TO PP3-37
               MOVE A14E2HWM                              TO PP40-49
               MOVE 'Send Session Count. . . . . . . . . . . :'
                                                          TO PP68-108
               IF SEND-COUNT NOT = -1 THEN
                   MOVE SEND-COUNT    TO PP111-120
               ELSE
                   MOVE 0             TO PP111-120
               END-IF
               PERFORM WRITE-OUTPUT
               MOVE 'ATIs satisfied by Winners . . . . :' TO PP3-37
               MOVE A14ES2                                TO PP40-49
               PERFORM WRITE-OUTPUT
               MOVE 'Current AIDs in chain . . . . . . :' TO PP3-37
               MOVE A14EALL                               TO PP40-49
               MOVE 'Generic AIDs in chain . . . . . . . . . :'
                                                          TO PP68-108
               MOVE A14ESALL                              TO PP111-120
               PERFORM WRITE-OUTPUT
               MOVE 'Peak AIDs in chain. . . . . . . . :'
                                                          TO PP3-37
               MOVE A14EAHWM                              TO PP40-49
               PERFORM WRITE-OUTPUT
               MOVE 'Total number of Bids sent . . . . :' TO PP3-37
               MOVE A14ESBID                              TO PP40-49
               PERFORM WRITE-OUTPUT
               MOVE 'Current Bids in progress. . . . . :' TO PP3-37
               MOVE A14EBID                               TO PP40-49
               MOVE 'Peak Bids in progress . . . . . . . . . :'
                                                          TO PP68-108
               MOVE A14EBHWM                              TO PP111-120
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Total Allocates . . . . . . . . . :' TO PP3-37
               MOVE A14ESTAS                              TO PP40-49
               MOVE 'Allocates per second. . . . . . . . . . :'
                                                          TO PP68-108
               IF ELAPSED-SECONDS > 0 THEN
                   COMPUTE ALLOCATE-RATE = (A14ESTAS / ELAPSED-SECONDS)
                   END-COMPUTE
               ELSE
                   MOVE A14ESTAS  TO ALLOCATE-RATE
               END-IF
               MOVE ALLOCATE-RATE TO PP114-120N
               PERFORM WRITE-OUTPUT
               MOVE 'Allocates Queued. . . . . . . . . :' TO PP3-37
               MOVE A14ESTAQ                              TO PP40-49
               PERFORM WRITE-OUTPUT

               MOVE 'Peak Allocates Queued . . . . . . :' TO PP3-37
               MOVE A14ESTAM                              TO PP40-49
               MOVE 'Allocate Max Queue Time . . . . . . . . :'
                                                          TO PP68-108
      *******************************************************     @01A
      * Check if the Connection Max Queue Time is set to NO,      @01A
      * ie DFHBSS will have set it to -1. If it is then           @01A
      * write No out to the DFH0STAT field. Otherwise             @01A
      * write out the true value.                                 @01A
      *******************************************************     @01A
               IF A14EMXQT = -1 THEN
                   MOVE 'No'                              TO PP119-120
               ELSE
                   MOVE A14EMXQT                          TO PP111-120
               END-IF
               PERFORM WRITE-OUTPUT
               MOVE 'Allocates Failed - Link . . . . . :' TO PP3-37
               MOVE A14ESTAF                              TO PP40-49
               MOVE 'Allocate Queue Limit. . . . . . . . . . :'
                                                          TO PP68-108
      *******************************************************     @01A
      * Check if the Connection Queue Limit is set to NO,         @01A
      * ie DFHBSS will have set it to -1. If it is then           @01A
      * write No out to the DFH0STAT field. Otherwise             @01A
      * write out the true value.                                 @01A
      *******************************************************     @01A
               IF A14EALIM = -1 THEN
                   MOVE 'No'                              TO PP119-120
               ELSE
                  MOVE A14EALIM                           TO PP111-120
               END-IF
               PERFORM WRITE-OUTPUT
               MOVE 'Allocates Failed - Other. . . . . :' TO PP3-37
               MOVE A14ESTAO                              TO PP40-49
               PERFORM WRITE-OUTPUT
               MOVE 'Allocates Rejected - Queue Limit. . . . :'
                                                          TO PP68-108
               MOVE A14EALRJ                              TO PP111-120
               PERFORM WRITE-OUTPUT

               MOVE 'Transaction Routing Requests'        TO PP3-30
               MOVE 'Max Queue Time - Allocate Purge . . . . :'
                                                          TO PP68-108
               MOVE A14EQPCT                              TO PP111-120
               PERFORM WRITE-OUTPUT
               MOVE '+' TO PRINT-CONTROL
               MOVE ALL '_'                               TO PP3-30
               PERFORM WRITE-OUTPUT

               MOVE 'Transaction Routing - Total . . . :' TO PP3-37
               MOVE A14ESTTC                              TO PP40-49
               MOVE 'Allocates Purged - Max Queue Time . . . :'
                                                          TO PP68-108
               MOVE A14EMQPC                              TO PP111-120
               PERFORM WRITE-OUTPUT

               MOVE '  Transaction Routing - Channel . :' TO PP3-37
               MOVE A14ESTTC-CHANNEL                      TO PP40-49
               PERFORM WRITE-OUTPUT

               ADD A14ESTFC, A14ESTIC, A14ESTTD, A14ESTTS, A14ESTPC
                   GIVING TOTAL-FUNCSHIP-REQS
                       ON SIZE ERROR CONTINUE
               END-ADD

               MOVE 'Allocates Rejected - XZIQUE . . . . . . :'
                                                          TO PP68-108
               MOVE A14EZQRJ                              TO PP111-120
               PERFORM WRITE-OUTPUT

               MOVE 'Function Shipping Requests' TO PP3-28
               MOVE 'XZIQUE - Allocate Purge . . . . . . . . :'
                                                          TO PP68-108
               MOVE A14EZQPU                              TO PP111-120
               PERFORM WRITE-OUTPUT
               MOVE '+'     TO PRINT-CONTROL
               MOVE ALL '_'                      TO PP3-28
               PERFORM WRITE-OUTPUT

               MOVE '  File Control. . . . . . . . . . :' TO PP3-37
               MOVE A14ESTFC                              TO PP40-49
               MOVE 'Allocates Purged - XZIQUE . . . . . . . :'
                                                          TO PP68-108
               MOVE A14EZQPC                              TO PP111-120
               PERFORM WRITE-OUTPUT
               MOVE '  Interval Control - Total. . . . :' TO PP3-37
               MOVE A14ESTIC                              TO PP40-49
               PERFORM WRITE-OUTPUT
               MOVE '    Interval Control - Channel. . :' TO PP3-37
               MOVE A14ESTIC-CHANNEL                      TO PP40-49
               PERFORM WRITE-OUTPUT
               MOVE '  Transient Data. . . . . . . . . :' TO PP3-37
               MOVE A14ESTTD                              TO PP40-49
               PERFORM WRITE-OUTPUT
               MOVE '  Temporary Storage . . . . . . . :' TO PP3-37
               MOVE A14ESTTS                              TO PP40-49
               PERFORM WRITE-OUTPUT
               MOVE '  Program Control - Total . . . . :' TO PP3-37
               MOVE A14ESTPC                              TO PP40-49
               PERFORM WRITE-OUTPUT
               MOVE '    Program Control - Channel . . :' TO PP3-37
               MOVE A14ESTPC-CHANNEL                      TO PP40-49
               PERFORM WRITE-OUTPUT

               MOVE '+'     TO PRINT-CONTROL
               MOVE ALL '_' TO PP3-49
               PERFORM WRITE-OUTPUT
               MOVE 'Total . . . . . . . . . . . . . . :' TO PP3-37
               MOVE TOTAL-FUNCSHIP-REQS                   TO PP40-49
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Bytes Sent by Transaction Routing requests . . :'
                                                          TO PP3-50
               MOVE A14ESTTC-CHANNEL-SENT                 TO PP53-62
               MOVE 'Average Bytes Sent by Routing requests. :'
                                                          TO PP68-108
               IF A14ESTTC-CHANNEL > 0 THEN
                   COMPUTE RESULT =
                       (A14ESTTC-CHANNEL-SENT / A14ESTTC-CHANNEL)
                   END-COMPUTE
               ELSE
                   MOVE 0 TO RESULT
               END-IF
               MOVE RESULT                                TO PP111-120
               PERFORM WRITE-OUTPUT
               MOVE 'Bytes Received by Transaction Routing requests :'
                                                          TO PP3-50
               MOVE A14ESTTC-CHANNEL-RCVD                 TO PP53-62
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Bytes Sent by Program Channel requests . . . . :'
                                                          TO PP3-50
               MOVE A14ESTPC-CHANNEL-SENT                 TO PP53-62
               MOVE 'Average Bytes Sent by Channel request . :'
                                                          TO PP68-108
               IF A14ESTPC-CHANNEL > 0 THEN
                   COMPUTE RESULT =
                       (A14ESTPC-CHANNEL-SENT / A14ESTPC-CHANNEL)
                   END-COMPUTE
               ELSE
                   MOVE 0 TO RESULT
               END-IF
               MOVE RESULT                                TO PP111-120
               PERFORM WRITE-OUTPUT
               MOVE 'Bytes Received by Program Channel requests . . :'
                                                          TO PP3-50
               MOVE A14ESTPC-CHANNEL-RCVD                 TO PP53-62
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Bytes Sent by Interval Channel requests. . . . :'
                                                          TO PP3-50
               MOVE A14ESTIC-CHANNEL-SENT                 TO PP53-62
               MOVE 'Average Bytes Sent by Channel request . :'
                                                          TO PP68-108
               IF A14ESTIC-CHANNEL > 0 THEN
                   COMPUTE RESULT =
                       (A14ESTIC-CHANNEL-SENT / A14ESTIC-CHANNEL)
                   END-COMPUTE
               ELSE
                   MOVE 0 TO RESULT
               END-IF
               MOVE RESULT                                TO PP111-120
               PERFORM WRITE-OUTPUT
               MOVE 'Bytes Received by Interval Channel requests. . :'
                                                          TO PP3-50
               MOVE A14ESTIC-CHANNEL-RCVD                 TO PP53-62
               PERFORM WRITE-OUTPUT

      *---------------------------------------------------------------*
               IF PROTOCOL = DFHVALUE(APPC) THEN
                   PERFORM PRINT-MODENAME-STATISTICS
               END-IF

               MOVE '0' TO PRINT-CONTROL
               MOVE ALL '-' TO PP3-132
               PERFORM WRITE-OUTPUT

               EXEC CICS INQUIRE CONNECTION(CONNECTION-NAME) NEXT
                   ACCESSMETHOD(ACCESS-METHOD)
                   CONNTYPE(CONNECTION-TYPE)
                   NETNAME(CONNECTION-NETNAME)
                   PROTOCOL(PROTOCOL)
                   RECEIVECOUNT(RECEIVE-COUNT)
                   SENDCOUNT(SEND-COUNT)
                   RESP(INQUIRE-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE CONNECTION END
           END-EXEC.

       CONNECTION-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** MODENAME-STATISTICS                                       ***
      ***   Print the heading and the modename statistics for a     ***
      *** specified connection.                                     ***
      *****************************************************************
       PRINT-MODENAME-STATISTICS SECTION.

           IF LINE-COUNT > (LINES-PER-PAGE - 10) THEN
               PERFORM PAGE-HEADINGS
           END-IF.

           MOVE '-'         TO PRINT-CONTROL.
           MOVE 'Modenames' TO PP5-13.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP5-13.
           PERFORM WRITE-OUTPUT.

           MOVE CONNECTION-NAME TO MODENAME-CONNECTION.

           EXEC CICS INQUIRE MODENAME START
           END-EXEC.

           EXEC CICS INQUIRE MODENAME(MODE-NAME) NEXT
               CONNECTION(MODENAME-CONNECTION)
               ACTIVE(ACTIVE-SESSIONS)
               AVAILABLE(AVAILABLE-SESSIONS)
               MAXIMUM(MAXIMUM-SESSIONS)
               MAXWINNERS(MAXIMUM-WINNERS)
               RESP(INQUIRE-MODENAME-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-MODENAME-RESP = DFHRESP(END) OR
                         MODENAME-CONNECTION NOT = CONNECTION-NAME

               IF LINE-COUNT > (LINES-PER-PAGE - 7) THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Modename Connection Name. . . . . :' TO PP7-41
               MOVE MODENAME-CONNECTION                   TO PP44-47
               PERFORM WRITE-OUTPUT
               MOVE 'Modename. . . . . . . . . . . . . :' TO PP7-41
               MOVE MODE-NAME                             TO PP44-51
               PERFORM WRITE-OUTPUT
               MOVE 'Active Sessions . . . . . . . . . :' TO PP7-41
               MOVE ACTIVE-SESSIONS                       TO PP44-53
               PERFORM WRITE-OUTPUT
               MOVE 'Available Sessions. . . . . . . . :' TO PP7-41
               MOVE AVAILABLE-SESSIONS                    TO PP44-53
               PERFORM WRITE-OUTPUT
               MOVE 'Maximum Sessions. . . . . . . . . :' TO PP7-41
               MOVE MAXIMUM-SESSIONS                      TO PP44-53
               PERFORM WRITE-OUTPUT
               MOVE 'Maximum Contention Winners. . . . :' TO PP7-41
               MOVE MAXIMUM-WINNERS                       TO PP44-53
               PERFORM WRITE-OUTPUT

               EXEC CICS INQUIRE MODENAME(MODE-NAME) NEXT
                   CONNECTION(MODENAME-CONNECTION)
                   ACTIVE(ACTIVE-SESSIONS)
                   AVAILABLE(AVAILABLE-SESSIONS)
                   MAXIMUM(MAXIMUM-SESSIONS)
                   MAXWINNERS(MAXIMUM-WINNERS)
                   RESP(INQUIRE-MODENAME-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE MODENAME END
           END-EXEC.

           MOVE 'Y' TO SUB-HEADINGS.

       PRINT-MODENAME-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** PGM-AUTOINSTALL-STATISTICS                                ***
      ***   Print headings and Program Autoinstall statistics       ***
      *****************************************************************
       PGM-AUTOINSTALL-STATS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO PGM-AUTOINSTALL-PAGENO.

      *****************************************************************
      *** Program Autoinstall statistics.                           ***
      *****************************************************************
           EXEC CICS INQUIRE SYSTEM
               PROGAUTOCTLG(PROGAUTO-CATLG)
               PROGAUTOINST(PROGRAM-AUTOINSTALL)
               PROGAUTOEXIT(AUTOINSTALL-PROGRAM)
           END-EXEC.

           EXEC CICS EXTRACT STATISTICS PROGAUTO
               SET(ADDRESS OF DFHPGGDS)
               NOHANDLE
           END-EXEC.

           MOVE '-' TO PRINT-CONTROL.
           MOVE 'Program Autoinstall' TO PP1-19.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-19.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Program Autoinstall Status. . . . :' TO PP3-37.
           EVALUATE PROGRAM-AUTOINSTALL
               WHEN DFHVALUE(AUTOACTIVE)
                   MOVE 'ACTIVE  ' TO PP40-47
               WHEN DFHVALUE(AUTOINACTIVE)
                   MOVE 'INACTIVE' TO PP40-47
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           PERFORM WRITE-OUTPUT.
           MOVE 'Autoinstall Program . . . . . . . :' TO PP3-37.
           MOVE AUTOINSTALL-PROGRAM                   TO PP40-47.
           PERFORM WRITE-OUTPUT.
           MOVE 'Catalog Program Definitions . . . :' TO PP3-37.
           EVALUATE PROGAUTO-CATLG
               WHEN DFHVALUE(CTLGALL)
                   MOVE 'ALL      ' TO PP40-47
               WHEN DFHVALUE(CTLGMODIFY)
                   MOVE 'MODIFY   ' TO PP40-47
               WHEN DFHVALUE(CTLGNONE)
                   MOVE 'NONE     ' TO PP40-47
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Autoinstalls attempted. . . . . . :' TO PP3-37.
           MOVE PGGATT                                TO PP40-49.
           PERFORM WRITE-OUTPUT.
           MOVE 'Autoinstalls rejected . . . . . . :' TO PP3-37.
           MOVE PGGREJ                                TO PP40-49.
           PERFORM WRITE-OUTPUT.
           MOVE 'Autoinstalls failed . . . . . . . :' TO PP3-37.
           MOVE PGGFAIL                               TO PP40-49.
           PERFORM WRITE-OUTPUT.

       PGM-AUTOINSTALL-STATS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** AUTOINSTALL-VTAM-STATISTICS                               ***
      ***   Print headings, Terminal Autoinstall and VTAM           ***
      *** statistics.                                               ***
      *****************************************************************
       AUTOINSTALL-VTAM-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO AUTOINSTALL-PAGENO.

      *****************************************************************
      *** Terminal Autoinstall statistics.                          ***
      *****************************************************************
           EXEC CICS INQUIRE AUTOINSTALL
               AIBRIDGE(AUTOINSTALL-BRIDGE)
               CONSOLES(AUTOINSTALL-CONSOLES)
               CURREQS(CURRENT-REQUESTS)
               ENABLESTATUS(AUTOINSTALL-STATUS)
               MAXREQS(MAXIMUM-REQUESTS)
               PROGRAM(AUTOINSTALL-PROGRAM)
           END-EXEC.

           EXEC CICS COLLECT STATISTICS AUTOINSTALL
               SET(ADDRESS OF DFHA04DS)
               NOHANDLE
           END-EXEC.

      *****************************************************************
      *** Print the Terminal Autoinstall statistics.                ***
      *****************************************************************
           MOVE '-' TO PRINT-CONTROL.
           MOVE 'Terminal Autoinstall - Local Terminals' TO PP1-38.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-38.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Terminal Autoinstall Status . . . :' TO PP3-37.
           EVALUATE AUTOINSTALL-STATUS
               WHEN DFHVALUE(ENABLED)
                   MOVE 'ENABLED ' TO PP40-47
               WHEN DFHVALUE(DISABLED)
                   MOVE 'DISABLED' TO PP40-47
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           PERFORM WRITE-OUTPUT.
           MOVE 'Bridge Autoinstall. . . . . . . . :' TO PP3-37.
           EVALUATE AUTOINSTALL-BRIDGE
               WHEN DFHVALUE(AUTOTERMID)
                   MOVE 'AUTO    ' TO PP40-47
               WHEN DFHVALUE(URMTERMID)
                   MOVE 'URM     ' TO PP40-47
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           PERFORM WRITE-OUTPUT.
           MOVE 'Console Autoinstall . . . . . . . :' TO PP3-37.
           EVALUATE AUTOINSTALL-CONSOLES
               WHEN DFHVALUE(PROGAUTO)
                   MOVE 'PROGRAM ' TO PP40-47
               WHEN DFHVALUE(FULLAUTO)
                   MOVE 'FULL    ' TO PP40-47
               WHEN DFHVALUE(NOAUTO)
                   MOVE 'NO      ' TO PP40-47
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           PERFORM WRITE-OUTPUT.
           MOVE 'Autoinstall Program . . . . . . . :' TO PP3-37.
           MOVE AUTOINSTALL-PROGRAM                   TO PP40-47.
           PERFORM WRITE-OUTPUT.
           MOVE 'Current Autoinstall Requests. . . :' TO PP3-37.
           MOVE CURRENT-REQUESTS                      TO PP40-49.
           PERFORM WRITE-OUTPUT.
           MOVE 'Peak Autoinstall Requests . . . . :' TO PP3-37.
           MOVE MAXIMUM-REQUESTS                      TO PP40-49.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Autoinstalls Attempted. . . . . . :' TO PP3-37.
           MOVE A04VADAT                              TO PP40-49.
           PERFORM WRITE-OUTPUT.
           MOVE 'Autoinstalls Rejected . . . . . . :' TO PP3-37.
           MOVE A04VADRJ                              TO PP40-49.
           PERFORM WRITE-OUTPUT.
           MOVE 'Autoinstalls Deleted. . . . . . . :' TO PP3-37.
           MOVE A04VADLO                              TO PP40-49.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Peak Concurrent Autoinstalls. . . :' TO PP3-37.
           MOVE A04VADPK                              TO PP40-49.
           PERFORM WRITE-OUTPUT.
           MOVE 'Times Peak Concurrent reached . . :' TO PP3-37.
           MOVE A04VADPX                              TO PP40-49.
           PERFORM WRITE-OUTPUT.
           MOVE 'Times SETLOGON HOLD issued. . . . :' TO PP3-37.
           MOVE A04VADSH                              TO PP40-49.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Number of Queued Logons . . . . . :' TO PP3-37.
           MOVE A04VADQT                              TO PP40-49.
           PERFORM WRITE-OUTPUT.
           MOVE 'Peak Number of Queued Logons. . . :' TO PP3-37.
           MOVE A04VADQK                              TO PP40-49.
           PERFORM WRITE-OUTPUT.
           MOVE 'Times Peak Queued Logons reached. :' TO PP3-37.
           MOVE A04VADQX                              TO PP40-49.
           PERFORM WRITE-OUTPUT.

           MOVE '-' TO PRINT-CONTROL.
           MOVE 'Terminal Autoinstall - Shipped Terminals' TO PP1-40.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-40.
           PERFORM WRITE-OUTPUT.

      *     Remote terminal statistics - shipped definitions
      *
           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Delete shipped definitions interval. . . :' TO PP3-44.
           MOVE A04RDINT TO PP47-54.
           INSPECT PP47-54 REPLACING ALL ' ' BY ':'.
           PERFORM WRITE-OUTPUT.
           MOVE 'Delete shipped definitions Idle time . . :' TO PP3-44.
           MOVE A04RDIDL TO PP47-54.
           INSPECT PP47-54 REPLACING ALL ' ' BY ':'.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Shipped remote terminals built . . . . . :' TO PP3-44.
           MOVE A04SKBLT TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Shipped remote terminals installed . . . :' TO PP3-44.
           MOVE A04SKINS TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Shipped remote terminals deleted . . . . :' TO PP3-44.
           MOVE A04SKDEL TO PP47-56.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Times remote delete interval expired . . :' TO PP3-44.
           MOVE A04TIEXP TO PP47-56.

           MOVE '0' TO PRINT-CONTROL.
           PERFORM WRITE-OUTPUT.
           MOVE 'Remote terminal deletes received . . . . :' TO PP3-44.
           MOVE A04RDREC TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Remote terminal deletes issued . . . . . :' TO PP3-44.
           MOVE A04RDISS TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Successful remote terminal deletes . . . :' TO PP3-44.
           MOVE A04RDDEL TO PP47-56.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Current idle terminals awaiting reuse. . :' TO PP3-44.
           MOVE A04CIDCT TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Current idle time awaiting reuse . . . . :' TO PP3-44.
           MOVE A04CIDLE  TO CLOCK-FIELD
           PERFORM STOPWATCH-CONVERT.
           MOVE STOPWATCH TO PP47-60.
           PERFORM WRITE-OUTPUT.
           MOVE 'Current maximum idle time awaiting reuse :' TO PP3-44.
           MOVE A04CMAXI  TO CLOCK-FIELD
           PERFORM STOPWATCH-CONVERT.
           MOVE STOPWATCH TO PP47-60.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Total idle terminal count awaiting reuse :' TO PP3-44.
           MOVE A04TIDCT TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Total idle time awaiting reuse . . . . . :' TO PP3-44.
           MOVE A04TIDLE  TO CLOCK-FIELD
           PERFORM STOPWATCH-CONVERT.
           MOVE STOPWATCH TO PP47-60.
           PERFORM WRITE-OUTPUT.
           MOVE 'Average idle time awaiting reuse . . . . :' TO PP3-44.
           IF A04TIDCT > 0 THEN
               MOVE A04TIDLE TO CLOCK-WORK-FIELD
               COMPUTE CLOCK-FIELD-S = (CLOCK-WORK-FIELD-S / A04TIDCT)
               END-COMPUTE
           ELSE
               MOVE 0 TO CLOCK-FIELD-S
           END-IF.
           PERFORM STOPWATCH-CONVERT.
           MOVE STOPWATCH TO PP47-60.
           PERFORM WRITE-OUTPUT.
           MOVE 'Maximum idle time awaiting reuse . . . . :' TO PP3-44.
           MOVE A04TMAXI  TO CLOCK-FIELD
           PERFORM STOPWATCH-CONVERT.
           MOVE STOPWATCH TO PP47-60.
           PERFORM WRITE-OUTPUT.
           EJECT
      *****************************************************************
      *** Print the VTAM Global statistics.                         ***
      *****************************************************************
           PERFORM PAGE-HEADINGS.
           MOVE PAGENO  TO VTAM-PAGENO.

           MOVE '-'     TO PRINT-CONTROL.
           MOVE 'VTAM'  TO PP1-4.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-4.
           PERFORM WRITE-OUTPUT.

           EXEC CICS INQUIRE VTAM
               OPENSTATUS(VTAM-OPEN-STATUS)
               GRNAME(GENERIC-RESOURCE-NAME)
               GRSTATUS(GENERIC-RESOURCE-STATUS)
               PSTYPE(VTAM-PSTYPE)
               PSDINTERVAL(VTAM-PSDINTERVAL)
           END-EXEC.

           EXEC CICS COLLECT STATISTICS VTAM
               SET(ADDRESS OF DFHA03DS)
               NOHANDLE
           END-EXEC.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'VTAM Open Status. . . . . . . . . :' TO PP3-37.
           EVALUATE VTAM-OPEN-STATUS
               WHEN DFHVALUE(OPEN)
                   MOVE 'OPEN       ' TO PP40-50
               WHEN DFHVALUE(CLOSED)
                   MOVE 'CLOSED     ' TO PP40-50
               WHEN DFHVALUE(IMMCLOSE)
                   MOVE 'IMMCLOSED  ' TO PP40-50
               WHEN DFHVALUE(FORCECLOSE)
                   MOVE 'FORCECLOSED' TO PP40-50
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           PERFORM WRITE-OUTPUT.
           MOVE 'Dynamic open count. . . . . . . . :' TO PP3-37.
           MOVE A03DOC                                TO PP40-49.
           PERFORM WRITE-OUTPUT.
           MOVE 'VTAM Short-on-Storage . . . . . . :' TO PP3-37.
           MOVE A03VTSOS                              TO PP40-49.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'MAX RPLs. . . . . . . . . . . . . :' TO PP3-37.
           MOVE A03RPLX                               TO PP40-49.
           PERFORM WRITE-OUTPUT.
           MOVE 'Times at MAX RPLs . . . . . . . . :' TO PP3-37.
           MOVE A03RPLXT                              TO PP40-49.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Current LUs in session. . . . . . :' TO PP3-37.
           MOVE A03LUNUM                              TO PP40-49.
           PERFORM WRITE-OUTPUT.
           MOVE 'Peak LUs in session . . . . . . . :' TO PP3-37.
           MOVE A03LUHWM                              TO PP40-49.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Generic Resource name . . . . . . :' TO PP3-37.
           MOVE GENERIC-RESOURCE-NAME                 TO PP40-47.
           PERFORM WRITE-OUTPUT.
           MOVE 'Generic Resource status . . . . . :' TO PP3-37.
           EVALUATE GENERIC-RESOURCE-STATUS
               WHEN DFHVALUE(REGISTERED)
                   MOVE 'REGISTERED  ' TO PP40-51
               WHEN DFHVALUE(UNREGISTERED)
                   MOVE 'UNREGISTERED' TO PP40-51
               WHEN DFHVALUE(DEREGISTERED)
                   MOVE 'DEREGISTERED' TO PP40-51
               WHEN DFHVALUE(UNAVAILABLE)
                   MOVE 'UNAVAILABLE ' TO PP40-51
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           PERFORM WRITE-OUTPUT.

      * @DCA - Added display of PSTYPE and PSDINTERVAL - start
           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Persistent Session Type. . . . .  :' TO PP3-37.
           EVALUATE VTAM-PSTYPE
               WHEN DFHVALUE(NOPS)
                   MOVE 'NOPS' TO PP40-43
               WHEN DFHVALUE(SNPS)
                   MOVE 'SNPS' TO PP40-43
               WHEN DFHVALUE(MNPS)
                   MOVE 'MNPS' TO PP40-43
               WHEN OTHER
                   MOVE '????' TO PP40-43
           END-EVALUATE.
           PERFORM WRITE-OUTPUT.

           MOVE 'Persistent Session Interval. . .  :' TO PP3-37.
           MOVE VTAM-PSDINTERVAL                      TO PP42-49.
           INSPECT PP42-49 REPLACING ALL ' ' BY ':'.
           PERFORM WRITE-OUTPUT.
      * @DCA - Added display of PSTYPE and PSDINTERVAL - end

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Persistent Session Inquire count. :' TO PP3-37.
           MOVE A03PSIC                               TO PP40-49.
           PERFORM WRITE-OUTPUT.
           MOVE 'Persistent Session NIB count. . . :' TO PP3-37.
           MOVE A03PSNC                               TO PP40-49.
           PERFORM WRITE-OUTPUT.
           MOVE 'Persistent Session Opndst count . :' TO PP3-37.
           MOVE A03PSOC                               TO PP40-49.
           PERFORM WRITE-OUTPUT.
           MOVE 'Persistent Session Unbind count . :' TO PP3-37.
           MOVE A03PSUC                               TO PP40-49.
           PERFORM WRITE-OUTPUT.
           MOVE 'Persistent Session Error count. . :' TO PP3-37.
           MOVE A03PSEC                               TO PP40-49.
           PERFORM WRITE-OUTPUT.

      * @R120476 - Add BMS 3270 Statistics - start
           MOVE '0' TO PRINT-CONTROL.
           MOVE 'BMS 3270 Validation . . . . . . . :' TO PP3-37.
           IF A03VON THEN
               MOVE 'ON ' TO PP40-42
           ELSE
               MOVE 'OFF' TO PP40-42
           END-IF.
           PERFORM WRITE-OUTPUT.
           MOVE 'Validation Failures Ignored count :' TO PP3-37.
           MOVE A03BMIG                               TO PP40-49.
           PERFORM WRITE-OUTPUT.
           MOVE 'Validation Failures Logged count  :' TO PP3-37.
           MOVE A03BMLG                               TO PP40-49.
           PERFORM WRITE-OUTPUT.
           MOVE 'Validation Failures Abended count :' TO PP3-37.
           MOVE A03BMAB                               TO PP40-49.
           PERFORM WRITE-OUTPUT.
      * @R120476 - Add BMS 3270 Statistics - end

       AUTOINST-VTAM-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** TCPIP-STATISTICS                                          ***
      ***                                                           ***
      *****************************************************************
       TCPIP-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO TCPIP-PAGENO.

           EXEC CICS INQUIRE DISPATCHER
               ACTSSLTCBS(ACTIVE-SSL-TCBS)
               MAXSSLTCBS(MAX-SSL-TCBS)
           END-EXEC.

           EXEC CICS INQUIRE TCPIP
               ACTSOCKETS(ACTIVE-SOCKETS)
               MAXSOCKETS(MAX-SOCKETS)
               OPENSTATUS(TCPIP-OPEN-STATUS)
               SSLCACHE(SSL-CACHE)
           END-EXEC.

           EXEC CICS INQUIRE WEB
               GARBAGEINT(GARBAGE-COLLECTION-INT)
               TIMEOUTINT(TERMINAL-INPUT-TIMEOUT-INT)
           END-EXEC.

      *****************************************************************
      *** Collect the related Dispatcher statistics.                ***
      *****************************************************************
           EXEC CICS EXTRACT STATISTICS DISPATCHER
               SET(ADDRESS OF DFHDSGDS)
           END-EXEC.

      *****************************************************************
      *** Print headings and collect the TCP/IP global statistics.  ***
      *****************************************************************
           MOVE '-'      TO PRINT-CONTROL.
           MOVE 'TCP/IP' TO PP1-6.
           PERFORM WRITE-OUTPUT.
           MOVE '+'      TO PRINT-CONTROL.
           MOVE ALL '_'  TO PP1-6.
           PERFORM WRITE-OUTPUT.

           EXEC CICS EXTRACT STATISTICS TCPIP
               SET(ADDRESS OF DFHSOGDS)
               NOHANDLE
           END-EXEC.

      *****************************************************************
      ***   Print the TCP/IP global statistics.                     ***
      *****************************************************************
           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'TCP/IP Status. . . . . . . . . . . . . . . . . :' TO
           PP3-50.
           EVALUATE TCPIP-OPEN-STATUS
               WHEN DFHVALUE(OPEN)
                   MOVE '        OPEN' TO PP53-64
               WHEN DFHVALUE(CLOSED)
                   MOVE '      CLOSED' TO PP53-64
               WHEN DFHVALUE(CLOSING)
                   MOVE '     CLOSING' TO PP53-64
               WHEN DFHVALUE(IMMCLOSING)
                   MOVE '  IMMCLOSING' TO PP53-64
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'SSLCACHE setting . . . . . . . . . . . . . . . :' TO
           PP3-50.
           EVALUATE TRUE
               WHEN SOG-SSLCACHE-CICS
                   MOVE '        CICS' TO PP53-64
               WHEN SOG-SSLCACHE-SYSPLEX
                   MOVE '     SYSPLEX' TO PP53-64
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Active SSL TCBs. . . . . . . . . . . . . . . . :' TO
           PP3-50.
           MOVE ACTIVE-SSL-TCBS                       TO PP55-64.
           MOVE 'Maximum SSL TCBs (MAXSSLTCBS). . . :' TO
           PP69-104.
           MOVE MAX-SSL-TCBS                          TO PP109-118.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Max IP Sockets (MAXSOCKETS) limit. . . . . . . :' TO
           PP3-50.
           MOVE SOG-MAXSOCKETS-LIMIT                  TO PP55-64.
           PERFORM WRITE-OUTPUT.
           MOVE 'Times the MAXSOCKETS limit was reached . . . . :' TO
           PP3-50.
           MOVE SOG-TIMES-AT-MAXSOCKETS               TO PP55-64.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Current Active IP Sockets. . . . . . . . . . . :' TO
           PP3-50.
           MOVE ACTIVE-SOCKETS                        TO PP55-64.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Current number of inbound sockets. . . . . . . :' TO
           PP3-50.
           MOVE SOG-CURR-INBOUND-SOCKETS              TO PP55-64.
           PERFORM WRITE-OUTPUT.
           MOVE 'Current non-persistent inbound sockets . . . . :' TO
           PP3-50.
           MOVE SOG-CURR-NPERS-INB-SOCKETS            TO PP55-64.
           MOVE 'Current persistent inbound sockets :' TO
           PP69-104.
           SUBTRACT SOG-CURR-NPERS-INB-SOCKETS FROM
               SOG-CURR-INBOUND-SOCKETS GIVING CURR-PERS-INB-SOCKETS.
           MOVE CURR-PERS-INB-SOCKETS                 TO PP109-118.
           PERFORM WRITE-OUTPUT.

           MOVE 'Peak number of inbound sockets . . . . . . . . :' TO
           PP3-50.
           MOVE SOG-PEAK-INBOUND-SOCKETS              TO PP55-64.
           PERFORM WRITE-OUTPUT.

           MOVE 'Peak non-persistent inbound sockets. . . . . . :' TO
           PP3-50.
           MOVE SOG-PEAK-NPERS-INB-SOCKETS            TO PP55-64.
           MOVE 'Peak persistent inbound sockets. . :' TO
           PP69-104.
           MOVE SOG-PEAK-PERS-INB-SOCKETS             TO PP109-118.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Number of inbound sockets created. . . . . . . :' TO
           PP3-50.
           MOVE SOG-INBOUND-SOCKETS-CREATED           TO PP55-64.
           PERFORM WRITE-OUTPUT.

           MOVE 'Non-persistent inbound sockets created . . . . :' TO
           PP3-50.
           MOVE SOG-NPERS-INB-SOCKETS-CREATED         TO PP55-64.
           MOVE 'Persistent inbound sockets created :' TO
           PP69-104.
           COMPUTE TOTAL-PERS-INB-SOCKETS =
               SOG-INBOUND-SOCKETS-CREATED -
                   SOG-NPERS-INB-SOCKETS-CREATED
           END-COMPUTE.
           MOVE TOTAL-PERS-INB-SOCKETS                 TO PP109-118.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Current number of outbound sockets . . . . . . :' TO
           PP3-50.
           COMPUTE CURR-OUTB-SOCKETS =
               SOG-CURR-OUTBOUND-SOCKETS + SOG-CURR-PERS-OUTB-SOCKETS
           END-COMPUTE
           MOVE CURR-OUTB-SOCKETS                     TO PP55-64.
           PERFORM WRITE-OUTPUT.

           MOVE 'Current non-persistent outbound sockets. . . . :' TO
           PP3-50.
           MOVE SOG-CURR-OUTBOUND-SOCKETS             TO PP55-64.
           MOVE 'Current persistent outbound sockets:' TO
           PP69-104.
           MOVE SOG-CURR-PERS-OUTB-SOCKETS            TO PP109-118.
           PERFORM WRITE-OUTPUT.

           MOVE 'Peak number of outbound sockets. . . . . . . . :' TO
           PP3-50.
           MOVE SOG-PEAK-BOTH-OUTB-SOCKETS            TO PP55-64.
           PERFORM WRITE-OUTPUT.

           MOVE 'Peak non-persistent outbound sockets . . . . . :' TO
           PP3-50.
           MOVE SOG-PEAK-OUTBOUND-SOCKETS             TO PP55-64.
           MOVE 'Peak persistent outbound sockets . :' TO
           PP69-104.
           MOVE SOG-PEAK-PERS-OUTB-SOCKETS            TO PP109-118.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Total times pooled sockets reused. . . . . . . :' TO
           PP3-50.
           MOVE SOG-TIMES-OUTB-REUSED                 TO PP55-64.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Number of outbound sockets created . . . . . . :' TO
           PP3-50.
           MOVE SOG-OUTBOUND-SOCKETS-CREATED          TO PP55-64.
           MOVE 'Persistent outbound sockets created:' TO
           PP69-104.
           MOVE SOG-PERS-OUTBOUND-CREATED             TO PP109-118.
           PERFORM WRITE-OUTPUT.

           MOVE 'Number of outbound sockets closed. . . . . . . :' TO
           PP3-50.
           MOVE SOG-OUTBOUND-SOCKETS-CLOSED           TO PP55-64.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Total inbound and outbound sockets created . . :' TO
           PP3-50.
           COMPUTE TOTAL-SOCKETS-CREATED =
                       (SOG-INBOUND-SOCKETS-CREATED +
                                   SOG-OUTBOUND-SOCKETS-CREATED).
           MOVE TOTAL-SOCKETS-CREATED TO PP55-64.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Create socket requests delayed by MAXSOCKETS . :' TO
           PP3-50.
           MOVE SOG-DELAYED-AT-MAXSOCKETS             TO PP55-64.
           PERFORM WRITE-OUTPUT.

           MOVE 'Total MAXSOCKETS delay time. . . . . . . . . . :' TO
           PP3-50.
           MOVE SOG-QTIME-AT-MAXSOCKETS TO CLOCK-FIELD.
           PERFORM STOPWATCH-CONVERT.
           MOVE STOPWATCH TO PP53-64.
           MOVE 'Average MAXSOCKETS delay time. . . :' TO
           PP69-104.
           IF SOG-DELAYED-AT-MAXSOCKETS > 0 THEN
               MOVE SOG-QTIME-AT-MAXSOCKETS TO CLOCK-WORK-FIELD
               COMPUTE CLOCK-FIELD-S =
                   (CLOCK-WORK-FIELD-S / SOG-DELAYED-AT-MAXSOCKETS)
               END-COMPUTE
           ELSE
               MOVE 0 TO CLOCK-FIELD-S
           END-IF.
           PERFORM STOPWATCH-CONVERT.
           MOVE STOPWATCH TO PP107-118.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Create requests that timed-out at MAXSOCKETS . :' TO
           PP3-50.
           MOVE SOG-TIMEDOUT-AT-MAXSOCKETS            TO PP55-64.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Current create requests delayed by MAXSOCKETS. :' TO
           PP3-50.
           MOVE SOG-CURR-DELAYED-AT-MAX               TO PP55-64.
           PERFORM WRITE-OUTPUT.

           MOVE 'Peak create socket reqs delayed by MAXSOCKETS. :' TO
           PP3-50.
           MOVE SOG-PEAK-DELAYED-AT-MAX               TO PP55-64.
           PERFORM WRITE-OUTPUT.

           MOVE 'Total delay time for current create requests . :' TO
           PP3-50.
           MOVE SOG-CURRENT-QTIME-AT-MAX TO CLOCK-FIELD.
           PERFORM STOPWATCH-CONVERT.
           MOVE STOPWATCH TO PP53-64.
           MOVE 'Average delay for current requests :' TO
           PP69-104.
           IF SOG-CURR-DELAYED-AT-MAX > 0 THEN
               MOVE SOG-CURRENT-QTIME-AT-MAX TO CLOCK-WORK-FIELD
               COMPUTE CLOCK-FIELD-S =
                   (CLOCK-WORK-FIELD-S / SOG-CURR-DELAYED-AT-MAX)
               END-COMPUTE
           ELSE
               MOVE 0 TO CLOCK-FIELD-S
           END-IF.
           PERFORM STOPWATCH-CONVERT.
           MOVE STOPWATCH TO PP107-118.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Performance tuning for HTTP connections. . . . :' TO
           PP3-50.
           EVALUATE TRUE
               WHEN SOG-SOTUNING-YES
                   MOVE '         YES' TO PP53-64
               WHEN SOG-SOTUNING-V520
                   MOVE '          NO' TO PP53-64
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           PERFORM WRITE-OUTPUT.

           MOVE 'Listener pausing listening for HTTP connections:' TO
           PP3-50.
           EVALUATE TRUE
               WHEN SOG-PAUSING-LISTENING-ON
                   MOVE '         YES' TO PP53-64
               WHEN SOG-PAUSING-LISTENING-OFF
                   MOVE '          NO' TO PP53-64
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           PERFORM WRITE-OUTPUT.

           MOVE 'Times listener notified at task accept limit . :' TO
           PP3-50.
           MOVE SOG-TIMES-AT-ACCEPT-LIMIT             TO PP55-64.
           MOVE 'Time last paused listening . . . . :' TO
           PP69-104.
           IF SOG-TIME-LAST-PAUSED-LISTENING NOT = LOW-VALUES THEN
             MOVE SOG-TIME-LAST-PAUSED-LISTENING TO CLOCK-FIELD
             PERFORM TIMESTAMP-CONVERT
             MOVE MMDDYYYY                            TO PP107-116
             MOVE STOPWATCH                           TO PP119-132
           ELSE
               MOVE NULL-DATE-FIELD                   TO PP107-116
               MOVE NULL-TIME-FIELD                   TO PP119-132
           END-IF.
           PERFORM WRITE-OUTPUT

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Region stopping HTTP connection persistence. . :' TO
           PP3-50.
           EVALUATE TRUE
               WHEN SOG-STOPPING-PERSISTENCE-ON
                   MOVE '         YES' TO PP53-64
               WHEN SOG-STOPPING-PERSISTENCE-OFF
                   MOVE '          NO' TO PP53-64
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           PERFORM WRITE-OUTPUT.

           MOVE 'Times region stopped HTTP persistence. . . . . :' TO
           PP3-50.
           MOVE SOG-TIMES-STOPPED-PERSISTENT          TO PP55-64.
           MOVE 'Region last stopped persistence. . :' TO
           PP69-104.
           IF SOG-TIME-LAST-STOPPED-PERSIST NOT = LOW-VALUES THEN
             MOVE SOG-TIME-LAST-STOPPED-PERSIST TO CLOCK-FIELD
             PERFORM TIMESTAMP-CONVERT
             MOVE MMDDYYYY                            TO PP107-116
             MOVE STOPWATCH                           TO PP119-132
           ELSE
               MOVE NULL-DATE-FIELD                   TO PP107-116
               MOVE NULL-TIME-FIELD                   TO PP119-132
           END-IF.
           PERFORM WRITE-OUTPUT

           MOVE 'Persistent connections made non-persistent . . :' TO
           PP3-50.
           MOVE SOG-TIMES-MADE-NON-PERSISTENT         TO PP55-64.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Times disconnected a connection at max uses. . :' TO
           PP3-50.
           MOVE SOG-TIMES-CONN-DISC-AT-MAX            TO PP55-64.
           PERFORM WRITE-OUTPUT.

       TCPIP-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** TCPIPSERVICE-STATISTICS                                   ***
      ***                                                           ***
      *****************************************************************
       TCPIPSERVICE-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO TCPIPSERVICES-PAGENO.

      *****************************************************************
      *** Print the TCP/IP service headings.                        ***
      *****************************************************************
           MOVE '-'                TO PRINT-CONTROL.
           MOVE 'TCP/IP Services'  TO PP1-15.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-15.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      ***   Print the TCP/IP Service statistics - Pass 1.           ***
      *****************************************************************
           MOVE '0'      TO PRINT-CONTROL.

           EXEC CICS INQUIRE TCPIPSERVICE START
           END-EXEC.

           EXEC CICS INQUIRE TCPIPSERVICE(TCPIPSERVICE-NAME) NEXT
               ATTACHSEC(TCPIPSERVICE-ATTACHSEC)
               AUTHENTICATE(TCPIPSERVICE-AUTHENTICATE)
               BACKLOG(TCPIPSERVICE-BACKLOG)
               MAXDATALEN(TCPIPSERVICE-MAXDATA)
               OPENSTATUS(TCPIPSERVICE-OPENSTATUS)
               PORT(TCPIPSERVICE-PORT)
               PRIVACY(TCPIPSERVICE-PRIVACY)
               PROTOCOL(TCPIPSERVICE-PROTOCOL)
               SSLTYPE(TCPIPSERVICE-SSLTYPE)
               TRANSID(TCPIPSERVICE-TRANSID)
               URM(TCPIPSERVICE-URM)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

               IF LINE-COUNT > (LINES-PER-PAGE - 33) THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               EXEC CICS EXTRACT STATISTICS TCPIPSERVICE
                   RESID(TCPIPSERVICE-NAME)
                   SET(ADDRESS OF DFHSORDS)
                   NOHANDLE
               END-EXEC

               EXEC CICS INQUIRE TRANSACTION(TCPIPSERVICE-TRANSID)
                   NOHANDLE
               END-EXEC

               IF TCPIPSERVICE-URM NOT = SPACES
                   EXEC CICS INQUIRE PROGRAM(TCPIPSERVICE-URM)
                       NOHANDLE
                   END-EXEC
               END-IF

               ADD 1 TO TCPIPSERVICE-TOTAL

               MOVE '0' TO PRINT-CONTROL
               MOVE 'TCPIPSERVICE Name . . . . . . . . :' TO PP3-37
               MOVE TCPIPSERVICE-NAME                     TO PP40-47
               PERFORM WRITE-OUTPUT

               MOVE 'TCPIPSERVICE Open Status. . . . . :' TO PP3-37
               EVALUATE TCPIPSERVICE-OPENSTATUS
                   WHEN DFHVALUE(OPEN)
                       MOVE 'OPEN       '                 TO PP40-49X
                   WHEN DFHVALUE(CLOSED)
                       MOVE 'CLOSED     '                 TO PP40-49X
                   WHEN DFHVALUE(CLOSING)
                       MOVE 'CLOSING    '                 TO PP40-49X
                   WHEN DFHVALUE(IMMCLOSING)
                       MOVE 'IMMCLOSING'                  TO PP40-49X
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               IF TCPIPSERVICE-OPENSTATUS = DFHVALUE(OPEN) THEN
                   MOVE 'Open Date and Time. . . . . . . . :'
                                                          TO PP68-102
                   MOVE SOR-OPEN-LOCAL TO CLOCK-FIELD
                   PERFORM TIMESTAMP-CONVERT
                   MOVE MMDDYYYY       TO PP105-114X
                   MOVE STOPWATCH      TO PP117-130
               END-IF
               PERFORM WRITE-OUTPUT

               MOVE 'TCPIPSERVICE Protocol . . . . . . :' TO PP3-37
               EVALUATE TCPIPSERVICE-PROTOCOL
                   WHEN DFHVALUE(HTTP)
                       MOVE 'HTTP'                        TO PP40-43
                   WHEN DFHVALUE(IIOP)
                       MOVE 'IIOP'                        TO PP40-43
                   WHEN DFHVALUE(ECI)
                       MOVE 'ECI '                        TO PP40-43
                   WHEN DFHVALUE(USER)
                       MOVE 'USER'                        TO PP40-43
                   WHEN DFHVALUE(IPIC)
                       MOVE 'IPIC'                        TO PP40-43
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               MOVE 'TCPIPSERVICE Port . . . . . . . . :' TO PP3-37
               MOVE TCPIPSERVICE-PORT                     TO PP40-44
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'TCPIPSERVICE Host . . . . . . . . :' TO PP3-37
               MOVE SOR-HOSTNAME(1:85)                    TO PP40-124
               PERFORM WRITE-OUTPUT
               IF SOR-HOSTNAME(86:31) NOT = SPACES THEN
                   MOVE ':' TO PP37
                   MOVE SOR-HOSTNAME(86:31)               TO PP40-70
                   PERFORM WRITE-OUTPUT
               END-IF

               MOVE 'TCPIPSERVICE IP Family. . . . . . :' TO PP3-37
               EVALUATE TRUE
                   WHEN SOR-IP-FAMILY-IPV4
                       MOVE 'IPv4'                        TO PP40-43
                   WHEN SOR-IP-FAMILY-IPV6
                       MOVE 'IPv6'                        TO PP40-43
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               MOVE 'TCPIPSERVICE IP Resolved Address. :' TO PP3-37
               MOVE SOR-IP-ADDRESS                        TO PP40-78
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'TCPIPSERVICE Transaction ID . . . :' TO PP3-37
               MOVE TCPIPSERVICE-TRANSID                  TO PP40-43
               MOVE 'TCPIPSERVICE Backlog Setting. . . :' TO PP68-102
               MOVE TCPIPSERVICE-BACKLOG                  TO PP105-114
               PERFORM WRITE-OUTPUT

               MOVE 'TCPIPSERVICE URM. . . . . . . . . :' TO PP3-37
               MOVE TCPIPSERVICE-URM                      TO PP40-47
               MOVE 'Current Maximum Backlog . . . . . :' TO PP68-102
               MOVE SOR-CURR-MAX-BACKLOG                  TO PP105-114
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'TCPIPSERVICE SSL Type . . . . . . :' TO PP3-37
               EVALUATE TCPIPSERVICE-SSLTYPE
                   WHEN DFHVALUE(SSL)
                       MOVE 'SSL   '                      TO PP40-45
                   WHEN DFHVALUE(NOSSL)
                       MOVE 'None  '                      TO PP40-45
                   WHEN DFHVALUE(CLIENTAUTH)
                       MOVE 'Client'                      TO PP40-45
                   WHEN DFHVALUE(ATTLSAWARE)
                       MOVE 'ATTAWR'                      TO PP40-45
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               MOVE 'TCPIPSERVICE Maxdata. . . . . . . :' TO PP68-102
               MOVE TCPIPSERVICE-MAXDATA                  TO PP105-114
               PERFORM WRITE-OUTPUT

               MOVE 'TCPIPSERVICE Authenticate . . . . :' TO PP3-37
               EVALUATE TCPIPSERVICE-AUTHENTICATE
                   WHEN DFHVALUE(NOAUTHENTIC)
                       MOVE 'None       '                 TO PP40-50
                   WHEN DFHVALUE(BASICAUTH)
                       MOVE 'Basic      '                 TO PP40-50
                   WHEN DFHVALUE(CERTIFICAUTH)
                       MOVE 'Certificate'                 TO PP40-50
                   WHEN DFHVALUE(AUTOREGISTER)
                       MOVE 'Register   '                 TO PP40-50
                   WHEN DFHVALUE(AUTOAUTH)
                       MOVE 'Automatic  '                 TO PP40-50
                   WHEN DFHVALUE(ASSERTED)
                       MOVE 'Asserted   '                 TO PP40-50
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               MOVE 'TCPIPSERVICE Privacy. . . . . . . :' TO PP3-37
               EVALUATE TCPIPSERVICE-PRIVACY
                   WHEN DFHVALUE(NOTSUPPORTED)
                       MOVE 'Notsupported'                TO PP40-51
                   WHEN DFHVALUE(SUPPORTED)
                       MOVE 'Supported   '                TO PP40-51
                   WHEN DFHVALUE(REQUIRED)
                       MOVE 'Required    '                TO PP40-51
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               MOVE 'TCPIPSERVICE Attachsec. . . . . . :' TO PP3-37
               EVALUATE TCPIPSERVICE-ATTACHSEC
                   WHEN DFHVALUE(LOCAL)
                       MOVE 'Local '                      TO PP40-45
                   WHEN DFHVALUE(VERIFY)
                       MOVE 'Verify'                      TO PP40-45
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Current Connections . . . . . . . :' TO PP3-37
               MOVE SOR-CURRENT-CONNS                     TO PP40-49
               MOVE 'Peak Connections. . . . . . . . . :' TO PP68-102
               MOVE SOR-PEAK-CONNS                        TO PP105-114
               PERFORM WRITE-OUTPUT

               MOVE 'Transactions Attached . . . . . . :' TO PP3-37
               MOVE SOR-TRANS-ATTACHED                    TO PP40-49
               MOVE 'Total Connections . . . . . . . . :' TO PP68-102
               MOVE SOR-TOTAL-CONNS                       TO PP105-114
               PERFORM WRITE-OUTPUT

               MOVE 'Send requests . . . . . . . . . . :' TO PP3-37
               MOVE SOR-SENDS                             TO PP40-49
               MOVE 'Total Bytes Sent. . . . . . . . . :' TO PP68-102
               MOVE SOR-BYTES-SENT                        TO PP105-118
               PERFORM WRITE-OUTPUT

               MOVE 'Receive requests. . . . . . . . . :' TO PP3-37
               MOVE SOR-RECEIVES                          TO PP40-49
               MOVE 'Total Bytes Received. . . . . . . :' TO PP68-102
               MOVE SOR-BYTES-RECEIVED                    TO PP105-118
               PERFORM WRITE-OUTPUT

               MOVE 'Requests processed. . . . . . . . :' TO PP3-37
               MOVE SOR-REQUESTS                          TO PP40-49
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Maximum Persistent Connections. . :' TO PP3-37
               IF SOR-TCPIPS-MAX-PERSIST < 0 THEN
                   MOVE 'NO        '                      TO PP40-49X
               ELSE
                   MOVE SOR-TCPIPS-MAX-PERSIST            TO PP40-49
               END-IF
               MOVE 'Non-Persistent Connections. . . . :' TO PP68-102
               MOVE SOR-TCPIPS-NON-PERSIST                TO PP105-114
               PERFORM WRITE-OUTPUT

               MOVE 'Made non-persistent at MAXPERSIST :' TO PP3-37
               MOVE SOR-NONP-AT-MAXPERSIST                TO PP40-49
               MOVE 'Disconnected after maximum uses . :' TO PP68-102
               MOVE SOR-DISC-AT-MAX-USES                  TO PP105-114
               PERFORM WRITE-OUTPUT

               MOVE 'Made non-persistent at task limit :' TO PP3-37
               MOVE SOR-NONP-AT-TASK-LIMIT                TO PP40-49
               MOVE 'Disconnected at task limit. . . . :' TO PP68-102
               MOVE SOR-DISC-AT-TASK-LIMIT                TO PP105-114
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Current backlog . . . . . . . . . :' TO PP3-37
               MOVE SOR-CURR-BACKLOG                      TO PP40-49
               PERFORM WRITE-OUTPUT

               MOVE 'Connections dropped . . . . . . . :' TO PP3-37
               MOVE SOR-CONNS-DROPPED                     TO PP40-49
               MOVE 'Time connection last dropped. . . :' TO PP68-102
               IF SOR-CONN-LAST-DROPPED NOT = LOW-VALUES THEN
                 MOVE SOR-CONN-LAST-DROPPED TO CLOCK-FIELD
                 PERFORM TIMESTAMP-CONVERT
                 MOVE MMDDYYYY                            TO PP105-114X
                 MOVE STOPWATCH                           TO PP117-130
               ELSE
                   MOVE NULL-DATE-FIELD                   TO PP105-114X
                   MOVE NULL-TIME-FIELD                   TO PP117-130
               END-IF
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE ALL '-' TO PP3-132
               PERFORM WRITE-OUTPUT

               EXEC CICS INQUIRE TCPIPSERVICE(TCPIPSERVICE-NAME) NEXT
                   ATTACHSEC(TCPIPSERVICE-ATTACHSEC)
                   AUTHENTICATE(TCPIPSERVICE-AUTHENTICATE)
                   BACKLOG(TCPIPSERVICE-BACKLOG)
                   MAXDATALEN(TCPIPSERVICE-MAXDATA)
                   OPENSTATUS(TCPIPSERVICE-OPENSTATUS)
                   PORT(TCPIPSERVICE-PORT)
                   PRIVACY(TCPIPSERVICE-PRIVACY)
                   PROTOCOL(TCPIPSERVICE-PROTOCOL)
                   SSLTYPE(TCPIPSERVICE-SSLTYPE)
                   TRANSID(TCPIPSERVICE-TRANSID)
                   URM(TCPIPSERVICE-URM)
                   RESP(INQUIRE-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE TCPIPSERVICE END
           END-EXEC.

       TCPIPSERVICE-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** IPCONN-STATISTICS                                         ***
      ***                                                           ***
      *****************************************************************
       IPCONN-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO IPCONNS-PAGENO.

      *****************************************************************
      *** Print headings and collect the IPCONN statistics.         ***
      *****************************************************************
           MOVE '-'       TO PRINT-CONTROL.
           MOVE 'IPCONNs' TO PP1-7.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-7.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      ***   Print the IPCONN statistics.                            ***
      *****************************************************************
           EXEC CICS INQUIRE IPCONN START
           END-EXEC.

           EXEC CICS INQUIRE IPCONN(IPCONN-NAME) NEXT
               APPLID(IPCONN-APPLID)
               CONNSTATUS(IPCONN-CONNECTION-STATUS)
               HOST(IPCONN-HOST-NAME)
               LINKAUTH(IPCONN-LINKAUTH)
               MAXQTIME(IPCONN-MAX-QUEUE-TIME)
               NETWORKID(IPCONN-NETWORK-ID)
               PORT(IPCONN-PORT-NUMBER)
               QUEUELIMIT(IPCONN-QUEUE-LIMIT)
               SERVSTATUS(IPCONN-SERVICE-STATUS)
               SSLTYPE(IPCONN-SSLTYPE)
               TCPIPSERVICE(IPCONN-TCPIPSERVICE)
               USERAUTH(IPCONN-USERAUTH)
               MIRRORLIFE(IPCONN-MIRRORLIFE)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

               IF LINE-COUNT > (LINES-PER-PAGE - 48) THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               EXEC CICS EXTRACT STATISTICS IPCONN
                   RESID(IPCONN-NAME)
                   SET(ADDRESS OF DFHISRDS)
                   NOHANDLE
               END-EXEC

               MOVE '0' TO PRINT-CONTROL

               ADD 1 TO IPCONN-TOTAL

               MOVE 'IPCONN Name . . . . . . . . . . . :' TO PP3-37
               MOVE IPCONN-NAME                           TO PP40-47
               IF ISR-IPCONN-CREATE-TIME NOT = LOW-VALUES THEN
                   MOVE 'Autoinstalled IPCONN Create Time. . . . :'
                                                          TO PP68-108
                   MOVE ISR-IPCONN-CREATE-TIME TO CLOCK-FIELD
                   PERFORM TIMESTAMP-CONVERT
                   MOVE MMDDYYYY  TO PP107-116
                   MOVE STOPWATCH TO PP119-132
               END-IF
               PERFORM WRITE-OUTPUT

               MOVE 'IPCONN Applid . . . . . . . . . . :' TO PP3-37
               MOVE IPCONN-APPLID                         TO PP40-47

               MOVE 'IPCONN Network ID . . . . . . . . . . . :'
                                                          TO PP68-108
               MOVE ISR-NETWORK-ID                        TO PP111-118
               PERFORM WRITE-OUTPUT

               MOVE 'IPCONN Status . . . . . . . . . . :' TO PP3-37
               EVALUATE IPCONN-CONNECTION-STATUS
                   WHEN DFHVALUE(ACQUIRED)
                       MOVE 'Acquired '                   TO PP40-48
                   WHEN DFHVALUE(RELEASED)
                       MOVE 'Released '                   TO PP40-48
                   WHEN DFHVALUE(OBTAINING)
                       MOVE 'Obtaining'                   TO PP40-48
                   WHEN DFHVALUE(FREEING)
                       MOVE 'Freeing  '                   TO PP40-48
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE

               MOVE 'IPCONN Service Status . . . . . . . . . :'
                                                          TO PP68-108
               EVALUATE IPCONN-SERVICE-STATUS
                   WHEN DFHVALUE(INSERVICE)
                       MOVE 'Inservice'                   TO PP111-119
                   WHEN DFHVALUE(OUTSERVICE)
                       MOVE 'Outservice'                  TO PP111-119
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               MOVE 'IPCONN Port Number. . . . . . . . :' TO PP3-37
               IF ISR-PORT-NUMBER = -1 THEN
                   MOVE 'No '                             TO PP40-42
               ELSE
                   MOVE ISR-PORT-NUMBER                   TO PP45-49
               END-IF

               MOVE 'TCPIPSERVICE Name . . . . . . . . . . . :'
                                                          TO PP68-108
               MOVE IPCONN-TCPIPSERVICE                   TO PP111-118
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'IPCONN Host . . . . . . . . . . . :' TO PP3-37
               MOVE IPCONN-HOST-NAME(1:85)                TO PP40-124
               PERFORM WRITE-OUTPUT

               IF IPCONN-HOST-NAME(86:31) NOT = SPACES THEN
                   MOVE ':' TO PP37
                   MOVE IPCONN-HOST-NAME(86:31)           TO PP40-70
                   PERFORM WRITE-OUTPUT
               END-IF

               MOVE 'IPCONN IP Resolved Address. . . . :' TO PP3-37
               MOVE ISR-IPCONN-IP-ADDRESS                 TO PP40-78
               PERFORM WRITE-OUTPUT

               MOVE 'IPCONN IP Family. . . . . . . . . :' TO PP3-37
               EVALUATE TRUE
                   WHEN ISR-IP-FAMILY-IPV4
                       MOVE 'IPv4'                        TO PP40-43
                   WHEN ISR-IP-FAMILY-IPV6
                       MOVE 'IPv6'                        TO PP40-43
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE

               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'SSL Authentication. . . . . . . . :' TO PP3-37
               EVALUATE IPCONN-SSLTYPE
                   WHEN DFHVALUE(SSL)
                       MOVE 'Yes ' TO PP40-43
                   WHEN DFHVALUE(NOSSL)
                       MOVE 'No  ' TO PP40-43
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE

               MOVE 'User Authentication . . . . . . . . . . :'
                                                          TO PP68-108
               EVALUATE IPCONN-USERAUTH
                   WHEN DFHVALUE(DEFAULTUSER)
                       MOVE 'Defaultuser'     TO PP111-121
                   WHEN DFHVALUE(IDENTIFY)
                       MOVE 'Identify   '     TO PP111-121
                   WHEN DFHVALUE(LOCAL)
                       MOVE 'Local      '     TO PP111-121
                   WHEN DFHVALUE(VERIFY)
                       MOVE 'Verify     '     TO PP111-121
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               MOVE 'Link Security . . . . . . . . . . :' TO PP3-37
               EVALUATE IPCONN-LINKAUTH
                   WHEN DFHVALUE(SECUSER)
                       MOVE 'Securityname'    TO PP40-51
                   WHEN DFHVALUE(CERTUSER)
                       MOVE 'Certificate '    TO PP40-51
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE

               MOVE 'Mirror Lifetime . . . . . . . . . . . . :'
                                                          TO PP68-108
               EVALUATE IPCONN-MIRRORLIFE
                   WHEN DFHVALUE(REQUEST)
                       MOVE 'Request    '     TO PP111-121
                   WHEN DFHVALUE(TASK)
                       MOVE 'Task       '     TO PP111-121
                   WHEN DFHVALUE(UOW)
                       MOVE 'UOW        '     TO PP111-121
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Receive Session Count . . . . . . :' TO PP3-37
               MOVE ISR-RECEIVE-SESSIONS                  TO PP40-49
               MOVE 'Send Session Count. . . . . . . . . . . :'
                                                          TO PP68-108
               MOVE ISR-SEND-SESSIONS                     TO PP111-120
               PERFORM WRITE-OUTPUT

               MOVE 'Current Receive Session Count . . :' TO PP3-37
               MOVE ISR-CURRENT-RECEIVE-SESSIONS          TO PP40-49
               MOVE 'Current Send Session Count. . . . . . . :'
                                                          TO PP68-108
               MOVE ISR-CURRENT-SEND-SESSIONS             TO PP111-120
               PERFORM WRITE-OUTPUT

               MOVE 'Peak Receive Session Count. . . . :' TO PP3-37
               MOVE ISR-PEAK-RECEIVE-SESSIONS             TO PP40-49
               MOVE 'Peak Send Session Count . . . . . . . . :'
                                                          TO PP68-108
               MOVE ISR-PEAK-SEND-SESSIONS                TO PP111-120
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Total Allocates . . . . . . . . . :' TO PP3-37
               MOVE ISR-TOTAL-ALLOCATES                   TO PP40-49
               MOVE 'Allocates per second. . . . . . . . . . :'
                                                          TO PP68-108
               IF ELAPSED-SECONDS > 0 THEN
                   COMPUTE ALLOCATE-RATE =
                               (ISR-TOTAL-ALLOCATES / ELAPSED-SECONDS)
                   END-COMPUTE
               ELSE
                   MOVE ISR-TOTAL-ALLOCATES TO ALLOCATE-RATE
               END-IF
               MOVE ALLOCATE-RATE TO PP114-120N
               PERFORM WRITE-OUTPUT

               MOVE 'Current Allocates Queued. . . . . :' TO PP3-37
               MOVE ISR-CURRENT-QUEUED-ALLOCATES          TO PP40-49
               MOVE 'Allocate Queue Limit. . . . . . . . . . :'
                                                          TO PP68-108
               IF ISR-ALLOCATE-QUEUE-LIMIT = -1 THEN
                   MOVE 'No'                              TO PP119-120
               ELSE
                   MOVE ISR-ALLOCATE-QUEUE-LIMIT          TO PP111-120
               END-IF
               PERFORM WRITE-OUTPUT

               MOVE 'Peak Allocates Queued . . . . . . :' TO PP3-37
               MOVE ISR-PEAK-QUEUED-ALLOCATES             TO PP40-49
               MOVE 'Allocates Rejected - Queue Limit. . . . :'
                                                          TO PP68-108
               MOVE ISR-QLIMIT-ALLOC-REJECTS              TO PP111-120
               PERFORM WRITE-OUTPUT

               MOVE 'Allocates Failed - Link . . . . . :' TO PP3-37
               MOVE ISR-ALLOCATES-FAILED-LINK             TO PP40-49
               PERFORM WRITE-OUTPUT

               MOVE 'Allocates Failed - Other. . . . . :' TO PP3-37
               MOVE ISR-ALLOCATES-FAILED-OTHER            TO PP40-49
               MOVE 'Max Queue Time (seconds). . . . . . . . :'
                                                          TO PP68-108
               IF ISR-MAX-QUEUE-TIME = -1 THEN
                   MOVE 'No'                              TO PP119-120
               ELSE
                   MOVE ISR-MAX-QUEUE-TIME                TO PP111-120
               END-IF
               PERFORM WRITE-OUTPUT

               MOVE 'Max Queue Time - Allocate Queue Purge . :'
                                                          TO PP68-108
               MOVE ISR-MAXQTIME-ALLOC-QPURGES            TO PP111-120
               PERFORM WRITE-OUTPUT

               MOVE 'Number of Transactions Attached . :' TO PP3-37
               MOVE ISR-TRANS-ATTACHED                    TO PP40-49
               MOVE 'Max Queue Time - Allocates Purged . . . :'
                                                          TO PP68-108
               MOVE ISR-MAXQTIME-ALLOCS-PURGED            TO PP111-120
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Remote Terminal Starts. . . . . . :' TO PP3-37
               MOVE ISR-REMOTE-TERM-STARTS                TO PP40-49
               MOVE 'XISQUE - Allocates Rejected . . . . . . :'
                                                          TO PP68-108
               MOVE ISR-XISQUE-ALLOC-REJECTS              TO PP111-120
               PERFORM WRITE-OUTPUT

               MOVE 'Unsupported Requests. . . . . . . :' TO PP3-37
               MOVE ISR-UNSUPPORTED-REQUESTS              TO PP40-49
               MOVE 'XISQUE - Allocate Queue Purge . . . . . :'
                                                          TO PP68-108
               MOVE ISR-XISQUE-ALLOC-QPURGES              TO PP111-120
               PERFORM WRITE-OUTPUT

               MOVE 'XISQUE - Allocates Purged . . . . . . . :'
                                                          TO PP68-108
               MOVE ISR-XISQUE-ALLOCS-PURGED              TO PP111-120
               PERFORM WRITE-OUTPUT

               MOVE 'Transaction Routing'                 TO PP3-46
               PERFORM WRITE-OUTPUT
               MOVE '+'     TO PRINT-CONTROL
               MOVE '___________________'                 TO PP3-46
               PERFORM WRITE-OUTPUT

               MOVE '  Requests . . . . . . . . . . . . . . . . :'
                                                          TO PP3-46
               MOVE ISR-TR-REQUESTS                       TO PP49-62
               MOVE 'Total / Average Bytes Sent . . :'    TO PP68-99
               MOVE ISR-TR-BYTES-SENT                     TO PP103-116
               MOVE ' / '                                 TO PP117-119
               IF ISR-TR-REQUESTS > 0 THEN
                   COMPUTE RESULT =
                       (ISR-TR-BYTES-SENT / ISR-TR-REQUESTS)
                   END-COMPUTE
               ELSE
                   MOVE 0 TO RESULT
               END-IF
               MOVE RESULT                                TO PP120-129
               PERFORM WRITE-OUTPUT

               MOVE 'Total Bytes Received . . . . . :'
                                                          TO PP68-99
               MOVE ISR-TR-BYTES-RECEIVED                 TO PP103-116
               PERFORM WRITE-OUTPUT

               MOVE 'Function Shipping'                   TO PP3-46
               PERFORM WRITE-OUTPUT

               MOVE '+'     TO PRINT-CONTROL
               MOVE '_________________'                   TO PP3-46
               PERFORM WRITE-OUTPUT

               MOVE '    Program requests . . . . . . . . . . . :'
                                                          TO PP3-46
               MOVE ISR-FS-PG-REQUESTS                    TO PP49-62

               MOVE 'Program Requests'                    TO PP68-108
               PERFORM WRITE-OUTPUT

               MOVE '+' TO PRINT-CONTROL
               MOVE '________________'                    TO PP68-108
               PERFORM WRITE-OUTPUT

               MOVE '    Interval Control requests. . . . . . . :'
                                                          TO PP3-46
               MOVE ISR-FS-IC-REQUESTS                    TO PP49-62

               MOVE '  Total / Average Bytes Sent . :'    TO PP68-99
               MOVE ISR-FS-PG-BYTES-SENT                  TO PP103-116
               MOVE ' / '                                 TO PP117-119
               IF ISR-FS-PG-REQUESTS > 0 THEN
                   COMPUTE RESULT =
                       (ISR-FS-PG-BYTES-SENT / ISR-FS-PG-REQUESTS)
                   END-COMPUTE
               ELSE
                   MOVE 0 TO RESULT
               END-IF
               MOVE RESULT                                TO PP120-129
               PERFORM WRITE-OUTPUT

               MOVE '    File Control requests. . . . . . . . . :'
                                                          TO PP3-46
               MOVE ISR-FS-FC-REQUESTS                    TO PP49-62

               MOVE '  Total Bytes Received . . . . :'
                                                          TO PP68-99
               MOVE ISR-FS-PG-BYTES-RECEIVED              TO PP103-116
               PERFORM WRITE-OUTPUT

               MOVE '    Transient Data requests. . . . . . . . :'
                                                          TO PP3-46
               MOVE ISR-FS-TD-REQUESTS                    TO PP49-62
               PERFORM WRITE-OUTPUT

               MOVE '    Temporary Storage requests . . . . . . :'
                                                          TO PP3-46
               MOVE ISR-FS-TS-REQUESTS                    TO PP49-62

               MOVE 'Interval Control Requests'           TO PP68-108
               PERFORM WRITE-OUTPUT

               MOVE '+' TO PRINT-CONTROL
               MOVE '_________________________'           TO PP68-108
               PERFORM WRITE-OUTPUT

               ADD ISR-FS-PG-REQUESTS, ISR-FS-IC-REQUESTS,
                   ISR-FS-FC-REQUESTS, ISR-FS-TD-REQUESTS,
                   ISR-FS-TS-REQUESTS,
                   GIVING TOTAL-FUNCSHIP-REQS
                       ON SIZE ERROR CONTINUE
               END-ADD
               MOVE '  Total requests . . . . . . . . . . . . . :'
                                                          TO PP3-46
               MOVE TOTAL-FUNCSHIP-REQS                   TO PP49-62

               MOVE '  Total / Average Bytes Sent . :'    TO PP68-99
               MOVE ISR-FS-IC-BYTES-SENT                  TO PP103-116
               MOVE ' / '                                 TO PP117-119
               IF ISR-FS-IC-REQUESTS > 0 THEN
                   COMPUTE RESULT =
                       (ISR-FS-IC-BYTES-SENT / ISR-FS-IC-REQUESTS)
                   END-COMPUTE
               ELSE
                   MOVE 0 TO RESULT
               END-IF
               MOVE RESULT                                TO PP120-129
               PERFORM WRITE-OUTPUT

               MOVE '  Total Bytes Received . . . . :'
                                                          TO PP68-99
               MOVE ISR-FS-IC-BYTES-RECEIVED              TO PP103-116
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'File Control Requests'               TO PP68-108
               PERFORM WRITE-OUTPUT

               MOVE '+' TO PRINT-CONTROL
               MOVE '_____________________'               TO PP68-108
               PERFORM WRITE-OUTPUT

               MOVE '  Total / Average Bytes Sent . :'    TO PP68-99
               MOVE ISR-FS-FC-BYTES-SENT                  TO PP103-116
               MOVE ' / '                                 TO PP117-119
               IF ISR-FS-FC-REQUESTS > 0 THEN
                   COMPUTE RESULT =
                       (ISR-FS-FC-BYTES-SENT / ISR-FS-FC-REQUESTS)
                   END-COMPUTE
               ELSE
                   MOVE 0 TO RESULT
               END-IF
               MOVE RESULT                                TO PP120-129
               PERFORM WRITE-OUTPUT
               MOVE '  Total Bytes Received . . . . :'
                                                          TO PP68-99
               MOVE ISR-FS-FC-BYTES-RECEIVED              TO PP103-116
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Transient Data Requests'             TO PP68-108
               PERFORM WRITE-OUTPUT

               MOVE '+' TO PRINT-CONTROL
               MOVE '_______________________'             TO PP68-108
               PERFORM WRITE-OUTPUT

               MOVE '  Total / Average Bytes Sent . :'    TO PP68-99
               MOVE ISR-FS-TD-BYTES-SENT                  TO PP103-116
               MOVE ' / '                                 TO PP117-119
               IF ISR-FS-TD-REQUESTS > 0 THEN
                   COMPUTE RESULT =
                       (ISR-FS-TD-BYTES-SENT / ISR-FS-TD-REQUESTS)
                   END-COMPUTE
               ELSE
                   MOVE 0 TO RESULT
               END-IF
               MOVE RESULT                                TO PP120-129
               PERFORM WRITE-OUTPUT
               MOVE '  Total Bytes Received . . . . :'
                                                          TO PP68-99
               MOVE ISR-FS-TD-BYTES-RECEIVED              TO PP103-116
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Temporary Storage Requests'          TO PP68-108
               PERFORM WRITE-OUTPUT

               MOVE '+' TO PRINT-CONTROL
               MOVE '__________________________'          TO PP68-108
               PERFORM WRITE-OUTPUT

               MOVE '  Total / Average Bytes Sent . :'    TO PP68-99
               MOVE ISR-FS-TS-BYTES-SENT                  TO PP103-116
               MOVE ' / '                                 TO PP117-119
               IF ISR-FS-TS-REQUESTS > 0 THEN
                   COMPUTE RESULT =
                       (ISR-FS-TS-BYTES-SENT / ISR-FS-TS-REQUESTS)
                   END-COMPUTE
               ELSE
                   MOVE 0 TO RESULT
               END-IF
               MOVE RESULT                                TO PP120-129
               PERFORM WRITE-OUTPUT
               MOVE '  Total Bytes Received . . . . :'
                                                          TO PP68-99
               MOVE ISR-FS-TS-BYTES-RECEIVED              TO PP103-116
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE ALL '-' TO PP3-132
               PERFORM WRITE-OUTPUT

               EXEC CICS INQUIRE IPCONN(IPCONN-NAME) NEXT
                   APPLID(IPCONN-APPLID)
                   CONNSTATUS(IPCONN-CONNECTION-STATUS)
                   HOST(IPCONN-HOST-NAME)
                   LINKAUTH(IPCONN-LINKAUTH)
                   MAXQTIME(IPCONN-MAX-QUEUE-TIME)
                   NETWORKID(IPCONN-NETWORK-ID)
                   PORT(IPCONN-PORT-NUMBER)
                   QUEUELIMIT(IPCONN-QUEUE-LIMIT)
                   SERVSTATUS(IPCONN-SERVICE-STATUS)
                   SSLTYPE(IPCONN-SSLTYPE)
                   TCPIPSERVICE(IPCONN-TCPIPSERVICE)
                   USERAUTH(IPCONN-USERAUTH)
                   MIRRORLIFE(IPCONN-MIRRORLIFE)
                   RESP(INQUIRE-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE IPCONN END
           END-EXEC.

       IPCONN-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** TOTALS-INITIALISE                                         ***
      *****************************************************************
       TOTALS-INITIALISE SECTION.

           MOVE 0 TO TCPIPSERVICE-TOTAL IPCONN-TOTAL.

       TOTALS-INITIALISE-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** PAGE-HEADINGS                                             ***
      ***   Increment page                                          ***
      ***   Write new heading line to spool                         ***
      *****************************************************************
       PAGE-HEADINGS SECTION.

           ADD 1       TO PAGENO.
           MOVE PAGENO TO PAGE-NUMBER.
           MOVE 1      TO LINE-COUNT.

           MOVE '1'          TO PRINT-CONTROL.
           MOVE HEADING-LINE TO PP1-132.
           PERFORM WRITE-OUTPUT.
           MOVE '+'          TO PRINT-CONTROL.
           MOVE ALL '_'      TO PP1-132.
           PERFORM WRITE-OUTPUT.

           MOVE 'Y' TO SUB-HEADINGS.

       PAGE-HEADINGS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** STOPWATCH-CONVERT                                         ***
      *** Call program DFH$STCN to convert S/370 format TOD to      ***
      ***   EBCDIC date and time                                    ***
      *****************************************************************
       STOPWATCH-CONVERT SECTION.

           MOVE 'S'    TO CLOCK-TYPE.
           MOVE SPACES TO CLOCK-OUTPUT.

           MOVE LENGTH OF CLOCK-FIELDS TO STCN-COMMAREA.

           EXEC CICS LINK PROGRAM('DFH$STCN')
               COMMAREA(CLOCK-FIELDS)
               LENGTH(STCN-COMMAREA)
           END-EXEC.

           MOVE LOW-VALUES TO CLOCK-FIELD.

       STOPWATCH-CONVERT-EXIT.
           EXIT.

      *****************************************************************
      *** TIMESTAMP-CONVERT                                         ***
      *** Call program DFH$STCN to convert S/370 format TOD to      ***
      ***   EBCDIC date and time                                    ***
      *****************************************************************
       TIMESTAMP-CONVERT SECTION.

           MOVE 'T'    TO CLOCK-TYPE.
           MOVE SPACES TO CLOCK-OUTPUT.

           MOVE LENGTH OF CLOCK-FIELDS TO STCN-COMMAREA.

           EXEC CICS LINK PROGRAM('DFH$STCN')
               COMMAREA(CLOCK-FIELDS)
               LENGTH(STCN-COMMAREA)
           END-EXEC.

           MOVE LOW-VALUES TO CLOCK-FIELD.

       TIMESTAMP-CONVERT-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** WRITE-OUTPUT                                              ***
      ***   Write current print line to report output destination.  ***
      *****************************************************************
       WRITE-OUTPUT SECTION.

           PERFORM WRITE-SPOOL.

       WRITE-OUTPUT-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** WRITE-OUTPUT                                              ***
      *** Write current print line to spool                         ***
      *** Increment line-count                                      ***
      *** IF CICS exceptional condition is raised THEN              ***
      ***    call error routine                                     ***
      *****************************************************************
       WRITE-SPOOL SECTION.

           EXEC CICS SPOOLWRITE TOKEN(OUTPUT-TOKEN)
               FROM(PRINT-LINE) FLENGTH(LINE-LENGTH)
               LINE
               NOHANDLE
           END-EXEC.

           EVALUATE PRINT-CONTROL
               WHEN ' '
                   ADD 1 TO LINE-COUNT
               WHEN '0'
                   ADD 2 TO LINE-COUNT
               WHEN '-'
                   ADD 3 TO LINE-COUNT
               WHEN '+'
                   ADD 0 TO LINE-COUNT
               WHEN '1'
                   MOVE 1 TO LINE-COUNT
           END-EVALUATE.

      *                                       Check return code
           IF EIBRESP NOT = DFHRESP(NORMAL) THEN
               MOVE 'SPOOLWRITE' TO CICS-COMMAND
               PERFORM ERROR-ROUTINE
           ELSE
               MOVE ' '    TO PRINT-CONTROL
               MOVE SPACES TO PP1-132
           END-IF.

       WRITE-SPOOL-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  ERROR ROUTINE                                            ***
      ***  Error routine produces appropriate responses to CICS     ***
      ***  spool commands. To ensure this the resp and resp2 values ***
      ***  are returned to the main program DFH0STAT for appropriate***
      ***  outputing of the messages.                               ***
      *****************************************************************
       ERROR-ROUTINE SECTION.

      *  If an error occurs which is not due to the users input or if
      *  the program is not being run from a terminal then a general
      *  message is sent to the screen/console and the RESP and RESP2
      *  fields are given along with the spool command which failed.
           MOVE 'Y'      TO ERROR-FOUND.
           MOVE EIBRESP  TO STCM-RESP.
           MOVE EIBRESP2 TO STCM-RESP2.

      *  Whatever happens ensure we return to DFH0STAT quickly.
           EXEC CICS RETURN
           END-EXEC.

       ERROR-ROUTINE-EXIT.
           EXIT.
