       CBL CICS('COBOL3')
      *****************************************************************
      *                                                               *
      * MODULE NAME = DFH0VAB                                         *
      *                                                               *
      * DESCRIPTIVE NAME = Abend Handler for Sample Application       *
      *                                                               *
      *                                                               *
      *                                                               *
      *     Licensed Materials - Property of IBM                      *
      *                                                               *
      *     "Restricted Materials of IBM"                             *
      *                                                               *
      *     5655-Y04                                                  *
      *                                                               *
      *     (C) Copyright IBM Corp. 1990, 1991"                       *
      *                                                               *
      *                                                               *
      *                                                               *
      *                                                               *
      * STATUS = 7.2.0                                                *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      * DESCRIPTION :                                                 *
      *                                                               *
      *  This module is called from any application module that gets  *
      *  a CICS abend.                                                *
      *                                                               *
      *  For a full description of the program operation see the      *
      *  CICS/ESA Book entitled "Sample Applications Guide" Version 3 *
      *  Release 2, Document Number SC33-0731.                        *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      * CHANGE ACTIVITY :                                             *
      *                                                               *
      *     $MOD(DFH0VAB),COMP(SAMPLES),PROD(CICS TS ):               *
      *                                                               *
      *     PN= REASON REL YYMMDD HDXIII : REMARKS                    *
      *    $P0= .      320 900906        : Created.                   *
      *    $P1= M90474 330 910807 HDBWSH : Prologue fixed.            *
      *                                                               *
      *****************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID. DFH0VAB.
       ENVIRONMENT DIVISION.
       DATA DIVISION.
       WORKING-STORAGE SECTION.
       01  CALLID.
           05  CALLID-EIBTRNID                PIC X(4) VALUE SPACES.
           05  CALLID-CALL-NUMBER             PIC X(5) VALUE SPACES.
           COPY DFH0BCA.
           COPY DFH0BMSG.
           COPY DFHAID.
           COPY DFHBMSCA.
           COPY DFH0AB.
       LINKAGE SECTION.
       01  DFHCOMMAREA.
           05  COMM-DATA-AREA     PIC X(200).

       PROCEDURE DIVISION.

       MAIN-SECTION SECTION.

           MOVE LOW-VALUES TO ABI.

           MOVE DFHCOMMAREA TO WS-HOLD-AREA.

           IF TRANSFER-CONTROL
               MOVE SPACE TO COMM-STATE-IND
               MOVE EIBTRNID TO CALLID-EIBTRNID
               MOVE COMM-CALL-NUMBER TO CALLID-CALL-NUMBER
               MOVE CALLID TO ABCALLO
               MOVE COMM-RESPONSE TO ABCODEO
               MOVE COMM-RESOURCE TO ABRSRCEO
               MOVE -1 TO ABCALLL
               PERFORM SEND-ABEND-PANEL
               PERFORM RETURN-AC2D
           END-IF.

           IF EIBAID = DFHPF3 THEN
               PERFORM PROCESS-F3
           END-IF.

           PERFORM PROCESS-INVALID-FKEY.

           GOBACK.

       END-MAIN-SECTION.
           EXIT.

       PROCESS-F3 SECTION.

           EXEC CICS SEND CONTROL
                          FREEKB
                          ERASE
                          RESP(COMM-RESPONSE)
                     END-EXEC.

           EXEC CICS RETURN
                     END-EXEC.

       PROCESS-F3-EXIT.
           EXIT.

       PROCESS-INVALID-FKEY SECTION.

           MOVE M002 TO ABMSGO.
           PERFORM RETURN-ERROR.

       PROCESS-INVALID-FKEY-EXIT.
           EXIT.

       RETURN-ERROR SECTION.

           MOVE -1 TO ABCALLL.
           PERFORM SEND-ABEND-PANEL.
           PERFORM SEND-CONTROL-ALARM.
           PERFORM RETURN-AC2D.

       RETURN-ERROR-EXIT.
           EXIT.

       SEND-CONTROL-ALARM SECTION.

           EXEC CICS SEND CONTROL
                          FREEKB
                          ALARM
                          RESP(COMM-RESPONSE)
                     END-EXEC.

       SEND-CONTROL-ALARM-EXIT.
           EXIT.

       SEND-ABEND-PANEL SECTION.

           EXEC CICS SEND MAP('AB')
                          MAPSET('DFH0AB')
                          FROM(ABO)
                          ERASE
                          CURSOR
                          RESP(COMM-RESPONSE)
                     END-EXEC.

       SEND-ABEND-PANEL-EXIT.
           EXIT.

       RETURN-AC2D SECTION.

           EXEC CICS RETURN TRANSID('AC2D')
                            COMMAREA(COMM-COMMAR)
                            LENGTH(LENGTH OF COMM-COMMAR)
                            RESP(COMM-RESPONSE)
                     END-EXEC.

       RETURN-AC2D-EXIT.
           EXIT.
