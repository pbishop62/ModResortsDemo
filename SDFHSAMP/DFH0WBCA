CBL CICS('COBOL3') APOST
      *****************************************************************
      *
      *  MODULE NAME = DFH0WBCA
      *
      *  DESCRIPTIVE NAME = CICS TS  Sample Client Authentication pgm
      *
      *
      *
      *       Licensed Materials - Property of IBM
      *
      *       "Restricted Materials of IBM"
      *
      *       5655-Y04
      *
      *       (C) Copyright IBM Corp. 1998, 2015"
      *
      *
      *
      *
      *  STATUS = 7.2.0
      *
      *  TRANSACTION NAME = User-defined
      *
      *  FUNCTION =
      *     This module is a sample CICS Web Interface transaction
      *     that demostrates the use of the Client Authentication
      *     protocol within the Secure Sockets Layer.
      *
      *  NOTES :
      *   DEPENDENCIES = S/390
      *   RESTRICTIONS =
      *   PATCH LABEL = None
      *   MODULE TYPE = Executable
      *   PROCESSOR = COBOL
      *   ATTRIBUTES = Read only, Serially Reusable
      *
      *-----------------------------------------------------------------
      *
      *  CHANGE ACTIVITY :
      *       $MOD(DFH0WBCA),COMP(CICSWEB),PROD(CICS TS ):
      *
      *  PN= REASON REL YYMMDD HDXXIII : REMARKS
      * $L0= 733    530 981009 HD2JPEH : Client Authentication sample
      * $L1= 781    640 041010 HD4PALS : CHARACTERSET for CLNTCODEPAGE
      * $P1= D03264 620 011024 HD2JPEH : Remove binary CRLFs from data
      * $D1= I06828 610 000605 HD2JPEH : Add AUTHENTICATE option
      * $D2= I07267 630 020404 HDHYJB  : Compile all samples          *
      * $D2= I07206 620 010430 HD7OPJW : Remove support for old compi *
      *      R92249 700 141218 HDLGJCH : New SSLTYPE ATTLSAWARE       *
      *
      ******************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    DFH0WBCA.
       DATE-WRITTEN.  October 1998.
       DATE-COMPILED. June 2000.
      ******************************************************************
      ****                                                          ****
      ****        C L I E N T   A U T H E N T I C A T I O N         ****
      ****                                                          ****
      ****                                                          ****
      ****    This program is a demonstration of how to use         ****
      ****    Client Certificates within the Secure Sockets Layer   ****
      ****    to display information about the client using the     ****
      ****    EXTRACT CERTIFICATE command.                          ****
      ****                                                          ****
      ******************************************************************

       ENVIRONMENT DIVISION.

       DATA DIVISION.

       WORKING-STORAGE SECTION.

        77 SYMBOL-NAME        PIC X(32).
        77 DTOKEN             PIC X(16).
        77 TRANSACTION-USERID PIC X(8).
        77 DSIZE              PIC S9(8) COMP SYNCHRONIZED.
        77 FIELD-SIZE         PIC S9(8) COMP SYNCHRONIZED.
        77 CLIENT-NAME-LENGTH PIC S9(8) COMP SYNCHRONIZED.
        77 SERVER-NAME-LENGTH PIC S9(8) COMP SYNCHRONIZED.
        77 CLIENT-ADDR-LENGTH PIC S9(8) COMP SYNCHRONIZED.
        77 SERVER-ADDR-LENGTH PIC S9(8) COMP SYNCHRONIZED.
        77 CLIENT-ADDR-BINARY PIC S9(8) COMP SYNCHRONIZED.
        77 SERVER-ADDR-BINARY PIC S9(8) COMP SYNCHRONIZED.
        77 PORT-NUMBER-BINARY PIC S9(8) COMP SYNCHRONIZED.
        77 USERID-LENGTH      PIC S9(8) COMP SYNCHRONIZED.
        77 SPACE-COUNT        PIC S9(8) COMP SYNCHRONIZED.
        77 SSLTYPE-CVDA       PIC S9(8) COMP SYNCHRONIZED.
        77 AUTHENT-CVDA       PIC S9(8) COMP SYNCHRONIZED.

      * This template contains the standard HTML front matter.
      * Note that some values below contain EBCDIC CRLF characters.
        01 WBCA-DOCUMENT-HEAD.
           02 FILLER PIC X(22) VALUE '<!doctype html public '.
           02 FILLER PIC X(27) VALUE '"-//W3C//DTD HTML 3.2//EN">'.
           02 FILLER PIC X(02) VALUE X'0D25'.
           02 FILLER PIC X(12) VALUE '<html><head>'.
           02 FILLER PIC X(02) VALUE X'0D25'.
           02 FILLER PIC X(17) VALUE '<title>DFH0WBCA -'.
           02 FILLER PIC X(26) VALUE ' CICS Transaction Server -'.
           02 FILLER PIC X(29) VALUE ' Client Authentication Sample'.
           02 FILLER PIC X(08) VALUE '</title>'.
           02 FILLER PIC X(02) VALUE X'0D25'.
           02 FILLER PIC X(13) VALUE '</head><body>'.
           02 FILLER PIC X(02) VALUE X'0D25'.

        01 WBCA-DOCUMENT-FOOT.
           02 FILLER PIC X(02) VALUE X'0D25'.
           02 FILLER PIC X(14) VALUE '</body></html>'.

      * The following symbols contain "escaped values" for some
      * special characters:
      *  %0D = Carriage Return  %0A = LineFeed
      *  %26 = Ampersand (&)    %3B = Semicolon (;)
      *  &nbsp; (coded as %26nbsp%3B) is the HTML entity for a
      *                               non-breaking space character.
        01 WBCA-SYMBOL-LIST.
           02 FILLER PIC X(11) VALUE 'CRLF=%0D%0A'.
           02 FILLER PIC X(26) VALUE '&personname=Unknown Person'.
           02 FILLER PIC X(21) VALUE '&serialnum=%26nbsp%3B'.
           02 FILLER PIC X(22) VALUE '&commonname=%26nbsp%3B'.
           02 FILLER PIC X(19) VALUE '&country=%26nbsp%3B'.
           02 FILLER PIC X(17) VALUE '&state=%26nbsp%3B'.
           02 FILLER PIC X(20) VALUE '&locality=%26nbsp%3B'.
           02 FILLER PIC X(24) VALUE '&organization=%26nbsp%3B'.
           02 FILLER PIC X(19) VALUE '&orgunit=%26nbsp%3B'.
           02 FILLER PIC X(22) VALUE '&issuername=%26nbsp%3B'.
           02 FILLER PIC X(25) VALUE '&issuercountry=%26nbsp%3B'.
           02 FILLER PIC X(23) VALUE '&issuerstate=%26nbsp%3B'.
           02 FILLER PIC X(26) VALUE '&issuerlocality=%26nbsp%3B'.
           02 FILLER PIC X(22) VALUE '&issuer_org=%26nbsp%3B'.
           02 FILLER PIC X(26) VALUE '&issuer_orgunit=%26nbsp%3B'.

      * This template is displayed if the client has a certificate:
        01 WBCA-CLIENT-WELCOME-TEMPLATE.
           02 FILLER PIC X(31) VALUE '<h1>Welcome, &personname;!</h1>'.
           02 FILLER PIC X(06) VALUE '&CRLF;'.
           02 FILLER PIC X(26) VALUE '<p>Welcome to &personname;'.
           02 FILLER PIC X(08) VALUE ' of the '.
           02 FILLER PIC X(28) VALUE '&organization; organization.'.
           02 FILLER PIC X(09) VALUE '&CRLF;<p>'.
           02 FILLER PIC X(29) VALUE 'You, and any other member of '.
           02 FILLER PIC X(10) VALUE '&orgunit;,'.
           02 FILLER PIC X(25) VALUE ' are always welcome here.'.
           02 FILLER PIC X(06) VALUE '&CRLF;'.
           02 FILLER PIC X(30) VALUE '<br>(This site is only visible'.
           02 FILLER PIC X(21) VALUE ' to owners of a valid'.
           02 FILLER PIC X(27) VALUE ' <strong>client certificate'.
           02 FILLER PIC X(11) VALUE '</strong>.)'.

      * This template displays the certificate contents:
        01 WBCA-CLIENT-CERT-DETAILS.
           02 FILLER PIC X(09) VALUE '&CRLF;<p>'.
           02 FILLER PIC X(26) VALUE 'For your information, your'.
           02 FILLER PIC X(24) VALUE ' client certificate from'.
           02 FILLER PIC X(26) VALUE ' &issuer_org; contains the'.
           02 FILLER PIC X(19) VALUE ' following details:'.
           02 FILLER PIC X(21) VALUE '&CRLF;<table border=1'.
           02 FILLER PIC X(29) VALUE ' cellpadding=3 cellspacing=3>'.
           02 FILLER PIC X(25) VALUE '&CRLF;<tr><th align=left>'.
           02 FILLER PIC X(18) VALUE 'Serial Number</th>'.
           02 FILLER PIC X(20) VALUE '<td>&serialnum;</td>'.
           02 FILLER PIC X(30) VALUE '</tr>&CRLF;<tr><th align=left>'.
           02 FILLER PIC X(16) VALUE 'Common name</th>'.
           02 FILLER PIC X(21) VALUE '<td>&commonname;</td>'.
           02 FILLER PIC X(30) VALUE '</tr>&CRLF;<tr><th align=left>'.
           02 FILLER PIC X(12) VALUE 'Country</th>'.
           02 FILLER PIC X(18) VALUE '<td>&country;</td>'.
           02 FILLER PIC X(30) VALUE '</tr>&CRLF;<tr><th align=left>'.
           02 FILLER PIC X(22) VALUE 'State or Province</th>'.
           02 FILLER PIC X(16) VALUE '<td>&state;</td>'.
           02 FILLER PIC X(30) VALUE '</tr>&CRLF;<tr><th align=left>'.
           02 FILLER PIC X(13) VALUE 'Locality</th>'.
           02 FILLER PIC X(19) VALUE '<td>&locality;</td>'.
           02 FILLER PIC X(30) VALUE '</tr>&CRLF;<tr><th align=left>'.
           02 FILLER PIC X(17) VALUE 'Organization</th>'.
           02 FILLER PIC X(23) VALUE '<td>&organization;</td>'.
           02 FILLER PIC X(30) VALUE '</tr>&CRLF;<tr><th align=left>'.
           02 FILLER PIC X(24) VALUE 'Organizational Unit</th>'.
           02 FILLER PIC X(18) VALUE '<td>&orgunit;</td>'.
           02 FILLER PIC X(30) VALUE '</tr>&CRLF;<tr><th align=left>'.
           02 FILLER PIC X(23) VALUE 'Issuer common name</th>'.
           02 FILLER PIC X(21) VALUE '<td>&issuername;</td>'.
           02 FILLER PIC X(30) VALUE '</tr>&CRLF;<tr><th align=left>'.
           02 FILLER PIC X(19) VALUE 'Issuer country</th>'.
           02 FILLER PIC X(24) VALUE '<td>&issuercountry;</td>'.
           02 FILLER PIC X(30) VALUE '</tr>&CRLF;<tr><th align=left>'.
           02 FILLER PIC X(17) VALUE 'Issuer state</th>'.
           02 FILLER PIC X(22) VALUE '<td>&issuerstate;</td>'.
           02 FILLER PIC X(30) VALUE '</tr>&CRLF;<tr><th align=left>'.
           02 FILLER PIC X(20) VALUE 'Issuer locality</th>'.
           02 FILLER PIC X(25) VALUE '<td>&issuerlocality;</td>'.
           02 FILLER PIC X(30) VALUE '</tr>&CRLF;<tr><th align=left>'.
           02 FILLER PIC X(24) VALUE 'Issuer organization</th>'.
           02 FILLER PIC X(21) VALUE '<td>&issuer_org;</td>'.
           02 FILLER PIC X(30) VALUE '</tr>&CRLF;<tr><th align=left>'.
           02 FILLER PIC X(16) VALUE 'Issuer unit</th>'.
           02 FILLER PIC X(25) VALUE '<td>&issuer_orgunit;</td>'.
           02 FILLER PIC X(19) VALUE '</tr>&CRLF;</table>'.
           02 FILLER PIC X(30) VALUE '<p>The userid assigned to this'.
           02 FILLER PIC X(27) VALUE ' certificate is &certuser;.'.
           02 FILLER PIC X(31) VALUE '<br>The userid assigned to this'.
           02 FILLER PIC X(22) VALUE ' request is &requser;.'.

      * This template is displayed if the client doesn't have a valid
      * certificate (or SSL(CLIENTAUTH) is not being used):
        01 WBCA-CLIENT-REJECT-TEMPLATE.
           02 FILLER PIC X(28) VALUE '<h1>Missing certificate</h1>'.
           02 FILLER PIC X(06) VALUE '&CRLF;'.
           02 FILLER PIC X(29) VALUE '<p>Unfortunately, you are not'.
           02 FILLER PIC X(27) VALUE ' allowed to visit this site'.
           02 FILLER PIC X(16) VALUE ' without a valid'.
           02 FILLER PIC X(28) VALUE ' <strong>client certificate'.
           02 FILLER PIC X(10) VALUE '</strong>.'.

      * The following table is used to map the pointers and lengths
      * returned by EXTRACT CERTIFICATE into symbols for substitition
      * into the templates above.
        01 WBCA-CLIENT-CERT-LOCATORS.
           02 WBCA-CLIENT-CERT-FIELDS.
              03 SERIALNUM-VAR       PIC X(16) VALUE 'serialnum       '.
              03 SERIALNUM-PTR       USAGE IS POINTER.
              03 SERIALNUM-LEN       PIC S9(8) COMP.
              03 COMMONNAME-VAR      PIC X(16) VALUE 'commonname      '.
              03 COMMONNAME-PTR      USAGE IS POINTER.
              03 COMMONNAME-LEN      PIC S9(8) COMP.
              03 COUNTRY-VAR         PIC X(16) VALUE 'country         '.
              03 COUNTRY-PTR         USAGE IS POINTER.
              03 COUNTRY-LEN         PIC S9(8) COMP.
              03 STATE-VAR           PIC X(16) VALUE 'state           '.
              03 STATE-PTR           USAGE IS POINTER.
              03 STATE-LEN           PIC S9(8) COMP.
              03 LOCALITY-VAR        PIC X(16) VALUE 'locality        '.
              03 LOCALITY-PTR        USAGE IS POINTER.
              03 LOCALITY-LEN        PIC S9(8) COMP.
              03 ORGANIZATION-VAR    PIC X(16) VALUE 'organization    '.
              03 ORGANIZATION-PTR    USAGE IS POINTER.
              03 ORGANIZATION-LEN    PIC S9(8) COMP.
              03 ORGUNIT-VAR         PIC X(16) VALUE 'orgunit         '.
              03 ORGUNIT-PTR         USAGE IS POINTER.
              03 ORGUNIT-LEN         PIC S9(8) COMP.
              03 CA-COMMONNAME-VAR   PIC X(16) VALUE 'issuername      '.
              03 CA-COMMONNAME-PTR   USAGE IS POINTER.
              03 CA-COMMONNAME-LEN   PIC S9(8) COMP.
              03 CA-COUNTRY-VAR      PIC X(16) VALUE 'issuercountry   '.
              03 CA-COUNTRY-PTR      USAGE IS POINTER.
              03 CA-COUNTRY-LEN      PIC S9(8) COMP.
              03 CA-STATE-VAR        PIC X(16) VALUE 'issuerstate     '.
              03 CA-STATE-PTR        USAGE IS POINTER.
              03 CA-STATE-LEN        PIC S9(8) COMP.
              03 CA-LOCALITY-VAR     PIC X(16) VALUE 'issuerlocality  '.
              03 CA-LOCALITY-PTR     USAGE IS POINTER.
              03 CA-LOCALITY-LEN     PIC S9(8) COMP.
              03 CA-ORGANIZATION-VAR PIC X(16) VALUE 'issuer_org      '.
              03 CA-ORGANIZATION-PTR USAGE IS POINTER.
              03 CA-ORGANIZATION-LEN PIC S9(8) COMP.
              03 CA-ORGUNIT-VAR      PIC X(16) VALUE 'issuer_orgunit  '.
              03 CA-ORGUNIT-PTR      USAGE IS POINTER.
              03 CA-ORGUNIT-LEN      PIC S9(8) COMP.
           02 WBCA-CLIENT-CERT-FIELD REDEFINES WBCA-CLIENT-CERT-FIELDS
                                     OCCURS 13 INDEXED BY FIELD-INDEX.
              03 CERT-FIELD-VAR      PIC X(16).
              03 CERT-FIELD-PTR      USAGE IS POINTER.
              03 CERT-FIELD-LEN      PIC S9(8) COMP.

        01 WBCA-CERTIFICATE-USERID.
           02 CERTIFICATE-USERID PIC X(8).

        01 WBCA-TCPIP-DETAILS.
           02 FILLER PIC X(9)   VALUE '&CRLF;<p>'.
           02 FILLER PIC X(29)  VALUE 'Connection details:&CRLF;<dl>'.
           02 FILLER PIC X(26)  VALUE '&CRLF;<dt>TCPIPSERVICE<dd>'.
           02 SERVIC PIC X(8)   VALUE SPACES.
           02 FILLER PIC X(25)  VALUE '&CRLF;<dt>Client name<dd>'.
           02 CLI-NM PIC X(256) VALUE SPACES.
           02 FILLER PIC X(25)  VALUE '&CRLF;<dt>Server name<dd>'.
           02 SRV-NM PIC X(256) VALUE SPACES.
           02 FILLER PIC X(28)  VALUE '&CRLF;<dt>Client address<dd>'.
           02 CLI-AD PIC X(15)  VALUE SPACES.
           02 FILLER PIC X(28)  VALUE '&CRLF;<dt>Server address<dd>'.
           02 SRV-AD PIC X(15)  VALUE SPACES.
           02 FILLER PIC X(31)  VALUE '&CRLF;<dt>Server portnumber<dd>'.
           02 PORTNO PIC X(15)  VALUE SPACES.
           02 FILLER PIC X(28)  VALUE '&CRLF;<dt>Secure Sockets<dd>'.
           02 SSLTYP PIC X(10)  VALUE SPACES.
           02 FILLER PIC X(26)  VALUE '&CRLF;<dt>Authenticate<dd>'.
           02 AUTHEN PIC X(12)  VALUE SPACES.
           02 FILLER PIC X(28)  VALUE '&CRLF;</dl>'.

       LINKAGE SECTION.
        01 WBCA-CLIENT-CERT-FIELD-VALUE.
           02 CERT-FIELD-CHAR PIC X OCCURS 1 TO 256
                                    DEPENDING ON FIELD-SIZE.

       PROCEDURE DIVISION.
       0000-MAINLINE SECTION.

      * Create a document to contain the certificate details.
           EXEC CICS DOCUMENT CREATE DOCTOKEN(DTOKEN)
                              TEXT(WBCA-DOCUMENT-HEAD)
                              LENGTH(LENGTH OF WBCA-DOCUMENT-HEAD)
                              SYMBOLLIST(WBCA-SYMBOL-LIST)
                              LISTLENGTH(LENGTH OF WBCA-SYMBOL-LIST)
                              DOCSIZE(DSIZE)
           END-EXEC.

      * Extract details about the issuer (Certificate Authority) first.
           EXEC CICS EXTRACT CERTIFICATE ISSUER
                     COMMONNAME   (CA-COMMONNAME-PTR)
                     COMMONNAMLEN (CA-COMMONNAME-LEN)
                     COUNTRY      (CA-COUNTRY-PTR)
                     COUNTRYLEN   (CA-COUNTRY-LEN)
                     STATE        (CA-STATE-PTR)
                     STATELEN     (CA-STATE-LEN)
                     LOCALITY     (CA-LOCALITY-PTR)
                     LOCALITYLEN  (CA-LOCALITY-LEN)
                     ORGANIZATION (CA-ORGANIZATION-PTR)
                     ORGANIZATLEN (CA-ORGANIZATION-LEN)
                     ORGUNIT      (CA-ORGUNIT-PTR)
                     ORGUNITLEN   (CA-ORGUNIT-LEN)
           END-EXEC.

      * Copy the results into local storage,
      * and define them as template symbols.
           PERFORM 0090-SET-SYMBOL VARYING FIELD-INDEX FROM 8 BY 1
                                           UNTIL FIELD-INDEX > 13.

      * Now extract details about the certificate subject (or owner).
           EXEC CICS EXTRACT CERTIFICATE OWNER
                     SERIALNUM    (SERIALNUM-PTR)
                     SERIALNUMLEN (SERIALNUM-LEN)
                     COMMONNAME   (COMMONNAME-PTR)
                     COMMONNAMLEN (COMMONNAME-LEN)
                     COUNTRY      (COUNTRY-PTR)
                     COUNTRYLEN   (COUNTRY-LEN)
                     STATE        (STATE-PTR)
                     STATELEN     (STATE-LEN)
                     LOCALITY     (LOCALITY-PTR)
                     LOCALITYLEN  (LOCALITY-LEN)
                     ORGANIZATION (ORGANIZATION-PTR)
                     ORGANIZATLEN (ORGANIZATION-LEN)
                     ORGUNIT      (ORGUNIT-PTR)
                     ORGUNITLEN   (ORGUNIT-LEN)
                     USERID       (CERTIFICATE-USERID)
           END-EXEC.

      * Set up a symbol for the userid derived from the certificate.
           MOVE ZERO TO SPACE-COUNT.
           INSPECT CERTIFICATE-USERID
                   TALLYING SPACE-COUNT FOR ALL SPACES.
           COMPUTE USERID-LENGTH = LENGTH OF CERTIFICATE-USERID
                                   - SPACE-COUNT.
           MOVE 'certuser' TO SYMBOL-NAME.
           EXEC CICS DOCUMENT SET DOCTOKEN(DTOKEN)
                     SYMBOL (SYMBOL-NAME)
                     VALUE  (CERTIFICATE-USERID)
                     LENGTH (USERID-LENGTH)
           END-EXEC.
      * Set up a symbol for the userid assigned to the transaction.
           EXEC CICS ASSIGN USERID(TRANSACTION-USERID)
           END-EXEC.
           MOVE ZERO TO SPACE-COUNT.
           INSPECT TRANSACTION-USERID
                   TALLYING SPACE-COUNT FOR ALL SPACES.
           COMPUTE USERID-LENGTH = LENGTH OF TRANSACTION-USERID
                                   - SPACE-COUNT.
           MOVE 'requser' TO SYMBOL-NAME.
           EXEC CICS DOCUMENT SET DOCTOKEN(DTOKEN)
                     SYMBOL (SYMBOL-NAME)
                     VALUE  (TRANSACTION-USERID)
                     LENGTH (USERID-LENGTH)
           END-EXEC.
      * Create symbols for the certificate owner fields.
           PERFORM 0090-SET-SYMBOL VARYING FIELD-INDEX
                                   FROM 1 BY 1 UNTIL FIELD-INDEX > 7.

       0020-ADD-PERSON-NAME.
      * A bug in some versions of System SSL prevented it returning the
      * common-name field, so we use a symbol of "personname" above
      * and override it here if the common-name was actually returned.
           IF COMMONNAME-LEN IS GREATER THAN ZERO THEN
              SET ADDRESS OF WBCA-CLIENT-CERT-FIELD-VALUE
                          TO COMMONNAME-PTR
              MOVE 'personname' TO SYMBOL-NAME
              EXEC CICS DOCUMENT SET DOCTOKEN(DTOKEN)
                        SYMBOL (SYMBOL-NAME)
                        VALUE  (WBCA-CLIENT-CERT-FIELD-VALUE)
                        LENGTH (COMMONNAME-LEN)
              END-EXEC.

       0030-ADD-TEMPLATE.
      * Use the DOCUMENT INSERT command to insert the templates into
      * the current document, substituting all the symbol values.
      * If the SERIALNUM is absent, it implies that no certificate
      * is present and the rejection template is used.
      * Otherwise, the welcome template and the certificate detail
      * template are used.
           IF SERIALNUM-LEN IS EQUAL TO ZERO THEN
              EXEC CICS DOCUMENT INSERT DOCTOKEN(DTOKEN)
                        FROM(WBCA-CLIENT-REJECT-TEMPLATE)
                        LENGTH(LENGTH OF WBCA-CLIENT-REJECT-TEMPLATE)
              END-EXEC
           ELSE
              EXEC CICS DOCUMENT INSERT DOCTOKEN(DTOKEN)
                        FROM(WBCA-CLIENT-WELCOME-TEMPLATE)
                        LENGTH(LENGTH OF WBCA-CLIENT-WELCOME-TEMPLATE)
              END-EXEC
              EXEC CICS DOCUMENT INSERT DOCTOKEN(DTOKEN)
                     FROM(WBCA-CLIENT-CERT-DETAILS)
                     LENGTH(LENGTH OF WBCA-CLIENT-CERT-DETAILS)
              END-EXEC.

       0050-EXTRACT-TCPIP-DETAILS.
      * This paragraph demonstrates the use of the EXTRACT TCPIP
      * command to obtain client and server TCPIP details.
           MOVE LENGTH OF CLI-NM TO CLIENT-NAME-LENGTH.
           MOVE LENGTH OF SRV-NM TO SERVER-NAME-LENGTH.
           MOVE LENGTH OF CLI-AD TO CLIENT-ADDR-LENGTH.
           MOVE LENGTH OF SRV-AD TO SERVER-ADDR-LENGTH.
           EXEC CICS EXTRACT TCPIP
                     TCPIPSERVICE(SERVIC)
                     CLIENTNAME(CLI-NM) CNAMELENGTH(CLIENT-NAME-LENGTH)
                     SERVERNAME(SRV-NM) SNAMELENGTH(SERVER-NAME-LENGTH)
                     CLIENTADDR(CLI-AD) CADDRLENGTH(CLIENT-ADDR-LENGTH)
                     SERVERADDR(SRV-AD) SADDRLENGTH(SERVER-ADDR-LENGTH)
                     PORTNUMBER(PORTNO)
                     CLIENTADDRNU(CLIENT-ADDR-BINARY)
                     SERVERADDRNU(SERVER-ADDR-BINARY)
                     PORTNUMNU(PORT-NUMBER-BINARY)
                     SSLTYPE(SSLTYPE-CVDA)
                     AUTHENTICATE(AUTHENT-CVDA)
           END-EXEC.

           EVALUATE SSLTYPE-CVDA
             WHEN DFHVALUE(CLIENTAUTH)
               MOVE 'CLIENTAUTH' TO SSLTYP
             WHEN DFHVALUE(SSL)
               MOVE 'YES       ' TO SSLTYP
             WHEN DFHVALUE(ATTLSAWARE)
               MOVE 'ATTLSAWARE' TO SSLTYP
             WHEN OTHER
               MOVE 'NO        ' TO SSLTYP
           END-EVALUATE.

           EVALUATE AUTHENT-CVDA
             WHEN DFHVALUE(BASICAUTH)
               MOVE 'BASIC       ' TO AUTHEN
             WHEN DFHVALUE(CERTIFICAUTH)
               MOVE 'CERTIFICATE ' TO AUTHEN
             WHEN DFHVALUE(AUTOREGISTER)
               MOVE 'AUTOREGISTER' TO AUTHEN
             WHEN DFHVALUE(AUTOAUTH)
               MOVE 'AUTOMATIC   ' TO AUTHEN
             WHEN OTHER
               MOVE 'NO          ' TO AUTHEN
           END-EVALUATE.

           EXEC CICS DOCUMENT INSERT DOCTOKEN(DTOKEN)
                     FROM(WBCA-TCPIP-DETAILS)
                     LENGTH(LENGTH OF WBCA-TCPIP-DETAILS)
           END-EXEC.

       0070-WEB-SEND.
      * Insert the closing tags to complete the document.
           EXEC CICS DOCUMENT INSERT DOCTOKEN(DTOKEN)
                     TEXT(WBCA-DOCUMENT-FOOT)
                     LENGTH(LENGTH OF WBCA-DOCUMENT-FOOT)
           END-EXEC.

      * Finally, send the completed document back to the client.
           EXEC CICS WEB SEND DOCTOKEN(DTOKEN) CHARACTERSET('8859-1')
           END-EXEC.

           EXEC CICS RETURN
           END-EXEC.

      * The following paragraph is executed iteratively to copy the
      * extracted certificate fields into the document's symbol table.
      * If a field is not present its length is zero, and its value
      * remains at its initial value of &nbsp;
       0090-SET-SYMBOL.
           SET ADDRESS OF WBCA-CLIENT-CERT-FIELD-VALUE
                       TO CERT-FIELD-PTR(FIELD-INDEX).
           MOVE CERT-FIELD-LEN(FIELD-INDEX) TO FIELD-SIZE.
           MOVE CERT-FIELD-VAR(FIELD-INDEX) TO SYMBOL-NAME.
           IF FIELD-SIZE IS GREATER THAN ZERO THEN
              EXEC CICS DOCUMENT SET DOCTOKEN(DTOKEN)
                        SYMBOL (SYMBOL-NAME)
                        VALUE  (WBCA-CLIENT-CERT-FIELD-VALUE)
                        LENGTH (FIELD-SIZE)
              END-EXEC.
