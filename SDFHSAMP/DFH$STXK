         TITLE 'DFH$STXK - CONVERT S/370 FORMAT TOD TO DATE AND TIME'
***********************************************************************
*                                                                     *
* MODULE NAME = DFH$STXK                                              *
*                                                                     *
* DESCRIPTIVE NAME = CICS TS  Convert TOD to Date and Time Sample     *
*                                                                     *
*                                                                     *
*                                                                     *
*        Licensed Materials - Property of IBM                         *
*                                                                     *
*        "Restricted Materials of IBM"                                *
*                                                                     *
*        5655-Y04                                                     *
*                                                                     *
*        (C) Copyright IBM Corp. 2007"                                *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
* STATUS = 7.2.0                                                      *
*                                                                     *
* FUNCTION =                                                          *
*        Convert a S/370 format TOD to EBCDIC date and time.          *
*                                                                     *
* CALLS TO OTHER ROUTINES - NONE                                      *
*                                                                     *
* OPERATION - Convert date and time and Edit into user supplied       *
*             fields. This sample program is used by the sample       *
*             statistics utility program DFHSTUP EXTRACT program      *
*             DFH0STXD.                                               *
*                                                                     *
* ENTRY -  R1 = Address of caller's parameters in the following       *
*               format:-                                              *
*                                                                     *
*   DS   XL8   8 bytes containing time to be converted                *
*   DS   CL10  date - mm/dd/yyyy                                      *
*   DS   CL15  time - hh:mm:ss.thmiju                                 *
*                             t = tenths of seconds                   *
*                             h = hundredths of seconds               *
*                             m = milliseconds                        *
*                             i = ten-thousandths of seconds          *
*                             j = hundred-thousandths of seconds.     *
*                             u = microseconds.                       *
*                                                                     *
*                                                                     *
* EXIT - Date and Time have been generated and moved to the           *
*        specified output areas.                                      *
*                                                                     *
* RESPONSES - NONE                                                    *
*                                                                     *
* ERROR MESSAGES - NONE                                               *
*                                                                     *
*                                                                     *
*-------------------------------------------------------------------- *
*                                                                     *
* CHANGE ACTIVITY :                                                   *
*        $SEG(DFH$STXK),COMP(SAMPLES),PROD(CICS TS ):                 *
*                                                                     *
*     PN= REASON REL YYMMDD HDXIII : REMARKS                          *
*    $L0= 907    660 071207 HD1VCJB: DFHSTUP EXTRACT Sample creation  *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT ,
DFH$STXK CSECT
DFH$STXK AMODE 31
DFH$STXK RMODE ANY
***********************************************************************
*        Get callers parameters, passed via register 1                *
*        Convert STCK using MVS STCKCONV service                      *
*        Edit converted date/time and move to output area             *
*        Return to caller                                             *
***********************************************************************
         USING *,R15                   Addressability
         SAVE  (14,12)                 Save MVS registers
         ST    R13,SAVEAREA+4          Save area chain
         LA    R13,SAVEAREA            Establish save area
         LR    R12,R15                 Establish base register
         DROP  R15
         USING DFH$STXK,R12            Establish addressability
         LM    R2,R4,0(R1)             Get callers parameters
         SPACE ,
         STCKCONV STCKVAL=(R2),CONVVAL=TODOP,DATETYPE=MMDDYYYY,        X
               TIMETYPE=DEC
         SPACE ,
         MVC   DATEWORK(11),=X'F021206120206120202020'
         ED    DATEWORK(11),TODOP+8    EDIT CONVERTED DATE
         MVC   0(10,R3),DATEWORK+1     MOVE TO OUTPUT FIELD
         MVC   TIMEWORK(16),=X'F021207A20207A20204B202020202020'
         ED    TIMEWORK(16),TODOP      EDIT CONVERTED TIME
         MVC   0(15,R4),TIMEWORK+1     MOVE TO OUTPUT FIELD
RETURN   DS    0H
         L     13,SAVEAREA+4           RESTORE SAVE AREA
         RETURN (14,12),RC=0           Restore regs and return
         EJECT ,
***********************************************************************
SAVEAREA DS    18F                 Register save area
TODOP    DC    16C' '              Date & Time conversion output
TIMEWORK DC    16C' '              Date & Time conversion output
DATEWORK DC    11C' '              Date & Time conversion output
         SPACE ,
         LTORG ,
         SPACE ,
         END
