      CBL CICS('COBOL3,SP') APOST
PROCESS MAP,XREF,TRUNC(BIN)
      *****************************************************************
      *                                                               *
      *                                                               *
      * MODULE NAME = DFH0STTS                                        *
      *                                                               *
      * DESCRIPTIVE NAME = Collect and Print Statistics Sample        *
      *                                                               *
      *                                                               *
      *                                                               *
      *                                                               *
      *      Licensed Materials - Property of IBM                     *
      *                                                               *
      *      "Restricted Materials of IBM"                            *
      *                                                               *
      *      5655-Y04                                                 *
      *                                                               *
      *      (C) Copyright IBM Corp. 2008, 2015"                      *
      *                                                               *
      *                                                               *
      *                                                               *
      *                                                               *
      * STATUS = 7.2.0                                                8
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      * DESCRIPTION :                                                 *
      *                                                               *
      *     This set of programs illustrate ways of using the EXEC    *
      *     CICS INQUIRE, COLLECT and EXTRACT STATISTICS commands of  *
      *     CICS to produce information about a CICS system.          *
      *     The reports include a CICS and MVS storage analysis.      *
      *     Statistics obtained using these commands can be used to   *
      *     aid the specification of the DSA Size parameters.         *
      *     (See the CICS Performance Guide and CICS Data Areas       *
      *      manuals for more information, and the CICS System        *
      *      Programming Reference for use of commands)               *
      *                                                               *
      *     These programs can be invoked from the PLT at PLTSD or    *
      *     as a CICS transaction (conversationally).                 *
      *     The output is sent via the CICS JES SPOOL interface for   *
      *     which a number of default parameters can be changed by    *
      *     the user to specify the distribution of the report(s).    *
      *     These defaults are defined in the working-storage         *
      *     section of the DFH0STAT program under the 01 level        *
      *     "OUTPUT-DEFAULTS".                                        *
      *                                                               *
      *     Installation instructions:-                               *
      *     Require SIT parameters:-  SPOOL=YES                       *
      *     Required CICS definitions:-                               *
      *         Transactions:-  STAT -> DFH0STAT                      *
      *         Programs:-    DFH0STAT   IBM COBOL                    *
      *                       DFH0STDB       for OS/390 & VM          *
      *                       DFH0STEJ           2.1.1 or later       *
      *                       DFH0STEP       "   "   "                *
      *                       DFH0STGN       "   "   "                *
      *                       DFH0STLK       "   "   "                *
      *                       DFH0STPR       "   "   "                *
      *                       DFH0STSA       "   "   "                *
      *                       DFH0STSY       "   "   "                *
      *                       DFH0STTP       "   "   "                *
      *                       DFH0STTS       "   "   "                *
      *                       DFH0STWB       "   "   "                *
      *                       DFH$STAS   Assembler                    *
      *                       DFH$STCN   Assembler                    *
      *                       DFH$STTB   Assembler                    *
      *         Mapsets:-     DFH0STM                                 *
      *                       DFH0STS                                 *
      *                                                               *
      *     The CICS-supplied RDO group, DFH$STAT, contains all       *
      *     the necessary resource definitions for this sample.       *
      *                                                               *
      *     All programs are command level and run above the          *
      *     16Mb line. Note: that DFH0STAT, DFH0STDB, DFH0STEJ,       *
      *     DFH0STEP, DFH0STGN, DFH0STLK, DFH0STPR, DFH0STSA,         *
      *     DFH0STSY, DFH0STTP, DFH0STTS, and DFH0STWB must be        *
      *     translated with the Translator options "COBOL3 and SP".   *
      *                                                               *
      *   SPOOL errors.  If an EXEC CICS SPOOL .. command fails       *
      *   when the program is run as a transaction, an error message  *
      *   is displayed on the users screen and the transaction will   *
      *   continue.  If the program is not being run from a terminal  *
      *   then a message will be sent to the console using EXEC       *
      *   CICS WRITE OPERATOR commands and the transaction will be    *
      *   terminated normally.                                        *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      * CHANGE ACTIVITY :                                             *
      *      $SEG(DFH0STTS),COMP(SAMPLES),PROD(CICS TS ):             *
      *                                                               *
      *   PN= REASON REL YYMMDD HDXXIII : REMARKS                     *
      *  $L0= 927    660 080605 HD1VCJB : Module created for DFH0STAT *
      *  $L1= R00207 670 091208 HD4PALT : TSMODEL defn extended       *
      *  $L2= R00207 670 100423 HD1VCJB : Statistics for TS.          *
      *  $L3= R00207 670 100602 HDAFDRB : TS final statistics changes.*
      *  $L4= R00327 670 101110 HD1VCJB : Storage Statistics Update   *
      *       R31410 680 111025 HD1VCJB : GxDSA Support               *
      *       R38042 690 130404 HDAFDRB : Use EXTRACT STATISTICS      *
      *       R38042 690 130422 HDIDNCS : Use EXTRACT STATISTICS      *
      *  $P1= D22695 660 081031 HD1VCJB : Correct Tsqueues Headings   *
      *  $P2= D16681 670 100705 HDAFDRB : clean up fields             *
      *  $P3= D18560 670 100831 HD1VCJB : Tsqueue length field capacit*
      *  $P4= D27982 670 110323 HD1VCJB : 64-bit S2 Performance Update*
      *       D67762 690 130314 HDAFDRB : More data for shared TS     *
      *       R92456 700 140821 HDMUXYZ : Peak item no. for INTRA TDQ *
      *       D98593 700 150106 HD3BADW: Fix comments                 *
      *      D150786 720 181023 HDFVGMB: Add TRUNC(BIN) to PROCESS    *
      *                                                               *
      *****************************************************************
       ID DIVISION.
       PROGRAM-ID. DFH0STTS.

       ENVIRONMENT DIVISION.
           EJECT
       DATA DIVISION.
       WORKING-STORAGE SECTION.
       77  STCN-COMMAREA               PIC S9(4)  COMP.
       77  INQUIRE-RESP                PIC S9(8)  COMP VALUE +0.
       77  INQUIRE-TSQNAME-RESP        PIC S9(8)  COMP VALUE +0.
       77  EXTRACT-SUBPOOL-RESP        PIC S9(8)  COMP VALUE +0.

       77  SUB1                        PIC S9(4)  COMP VALUE +0.
       77  SUB2                        PIC S9(4)  COMP VALUE +0.
       77  SUB3                        PIC S9(4)  COMP VALUE +0.

       77  PERCENTAGE-WORK             PIC 99V9999     VALUE 0.
       77  PERCENTAGE                  PIC 999V99      VALUE 0.
       77  RESULT                      PIC S9(8)  COMP VALUE +0.

       77  PUTQ-COMPRESSION-RATIO      PIC 9(7)V99     VALUE 0.

       77  DSA-LIMIT                   PIC S9(8)  COMP VALUE +0.
       77  EDSA-LIMIT                  PIC S9(8)  COMP VALUE +0.

       77  CDSA-SIZE                   PIC S9(8)  COMP VALUE +0.
       77  UDSA-SIZE                   PIC S9(8)  COMP VALUE +0.
       77  SDSA-SIZE                   PIC S9(8)  COMP VALUE +0.
       77  RDSA-SIZE                   PIC S9(8)  COMP VALUE +0.
       77  ECDSA-SIZE                  PIC S9(8)  COMP VALUE +0.
       77  EUDSA-SIZE                  PIC S9(8)  COMP VALUE +0.
       77  ESDSA-SIZE                  PIC S9(8)  COMP VALUE +0.
       77  ERDSA-SIZE                  PIC S9(8)  COMP VALUE +0.
       77  ETDSA-SIZE                  PIC S9(8)  COMP VALUE +0.
       77  GCDSA-SIZE                  PIC S9(8)  COMP VALUE +0.
       77  GUDSA-SIZE                  PIC S9(8)  COMP VALUE +0.
       77  GSDSA-SIZE                  PIC S9(8)  COMP VALUE +0.
           EJECT
       77  TSMODEL-NAME                PIC X(8)        VALUE SPACES.
       77  TSMODEL-LOCATION            PIC S9(8)  COMP VALUE +0.
       77  TSMODEL-EXPIRYINT           PIC S9(8)  COMP VALUE +0.
       77  TSMODEL-POOLNAME            PIC X(8)        VALUE SPACES.
       77  TSMODEL-PREFIX              PIC X(16)       VALUE SPACES.
       77  TSMODEL-RECOVERY-STATUS     PIC S9(8)  COMP VALUE +0.
       77  TSMODEL-REMOTE-PREFIX       PIC X(16)       VALUE SPACES.
       77  TSMODEL-REMOTE-SYSTEM       PIC X(4)        VALUE SPACES.
       77  TSMODEL-SECURITY-STATUS     PIC S9(8)  COMP VALUE +0.

       77  TSQUEUE-NAME                PIC X(16)       VALUE SPACES.
       77  TSQUEUE-LOCATION            PIC S9(8)  COMP VALUE +0.
       77  TSQUEUE-FLENGTH             PIC S9(8)  COMP VALUE +0.
       77  TSQUEUE-NUMITEMS            PIC S9(4)  COMP VALUE +0.
       77  TSQUEUE-MAXITEMLEN          PIC S9(4)  COMP VALUE +0.
       77  TSQUEUE-MINITEMLEN          PIC S9(4)  COMP VALUE +0.
       77  TSQUEUE-TRANSID             PIC X(8)        VALUE SPACES.
       77  TSQUEUE-LASTUSEDINT         PIC S9(8)  COMP VALUE +0.
       77  TSQUEUE-EXPIRYINT           PIC S9(8)  COMP VALUE +0.
       77  TSQUEUE-RECOVERY            PIC S9(8)  COMP VALUE +0.

       77  TSPOOL-NAME                 PIC X(8)        VALUE SPACES.
       77  TSPOOL-STATUS               PIC S9(8)  COMP VALUE +0.

       77  TDQUEUE-NAME                PIC X(4)        VALUE SPACES.
       77  ATI-FACILITY                PIC S9(8)  COMP VALUE +0.
       77  ATI-TERMID                  PIC X(4)        VALUE SPACES.
       77  ATI-USERID                  PIC X(8)        VALUE SPACES.

       77  TDQUEUE-TYPE                PIC S9(8)  COMP VALUE +0.
           EJECT
      *****************************************************************
      ***  Total fields                                             ***
      *****************************************************************
       77  TSQUEUE-TOTAL               PIC S9(9)  COMP-3 VALUE +0.
       77  AUX-TSQUEUES                PIC S9(9)  COMP-3 VALUE +0.
       77  AUX-TSQUEUE-ITEMS           PIC S9(9)  COMP-3 VALUE +0.
       77  MAIN-TSQUEUES               PIC S9(9)  COMP-3 VALUE +0.
       77  MAIN-TSQUEUE-ITEMS          PIC S9(9)  COMP-3 VALUE +0.
       77  TSPOOL-TSQUEUE-ITEMS        PIC S9(9)  COMP-3 VALUE +0.

       77  TSMODEL-TOTAL               PIC S9(9)  COMP-3 VALUE +0.

       77  TDQUEUE-TOTAL               PIC S9(9)  COMP-3 VALUE +0.
       77  INTRA-TDQUEUES              PIC S9(9)  COMP-3 VALUE +0.
       77  EXTRA-TDQUEUES              PIC S9(9)  COMP-3 VALUE +0.
       77  INDIRECT-TDQUEUES           PIC S9(9)  COMP-3 VALUE +0.
       77  REMOTE-TDQUEUES             PIC S9(9)  COMP-3 VALUE +0.

       77  INTRA-TDQUEUE-WRITES        PIC S9(9)  COMP-3 VALUE +0.
       77  INTRA-TDQUEUE-READS         PIC S9(9)  COMP-3 VALUE +0.
       77  INTRA-TDQUEUE-DELETES       PIC S9(9)  COMP-3 VALUE +0.

       77  EXTRA-TDQUEUE-WRITES        PIC S9(9)  COMP-3 VALUE +0.
       77  EXTRA-TDQUEUE-READS         PIC S9(9)  COMP-3 VALUE +0.

       77  INDIRECT-TDQUEUE-WRITES     PIC S9(9)  COMP-3 VALUE +0.
       77  INDIRECT-TDQUEUE-READS      PIC S9(9)  COMP-3 VALUE +0.
       77  INDIRECT-TDQUEUE-DELETES    PIC S9(9)  COMP-3 VALUE +0.

       77  REMOTE-TDQUEUE-WRITES       PIC S9(9)  COMP-3 VALUE +0.
       77  REMOTE-TDQUEUE-READS        PIC S9(9)  COMP-3 VALUE +0.
       77  REMOTE-TDQUEUE-DELETES      PIC S9(9)  COMP-3 VALUE +0.

      *****************************************************************
      ***  Subpool total fields for TS Main Storage Subpools.       ***
      *****************************************************************
       01  TSMAIN-SUBPOOL-TOTALS.
           05  TSMAIN-SUBPOOL-GETMAINS     PIC S9(9)  COMP-3.
           05  TSMAIN-SUBPOOL-FREEMAINS    PIC S9(9)  COMP-3.
           05  TSMAIN-SUBPOOL-ELEMENTS     PIC S9(9)  COMP-3.
           05  TSMAIN-SUBPOOL-ELEMENT-STG  PIC S9(9)  COMP-3.
           05  TSMAIN-SUBPOOL-PAGE-STG     PIC S9(9)  COMP-3.
           EJECT
      *****************************************************************
      *** Temporary Stg TSMAIN and TSBUFFRS Domain Subpool Stats.   ***
      *****************************************************************
       01  TSBUFFRS-SUBPOOL-STATS.
           03  TSBUFFRS-SUBPOOL-NAME       PIC X(8)  VALUE 'TSBUFFRS'.
           03  TSBUFFRS-SUBPOOL-DSANAME    PIC X(8)  VALUE SPACES.
           03  TSBUFFRS-INITIAL-FREE       PIC S9(8) COMP VALUE +0.
           03  TSBUFFRS-GETMAIN-REQUESTS   PIC S9(8) COMP VALUE +0.
           03  TSBUFFRS-FREEMAIN-REQUESTS  PIC S9(8) COMP VALUE +0.
           03  TSBUFFRS-CURRENT-ELEMS      PIC S9(8) COMP VALUE +0.
           03  TSBUFFRS-CURRENT-ELEM-STG   PIC S9(8) COMP VALUE +0.
           03  TSBUFFRS-CURRENT-PAGE-STG   PIC S9(8) COMP VALUE +0.
           03  TSBUFFRS-PEAK-PAGE-STG      PIC S9(8) COMP VALUE +0.
           EJECT
      *****************************************************************
      ***  Temporary Storage Main - Storage Subpools.               ***
      *****************************************************************
       01  TSMAIN-STORAGE-DOMAIN-SUBPOOLS.
           03  TSMAIN-SUBPOOL-NAMES.
               05  FILLER              PIC X(8)        VALUE 'TSMAIN  '.
               05  FILLER              PIC X(8)        VALUE 'TSMN0064'.
               05  FILLER              PIC X(8)        VALUE 'TSMN0128'.
               05  FILLER              PIC X(8)        VALUE 'TSMN0192'.
               05  FILLER              PIC X(8)        VALUE 'TSMN0256'.
               05  FILLER              PIC X(8)        VALUE 'TSMN0320'.
               05  FILLER              PIC X(8)        VALUE 'TSMN0384'.
               05  FILLER              PIC X(8)        VALUE 'TSMN0448'.
               05  FILLER              PIC X(8)        VALUE 'TSMN0512'.
               05  FILLER              PIC X(8)        VALUE 'TSMN0576'.
               05  FILLER              PIC X(8)        VALUE 'TSMN0640'.
               05  FILLER              PIC X(8)        VALUE 'TSMN0704'.
               05  FILLER              PIC X(8)        VALUE 'TSMN0768'.
               05  FILLER              PIC X(8)        VALUE 'TSMN0832'.
               05  FILLER              PIC X(8)        VALUE 'TSMN0896'.
               05  FILLER              PIC X(8)        VALUE 'TSMN0960'.
               05  FILLER              PIC X(8)        VALUE 'TSMN1024'.
           03  FILLER REDEFINES TSMAIN-SUBPOOL-NAMES OCCURS 17.
               05  TSMAIN-SUBPOOL-NAME     PIC X(8).
           EJECT
      *****************************************************************
      ***  Report print line definitions                            ***
      *****************************************************************
       01  PRINT-LINE.
           03  PRINT-CONTROL           PIC X.
           03  PP1-132.
               05  FILLER              PIC XX.
               05  PP3-66.
                   07  PP3-47.
                       09  PP3-44      PIC X(42).
                       09  FILLER      PIC X(4).
                   07  FILLER          PIC X(6).
                   07  PP55-64         PIC ZZ,ZZZ,ZZ9.
                   07  FILLER          PIC XX.
               05  FILLER              PIC XXX.
               05  PP70-73             PIC X(4).
               05  FILLER              PIC XXX.
               05  PP77-88             PIC X(12).
               05  FILLER              PIC X(44).
           03 FILLER REDEFINES PP1-132.
               05  PP1-24.
                   07  PP1-17.
                       09  PP1-14      PIC X(14).
                       09  FILLER      PIC XXX.
                   07  FILLER          PIC X(7).
               05  FILLER              PIC XX.
               05  PP27-36             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X(96).
           03 FILLER REDEFINES PP1-132.
               05  PP1-42.
                   07  FILLER          PIC X(36).
                   07  PP37-42         PIC ZZ,ZZ9.
               05  FILLER              PIC X(4).
               05  PP47-52             PIC ZZ,ZZ9.
               05  FILLER              PIC X(5).
               05  PP58-83             PIC X(26).
               05  FILLER              PIC XX.
               05  PP86-96             PIC X(11).
               05  FILLER              PIC X(36).
           03 FILLER REDEFINES PP1-132.
               05  PP1-21              PIC X(21).
               05  PP22-31             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X(8).
               05  PP40-47             PIC X(8).
               05  FILLER              PIC X(9).
               05  PP57-60             PIC X(4).
               05  FILLER              PIC X(5).
               05  PP66-69             PIC X(4).
               05  FILLER              PIC X(4).
               05  PP74-77             PIC X(4).
               05  FILLER              PIC XXX.
               05  PP81-86             PIC ZZ,ZZ9.
               05  FILLER              PIC XXX.
               05  PP90-95             PIC ZZ,ZZ9.
               05  FILLER              PIC XXX.
               05  PP99-104            PIC ZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP107-110           PIC X(4).
               05  FILLER              PIC XX.
               05  PP113-116           PIC X(4).
               05  FILLER              PIC XX.
               05  PP119-122           PIC X(4).
               05  FILLER              PIC XX.
               05  PP125-132           PIC X(8).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(17).
               05  PP18-27             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X.
               05  PP29-38             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X.
               05  PP40-49             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC XXX.
               05  PP53-56             PIC X(4).
               05  FILLER              PIC X(4).
               05  PP61-64             PIC X(4).
               05  FILLER              PIC XXX.
               05  PP68-71             PIC X(4).
               05  FILLER              PIC XX.
               05  PP74-79             PIC ZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP82-87             PIC ZZ,ZZ9.
               05  FILLER              PIC X(45).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(6).
               05  PP7-88.
                   07  PP7-22          PIC X(16).
                   07  FILLER          PIC XXX.
                   07  PP26-31         PIC ZZ,ZZ9.
                   07  FILLER          PIC X(57).
               05  FILLER              PIC X(44).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC XX.
               05  PP3-6               PIC X(4).
               05  FILLER              PIC XX.
               05  PP9-16              PIC X(8).
               05  FILLER              PIC X(22).
               05  PP39-48             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP51-60             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP63-72             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X(60).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC XX.
               05  PP3-24              PIC X(22).
               05  FILLER              PIC X(45).
               05  PP70-72             PIC XXX.
               05  FILLER              PIC X(60).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC XX.
               05  PP3-10              PIC X(8).
               05  FILLER              PIC XX.
               05  PP13-28             PIC X(16).
               05  FILLER              PIC XX.
               05  PP31-39             PIC X(9).
               05  FILLER              PIC XX.
               05  PP42-49             PIC X(8).
               05  FILLER              PIC X(6).
               05  PP56-58             PIC XXX.
               05  FILLER              PIC X(7).
               05  PP66-68             PIC 999.
               05  PP69                PIC X.
               05  PP70-71             PIC 99.
               05  FILLER              PIC X(61).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(72).
               05  PP73-82             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X(50).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(47).
               05  PP48-59             PIC Z,ZZZ,ZZ9.99.
               05  FILLER              PIC X(9).
               05  PP69-78             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X(54).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC XX.
               05  PP3-132.
                   07  PP3-102.
                       09  PP3-72.
                           11  PP3-37  PIC X(35).
                           11  PP38-72 PIC X(35).
                       09  PP73-102    PIC X(30).
                   07  PP103-132       PIC X(30).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(49).
               05  PP50-59             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X(51).
               05  PP111-123           PIC Z,ZZZ,ZZZ,ZZ9.
               05  PP124               PIC X.
               05  FILLER              PIC X(8).
           03 FILLER REDEFINES PP1-132.
               05  PP1-41              PIC X(41).
               05  FILLER              PIC X(5).
               05  PP47-59             PIC Z,ZZZ,ZZZ,ZZ9.
               05  PP60                PIC X.
               05  FILLER              PIC X(13).
               05  PP74-108.
                   07  PP74-99         PIC X(26).
                   07  FILLER          PIC X(9).
               05  FILLER              PIC X(7).
               05  PP116-123.
                   07  FILLER          PIC XX.
                   07  PP118-123       PIC ZZ9.99.
               05  FILLER              PIC X(9).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC XX.
               05  PP3-18              PIC X(16).
               05  FILLER              PIC XX.
               05  PP21-29             PIC X(9).
               05  FILLER              PIC XX.
               05  PP32-37             PIC ZZ,ZZ9.
               05  FILLER              PIC X(5).
               05  PP43-48             PIC ZZ,ZZ9.
               05  FILLER              PIC X(4).
               05  PP53-58             PIC ZZ,ZZ9.
               05  FILLER              PIC XXX.
               05  PP62-74             PIC Z,ZZZ,ZZZ,ZZ9.
               05  FILLER              PIC XXX.
               05  PP78-81             PIC X(4).
               05  FILLER              PIC XXX.
               05  PP85-96             PIC X(12).
               05  FILLER              PIC X(6).
               05  PP103-105           PIC XXX.
               05  FILLER              PIC X(7).
               05  PP113-115           PIC 999.
               05  PP116               PIC X.
               05  PP117-118           PIC 99.
               05  FILLER              PIC X(14).
           03  FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(12).
               05  PP13-20             PIC X(8).
               05  FILLER              PIC XX.
               05  PP23-30             PIC X(8).
               05  FILLER              PIC XX.
               05  PP33-39             PIC ZZZ,ZZ9.
               05  PP40                PIC X.
               05  FILLER              PIC XX.
               05  PP43-55             PIC Z,ZZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP58-70             PIC Z,ZZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  FILLER              PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP85-97             PIC Z,ZZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP100-109           PIC ZZ,ZZZ,ZZ9.
               05  PP110               PIC X.
               05  FILLER              PIC XX.
               05  PP113-118           PIC ZZ9.99.
               05  PP119               PIC X.
               05  FILLER              PIC XX.
               05  PP122-131           PIC ZZ,ZZZ,ZZ9.
               05  PP132               PIC X.
           EJECT
       LINKAGE SECTION.
           EJECT
      *****************************************************************
      ***  Statistics Sample Programs Commarea.                     ***
      *****************************************************************
       01  DFHCOMMAREA.
           COPY DFH0STCM.
           EJECT
      *****************************************************************
      *** Data structures for Storage Manager Domain Subpool stats  ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHSMDDS.
           EJECT
      *****************************************************************
      *** Data structures for Storage Manager DSA                   ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHSMSDS.
           EJECT
      *****************************************************************
      *** Data structures for Transient Data Global statistics      ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHTQGDS.
           EJECT
      *****************************************************************
      *** Data structures for Transient Data Resource statistics    ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHTQRDS.
           EJECT
      *****************************************************************
      *** Data structures for Temporary Storage Global statistics   ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHTSGDS.
           EJECT
      *****************************************************************
      ***                                                           ***
      ***           CICS Collect and print statistics               ***
      ***                                                           ***
      *****************************************************************
       PROCEDURE DIVISION.

      *****************************************************************
      *** MAIN-PROCESSING                                           ***
      ***     Initialise the totals fields                          ***
      ***     Get CICS system information                           ***
      ***     Print the statistics reports                          ***
      ***     Return to DFH0STLK                                    ***
      *****************************************************************

       MAIN-PROCESSING SECTION.

           PERFORM TOTALS-INITIALISE.

           PERFORM PRINT-STATISTICS.

           EXEC CICS RETURN
           END-EXEC.

       MAIN-PROCESSING-EXIT.
           GOBACK.
           EJECT
      *****************************************************************
      *****************************************************************
      ***         Collect and Print the CICS Statistics             ***
      *****************************************************************
      *****************************************************************
      ***                                                           ***
      *** 1. Print the statistics in the following order:-          ***
      ***    - Temporary Storage (Global)                           ***
      ***    - Temporary Storage Queues                             ***
      ***    - Transient Data (Global and Resource)                 ***
      ***                                                           ***
      *****************************************************************
      *****************************************************************

       PRINT-STATISTICS SECTION.

           IF SELECT-TEMPSTOR = 'Y' OR 'y' THEN
               PERFORM TEMP-STORAGE-STATISTICS
           END-IF.

           IF SELECT-TSMODELS = 'Y' OR 'y' THEN
               PERFORM TSMODEL-STATISTICS
           END-IF.

           IF SELECT-TSQUEUES = 'Y' OR 'y' THEN
               PERFORM TSQUEUE-STATISTICS
           END-IF.

           IF SELECT-TSQUEUES-BY-POOL = 'Y' OR 'y' THEN
               PERFORM TSQUEUE-BY-POOL-STATS
           END-IF.

           IF SELECT-TRANDATA = 'Y' OR 'y' THEN
               PERFORM TRANSIENT-DATA-STATISTICS
           END-IF.

           IF SELECT-TDQUEUES = 'Y' OR 'y' THEN
               PERFORM TDQUEUE-STATISTICS
           END-IF.

       PRINT-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** TEMP-STORAGE-STATISTICS                                   ***
      ***   Collect the Temporary Storage Global statistics.        ***
      ***   Print heading and Temporary Storage statistics.         ***
      *****************************************************************
       TEMP-STORAGE-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO TSQUEUE-PAGENO.

           MOVE '-'     TO PRINT-CONTROL.
           MOVE 'Temporary Storage' TO PP1-17.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-17.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      *** Get the Storage Manager DSA statistics.                   ***
      *****************************************************************
           EXEC CICS EXTRACT STATISTICS STORAGE
               SET(ADDRESS OF DFHSMSDS)
           END-EXEC.

           MOVE SMSDSALIMIT  TO DSA-LIMIT.
           MOVE SMSEDSALIMIT TO EDSA-LIMIT.

           MOVE SMSDSASZ(1)  TO CDSA-SIZE.
           MOVE SMSDSASZ(2)  TO UDSA-SIZE.
           MOVE SMSDSASZ(3)  TO SDSA-SIZE.
           MOVE SMSDSASZ(4)  TO RDSA-SIZE.
           MOVE SMSDSASZ(5)  TO ECDSA-SIZE.
           MOVE SMSDSASZ(6)  TO EUDSA-SIZE.
           MOVE SMSDSASZ(7)  TO ESDSA-SIZE.
           MOVE SMSDSASZ(8)  TO ERDSA-SIZE.
           MOVE SMSDSASZ(9)  TO ETDSA-SIZE.
           MOVE SMSDSASZ(10) TO GCDSA-SIZE.
      *    MOVE SMSDSASZ(11) TO GUDSA-SIZE.
      *    MOVE SMSDSASZ(12) TO GSDSA-SIZE.

      *****************************************************************
      *** Get the Storage Manager TS Domain Subpool statistics.     ***
      *****************************************************************
           EXEC CICS EXTRACT STATISTICS SUBPOOL
               RESID(TSBUFFRS-SUBPOOL-NAME)
               SET(ADDRESS OF DFHSMDDS) NOHANDLE
           END-EXEC.

           IF EIBRESP NOT = DFHRESP(NORMAL) THEN
               GO TO SKIP-TEMP-STORAGE-BUFFERS
           END-IF.

           MOVE SMDSPN     TO TSBUFFRS-SUBPOOL-NAME.
           MOVE SMDDSANAME TO TSBUFFRS-SUBPOOL-DSANAME.
           MOVE SMDIFREE   TO TSBUFFRS-INITIAL-FREE.
           MOVE SMDGMREQ   TO TSBUFFRS-GETMAIN-REQUESTS.
           MOVE SMDFMREQ   TO TSBUFFRS-FREEMAIN-REQUESTS.
           MOVE SMDCELEM   TO TSBUFFRS-CURRENT-ELEMS.
           MOVE SMDCES     TO TSBUFFRS-CURRENT-ELEM-STG.
           MOVE SMDCPS     TO TSBUFFRS-CURRENT-PAGE-STG.
           MOVE SMDHWMPS   TO TSBUFFRS-PEAK-PAGE-STG.

       SKIP-TEMP-STORAGE-BUFFERS.

      *****************************************************************
      *** Collect the Temporary Storage Global statistics.          ***
      *****************************************************************
           EXEC CICS EXTRACT STATISTICS TSQUEUE
               SET(ADDRESS OF DFHTSGDS)
               NOHANDLE
           END-EXEC.

           IF EIBRESP NOT = DFHRESP(NORMAL) THEN
               GO TO TEMP-STORAGE-STATISTICS-EXIT
           END-IF.

      *****************************************************************
      *** Print the Temporary Storage Global statistics.            ***
      *****************************************************************
           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Put/Putq main storage requests . . . . . :'
                         TO PP3-44.
           MOVE TSGSTA5F TO PP47-59.
           PERFORM WRITE-OUTPUT.

           MOVE 'Get/Getq main storage requests . . . . . :'
                         TO PP3-44.
           MOVE TSGNMG   TO PP47-59.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Current TSMAINLIMIT Setting. . . . . . . :'
                         TO PP3-44.
           COMPUTE RESULT = (TSGTSMLM / 1024).
           MOVE RESULT   TO PP47-59.
           MOVE 'K'      TO PP60.
           PERFORM WRITE-OUTPUT.
           MOVE 'Times at TSMAINLIMIT . . . . . . . . . . :'
                         TO PP3-44.
           MOVE TSGTSLHT TO PP47-59.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Current storage used for TSMAINLIMIT . . :'
                         TO PP3-44.
           COMPUTE RESULT = (TSGTSMUS / 1024).
           MOVE RESULT   TO PP47-59.
           MOVE 'K'      TO PP60.
           PERFORM WRITE-OUTPUT.
           MOVE 'Peak storage used for TSMAINLIMIT. . . . :'
                         TO PP3-44.
           COMPUTE RESULT = (TSGTSMAX / 1024).
           MOVE RESULT   TO PP47-59.
           MOVE 'K'      TO PP60.
           PERFORM WRITE-OUTPUT.

           MOVE 'Number of TS queues auto deleted . . . . :'
                         TO PP3-44.
           MOVE TSGTSQDL TO PP47-59.
           PERFORM WRITE-OUTPUT.

           MOVE 'Number of times cleanup task has run . . :'
                         TO PP3-44.
           MOVE TSGTSCTR TO PP47-59.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Put/Putq auxiliary storage requests. . . :'
                         TO PP3-44.
           MOVE TSGSTA7F TO PP47-59.
           PERFORM WRITE-OUTPUT.

           MOVE 'Get/Getq auxiliary storage requests. . . :'
                         TO PP3-44.
           MOVE TSGNAG   TO PP47-59.
           PERFORM WRITE-OUTPUT.

           PERFORM WRITE-OUTPUT.

           MOVE 'Times temporary storage queue created. . :'
                         TO PP3-44.
           MOVE TSGSTA3F TO PP47-59.
           PERFORM WRITE-OUTPUT.

           MOVE 'Peak temporary storage queues in use . . :'
                         TO PP3-44.
           MOVE TSGQNUMH TO PP47-59.
           PERFORM WRITE-OUTPUT.

           MOVE 'Current temporary storage queues in use. :'
                         TO PP3-44.
           MOVE TSGQNUM  TO PP47-59.
           PERFORM WRITE-OUTPUT.

           MOVE 'Items in longest queue . . . . . . . . . :'
                         TO PP3-44.
           MOVE TSGQINH  TO PP47-59.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Control interval size. . . . . . . . . . :'
                         TO PP3-44.
           MOVE TSGCSZ   TO PP47-59.
           PERFORM WRITE-OUTPUT.

           MOVE 'Control intervals in the DFHTEMP dataset :'
                         TO PP3-44.
           MOVE TSGNCI   TO PP47-59.
           PERFORM WRITE-OUTPUT.

           MOVE 'Peak control intervals used. . . . . . . :'
                         TO PP3-44.
           MOVE TSGNCIAH TO PP47-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Current control intervals in use . . . . :'
                         TO PP3-44.
           MOVE TSGNCIA  TO PP47-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Available bytes per control interval . . :'
                         TO PP3-44.
           MOVE TSGNAVB  TO PP47-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Segments per control interval. . . . . . :'
                         TO PP3-44.
           MOVE TSGSPCI  TO PP47-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Bytes per segment. . . . . . . . . . . . :'
                         TO PP3-44.
           MOVE TSGBPSEG TO PP47-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Writes bigger than control interval size :'
                         TO PP3-44.
           MOVE TSGSTABF TO PP47-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Largest record length written. . . . . . :'
                         TO PP3-44.
           MOVE TSGLAR   TO PP47-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Times auxiliary storage exhausted. . . . :'
                         TO PP3-44.
           MOVE TSGSTA8F TO PP47-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Number Temporary storage compressions. . :'
                         TO PP3-44.
           MOVE TSGSTA9F TO PP47-59.
           PERFORM WRITE-OUTPUT.

           MOVE 'Put auxiliary / compression ratio. . . . :'
                         TO PP3-44.
           IF TSGSTA9F > 0 THEN
               COMPUTE PUTQ-COMPRESSION-RATIO = (TSGSTA7F / TSGSTA9F)
               END-COMPUTE
           ELSE
               MOVE TSGSTA7F TO PUTQ-COMPRESSION-RATIO
           END-IF.
           MOVE PUTQ-COMPRESSION-RATIO TO PP48-59.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Temporary storage strings. . . . . . . . :'
                         TO PP3-44.
           MOVE TSGNVCA  TO PP47-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Peak Temporary storage strings in use. . :'
                         TO PP3-44.
           MOVE TSGNVCAH TO PP47-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Temporary storage string waits . . . . . :'
                         TO PP3-44.
           MOVE TSGVWTN  TO PP47-59.
           PERFORM WRITE-OUTPUT.

           MOVE 'Peak users waiting on string . . . . . . :'
                         TO PP3-44.
           MOVE TSGVUWTH TO PP47-59.
           PERFORM WRITE-OUTPUT.

           MOVE 'Current users waiting on string. . . . . :'
                         TO PP3-44.
           MOVE TSGVUWT  TO PP47-59.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Temporary storage buffers. . . . . . . . :'
                         TO PP3-44.
           MOVE TSGNBCA  TO PP47-59.
           MOVE '''TSBUFFRS'' Storage Subpool' TO PP74-99.
           PERFORM WRITE-OUTPUT.

           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP74-99.
           PERFORM WRITE-OUTPUT.

           MOVE 'Temporary storage buffer waits . . . . . :'
                         TO PP3-44.
           MOVE TSGBWTN  TO PP47-59.
           PERFORM WRITE-OUTPUT.

           MOVE 'Peak users waiting on buffer . . . . . . :'
                         TO PP3-44.
           MOVE TSGBUWTH TO PP47-59.
           MOVE 'Storage Subpool Location. . . . . :' TO PP74-108.
           MOVE TSBUFFRS-SUBPOOL-DSANAME              TO PP116-123.
           PERFORM WRITE-OUTPUT.

           MOVE 'Current users waiting on buffer. . . . . :'
                         TO PP3-44.
           MOVE TSGBUWT  TO PP47-59.
           MOVE 'Getmain Requests. . . . . . . . . :' TO PP74-108.
           MOVE TSBUFFRS-GETMAIN-REQUESTS             TO PP111-123.
           PERFORM WRITE-OUTPUT.

           MOVE 'Temporary storage buffer reads . . . . . :'
                         TO PP3-44.
           MOVE TSGTRDN  TO PP47-59.
           MOVE 'Freemain Requests . . . . . . . . :' TO PP74-108.
           MOVE TSBUFFRS-FREEMAIN-REQUESTS            TO PP111-123.
           PERFORM WRITE-OUTPUT.

           MOVE 'Temporary storage buffer writes. . . . . :'
                         TO PP3-44.
           MOVE TSGTWTN  TO PP47-59.
           MOVE 'Current Elements. . . . . . . . . :' TO PP74-108.
           MOVE TSBUFFRS-CURRENT-ELEMS                TO PP111-123.
           PERFORM WRITE-OUTPUT.

           MOVE 'Forced buffer writes for recovery. . . . :'
                         TO PP3-44.
           MOVE TSGTWTNR TO PP47-59.
           MOVE 'Current Element Storage . . . . . :' TO PP74-108.
           MOVE TSBUFFRS-CURRENT-ELEM-STG             TO PP111-123.
           PERFORM WRITE-OUTPUT.

           MOVE 'Format writes. . . . . . . . . . . . . . :'
                         TO PP3-44.
           MOVE TSGTWTNF TO PP47-59.
           MOVE 'Current Page Storage. . . . . . . :' TO PP74-108.
           COMPUTE RESULT = (TSBUFFRS-CURRENT-PAGE-STG / 1024).
           MOVE RESULT   TO PP111-123.
           MOVE 'K'      TO PP124.
           PERFORM WRITE-OUTPUT.

           MOVE '% of ECDSA. . . . . . . . . . . . :' TO PP74-108.
           MOVE 0 TO PERCENTAGE PERCENTAGE-WORK.
           IF ECDSA-SIZE > 0 THEN
               COMPUTE PERCENTAGE-WORK ROUNDED =
                   (TSBUFFRS-CURRENT-PAGE-STG / ECDSA-SIZE)
               END-COMPUTE
           END-IF.
           COMPUTE PERCENTAGE = (PERCENTAGE-WORK * 100)
           END-COMPUTE.
           MOVE PERCENTAGE TO PP118-123.
           MOVE '%'        TO PP124.
           PERFORM WRITE-OUTPUT.

           MOVE 'I/O errors on the DFHTEMP dataset. . . . :'
                         TO PP3-44.
           MOVE TSGSTAAF TO PP47-59.
           MOVE 'Peak Page Storage . . . . . . . . :' TO PP74-108.
           COMPUTE RESULT = (TSBUFFRS-PEAK-PAGE-STG / 1024).
           MOVE RESULT                                TO PP111-123.
           MOVE 'K'                                   TO PP124.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Shared Pools defined . . . . . . . . . . :'
                         TO PP3-44.
           MOVE TSGSHPDF TO PP47-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Shared Pools currently connected . . . . :'
                         TO PP3-44.
           MOVE TSGSHPCN TO PP47-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Shared temporary storage read requests . :'
                         TO PP3-44.
           MOVE TSGSHRDS TO PP47-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Shared temporary storage write requests. :'
                         TO PP3-44.
           MOVE TSGSHWTS TO PP47-59.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      ***                                                           ***
      *****************************************************************

           PERFORM PAGE-HEADINGS.

           MOVE '-'     TO PRINT-CONTROL.
           MOVE 'Temporary Storage Main - Storage Subpools' TO PP1-41.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-41.
           PERFORM WRITE-OUTPUT.

           PERFORM TSMAIN-SUBPOOL-HEADINGS.

      *****************************************************************
      *** Initialise TS Main Storage subpool totals                 ***
      *****************************************************************
           MOVE 0 TO TSMAIN-SUBPOOL-GETMAINS
                     TSMAIN-SUBPOOL-FREEMAINS
                     TSMAIN-SUBPOOL-ELEMENTS
                     TSMAIN-SUBPOOL-ELEMENT-STG
                     TSMAIN-SUBPOOL-PAGE-STG.

      *****************************************************************
      ***   Print the tsmain storage subpool statistics.            ***
      *****************************************************************

           MOVE 1 TO SUB1.

           PERFORM WITH TEST BEFORE
               UNTIL SUB1 > 17

               EXEC CICS EXTRACT STATISTICS SUBPOOL
                    RESID(TSMAIN-SUBPOOL-NAME(SUB1))
                    SET(ADDRESS OF DFHSMDDS)
                    RESP(EXTRACT-SUBPOOL-RESP)
               END-EXEC

               MOVE TSMAIN-SUBPOOL-NAME(SUB1) TO PP3-10
               IF EXTRACT-SUBPOOL-RESP = DFHRESP(NORMAL)
                   PERFORM TSMAIN-SUBPOOL-STATISTICS
               ELSE
                   MOVE '????    ' TO PP13-20 PP23-30

                   MOVE 0 TO PP33-39 PP43-55 PP58-70 PP73-82 PP85-97
                             PP100-109 PP113-118 PP122-131
               END-IF
               MOVE '%' TO PP119
               MOVE 'K' TO PP40
               EVALUATE TRUE
                   WHEN SMDGCDSA
                       MOVE 'M' TO PP110 PP132
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               MOVE ' ' TO PRINT-CONTROL

               ADD 1 TO SUB1

           END-PERFORM.

  *****************************************************************

           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP3-132.
           PERFORM WRITE-OUTPUT.
           MOVE 'Totals  '                 TO PP3-10.
           MOVE TSMAIN-SUBPOOL-GETMAINS    TO PP43-55.
           MOVE TSMAIN-SUBPOOL-FREEMAINS   TO PP58-70.
           MOVE TSMAIN-SUBPOOL-ELEMENTS    TO PP73-82.
           MOVE TSMAIN-SUBPOOL-ELEMENT-STG TO PP85-97.
           MOVE TSMAIN-SUBPOOL-PAGE-STG    TO PP100-109.
           MOVE 'M'                        TO PP110.
           PERFORM WRITE-OUTPUT.

       TEMP-STORAGE-STATISTICS-EXIT.
           EXIT.
       TSMAIN-SUBPOOL-STATISTICS SECTION.

           MOVE SMDDSANAME         TO PP13-20.
           EVALUATE TRUE
               WHEN SMDCICS
                   MOVE 'CICS    ' TO PP23-30
               WHEN SMDUSER
                   MOVE 'USER    ' TO PP23-30
               WHEN SMDREADONLY
                   MOVE 'READONLY' TO PP23-30
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           COMPUTE RESULT = (SMDIFREE / 1024)
           END-COMPUTE.
           MOVE RESULT             TO PP33-39.
           MOVE SMDGMREQ           TO PP43-55.
           MOVE SMDFMREQ           TO PP58-70.
           MOVE SMDCELEM           TO PP73-82.
           MOVE SMDCES             TO PP85-97.
           MOVE SMDCPS             TO PP100-109.
           MOVE 0                  TO PERCENTAGE.
           EVALUATE TRUE
               WHEN SMDGCDSA
                   IF GCDSA-SIZE > 0 THEN
                       COMPUTE PERCENTAGE-WORK ROUNDED =
                           (SMDCPS / GCDSA-SIZE)
                       END-COMPUTE
                       COMPUTE PERCENTAGE = (PERCENTAGE-WORK * 100)
                       END-COMPUTE
                   END-IF
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           MOVE PERCENTAGE         TO PP113-118.
           MOVE SMDHWMPS           TO PP122-131.

      *
      *** Maintain the Storage Subpool Totals                       ***
      *
           ADD SMDGMREQ TO TSMAIN-SUBPOOL-GETMAINS.
           ADD SMDFMREQ TO TSMAIN-SUBPOOL-FREEMAINS.
           ADD SMDCELEM TO TSMAIN-SUBPOOL-ELEMENTS.
           ADD SMDCES   TO TSMAIN-SUBPOOL-ELEMENT-STG.
           ADD SMDCPS   TO TSMAIN-SUBPOOL-PAGE-STG.

       TSMAIN-SUBPOOL-STATISTICS-EXIT.
           EXIT.
           EJECT
       TSMAIN-SUBPOOL-HEADINGS SECTION.

           MOVE '0' TO PRINT-CONTROL.
           PERFORM WRITE-OUTPUT.

           MOVE 'Subpool                       Initi' TO PP3-37.
           MOVE 'al      Getmain        Freemain    ' TO PP38-72.
           MOVE ' Current       Current       C'      TO PP73-102.
           MOVE 'urrent     % of       Peak    '      TO PP103-132.
           PERFORM WRITE-OUTPUT.

           MOVE '  Name    Location  Access      Fre' TO PP3-37.
           MOVE 'e       Requests       Requests    ' TO PP38-72.
           MOVE ' Elements    Element Stg    Pa'      TO PP73-102.
           MOVE 'ge Stg      DSA     Page Stg  '      TO PP103-132.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP3-132.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.
           MOVE 'N'     TO SUB-HEADINGS.

       TSMAIN-SUBPOOL-HEADINGS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** TSMODEL-STATISTICS                                        ***
      ***   Print heading and Temporary Storage Models.             ***
      *****************************************************************
       TSMODEL-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO TSMODELS-PAGENO.

           MOVE '-'     TO PRINT-CONTROL.
           MOVE 'Temporary Storage Models' TO PP1-24.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-24.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      ***   Print the tsmodel statistics.                           ***
      *****************************************************************

           MOVE '0'      TO PRINT-CONTROL.

           EXEC CICS INQUIRE TSMODEL START
           END-EXEC.

           EXEC CICS INQUIRE TSMODEL(TSMODEL-NAME) NEXT
               LOCATION(TSMODEL-LOCATION)
               EXPIRYINT(TSMODEL-EXPIRYINT)
               POOLNAME(TSMODEL-POOLNAME)
               PREFIX(TSMODEL-PREFIX)
               RECOVSTATUS(TSMODEL-RECOVERY-STATUS)
               REMOTEPREFIX(TSMODEL-REMOTE-PREFIX)
               REMOTESYSTEM(TSMODEL-REMOTE-SYSTEM)
               SECURITYST(TSMODEL-SECURITY-STATUS)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

               IF LINE-COUNT > LINES-PER-PAGE THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               IF SUB-HEADINGS = 'Y' THEN
                   PERFORM TSMODEL-SUB-HEADINGS
               END-IF

               ADD 1 TO TSMODEL-TOTAL

               MOVE TSMODEL-NAME        TO PP3-10
               MOVE TSMODEL-PREFIX      TO PP13-28
               EVALUATE TSMODEL-LOCATION
                   WHEN DFHVALUE(AUXILIARY)
                       MOVE 'Auxiliary' TO PP31-39
                   WHEN DFHVALUE(MAIN)
                       MOVE 'Main     ' TO PP31-39
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               MOVE TSMODEL-POOLNAME    TO PP42-49
               EVALUATE TSMODEL-RECOVERY-STATUS
                   WHEN DFHVALUE(RECOVERABLE)
                       MOVE 'Yes'       TO PP56-58
                   WHEN DFHVALUE(NOTRECOVABLE)
                       MOVE 'No '       TO PP56-58
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               DIVIDE TSMODEL-EXPIRYINT BY 24
                   GIVING PP66-68
                       REMAINDER PP70-71
               END-DIVIDE
               MOVE '-' TO PP69
               PERFORM WRITE-OUTPUT

               EXEC CICS INQUIRE TSMODEL(TSMODEL-NAME) NEXT
                   LOCATION(TSMODEL-LOCATION)
                   EXPIRYINT(TSMODEL-EXPIRYINT)
                   POOLNAME(TSMODEL-POOLNAME)
                   PREFIX(TSMODEL-PREFIX)
                   RECOVSTATUS(TSMODEL-RECOVERY-STATUS)
                   REMOTEPREFIX(TSMODEL-REMOTE-PREFIX)
                   REMOTESYSTEM(TSMODEL-REMOTE-SYSTEM)
                   SECURITYST(TSMODEL-SECURITY-STATUS)
                   RESP(INQUIRE-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE TSMODEL END
           END-EXEC.

       TSMODEL-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** TSMODEL-SUB-HEADINGS                                      ***
      *****************************************************************
       TSMODEL-SUB-HEADINGS SECTION.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'TSModel                     Tsmodel' TO PP3-37.
           MOVE '    Tsmodel                 Expiry ' TO PP38-72.
           PERFORM WRITE-OUTPUT.
           MOVE '  Name    Tsmodel Prefix    Locatio' TO PP3-37.
           MOVE 'n   Poolname  Recoverable  Interval' TO PP38-72.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP3-37.
           MOVE '___________________________________' TO PP38-72.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.
           MOVE 'N'     TO SUB-HEADINGS.

       TSMODEL-SUB-HEADINGS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** TSQUEUE-STATISTICS                                        ***
      ***   Print heading and Temporary Storage Queues.             ***
      *****************************************************************
       TSQUEUE-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO TSQUEUES-PAGENO.

           MOVE '-'     TO PRINT-CONTROL.
           MOVE 'Temporary Storage Queues' TO PP1-24.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-24.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      ***   Print the tsqueue statistics.                           ***
      *****************************************************************

           MOVE '0'      TO PRINT-CONTROL.

           EXEC CICS INQUIRE TSQNAME START
           END-EXEC.

           EXEC CICS INQUIRE TSQNAME(TSQUEUE-NAME) NEXT
               FLENGTH(TSQUEUE-FLENGTH)
               LOCATION(TSQUEUE-LOCATION)
               NUMITEMS(TSQUEUE-NUMITEMS)
               MAXITEMLEN(TSQUEUE-MAXITEMLEN)
               MINITEMLEN(TSQUEUE-MINITEMLEN)
               TRANSID(TSQUEUE-TRANSID)
               LASTUSEDINT(TSQUEUE-LASTUSEDINT)
               EXPIRYINT(TSQUEUE-EXPIRYINT)
               RECOVSTATUS(TSQUEUE-RECOVERY)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

               IF LINE-COUNT > LINES-PER-PAGE THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               IF SUB-HEADINGS = 'Y' THEN
                   PERFORM TSQUEUE-SUB-HEADINGS
               END-IF

               ADD 1 TO TSQUEUE-TOTAL

               MOVE TSQUEUE-NAME        TO PP3-18
               EVALUATE TSQUEUE-LOCATION
                   WHEN DFHVALUE(AUXILIARY)
                       MOVE 'Auxiliary' TO PP21-29
                       ADD 1 TO AUX-TSQUEUES
                       ADD TSQUEUE-NUMITEMS TO AUX-TSQUEUE-ITEMS
                   WHEN DFHVALUE(MAIN)
                       MOVE 'Main     ' TO PP21-29
                       ADD 1 TO MAIN-TSQUEUES
                       ADD TSQUEUE-NUMITEMS TO MAIN-TSQUEUE-ITEMS
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               MOVE TSQUEUE-NUMITEMS    TO PP32-37
               MOVE TSQUEUE-MINITEMLEN  TO PP43-48
               MOVE TSQUEUE-MAXITEMLEN  TO PP53-58
               MOVE TSQUEUE-FLENGTH     TO PP62-74
               MOVE TSQUEUE-TRANSID     TO PP78-81
               DIVIDE TSQUEUE-LASTUSEDINT BY 60
                   GIVING TIME-WORK
                       REMAINDER INTERVAL-SECONDS
               END-DIVIDE
               DIVIDE TIME-WORK BY 60
                   GIVING TIME-WORK
                       REMAINDER INTERVAL-MINUTES
               END-DIVIDE
               DIVIDE TIME-WORK BY 24
                   GIVING INTERVAL-DAYS
                       REMAINDER INTERVAL-HOURS
               END-DIVIDE
               MOVE INTERVAL-DAYS-TIME  TO PP85-96
               EVALUATE TSQUEUE-RECOVERY
                   WHEN DFHVALUE(RECOVERABLE)
                       MOVE 'Yes'       TO PP103-105
                   WHEN DFHVALUE(NOTRECOVABLE)
                       MOVE 'No '       TO PP103-105
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               DIVIDE TSQUEUE-EXPIRYINT BY 24
                   GIVING PP113-115
                       REMAINDER PP117-118
               END-DIVIDE
               MOVE '-' TO PP116
               PERFORM WRITE-OUTPUT

               EXEC CICS INQUIRE TSQNAME(TSQUEUE-NAME) NEXT
                   FLENGTH(TSQUEUE-FLENGTH)
                   LOCATION(TSQUEUE-LOCATION)
                   NUMITEMS(TSQUEUE-NUMITEMS)
                   MAXITEMLEN(TSQUEUE-MAXITEMLEN)
                   MINITEMLEN(TSQUEUE-MINITEMLEN)
                   TRANSID(TSQUEUE-TRANSID)
                   LASTUSEDINT(TSQUEUE-LASTUSEDINT)
                   EXPIRYINT(TSQUEUE-EXPIRYINT)
                   RECOVSTATUS(TSQUEUE-RECOVERY)
                   RESP(INQUIRE-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE TSQNAME END
           END-EXEC.

      *****************************************************************
      ***   Print the tsqueue totals.                               ***
      *****************************************************************
           IF TSQUEUE-TOTAL > 0 THEN
               PERFORM PRINT-TSQUEUE-TOTALS
           END-IF.

       TSQUEUE-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** TSQUEUE-SUB-HEADINGS                                      ***
      *****************************************************************
       TSQUEUE-SUB-HEADINGS SECTION.

           MOVE '0' TO PRINT-CONTROL.
           MOVE '                  Tsqueue   Number ' TO PP3-37.
           MOVE 'of  Min Item  Max Item      Tsqueue' TO PP38-72.
           MOVE '              Lastused        '      TO PP73-102.
           MOVE '          Expiry              '      TO PP103-132.
           PERFORM WRITE-OUTPUT.
           MOVE 'TSQueue Name      Location    Items' TO PP3-37.
           MOVE '     Length    Length       Flength' TO PP38-72.
           MOVE '    Tranid    Interval    Reco'      TO PP73-102.
           MOVE 'verable  Interval             '      TO PP103-132.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP3-37
                           PP38-72
                           PP73-102.
           MOVE '_________________             '      TO PP103-132.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.
           MOVE 'N'     TO SUB-HEADINGS.

       TSQUEUE-SUB-HEADINGS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** PRINT-TSQUEUE-TOTALS                                      ***
      *****************************************************************
       PRINT-TSQUEUE-TOTALS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO TSQUEUE-TOTALS-PAGENO.

           MOVE '-'              TO PRINT-CONTROL.
           MOVE 'Tsqueue Totals' TO PP1-14.
           PERFORM WRITE-OUTPUT.
           MOVE '+'              TO PRINT-CONTROL.
           MOVE ALL '_'          TO PP1-14.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Current temporary storage queues . . . . . . . . . . .
      -    '. . . . :'             TO PP3-66.
           MOVE TSQUEUE-TOTAL      TO PP69-78.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Current auxiliary temporary storage queues . . . . . .
      -    '. . . . :'       TO PP3-66.
           MOVE AUX-TSQUEUES TO PP69-78.
           PERFORM WRITE-OUTPUT.
           MOVE 'Current items in auxiliary temporary storage queues. .
      -    '. . . . :'            TO PP3-66.
           MOVE AUX-TSQUEUE-ITEMS TO PP69-78.
           PERFORM WRITE-OUTPUT.
           MOVE 'Average items per auxiliary temporary storage queue. .
      -    '. . . . :'       TO PP3-66.
           IF AUX-TSQUEUES > 0 THEN
               COMPUTE RESULT = (AUX-TSQUEUE-ITEMS / AUX-TSQUEUES)
           ELSE
               MOVE 0 TO RESULT
           END-IF.
           MOVE RESULT       TO PP69-78.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Current main temporary storage queues. . . . . . . . .
      -    '. . . . :'        TO PP3-66.
           MOVE MAIN-TSQUEUES TO PP69-78.
           PERFORM WRITE-OUTPUT.
           MOVE 'Current items in main temporary storage queues . . . .
      -    '. . . . :'             TO PP3-66.
           MOVE MAIN-TSQUEUE-ITEMS TO PP69-78.
           PERFORM WRITE-OUTPUT.
           MOVE 'Average items per main temporary storage queue . . . .
      -    '. . . . :'        TO PP3-66.
           IF MAIN-TSQUEUES > 0 THEN
               COMPUTE RESULT = (MAIN-TSQUEUE-ITEMS / MAIN-TSQUEUES)
           ELSE
               MOVE 0 TO RESULT
           END-IF.
           MOVE RESULT        TO PP69-78.
           PERFORM WRITE-OUTPUT.

       PRINT-TSQUEUE-TOTALS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** TSQUEUE-BY-POOL-STATISTICS                                ***
      ***   Print heading and Temporary Storage Queues by Pool.     ***
      *****************************************************************
       TSQUEUE-BY-POOL-STATS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO TSQUEUES-BY-POOL-PAGENO.

           MOVE '-'     TO PRINT-CONTROL.
           MOVE 'Temporary Storage Queues by Shared TS Pool' TO PP1-42.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-42.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      ***   Print the tsqueue statistics by shared pool.            ***
      *****************************************************************
           MOVE '0'      TO PRINT-CONTROL.

           EXEC CICS INQUIRE TSPOOL START
           END-EXEC.

           EXEC CICS INQUIRE TSPOOL(TSPOOL-NAME) NEXT
               CONNSTATUS(TSPOOL-STATUS)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

               IF LINE-COUNT > (LINES-PER-PAGE - 9) THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Shared TS Pool Name . . . . . . . :' TO PP3-37
               MOVE TSPOOL-NAME                           TO PP40-47
               MOVE 'Connection Status. . . . :' TO PP58-83
               EVALUATE TSPOOL-STATUS
                   WHEN DFHVALUE(CONNECTED)
                       MOVE 'CONNECTED  ' TO PP86-96
                   WHEN DFHVALUE(UNCONNECTED)
                       MOVE 'UNCONNECTED' TO PP86-96
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               IF TSPOOL-STATUS = DFHVALUE(CONNECTED) THEN
                   PERFORM TSPOOL-TSQUEUE-STATISTICS
               END-IF

               EXEC CICS INQUIRE TSPOOL(TSPOOL-NAME) NEXT
                   CONNSTATUS(TSPOOL-STATUS)
                   RESP(INQUIRE-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE TSPOOL END
           END-EXEC.

       TSQUEUE-BY-POOL-STATS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
       TSPOOL-TSQUEUE-STATISTICS SECTION.

           MOVE 0 TO TSPOOL-TSQUEUE-ITEMS.

           IF LINE-COUNT > (LINES-PER-PAGE - 6) THEN
               PERFORM PAGE-HEADINGS
           ELSE
               MOVE 'Y' TO SUB-HEADINGS
           END-IF

           EXEC CICS INQUIRE TSQNAME POOLNAME(TSPOOL-NAME) START
           END-EXEC.

           EXEC CICS INQUIRE TSQNAME(TSQUEUE-NAME) NEXT
               FLENGTH(TSQUEUE-FLENGTH)
               LOCATION(TSQUEUE-LOCATION)
               NUMITEMS(TSQUEUE-NUMITEMS)
               MAXITEMLEN(TSQUEUE-MAXITEMLEN)
               MINITEMLEN(TSQUEUE-MINITEMLEN)
               TRANSID(TSQUEUE-TRANSID)
               LASTUSEDINT(TSQUEUE-LASTUSEDINT)
               RESP(INQUIRE-TSQNAME-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-TSQNAME-RESP = DFHRESP(END)

               IF LINE-COUNT > LINES-PER-PAGE THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               IF SUB-HEADINGS = 'Y' THEN
                   PERFORM TSPOOL-SUB-HEADINGS
               END-IF

               MOVE TSQUEUE-NAME        TO PP7-22
               MOVE TSQUEUE-NUMITEMS    TO PP26-31
               MOVE TSQUEUE-MINITEMLEN  TO PP37-42
               MOVE TSQUEUE-MAXITEMLEN  TO PP47-52
               MOVE TSQUEUE-FLENGTH     TO PP55-64
               MOVE TSQUEUE-TRANSID     TO PP70-73
               DIVIDE TSQUEUE-LASTUSEDINT BY 60
                   GIVING TIME-WORK
                       REMAINDER INTERVAL-SECONDS
               END-DIVIDE
               DIVIDE TIME-WORK BY 60
                   GIVING TIME-WORK
                       REMAINDER INTERVAL-MINUTES
               END-DIVIDE
               DIVIDE TIME-WORK BY 24
                   GIVING INTERVAL-DAYS
                       REMAINDER INTERVAL-HOURS
               END-DIVIDE
               MOVE INTERVAL-DAYS-TIME  TO PP77-88
               PERFORM WRITE-OUTPUT

               ADD TSQUEUE-NUMITEMS TO TSPOOL-TSQUEUE-ITEMS

               EXEC CICS INQUIRE TSQNAME(TSQUEUE-NAME) NEXT
                   FLENGTH(TSQUEUE-FLENGTH)
                   LOCATION(TSQUEUE-LOCATION)
                   NUMITEMS(TSQUEUE-NUMITEMS)
                   MAXITEMLEN(TSQUEUE-MAXITEMLEN)
                   MINITEMLEN(TSQUEUE-MINITEMLEN)
                   TRANSID(TSQUEUE-TRANSID)
                   LASTUSEDINT(TSQUEUE-LASTUSEDINT)
                   RESP(INQUIRE-TSQNAME-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE TSQNAME END
           END-EXEC.

           IF TSPOOL-TSQUEUE-ITEMS > 0 THEN
               MOVE '+'     TO PRINT-CONTROL
               MOVE ALL '_' TO PP7-88
               PERFORM WRITE-OUTPUT
               MOVE TSPOOL-TSQUEUE-ITEMS TO PP22-31
               PERFORM WRITE-OUTPUT
           END-IF.

       TSPOOL-TSQUEUE-STATISTICS-EXIT.
           EXIT.

       TSPOOL-SUB-HEADINGS SECTION.

           MOVE '0' TO PRINT-CONTROL.
           MOVE '                  Number of  Min Item  Max Item    Tsqu
      -        'eue              Lastused ' TO PP7-88.
           PERFORM WRITE-OUTPUT.
           MOVE 'TSQueue Name        Items     Length    Length     Flen
      -        'gth    Tranid    Interval ' TO PP7-88.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP7-88.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.
           MOVE 'N'     TO SUB-HEADINGS.

       TSPOOL-SUB-HEADINGS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** TRANSIENT-DATA-STATISTICS                                 ***
      ***   Collect the Transient Data Global statistics.           ***
      ***   Print heading and Transient Data statistics.            ***
      ***   Collect and Print the Transaient Data Queue statistics. ***
      *****************************************************************
       TRANSIENT-DATA-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO TDQUEUE-PAGENO.

           MOVE '-'     TO PRINT-CONTROL.
           MOVE 'Transient Data' TO PP1-14.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-14.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      *** Collect the Transient Data Global statistics.             ***
      *****************************************************************
           EXEC CICS EXTRACT STATISTICS TDQUEUE
               SET(ADDRESS OF DFHTQGDS)
               NOHANDLE
           END-EXEC.

           IF EIBRESP NOT = DFHRESP(NORMAL) THEN
               GO TO TRANSIENT-DATA-STATISTICS-EXIT
           END-IF.

      *****************************************************************
      *** Print the Transient Data Global statistics.               ***
      *****************************************************************
           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Transient data reads. . . . . . . . . . . . :'
                         TO PP3-47.
           MOVE TQGACTGT TO PP50-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Transient data writes . . . . . . . . . . . :'
                         TO PP3-47.
           MOVE TQGACTPT TO PP50-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Transient data formatting writes. . . . . . :'
                         TO PP3-47.
           MOVE TQGACTFT TO PP50-59.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Control interval size . . . . . . . . . . . :'
                         TO PP3-47.
           MOVE TQGACISZ TO PP50-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Control intervals in the DFHINTRA dataset . :'
                         TO PP3-47.
           MOVE TQGANCIS TO PP50-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Peak control intervals used . . . . . . . . :'
                         TO PP3-47.
           MOVE TQGAMXCI TO PP50-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Times NOSPACE on DFHINTRA occurred. . . . . :'
                         TO PP3-47.
           MOVE TQGANOSP TO PP50-59.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Transient data strings. . . . . . . . . . . :'
                         TO PP3-47.
           MOVE TQGSNSTA TO PP50-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Times Transient data string in use. . . . . :'
                         TO PP3-47.
           MOVE TQGSTNAL TO PP50-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Peak Transient data strings in use. . . . . :'
                         TO PP3-47.
           MOVE TQGSMXAL TO PP50-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Times string wait occurred. . . . . . . . . :'
                         TO PP3-47.
           MOVE TQGSTNWT TO PP50-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Peak users waiting on string. . . . . . . . :'
                         TO PP3-47.
           MOVE TQGSMXWT TO PP50-59.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Transient data buffers. . . . . . . . . . . :'
                         TO PP3-47.
           MOVE TQGANBFA TO PP50-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Times Transient data buffer in use. . . . . :'
                         TO PP3-47.
           MOVE TQGATNAL TO PP50-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Peak Transient data buffers in use. . . . . :'
                         TO PP3-47.
           MOVE TQGAMXAL TO PP50-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Peak buffers containing valid data. . . . . :'
                         TO PP3-47.
           MOVE TQGAMXIU TO PP50-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Times buffer wait occurred. . . . . . . . . :'
                         TO PP3-47.
           MOVE TQGATNWT TO PP50-59.
           PERFORM WRITE-OUTPUT.
           MOVE 'Peak users waiting on buffer. . . . . . . . :'
                         TO PP3-47.
           MOVE TQGAMXWT TO PP50-59.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'I/O errors on the DFHINTRA dataset. . . . . :'
                         TO PP3-47.
           MOVE TQGACTIO TO PP50-59.
           PERFORM WRITE-OUTPUT.

       TRANSIENT-DATA-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** TDQUEUE-STATISTICS                                        ***
      ***   Print heading                                           ***
      ***   Collect and Print the Transaient Data Queue statistics. ***
      *****************************************************************
       TDQUEUE-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO TDQUEUES-PAGENO.

           MOVE '-'     TO PRINT-CONTROL.
           MOVE 'Transient Data Queues' TO PP1-21.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-21.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      ***   Print the tdqueue statistics.                           ***
      *****************************************************************

           MOVE '0'      TO PRINT-CONTROL.

           EXEC CICS INQUIRE TDQUEUE START
           END-EXEC.

           EXEC CICS INQUIRE TDQUEUE(TDQUEUE-NAME) NEXT
               ATIFACILITY(ATI-FACILITY)
               ATITERMID(ATI-TERMID)
               ATIUSERID(ATI-USERID)
               TYPE(TDQUEUE-TYPE)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

               IF LINE-COUNT > LINES-PER-PAGE THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               IF SUB-HEADINGS = 'Y' THEN
                   PERFORM TDQUEUE-SUB-HEADINGS
               END-IF

               EXEC CICS EXTRACT STATISTICS TDQUEUE
                 RESID(TDQUEUE-NAME)
                 SET(ADDRESS OF DFHTQRDS)
               END-EXEC

               ADD 1 TO TDQUEUE-TOTAL
               MOVE TDQUEUE-NAME TO PP3-6
               EVALUATE TDQUEUE-TYPE
                   WHEN DFHVALUE(EXTRA)
                       PERFORM EXTRA-TDQUEUE
                   WHEN DFHVALUE(INTRA)
                       PERFORM INTRA-TDQUEUE
                   WHEN DFHVALUE(INDIRECT)
                       PERFORM INDIRECT-TDQUEUE
                   WHEN DFHVALUE(REMOTE)
                       PERFORM REMOTE-TDQUEUE
               END-EVALUATE

               PERFORM WRITE-OUTPUT

               EXEC CICS INQUIRE TDQUEUE(TDQUEUE-NAME) NEXT
                   ATIFACILITY(ATI-FACILITY)
                   ATITERMID(ATI-TERMID)
                   ATIUSERID(ATI-USERID)
                   TYPE(TDQUEUE-TYPE)
                   RESP(INQUIRE-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE TDQUEUE END
           END-EXEC.

      *****************************************************************
      ***   Print the tdqueue totals.                               ***
      *****************************************************************
           IF TDQUEUE-TOTAL > 0 THEN
               PERFORM PRINT-TDQUEUE-TOTALS
           END-IF.

       TRANSIENT-DATA-STATISTICS-EXIT.
           EXIT.

       EXTRA-TDQUEUE SECTION.

           ADD  1        TO EXTRA-TDQUEUES.
           ADD  TQRWRITE TO EXTRA-TDQUEUE-WRITES.
           ADD  TQRREAD  TO EXTRA-TDQUEUE-READS.

           MOVE 'Extra   ' TO PP9-16.
           MOVE TQRWRITE   TO PP18-27.
           MOVE TQRREAD    TO PP29-38.

       EXTRA-TDQUEUE-EXIT.
           EXIT.

       INTRA-TDQUEUE SECTION.

           ADD  1        TO INTRA-TDQUEUES.
           ADD  TQRWRITE TO INTRA-TDQUEUE-WRITES.
           ADD  TQRREAD  TO INTRA-TDQUEUE-READS.
           ADD  TQRDELET TO INTRA-TDQUEUE-DELETES.

           MOVE 'Intra   ' TO PP9-16.
           MOVE TQRWRITE   TO PP18-27.
           MOVE TQRREAD    TO PP29-38.
           MOVE TQRDELET   TO PP40-49.

           MOVE TQRCNITM   TO PP74-79.
           MOVE TQRPNITM   TO PP82-87
           MOVE TQRTRIGN   TO PP90-95.
           MOVE TQRTRIGL   TO PP99-104.
           EVALUATE ATI-FACILITY
               WHEN DFHVALUE(TERMINAL)
                   MOVE 'TERM' TO PP107-110
               WHEN OTHER
                   MOVE '    ' TO PP107-110
           END-EVALUATE.
           MOVE ATI-TERMID TO PP113-116.
           MOVE TQRATRAN   TO PP119-122.
           MOVE ATI-USERID TO PP125-132.

       INTRA-TDQUEUE-EXIT.
           EXIT.

       INDIRECT-TDQUEUE SECTION.

           ADD  1        TO INDIRECT-TDQUEUES.
           ADD  TQRWRITE TO INDIRECT-TDQUEUE-WRITES.
           ADD  TQRREAD  TO INDIRECT-TDQUEUE-READS.
           ADD  TQRDELET TO INDIRECT-TDQUEUE-DELETES.

           MOVE 'Indirect' TO PP9-16.
           MOVE TQRWRITE   TO PP18-27.
           MOVE TQRREAD    TO PP29-38.
           MOVE TQRDELET   TO PP40-49.

           MOVE TQRIQID    TO PP53-56.

       INDIRECT-TDQUEUE-EXIT.
           EXIT.

       REMOTE-TDQUEUE SECTION.

           ADD  1        TO REMOTE-TDQUEUES.
           ADD  TQRWRITE TO REMOTE-TDQUEUE-WRITES.
           ADD  TQRREAD  TO REMOTE-TDQUEUE-READS.
           ADD  TQRDELET TO REMOTE-TDQUEUE-DELETES.

           MOVE 'Remote  ' TO PP9-16.
           MOVE TQRWRITE   TO PP18-27.
           MOVE TQRREAD    TO PP29-38.
           MOVE TQRDELET   TO PP40-49.

           MOVE TQRRSYS    TO PP61-64.
           MOVE TQRRQID    TO PP68-71.

       REMOTE-TDQUEUE-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** TDQUEUE-SUB-HEADINGS                                      ***
      *****************************************************************
       TDQUEUE-SUB-HEADINGS SECTION.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Dest  Queue      Tdqueue    Tdqueue' TO PP3-37.
           MOVE '    Tdqueue  Indirect Remote Remote' TO PP38-72.
           MOVE ' Current   Peak   <--Trigger--'      TO PP73-102.
           MOVE '>   <--------- ATI ---------->'      TO PP103-132.
           PERFORM WRITE-OUTPUT.
           MOVE ' Id   Type       Writes      Reads ' TO PP3-37.
           MOVE '    Deletes    Name   System  Name ' TO PP38-72.
           MOVE '  Items   Items   Number  Leve'      TO PP73-102.
           MOVE 'l   Fcty  Term  Tran   Userid '      TO PP103-132.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP3-132.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.
           MOVE 'N'     TO SUB-HEADINGS.

       TDQUEUE-SUB-HEADINGS-EXIT.
           EXIT.

      *****************************************************************
      *** PRINT-TDQUEUE-TOTALS                                      ***
      *****************************************************************
       PRINT-TDQUEUE-TOTALS SECTION.

           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP3-132.
           PERFORM WRITE-OUTPUT.
           COMPUTE RESULT = (INTRA-TDQUEUE-WRITES +
                             EXTRA-TDQUEUE-WRITES +
                             INDIRECT-TDQUEUE-WRITES +
                             REMOTE-TDQUEUE-WRITES).
           MOVE RESULT    TO PP18-27.
           COMPUTE RESULT = (INTRA-TDQUEUE-READS +
                             EXTRA-TDQUEUE-READS +
                             INDIRECT-TDQUEUE-READS +
                             REMOTE-TDQUEUE-READS).
           MOVE RESULT    TO PP29-38.
           COMPUTE RESULT = (INTRA-TDQUEUE-DELETES +
                             INDIRECT-TDQUEUE-DELETES +
                             REMOTE-TDQUEUE-DELETES).
           MOVE RESULT    TO PP40-49.
           PERFORM WRITE-OUTPUT.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO TDQUEUE-TOTALS-PAGENO.

           MOVE '-'              TO PRINT-CONTROL.
           MOVE 'Tdqueue Totals' TO PP1-14.
           PERFORM WRITE-OUTPUT.
           MOVE '+'              TO PRINT-CONTROL.
           MOVE ALL '_'          TO PP1-14.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE '                          No. of   ' TO PP3-37.
           MOVE '   Tdqueue     Tdqueue     Tdqueue ' TO PP38-72.
           PERFORM WRITE-OUTPUT.
           MOVE 'Tdqueue Type             Tdqueues  ' TO PP3-37.
           MOVE '   Writes       Reads      Deletes ' TO PP38-72.
           PERFORM WRITE-OUTPUT.
           MOVE '+' TO PRINT-CONTROL.
           MOVE ALL '_' TO PP3-37
                           PP38-72.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Intrapartition . . . :' TO PP3-24.
           MOVE INTRA-TDQUEUES           TO PP27-36.
           MOVE INTRA-TDQUEUE-WRITES     TO PP39-48.
           MOVE INTRA-TDQUEUE-READS      TO PP51-60.
           MOVE INTRA-TDQUEUE-DELETES    TO PP63-72.
           PERFORM WRITE-OUTPUT.
           MOVE 'Extrapartition . . . :' TO PP3-24.
           MOVE EXTRA-TDQUEUES           TO PP27-36.
           MOVE EXTRA-TDQUEUE-WRITES     TO PP39-48.
           MOVE EXTRA-TDQUEUE-READS      TO PP51-60.
           MOVE 'N/A'                    TO PP70-72.
           PERFORM WRITE-OUTPUT.
           MOVE 'Indirect . . . . . . :' TO PP3-24.
           MOVE INDIRECT-TDQUEUES        TO PP27-36.
           MOVE INDIRECT-TDQUEUE-WRITES  TO PP39-48.
           MOVE INDIRECT-TDQUEUE-READS   TO PP51-60.
           MOVE INDIRECT-TDQUEUE-DELETES TO PP63-72.
           PERFORM WRITE-OUTPUT.
           MOVE 'Remote . . . . . . . :' TO PP3-24.
           MOVE REMOTE-TDQUEUES          TO PP27-36.
           MOVE REMOTE-TDQUEUE-WRITES    TO PP39-48.
           MOVE REMOTE-TDQUEUE-READS     TO PP51-60.
           MOVE REMOTE-TDQUEUE-DELETES   TO PP63-72.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP3-37
                           PP38-72.
           PERFORM WRITE-OUTPUT.
           MOVE 'Total. . . . . . . . :' TO PP3-24.
           MOVE TDQUEUE-TOTAL            TO PP27-36.
           PERFORM WRITE-OUTPUT.

       PRINT-TDQUEUE-TOTALS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** TOTALS-INITIALISE                                         ***
      *****************************************************************
       TOTALS-INITIALISE SECTION.

           MOVE 0 TO TSQUEUE-TOTAL AUX-TSQUEUES MAIN-TSQUEUES
                     AUX-TSQUEUE-ITEMS MAIN-TSQUEUE-ITEMS.

           MOVE 0 TO TSMODEL-TOTAL.

           MOVE 0 TO TDQUEUE-TOTAL INTRA-TDQUEUES EXTRA-TDQUEUES
                     INDIRECT-TDQUEUES REMOTE-TDQUEUES
                     INTRA-TDQUEUE-WRITES INTRA-TDQUEUE-READS
                     INTRA-TDQUEUE-DELETES
                     EXTRA-TDQUEUE-WRITES EXTRA-TDQUEUE-READS
                     INDIRECT-TDQUEUE-WRITES INDIRECT-TDQUEUE-READS
                     INDIRECT-TDQUEUE-DELETES
                     REMOTE-TDQUEUE-WRITES REMOTE-TDQUEUE-READS
                     REMOTE-TDQUEUE-DELETES.

       TOTALS-INITIALISE-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** PAGE-HEADINGS                                             ***
      ***   Increment page                                          ***
      ***   Write new heading line to spool                         ***
      *****************************************************************
       PAGE-HEADINGS SECTION.

           ADD 1       TO PAGENO.
           MOVE PAGENO TO PAGE-NUMBER.
           MOVE 1      TO LINE-COUNT.

           MOVE '1'          TO PRINT-CONTROL.
           MOVE HEADING-LINE TO PP1-132.
           PERFORM WRITE-OUTPUT.
           MOVE '+'          TO PRINT-CONTROL.
           MOVE ALL '_'      TO PP1-132.
           PERFORM WRITE-OUTPUT.

           MOVE 'Y' TO SUB-HEADINGS.

       PAGE-HEADINGS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** WRITE-OUTPUT                                              ***
      ***   Write current print line to report output destination.  ***
      *****************************************************************
       WRITE-OUTPUT SECTION.

           PERFORM WRITE-SPOOL.

       WRITE-OUTPUT-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** WRITE-SPOOL                                               ***
      *** Write current print line to spool                         ***
      *** Increment line-count                                      ***
      *** IF CICS exceptional condition is raised THEN              ***
      ***    call error routine                                     ***
      *****************************************************************
       WRITE-SPOOL SECTION.

           EXEC CICS SPOOLWRITE TOKEN(OUTPUT-TOKEN)
               FROM(PRINT-LINE) FLENGTH(LINE-LENGTH)
               LINE
               NOHANDLE
           END-EXEC.

           EVALUATE PRINT-CONTROL
               WHEN ' '
                   ADD 1 TO LINE-COUNT
               WHEN '0'
                   ADD 2 TO LINE-COUNT
               WHEN '-'
                   ADD 3 TO LINE-COUNT
               WHEN '+'
                   ADD 0 TO LINE-COUNT
               WHEN '1'
                   MOVE 1 TO LINE-COUNT
           END-EVALUATE.

      *                                       Check return code
           IF EIBRESP NOT = DFHRESP(NORMAL) THEN
               MOVE 'SPOOLWRITE' TO CICS-COMMAND
               PERFORM ERROR-ROUTINE
           ELSE
               MOVE ' '    TO PRINT-CONTROL
               MOVE SPACES TO PP1-132
           END-IF.

       WRITE-SPOOL-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  ERROR ROUTINE                                            ***
      ***  Error routine produces appropriate responses to CICS     ***
      ***  spool commands. To ensure this the resp and resp2 values ***
      ***  are returned to the main program DFH0STAT for appropriate***
      ***  outputing of the messages.                               ***
      *****************************************************************
       ERROR-ROUTINE SECTION.

      *  If an error occurs which is not due to the users input or if
      *  the program is not being run from a terminal then a general
      *  message is sent to the screen/console and the RESP and RESP2
      *  fields are given along with the spool command which failed.
           MOVE 'Y'      TO ERROR-FOUND.
           MOVE EIBRESP  TO STCM-RESP.
           MOVE EIBRESP2 TO STCM-RESP2.

      *  Whatever happens ensure we return to DFH0STAT quickly.
           EXEC CICS RETURN
           END-EXEC.

       ERROR-ROUTINE-EXIT.
           EXIT.
