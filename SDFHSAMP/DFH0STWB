      CBL CICS('COBOL3,SP') APOST
PROCESS MAP,XREF,TRUNC(BIN)
      *****************************************************************
      *                                                               *
      * MODULE NAME = DFH0STWB                                        *
      *                                                               *
      * DESCRIPTIVE NAME = Collect and Print Statistics Sample        *
      *                                                               *
      *                                                               *
      *                                                               *
      *      Licensed Materials - Property of IBM                     *
      *                                                               *
      *      "Restricted Materials of IBM"                            *
      *                                                               *
      *      5655-Y04                                                 *
      *                                                               *
      *      (C) Copyright IBM Corp. 2008, 2015"                      *
      *                                                               *
      *                                                               *
      *                                                               *
      *                                                               *
      * STATUS = 7.2.0                                                *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      * DESCRIPTION :                                                 *
      *                                                               *
      *     This set of programs illustrate ways of using the EXEC    *
      *     CICS INQUIRE, COLLECT and EXTRACT STATISTICS commands of  *
      *     CICS to produce information about a CICS system.          *
      *     The reports include a CICS and MVS storage analysis.      *
      *     Statistics obtained using these commands can be used to   *
      *     aid the specification of the DSA Size parameters.         *
      *     (See the CICS Performance Guide and CICS Data Areas       *
      *      manuals for more information, and the CICS System        *
      *      Programming Reference for use of commands)               *
      *                                                               *
      *     These programs can be invoked from the PLT at PLTSD or    *
      *     as a CICS transaction (conversationally).                 *
      *     The output is sent via the CICS JES SPOOL interface for   *
      *     which a number of default parameters can be changed by    *
      *     the user to specify the distribution of the report(s).    *
      *     These defaults are defined in the working-storage         *
      *     section of the DFH0STAT program under the 01 level        *
      *     "OUTPUT-DEFAULTS".                                        *
      *                                                               *
      *     Installation instructions:-                               *
      *     Require SIT parameters:-  SPOOL=YES                       *
      *     Required CICS definitions:-                               *
      *         Transactions:-  STAT -> DFH0STAT                      *
      *         Programs:-    DFH0STAT   IBM COBOL                    *
      *                       DFH0STDB       for OS/390 & VM          *
      *                       DFH0STEJ           2.1.1 or later       *
      *                       DFH0STEP       "   "   "                *
      *                       DFH0STGN       "   "   "                *
      *                       DFH0STLK       "   "   "                *
      *                       DFH0STPR       "   "   "                *
      *                       DFH0STSA       "   "   "                *
      *                       DFH0STSY       "   "   "                *
      *                       DFH0STTP       "   "   "                *
      *                       DFH0STTS       "   "   "                *
      *                       DFH0STWB       "   "   "                *
      *                       DFH$STCN   Assembler                    *
      *                       DFH$STTB   Assembler                    *
      *         Mapsets:-     DFH0STM                                 *
      *                       DFH0STS                                 *
      *                                                               *
      *     The CICS-supplied RDO group, DFH$STAT, contains all       *
      *     the necessary resource definitions for this sample.       *
      *                                                               *
      *     All programs are command level and run above the          *
      *     16Mb line. Note: that DFH0STAT, DFH0STDB, DFH0STEJ,       *
      *     DFH0STEP, DFH0STGN, DFH0STLK, DFH0STPR, DFH0STSA,         *
      *     DFH0STSY, DFH0STTP, DFH0STTS, and DFH0STWB must be        *
      *     translated with the Translator options "COBOL3 and SP".   *
      *                                                               *
      *   SPOOL errors.  If an EXEC CICS SPOOL .. command fails       *
      *   when the program is run as a transaction, an error message  *
      *   is displayed on the users screen and the transaction will   *
      *   continue.  If the program is not being run from a terminal  *
      *   then a message will be sent to the console using EXEC       *
      *   CICS WRITE OPERATOR commands and the transaction will be    *
      *   terminated normally.                                        *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      * CHANGE ACTIVITY :                                             *
      *  $SEG(DFH0STWB),COMP(SAMPLES),PROD(CICS):                     *
      *                                                               *
      *  PN= REASON REL YYMMDD HDXXIII : REMARKS                      *
      * $L0= 923    660 080513 HD1VCJB : MODULE CREATED for DFH0STAT  *
      * $L1= 927    660 080604 HD1VCJB : Event Processing Support     *
      * $L2= 938    660 080623 HD1VCJB : Bundle Statistics Report     *
      * $L3= 923    660 080710 HD1VCJB : Atomservice statistics report*
      * $L4= 923    660 080716 HD1VCJB : Atomservice statistics - URIM*
      * $L5= 830    660 080724 HD1VCJB : Statistics Support for IPv6  *
      * $L6= 936    660 081105 HD1VCJB : XMLTRANSFORM Statistics Suppo*
      * $L7= 938    660 081121 HD4PALS : Add basescope attribute      *
      * $L8= 903    660 081222 HD1VCJB : AUTHENTICATE on URIMAP       *
      * $L9= R00559 670 091024 HDHCPC  : ATOMSERVICE from BUNDLE      *
      * $LA= R13127 670 100906 HDILMWC : Archivefile field            *
      * $LB= R00552 670 101027 HD4PALT : socketclose for connection po*
      * $LC= R00552 670 101126 HDJCIDH : HTTP Outbound pooling        *
      *      R38042 690 130404 HDAFDRB : Use EXTRACT STATISTICS       *
      * $P1= D22726 660 081104 HD1VCJB : Correct URIMAP Port field    *
      * $P2= D22732 660 081202 HD1VCJB : Correct URIMAP Host Name fiel*
      * $P3= D24118 660 090122 HD1VCJB : Correct XMLTRANSFORM report  *
      * $P4= D25395 660 090318 HD1VCJB : Statistics by HTTP method    *
      *      R51399 680 120725 HDIKTG  : URIMAP USAGE(JVMSERVER)      *
      *      D57360 680 120815 HD1VCJB : Correct URIMAP Report        *
      *      R72077 690 130723 HDLPLGQ : New WEBSERVCIE state         *
      *      R74509 690 130805 HDKEMW  : Add MSGFORMAT to PIPELINE    *
      *      R76115 690 131024 HDLRWCL : Print URIMAP entrypoint ref  *
      *      D98593 700 150106 HD3BADW : Fix comments                 *
      *      R92248 700 150128 HDLHJJH : Print URIMAP direct attach   *
      *      R91582 700 150430 HDLGJCH : Print PIPELINE JSON parser   *
      *     D150786 720 181023 HDFVGMB : Add TRUNC(BIN) to PROCESS    *
      *                                                               *
      *****************************************************************
       ID DIVISION.
       PROGRAM-ID. DFH0STWB.

       ENVIRONMENT DIVISION.
           EJECT
       DATA DIVISION.
       WORKING-STORAGE SECTION.
       77  INQUIRE-RESP                PIC S9(8)  COMP VALUE +0.
       77  INQUIRE-FILE-RESP           PIC S9(8)  COMP VALUE +0.
       77  INQUIRE-PROGRAM-RESP        PIC S9(8)  COMP VALUE +0.
       77  INQUIRE-TDQUEUE-RESP        PIC S9(8)  COMP VALUE +0.
       77  INQUIRE-TSQNAME-RESP        PIC S9(8)  COMP VALUE +0.
           EJECT
       77  BUNDLE-NAME                 PIC X(8)        VALUE SPACES.
       77  BUNDLE-ENABLE-STATUS        PIC S9(8)  COMP VALUE +0.
       77  BUNDLE-DIRECTORY-NAME       PIC X(255)      VALUE SPACES.
       77  BUNDLE-SCOPE-NAME           PIC X(255)      VALUE SPACES.
       77  BUNDLE-PART-COUNT           PIC S9(8)  COMP VALUE +0.
       77  BUNDLE-TARGET-COUNT         PIC S9(8)  COMP VALUE +0.
       77  BUNDLE-ENABLED-COUNT        PIC S9(8)  COMP VALUE +0.

       77  URIMAP-NAME                 PIC X(8)        VALUE SPACES.
       77  URIMAP-ANALYZER-STATUS      PIC S9(8)  COMP VALUE +0.
       77  URIMAP-CONVERTER            PIC X(8)        VALUE SPACES.
       77  URIMAP-ENABLE-STATUS        PIC S9(8)  COMP VALUE +0.
       77  URIMAP-HFSFILE-NAME         PIC X(255)      VALUE SPACES.
       77  URIMAP-LOCATION             PIC X(255)      VALUE SPACES.
       77  URIMAP-PATH-NAME            PIC X(255)      VALUE SPACES.
       77  URIMAP-PIPELINE-NAME        PIC X(8)        VALUE SPACES.
       77  URIMAP-PROGRAM-NAME         PIC X(8)        VALUE SPACES.
       77  URIMAP-TCPIPSERVICE-NAME    PIC X(8)        VALUE SPACES.
       77  URIMAP-TEMPLATE-NAME        PIC X(48)       VALUE SPACES.
       77  URIMAP-TRANSACTION-ID       PIC X(4)        VALUE SPACES.
       77  URIMAP-SCHEME               PIC S9(8)  COMP VALUE +0.
       77  URIMAP-USAGE                PIC S9(8)  COMP VALUE +0.
       77  URIMAP-REDIRECT-TYPE        PIC S9(8)  COMP VALUE +0.
       77  URIMAP-USERID               PIC X(8)        VALUE SPACES.
       77  URIMAP-WEBSERVICE-NAME      PIC X(32)       VALUE SPACES.
       77  URIMAP-ATOMSERVICE-NAME     PIC X(8)        VALUE SPACES.
       77  URIMAP-SOCKETCLOSE          PIC S9(8)  COMP VALUE +0.
       77  URIMAP-SOCKETPOOL-SIZE      PIC S9(8)  COMP VALUE +0.

       77  HOST-NAME                   PIC X(116)      VALUE SPACES.
       77  HOST-ENABLE-STATUS          PIC S9(8)  COMP VALUE +0.
       77  HOST-TCPIPSERVICE-NAME      PIC X(8)        VALUE SPACES.

       77  ATOMSERVICE-NAME            PIC X(8)        VALUE SPACES.
       77  ATOMSERVICE-TYPE            PIC S9(8)  COMP VALUE +0.
       77  ATOMSERVICE-ENABLE-STATUS   PIC S9(8)  COMP VALUE +0.
       77  ATOMSERVICE-RESOURCE-TYPE   PIC S9(8)  COMP VALUE +0.
       77  ATOMSERVICE-RESOURCE-NAME   PIC X(16)       VALUE SPACES.
       77  ATOMSERVICE-BINDING-FILE    PIC X(255)      VALUE SPACES.
       77  ATOMSERVICE-CONFIG-FILE     PIC X(255)      VALUE SPACES.
       77  ATOMSERVICE-FILE-NAME       PIC X(8)        VALUE SPACES.
       77  ATOMSERVICE-FILE-DSNAME     PIC X(44)       VALUE SPACES.
       77  ATOMSERVICE-PROGRAM-NAME    PIC X(8)        VALUE SPACES.
       77  ATOMSERVICE-PROGRAM-CONCUR  PIC S9(8)  COMP VALUE +0.
       77  ATOMSERVICE-TDQUEUE-NAME    PIC X(8)        VALUE SPACES.
       77  ATOMSERVICE-TDQUEUE-TYPE    PIC S9(8)  COMP VALUE +0.
       77  ATOMSERVICE-TDQUEUE-DDNAME  PIC X(8)        VALUE SPACES.
       77  ATOMSERVICE-TDQUEUE-DSNAME  PIC X(44)       VALUE SPACES.
       77  ATOMSERVICE-TSQUEUE-NAME    PIC X(16)       VALUE SPACES.
       77  ATOMSERVICE-TSQUEUE-LOC     PIC S9(8)  COMP VALUE +0.
       77  ATOMSERVICE-URIMAP          PIC X(8)        VALUE SPACES.
       77  ATOMSERVICE-XMLTRANSFORM    PIC X(32)       VALUE SPACES.

       77  PIPELINE-NAME               PIC X(8)        VALUE SPACES.
       77  PIPELINE-CONFIG-FILE        PIC X(255)      VALUE SPACES.
       77  PIPELINE-SHELF-DIRECTORY    PIC X(255)      VALUE SPACES.
       77  PIPELINE-WSDIR-DIRECTORY    PIC X(255)      VALUE SPACES.
       77  PIPELINE-ENABLE-STATUS      PIC S9(8)  COMP VALUE +0.
       77  PIPELINE-MODE               PIC S9(8)  COMP VALUE +0.
       77  PIPELINE-MSGFORMAT          PIC X(8)        VALUE SPACES.

       77  WEBSERVICE-NAME             PIC X(32)       VALUE SPACES.
       77  WEBSERVICE-STATUS           PIC S9(8)  COMP VALUE +0.
       77  WEBSERVICE-PGM-INTERFACE    PIC S9(8)  COMP VALUE +0.
       77  WEBSERVICE-MSG-VALIDATION   PIC S9(8)  COMP VALUE +0.
       77  WEBSERVICE-WSDL-BINDING     PIC X(255)      VALUE SPACES.
       77  WEBSERVICE-ENDPOINT-URI     PIC X(255)      VALUE SPACES.
       77  WEBSERVICE-WSBIND-FILE      PIC X(255)      VALUE SPACES.
       77  WEBSERVICE-WSDL-FILE        PIC X(255)      VALUE SPACES.
       77  WEBSERVICE-ARCHIVE-FILE     PIC X(255)      VALUE SPACES.
       77  WEBSERVICE-CONTAINER        PIC X(16)       VALUE SPACES.
       77  WEBSERVICE-PROGRAM-NAME     PIC X(8)        VALUE SPACES.
       77  WEBSERVICE-PIPELINE         PIC X(8)        VALUE SPACES.
       77  WEBSERVICE-URIMAP           PIC X(8)        VALUE SPACES.
       77  WEBSERVICE-LAST-MOD-TIME    PIC S9(15) COMP-3 VALUE +0.
       77  WEBSERVICE-DATE             PIC X(10)       VALUE SPACES.
       77  WEBSERVICE-TIME             PIC X(8)        VALUE SPACES.

       77  DOCTEMPLATE-NAME            PIC X(8)        VALUE SPACES.
       77  DOCTEMPLATE-APPEND-CRLF     PIC S9(8)  COMP VALUE +0.
       77  DOCTEMPLATE-DDNAME          PIC X(8)        VALUE SPACES.
       77  DOCTEMPLATE-DSNAME          PIC X(44)       VALUE SPACES.
       77  DOCTEMPLATE-EXIT-PROGRAM    PIC X(8)        VALUE SPACES.
       77  DOCTEMPLATE-FILE-NAME       PIC X(8)        VALUE SPACES.
       77  DOCTEMPLATE-FILE-DSNAME     PIC X(44)       VALUE SPACES.
       77  DOCTEMPLATE-HFSFILE-NAME    PIC X(255)      VALUE SPACES.
       77  DOCTEMPLATE-MEMBER-NAME     PIC X(8)        VALUE SPACES.
       77  DOCTEMPLATE-PROGRAM-NAME    PIC X(8)        VALUE SPACES.
       77  DOCTEMPLATE-TDQUEUE-NAME    PIC X(8)        VALUE SPACES.
       77  DOCTEMPLATE-TEMPLATE-NAME   PIC X(48)       VALUE SPACES.
       77  DOCTEMPLATE-TEMPLATE-TYPE   PIC S9(8)  COMP VALUE +0.
       77  DOCTEMPLATE-TSQUEUE-NAME    PIC X(16)       VALUE SPACES.
       77  DOCTEMPLATE-TYPE            PIC S9(8)  COMP VALUE +0.
       77  DOCTEMPLATE-PROGRAM-DATALOC PIC S9(8)  COMP VALUE +0.

       77  DOCTEMPLATE-TDQUEUE-TYPE    PIC S9(8)  COMP VALUE +0.
       77  DOCTEMPLATE-TDQUEUE-DDNAME  PIC X(8)        VALUE SPACES.
       77  DOCTEMPLATE-TDQUEUE-DSNAME  PIC X(44)       VALUE SPACES.
       77  DOCTEMPLATE-TSQUEUE-LOC     PIC S9(8)  COMP VALUE +0.

       77  XMLTRANSFORM-NAME           PIC X(32)       VALUE SPACES.
       77  XMLTRANSFORM-ENABLE-STATUS  PIC S9(8)  COMP VALUE +0.
       77  XMLTRANSFORM-VALIDATIONST   PIC S9(8)  COMP VALUE +0.
       77  XMLTRANSFORM-XSDBIND-FILE   PIC X(255)      VALUE SPACES.
       77  XMLTRANSFORM-XMLSCHEMA-FILE PIC X(255)      VALUE SPACES.

      *****************************************************************
      ***  Total fields                                             ***
      *****************************************************************
       77  ATOMSERVICE-TOTAL           PIC S9(9)  COMP-3 VALUE +0.
       77  BUNDLE-TOTAL                PIC S9(9)  COMP-3 VALUE +0.
       77  DOCTEMPLATE-TOTAL           PIC S9(9)  COMP-3 VALUE +0.
       77  HOST-TOTAL                  PIC S9(9)  COMP-3 VALUE +0.
       77  PIPELINE-TOTAL              PIC S9(9)  COMP-3 VALUE +0.
       77  URIMAP-TOTAL                PIC S9(9)  COMP-3 VALUE +0.
       77  WEBSERVICE-TOTAL            PIC S9(9)  COMP-3 VALUE +0.
       77  XMLTRANSFORM-TOTAL          PIC S9(9)  COMP-3 VALUE +0.

      *****************************************************************
      *** DOCTEMPLATE DHCACHE Domain Subpool Stats.                 ***
      *****************************************************************
       01  DHCACHE-SUBPOOL-STATS.
           03  DHCACHE-SUBPOOL-NAME        PIC X(8)  VALUE 'DHCACHE '.
           03  DHCACHE-SUBPOOL-DSANAME     PIC X(8)  VALUE SPACES.
           03  DHCACHE-INITIAL-FREE        PIC S9(8) COMP VALUE +0.
           03  DHCACHE-GETMAIN-REQUESTS    PIC S9(8) COMP VALUE +0.
           03  DHCACHE-FREEMAIN-REQUESTS   PIC S9(8) COMP VALUE +0.
           03  DHCACHE-CURRENT-ELEMS       PIC S9(8) COMP VALUE +0.
           03  DHCACHE-CURRENT-ELEM-STG    PIC S9(8) COMP VALUE +0.
           03  DHCACHE-CURRENT-PAGE-STG    PIC S9(8) COMP VALUE +0.
           03  DHCACHE-PEAK-PAGE-STG       PIC S9(8) COMP VALUE +0.
           EJECT
      *****************************************************************
      ***  Report print line definitions                            ***
      *****************************************************************
       01  PRINT-LINE.
           03  PRINT-CONTROL           PIC X.
           03  PP1-132.
               05  FILLER              PIC X(39).
               05  PP40-49             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X(83).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(39).
               05  PP40-49X.
                   07  PP40-47         PIC X(8).
                   07  FILLER          PIC XX.
               05  FILLER              PIC X(83).
           03 FILLER REDEFINES PP1-132.
               05  PP1-7               PIC X(7).
               05  FILLER              PIC X(29).
               05  PP37-46X.
                   07  PP37-45         PIC X(9).
                   07  FILLER          PIC X.
               05  FILLER              PIC X(86).
           03 FILLER REDEFINES PP1-132.
               05  PP1-13.
                   07  PP1-12.
                       09  PP1-11.
                           11  PP1-9   PIC X(9).
                           11  FILLER  PIC XX.
                       09  FILLER      PIC X.
                   07  FILLER          PIC X.
               05  FILLER              PIC X(119).
           03 FILLER REDEFINES PP1-132.
               05  PP1-18.
                   07  PP1-16          PIC X(16).
                   07  FILLER          PIC XX.
               05  FILLER              PIC X(18).
               05  PP37-84.
                   07  PP37-75.
                       09  PP37-52     PIC X(16).
                       09  FILLER      PIC X(23).
                   07  FILLER          PIC X(9).
               05  FILLER              PIC X(48).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(39).
               05  PP40-71.
                   07  PP40-51.
                       09  PP40-42     PIC XXX.
                       09  FILLER      PIC X(8).
                       09  PP51        PIC X.
                   07  FILLER          PIC X.
                   07  PP53-60         PIC X(8).
                   07  FILLER          PIC X(11).
               05  FILLER              PIC X(61).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(36).
               05  PP37-44.
                   07  PP37-42         PIC X(6).
                   07  FILLER          PIC XX.
               05  FILLER              PIC X(88).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC XX.
               05  PP3-34.
                   07  FILLER          PIC X(31).
                   07  PP34            PIC X.
               05  FILLER              PIC XX.
               05  PP37-121.
                   07  PP37-80.
                       09  PP37-67.
                           11  PP37-46 PIC ZZ,ZZZ,ZZ9.
                           11  FILLER  PIC X(21).
                       09  FILLER      PIC X(13).
                   07  FILLER          PIC X(41).
               05  FILLER              PIC X(11).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(36).
               05  PP37                PIC X.
               05  FILLER              PIC XX.
               05  PP40-124.
                   07  PP40-83.
                       09  PP40-55     PIC X(16).
                       09  FILLER      PIC X(28).
                   07  FILLER          PIC X(41).
               05  FILLER              PIC X(8).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(36).
               05  PP37-68.
                   07  PP37-41         PIC X(5).
                   07  FILLER          PIC X(27).
               05  FILLER REDEFINES PP37-68.
                   07  PP37-41N        PIC ZZZZ9.
                   07  FILLER          PIC X(27).
               05  FILLER              PIC X(64).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(36).
               05  PP37-40.
                   07  PP37-39         PIC XXX.
                   07  FILLER          PIC X.
               05  FILLER              PIC X(92).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC XX.
               05  PP3-132.
                   07  PP3-102.
                       09  PP3-72.
                           11  PP3-37  PIC X(35).
                           11  PP38-72 PIC X(35).
                       09  PP73-102    PIC X(30).
                   07  PP103-132       PIC X(30).
           EJECT
       LINKAGE SECTION.
           EJECT
      *****************************************************************
      ***  Statistics Sample Programs Commarea.                     ***
      *****************************************************************
       01  DFHCOMMAREA.
           COPY DFH0STCM.
           EJECT
      *****************************************************************
      *** Data structures for Doctemplate Resource statistics       ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHDHDDS.
           EJECT
      *****************************************************************
      *** Data structures for Xmltransform Resource statistics      ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHMLRDS.
           EJECT
      *****************************************************************
      *** Data structures for Pipeline Domain Resource statistics   ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHPIRDS.
           EJECT
      *****************************************************************
      *** Data structures for Pipeline Domain Resource statistics   ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHPIWDS.
           EJECT
      *****************************************************************
      *** Data structures for Resource Lifecycle Domain statistics  ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHRLRDS.
           EJECT
      *****************************************************************
      *** Data structures for Storage Manager Domain Subpools       ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHSMDDS.
           EJECT
      *****************************************************************
      *** Data structures for Web Domain Global statistics          ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHWBGDS.
           EJECT
      *****************************************************************
      *** Data structures for Web Domain Resource statistics        ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHWBRDS.
           EJECT
      *****************************************************************
      *** Data structures for Web Domain Resource statistics        ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHW2RDS.
           EJECT
      *****************************************************************
      ***                                                           ***
      ***           CICS Collect and print statistics               ***
      ***                                                           ***
      *****************************************************************
       PROCEDURE DIVISION.

      *****************************************************************
      *** MAIN-PROCESSING                                           ***
      ***     Initialise the totals fields                          ***
      ***     Print the statistics reports                          ***
      ***     Return to DFH0STLK                                    ***
      *****************************************************************

       MAIN-PROCESSING SECTION.

           PERFORM TOTALS-INITIALISE.

           PERFORM PRINT-STATISTICS.

           EXEC CICS RETURN
           END-EXEC.

       MAIN-PROCESSING-EXIT.
           GOBACK.
           EJECT
      *****************************************************************
      *****************************************************************
      ***         Collect and Print the CICS Statistics             ***
      *****************************************************************
      *****************************************************************
      ***                                                           ***
      *** 1. Print the statistics in the following order:-          ***
      ***    - BUNDLE Resources                                     ***
      ***    - URIMAP Global and Resources                          ***
      ***    - HOSTs                                                ***
      ***    - ATOMSERVICE Resources                                ***
      ***    - PIPELINE Resources                                   ***
      ***    - WEBSERVICE Resources                                 ***
      ***    - DOCTEMPLATE Resources                                ***
      ***    - XMLTRANSFORM Resources                               ***
      ***                                                           ***
      *****************************************************************
      *****************************************************************

       PRINT-STATISTICS SECTION.

           IF SELECT-BUNDLES = 'Y' OR 'y' THEN
               PERFORM BUNDLE-STATISTICS
           END-IF.

           IF SELECT-URIMAPS = 'Y' OR 'y' THEN
               PERFORM URIMAP-STATISTICS
           END-IF.

           IF SELECT-HOSTS = 'Y' OR 'y' THEN
               PERFORM HOST-STATISTICS
           END-IF.

           IF SELECT-ATOMSERVICES = 'Y' OR 'y' THEN
               PERFORM ATOMSERVICE-STATISTICS
           END-IF.

           IF SELECT-PIPELINES = 'Y' OR 'y' THEN
               PERFORM PIPELINE-STATISTICS
           END-IF.

           IF SELECT-WEBSERVICES = 'Y' OR 'y' THEN
               PERFORM WEBSERVICE-STATISTICS
           END-IF.

           IF SELECT-DOCTEMPLATES = 'Y' OR 'y' THEN
               PERFORM DOCTEMPLATE-STATISTICS
           END-IF.

           IF SELECT-XMLTRANSFORMS = 'Y' OR 'y' THEN
               PERFORM XMLTRANSFORM-STATISTICS
           END-IF.

       PRINT-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** BUNDLE-STATISTICS                                         ***
      ***                                                           ***
      *****************************************************************
       BUNDLE-STATISTICS SECTION.

      *****************************************************************
      *** Print the BUNDLE headings.                                ***
      *****************************************************************
           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO BUNDLES-PAGENO.

           MOVE '-'       TO PRINT-CONTROL.
           MOVE 'BUNDLEs' TO PP1-7.
           PERFORM WRITE-OUTPUT.
           MOVE '+'       TO PRINT-CONTROL.
           MOVE ALL '_'   TO PP1-7.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      ***   Print the BUNDLE statistics.                            ***
      *****************************************************************

           EXEC CICS INQUIRE BUNDLE START
           END-EXEC.

           EXEC CICS INQUIRE BUNDLE(BUNDLE-NAME) NEXT
               ENABLESTATUS(BUNDLE-ENABLE-STATUS)
               BUNDLEDIR(BUNDLE-DIRECTORY-NAME)
               BASESCOPE(BUNDLE-SCOPE-NAME)
               PARTCOUNT(BUNDLE-PART-COUNT)
               TARGETCOUNT(BUNDLE-TARGET-COUNT)
               ENABLEDCOUNT(BUNDLE-ENABLED-COUNT)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

               IF LINE-COUNT > (LINES-PER-PAGE - 18) THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               EXEC CICS EXTRACT STATISTICS BUNDLE
                   RESID(BUNDLE-NAME)
                   SET(ADDRESS OF DFHRLRDS)
                   NOHANDLE
               END-EXEC

               MOVE '0' TO PRINT-CONTROL

               ADD 1    TO BUNDLE-TOTAL

               MOVE 'BUNDLE Name. . . . . . . . . . :' TO PP3-34
               MOVE BUNDLE-NAME                        TO PP37-44
               PERFORM WRITE-OUTPUT
               MOVE 'BUNDLE Enable Status . . . . . :' TO PP3-34
               EVALUATE BUNDLE-ENABLE-STATUS
                   WHEN DFHVALUE(ENABLED)
                       MOVE 'Enabled   ' TO PP37-46X
                   WHEN DFHVALUE(DISABLED)
                       MOVE 'Disabled  ' TO PP37-46X
                   WHEN DFHVALUE(ENABLING)
                       MOVE 'Enabling  ' TO PP37-46X
                   WHEN DFHVALUE(DISABLING)
                       MOVE 'Disabling ' TO PP37-46X
                   WHEN DFHVALUE(DISCARDING)
                       MOVE 'Discarding' TO PP37-46X
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'BUNDLE Directory . . . . . . . :' TO PP3-34
               MOVE BUNDLE-DIRECTORY-NAME(1:85)        TO PP37-121
               PERFORM WRITE-OUTPUT
               IF BUNDLE-DIRECTORY-NAME(86:85) NOT = SPACES THEN
                   MOVE ':' TO PP34
                   MOVE BUNDLE-DIRECTORY-NAME(86:85)   TO PP37-121
                   PERFORM WRITE-OUTPUT
                   IF BUNDLE-DIRECTORY-NAME(171:85) NOT = SPACES THEN
                       MOVE ':' TO PP34
                       MOVE BUNDLE-DIRECTORY-NAME(171:85) TO PP37-121
                       PERFORM WRITE-OUTPUT
                   END-IF
               END-IF

               MOVE '0' TO PRINT-CONTROL
               MOVE 'BUNDLE Scope Name. . . . . . . :' TO PP3-34
               MOVE BUNDLE-SCOPE-NAME(1:85)            TO PP37-121
               PERFORM WRITE-OUTPUT
               IF BUNDLE-SCOPE-NAME(86:85) NOT = SPACES THEN
                   MOVE ':' TO PP34
                   MOVE BUNDLE-SCOPE-NAME(86:85)       TO PP37-121
                   PERFORM WRITE-OUTPUT
                   IF BUNDLE-SCOPE-NAME(171:85) NOT = SPACES THEN
                       MOVE ':' TO PP34
                       MOVE BUNDLE-SCOPE-NAME(171:85)  TO PP37-121
                       PERFORM WRITE-OUTPUT
                   END-IF
               END-IF

               MOVE '0' TO PRINT-CONTROL
               MOVE 'BUNDLEPART count . . . . . . . :' TO PP3-34
               MOVE BUNDLE-PART-COUNT                  TO PP37-46
               PERFORM WRITE-OUTPUT

               MOVE 'Target enabled definitions . . :' TO PP3-34
               MOVE BUNDLE-TARGET-COUNT                TO PP37-46
               PERFORM WRITE-OUTPUT

               MOVE 'Current enabled definitions. . :' TO PP3-34
               MOVE BUNDLE-ENABLED-COUNT               TO PP37-46
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE ALL '-' TO PP3-132
               PERFORM WRITE-OUTPUT

               EXEC CICS INQUIRE BUNDLE(BUNDLE-NAME) NEXT
                   ENABLESTATUS(BUNDLE-ENABLE-STATUS)
                   BUNDLEDIR(BUNDLE-DIRECTORY-NAME)
                   BASESCOPE(BUNDLE-SCOPE-NAME)
                   PARTCOUNT(BUNDLE-PART-COUNT)
                   TARGETCOUNT(BUNDLE-TARGET-COUNT)
                   ENABLEDCOUNT(BUNDLE-ENABLED-COUNT)
                   RESP(INQUIRE-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE BUNDLE END
           END-EXEC.

       BUNDLE-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** URIMAP-STATISTICS                                         ***
      ***                                                           ***
      *****************************************************************
       URIMAP-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO URIMAPS-PAGENO.

      *****************************************************************
           EXEC CICS EXTRACT STATISTICS URIMAP
               SET(ADDRESS OF DFHWBGDS)
               NOHANDLE
           END-EXEC.

      *****************************************************************
      ***   Print heading and Global URIMAP statistics.             ***
      *****************************************************************
           MOVE '-'      TO PRINT-CONTROL.
           MOVE 'URIMAPs - Global' TO PP1-16.
           PERFORM WRITE-OUTPUT.
           MOVE '+'      TO PRINT-CONTROL.
           MOVE ALL '_'  TO PP1-16.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'URIMAP reference count . . . . :' TO PP3-34.
           MOVE WBG-URIMAP-REFERENCE-COUNT         TO PP37-46.
           PERFORM WRITE-OUTPUT.
           MOVE 'Entry point reference count. . :' TO PP3-34.
           MOVE WBG-URIMAP-ENTRYPOINT-REF          TO PP37-46.
           PERFORM WRITE-OUTPUT.
           MOVE 'Host/Path no match count . . . :' TO PP3-34.
           MOVE WBG-URIMAP-NO-MATCH-COUNT          TO PP37-46.
           PERFORM WRITE-OUTPUT.
           MOVE 'Host/Path match count. . . . . :' TO PP3-34.
           MOVE WBG-URIMAP-MATCH-COUNT             TO PP37-46.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Disabled . . . . . . . . . . . :' TO PP3-34.
           MOVE WBG-URIMAP-MATCH-DISABLED          TO PP37-46.
           PERFORM WRITE-OUTPUT.
           MOVE 'Redirected . . . . . . . . . . :' TO PP3-34.
           MOVE WBG-URIMAP-MATCH-REDIRECT          TO PP37-46.
           PERFORM WRITE-OUTPUT.
           MOVE 'Analyzer used. . . . . . . . . :' TO PP3-34.
           MOVE WBG-URIMAP-MATCH-ANALYZER          TO PP37-46.
           PERFORM WRITE-OUTPUT.
           MOVE 'Static content delivered . . . :' TO PP3-34.
           MOVE WBG-URIMAP-STATIC-CONTENT          TO PP37-46.
           PERFORM WRITE-OUTPUT.
           MOVE 'Dynamic content delivered. . . :' TO PP3-34.
           MOVE WBG-URIMAP-DYNAMIC-CONTENT         TO PP37-46.
           PERFORM WRITE-OUTPUT.

           MOVE 'Direct attach count . . . . . .:' TO PP3-34.
           MOVE WBG-URIMAP-DIRECT-ATTACH           TO PP37-46.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'PIPELINE requests. . . . . . . :' TO PP3-34.
           MOVE WBG-URIMAP-PIPELINE-REQS           TO PP37-46.
           PERFORM WRITE-OUTPUT.
           MOVE 'ATOMSERVICE requests . . . . . :' TO PP3-34.
           MOVE WBG-URIMAP-ATOMSERV-REQS           TO PP37-46.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Scheme (HTTP) requests . . . . :' TO PP3-34.
           MOVE WBG-URIMAP-SCHEME-HTTP             TO PP37-46.
           PERFORM WRITE-OUTPUT.
           MOVE 'Scheme (HTTPS) requests. . . . :' TO PP3-34.
           MOVE WBG-URIMAP-SCHEME-HTTPS            TO PP37-46.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Virtual host disabled count. . :' TO PP3-34.
           MOVE WBG-HOST-DISABLED-COUNT            TO PP37-46.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      *** Print the URIMAP headings.                                ***
      *****************************************************************
           PERFORM PAGE-HEADINGS.

           MOVE '-'       TO PRINT-CONTROL.
           MOVE 'URIMAPs' TO PP1-7.
           PERFORM WRITE-OUTPUT.
           MOVE '+'       TO PRINT-CONTROL.
           MOVE ALL '_'   TO PP1-7.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      ***   Print the URIMAP statistics.                            ***
      *****************************************************************

           EXEC CICS INQUIRE URIMAP START
           END-EXEC.

           EXEC CICS INQUIRE URIMAP(URIMAP-NAME) NEXT
               ANALYZERSTAT(URIMAP-ANALYZER-STATUS)
               ATOMSERVICE(URIMAP-ATOMSERVICE-NAME)
               CONVERTER(URIMAP-CONVERTER)
               ENABLESTATUS(URIMAP-ENABLE-STATUS)
               HFSFILE(URIMAP-HFSFILE-NAME)
               LOCATION(URIMAP-LOCATION)
               PATH(URIMAP-PATH-NAME)
               PIPELINE(URIMAP-PIPELINE-NAME)
               PROGRAM(URIMAP-PROGRAM-NAME)
               REDIRECTTYPE(URIMAP-REDIRECT-TYPE)
               SCHEME(URIMAP-SCHEME)
               TCPIPSERVICE(URIMAP-TCPIPSERVICE-NAME)
               TEMPLATENAME(URIMAP-TEMPLATE-NAME)
               TRANSACTION(URIMAP-TRANSACTION-ID)
               USAGE(URIMAP-USAGE)
               USERID(URIMAP-USERID)
               WEBSERVICE(URIMAP-WEBSERVICE-NAME)
               SOCKETCLOSE(URIMAP-SOCKETCLOSE)
               SOCKPOOLSIZE(URIMAP-SOCKETPOOL-SIZE)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

               IF LINE-COUNT > (LINES-PER-PAGE - 46) THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               EXEC CICS EXTRACT STATISTICS URIMAP
                   RESID(URIMAP-NAME)
                   SET(ADDRESS OF DFHWBRDS)
                   NOHANDLE
               END-EXEC

               MOVE '0' TO PRINT-CONTROL

               ADD 1    TO URIMAP-TOTAL

               MOVE 'URIMAP Name. . . . . . . . . . :' TO PP3-34
               MOVE URIMAP-NAME                        TO PP37-44
               PERFORM WRITE-OUTPUT
               MOVE 'URIMAP Enable Status . . . . . :' TO PP3-34
               EVALUATE URIMAP-ENABLE-STATUS
                   WHEN DFHVALUE(ENABLED)
                       MOVE 'Enabled ' TO PP37-44
                   WHEN DFHVALUE(DISABLED)
                       MOVE 'Disabled' TO PP37-44
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT
               MOVE 'URIMAP Usage . . . . . . . . . :' TO PP3-34
               EVALUATE(URIMAP-USAGE)
                   WHEN DFHVALUE(CLIENT)
                       MOVE 'Client   '                TO PP37-45
                   WHEN DFHVALUE(SERVER)
                       MOVE 'Server   '                TO PP37-45
                   WHEN DFHVALUE(PIPELINE)
                       MOVE 'Pipeline '                TO PP37-45
                   WHEN DFHVALUE(ATOM)
                       MOVE 'Atom     '                TO PP37-45
                   WHEN DFHVALUE(JVMSERVER)
                       MOVE 'JVMSERVER'                TO PP37-45
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT
               MOVE 'URIMAP Scheme. . . . . . . . . :' TO PP3-34
               EVALUATE(URIMAP-SCHEME)
                   WHEN DFHVALUE(HTTP)
                       MOVE 'HTTP '                    TO PP37-41
                   WHEN DFHVALUE(HTTPS)
                       MOVE 'HTTPS'                    TO PP37-41
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'URIMAP Authenticate. . . . . . :' TO PP3-34
               EVALUATE TRUE
                   WHEN WBR-AUTHENTICATE-NONE
                       MOVE 'None '                    TO PP37-41
                   WHEN WBR-AUTHENTICATE-BASIC
                       MOVE 'Basic'                    TO PP37-41
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'URIMAP Port. . . . . . . . . . :' TO PP3-34
               IF WBR-URIMAP-PORT = -1 THEN
                   MOVE 'No '                          TO PP37-39
               ELSE
                   MOVE WBR-URIMAP-PORT                TO PP37-41N
               END-IF
               PERFORM WRITE-OUTPUT

               MOVE 'URIMAP Host. . . . . . . . . . :' TO PP3-34
               MOVE WBR-URIMAP-HOSTNAME(1:85)          TO PP37-121
               PERFORM WRITE-OUTPUT
               IF WBR-URIMAP-HOSTNAME(86:31) NOT = SPACES THEN
                   MOVE ':' TO PP34
                   MOVE WBR-URIMAP-HOSTNAME(86:31)     TO PP37-67
                   PERFORM WRITE-OUTPUT
               END-IF

               MOVE 'URIMAP IP Family . . . . . . . :' TO PP3-34
               EVALUATE TRUE
                   WHEN WBR-IP-FAMILY-IPV4
                       MOVE 'IPv4'                     TO PP37-40
                   WHEN WBR-IP-FAMILY-IPV6
                       MOVE 'IPv6'                     TO PP37-40
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-SPOOL

               MOVE 'URIMAP IP Resolved Address . . :' TO PP3-34
               MOVE WBR-URIMAP-IP-ADDRESS              TO PP37-75
               PERFORM WRITE-SPOOL

               MOVE 'URIMAP Path. . . . . . . . . . :' TO PP3-34
               MOVE URIMAP-PATH-NAME(1:85)             TO PP37-121
               PERFORM WRITE-OUTPUT
               IF URIMAP-PATH-NAME(86:85) NOT = SPACES THEN
                   MOVE ':' TO PP34
                   MOVE URIMAP-PATH-NAME(86:85)        TO PP37-121
                   PERFORM WRITE-OUTPUT
                   IF URIMAP-PATH-NAME(171:85) NOT = SPACES THEN
                       MOVE ':' TO PP34
                       MOVE URIMAP-PATH-NAME(171:85)   TO PP37-121
                       PERFORM WRITE-OUTPUT
                   END-IF
               END-IF

               MOVE '0' TO PRINT-CONTROL
               MOVE 'TCPIPSERVICE name. . . . . . . :' TO PP3-34
               MOVE URIMAP-TCPIPSERVICE-NAME           TO PP37-44
               PERFORM WRITE-OUTPUT
               MOVE 'WEBSERVICE name. . . . . . . . :' TO PP3-34
               MOVE URIMAP-WEBSERVICE-NAME             TO PP37-68
               PERFORM WRITE-OUTPUT
               MOVE 'PIPELINE name. . . . . . . . . :' TO PP3-34
               MOVE URIMAP-PIPELINE-NAME               TO PP37-44
               PERFORM WRITE-OUTPUT
               MOVE 'ATOMSERVICE name . . . . . . . :' TO PP3-34
               MOVE URIMAP-ATOMSERVICE-NAME            TO PP37-44
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Templatename . . . . . . . . . :' TO PP3-34
               MOVE URIMAP-TEMPLATE-NAME               TO PP37-84
               PERFORM WRITE-OUTPUT
               MOVE 'HFS File . . . . . . . . . . . :' TO PP3-34
               MOVE URIMAP-HFSFILE-NAME(1:85)          TO PP37-121
               PERFORM WRITE-OUTPUT
               IF URIMAP-HFSFILE-NAME(86:85) NOT = SPACES THEN
                   MOVE ':' TO PP34
                   MOVE URIMAP-HFSFILE-NAME(86:85)     TO PP37-121
                   PERFORM WRITE-OUTPUT
                   IF URIMAP-HFSFILE-NAME(171:85) NOT = SPACES THEN
                       MOVE ':' TO PP34
                       MOVE URIMAP-HFSFILE-NAME(171:85) TO PP37-121
                       PERFORM WRITE-OUTPUT
                   END-IF
               END-IF

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Analyzer . . . . . . . . . . . :' TO PP3-34
               EVALUATE(URIMAP-ANALYZER-STATUS)
                   WHEN DFHVALUE(ANALYZER)
                       MOVE 'Yes'                      TO PP37-39
                   WHEN DFHVALUE(NOANALYZER)
                       MOVE 'No '                      TO PP37-39
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               MOVE 'Converter. . . . . . . . . . . :' TO PP3-34
               MOVE URIMAP-CONVERTER                   TO PP37-44
               PERFORM WRITE-OUTPUT
               MOVE 'Transaction ID . . . . . . . . :' TO PP3-34
               MOVE URIMAP-TRANSACTION-ID              TO PP37-40
               PERFORM WRITE-OUTPUT
               MOVE 'Program name . . . . . . . . . :' TO PP3-34
               MOVE URIMAP-PROGRAM-NAME                TO PP37-44
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Redirection type . . . . . . . :' TO PP3-34
               EVALUATE(URIMAP-REDIRECT-TYPE)
                   WHEN DFHVALUE(NONE)
                       MOVE 'None     '                TO PP37-45
                   WHEN DFHVALUE(TEMPORARY)
                       MOVE 'Temporary'                TO PP37-45
                   WHEN DFHVALUE(PERMANENT)
                       MOVE 'Permanent'                TO PP37-45
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               MOVE 'Location for redirection . . . :' TO PP3-34
               MOVE URIMAP-LOCATION(1:85)              TO PP37-121
               PERFORM WRITE-OUTPUT
               IF URIMAP-LOCATION(86:85) NOT = SPACES THEN
                   MOVE ':' TO PP34
                   MOVE URIMAP-LOCATION(86:85)         TO PP37-121
                   PERFORM WRITE-OUTPUT
                   IF URIMAP-LOCATION(171:85) NOT = SPACES THEN
                       MOVE ':' TO PP34
                       MOVE URIMAP-LOCATION(171:85)     TO PP37-121
                       PERFORM WRITE-OUTPUT
                   END-IF
               END-IF

               MOVE '0' TO PRINT-CONTROL
               MOVE 'URIMAP reference count . . . . :' TO PP3-34
               MOVE WBR-URIMAP-REFERENCE-COUNT         TO PP37-46
               PERFORM WRITE-OUTPUT
               MOVE 'Disabled . . . . . . . . . . . :' TO PP3-34
               MOVE WBR-URIMAP-MATCH-DISABLED          TO PP37-46
               PERFORM WRITE-OUTPUT
               MOVE 'Redirected . . . . . . . . . . :' TO PP3-34
               MOVE WBR-URIMAP-MATCH-REDIRECT          TO PP37-46
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'URIMAP Socket close (timeout). :' TO PP3-34
               MOVE WBR-URIMAP-SOCKETCLOSE             TO PP37-46
               PERFORM WRITE-OUTPUT
               MOVE 'URIMAP Socket pool size. . . . :' TO PP3-34
               MOVE WBR-URIMAP-SOCKPOOLSIZE            TO PP37-46
               PERFORM WRITE-OUTPUT
               MOVE 'URIMAP Peak socket pool size . :' TO PP3-34
               MOVE WBR-URIMAP-SOCKPOOLSIZE-PEAK       TO PP37-46
               PERFORM WRITE-OUTPUT
               MOVE 'URIMAP Sockets reclaimed . . . :' TO PP3-34
               MOVE WBR-URIMAP-SOCKETS-RECLAIMED       TO PP37-46
               PERFORM WRITE-OUTPUT
               MOVE 'URIMAP Sockets timed-out . . . :' TO PP3-34
               MOVE WBR-URIMAP-SOCKETS-TIMEDOUT        TO PP37-46
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE ALL '-' TO PP3-132
               PERFORM WRITE-OUTPUT

               EXEC CICS INQUIRE URIMAP(URIMAP-NAME) NEXT
                   ANALYZERSTAT(URIMAP-ANALYZER-STATUS)
                   ATOMSERVICE(URIMAP-ATOMSERVICE-NAME)
                   CONVERTER(URIMAP-CONVERTER)
                   ENABLESTATUS(URIMAP-ENABLE-STATUS)
                   HFSFILE(URIMAP-HFSFILE-NAME)
                   LOCATION(URIMAP-LOCATION)
                   PATH(URIMAP-PATH-NAME)
                   PIPELINE(URIMAP-PIPELINE-NAME)
                   PROGRAM(URIMAP-PROGRAM-NAME)
                   REDIRECTTYPE(URIMAP-REDIRECT-TYPE)
                   SCHEME(URIMAP-SCHEME)
                   TCPIPSERVICE(URIMAP-TCPIPSERVICE-NAME)
                   TEMPLATENAME(URIMAP-TEMPLATE-NAME)
                   TRANSACTION(URIMAP-TRANSACTION-ID)
                   USAGE(URIMAP-USAGE)
                   USERID(URIMAP-USERID)
                   WEBSERVICE(URIMAP-WEBSERVICE-NAME)
                   SOCKETCLOSE(URIMAP-SOCKETCLOSE)
                   SOCKPOOLSIZE(URIMAP-SOCKETPOOL-SIZE)
                   RESP(INQUIRE-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE URIMAP END
           END-EXEC.

       URIMAP-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** HOST-STATISTICS                                           ***
      ***                                                           ***
      *****************************************************************
       HOST-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO HOSTS-PAGENO.

      *****************************************************************
      *** Print the Virtual Hosts headings.                         ***
      *****************************************************************
           MOVE '-'             TO PRINT-CONTROL.
           MOVE 'Virtual Hosts' TO PP1-13.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-13.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      ***   Print the Virtual Host statistics.                      ***
      *****************************************************************

           EXEC CICS INQUIRE HOST START
           END-EXEC.

           EXEC CICS INQUIRE HOST(HOST-NAME) NEXT
               ENABLESTATUS(HOST-ENABLE-STATUS)
               TCPIPSERVICE(HOST-TCPIPSERVICE-NAME)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

               IF LINE-COUNT > (LINES-PER-PAGE - 8) THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               MOVE '0' TO PRINT-CONTROL

               ADD 1    TO HOST-TOTAL

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Virtual Host name. . . . . . . :' TO PP3-34
               MOVE HOST-NAME(1:85)                    TO PP37-121
               PERFORM WRITE-OUTPUT
               IF HOST-NAME(86:31) NOT = SPACES THEN
                   MOVE ':'                            TO PP34
                   MOVE HOST-NAME(86:31)               TO PP37-67
                   PERFORM WRITE-OUTPUT
               END-IF
               MOVE 'TCPIPSERVICE name. . . . . . . :' TO PP3-34
               MOVE HOST-TCPIPSERVICE-NAME             TO PP37-44
               PERFORM WRITE-OUTPUT
               MOVE 'Virtual Host Enable Status . . :' TO PP3-34
               EVALUATE HOST-ENABLE-STATUS
                   WHEN DFHVALUE(ENABLED)
                       MOVE 'Enabled ' TO PP37-44
                   WHEN DFHVALUE(DISABLED)
                       MOVE 'Disabled' TO PP37-44
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE ALL '-' TO PP3-132
               PERFORM WRITE-OUTPUT

               EXEC CICS INQUIRE HOST(HOST-NAME) NEXT
                   TCPIPSERVICE(HOST-TCPIPSERVICE-NAME)
                   ENABLESTATUS(HOST-ENABLE-STATUS)
                   RESP(INQUIRE-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE HOST END
           END-EXEC.

       HOST-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** WEBSERVICE-STATISTICS                                     ***
      ***                                                           ***
      *****************************************************************
       WEBSERVICE-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO WEBSERVICES-PAGENO.

      *****************************************************************
      *** Print the Web Services headings.                          ***
      *****************************************************************
           MOVE '-'           TO PRINT-CONTROL.
           MOVE 'WEBSERVICEs' TO PP1-11.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-11.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      ***   Print the Web Service statistics.                       ***
      *****************************************************************

           EXEC CICS INQUIRE WEBSERVICE START
           END-EXEC.

           EXEC CICS INQUIRE WEBSERVICE(WEBSERVICE-NAME) NEXT
               ARCHIVEFILE(WEBSERVICE-ARCHIVE-FILE)
               BINDING(WEBSERVICE-WSDL-BINDING)
               CONTAINER(WEBSERVICE-CONTAINER)
               ENDPOINT(WEBSERVICE-ENDPOINT-URI)
               LASTMODTIME(WEBSERVICE-LAST-MOD-TIME)
               PIPELINE(WEBSERVICE-PIPELINE)
               PGMINTERFACE(WEBSERVICE-PGM-INTERFACE)
               PROGRAM(WEBSERVICE-PROGRAM-NAME)
               STATE(WEBSERVICE-STATUS)
               URIMAP(WEBSERVICE-URIMAP)
               VALIDATIONST(WEBSERVICE-MSG-VALIDATION)
               WSBIND(WEBSERVICE-WSBIND-FILE)
               WSDLFILE(WEBSERVICE-WSDL-FILE)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

               IF LINE-COUNT > (LINES-PER-PAGE - 36) THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               EXEC CICS EXTRACT STATISTICS WEBSERVICE
                   RESID(WEBSERVICE-NAME)
                   SET(ADDRESS OF DFHPIWDS)
                   NOHANDLE
               END-EXEC

               ADD 1    TO WEBSERVICE-TOTAL

               MOVE '0' TO PRINT-CONTROL
               MOVE 'WEBSERVICE Name . . . . . . . . . :' TO PP3-37
               MOVE WEBSERVICE-NAME                       TO PP40-71
               PERFORM WRITE-OUTPUT
               MOVE 'WEBSERVICE Status . . . . . . . . :' TO PP3-37
               EVALUATE WEBSERVICE-STATUS
                   WHEN DFHVALUE(INITING)
                       MOVE 'Initialising' TO PP40-51
                   WHEN DFHVALUE(INSERVICE)
                       MOVE 'Inservice   ' TO PP40-51
                   WHEN DFHVALUE(UNUSABLE)
                       MOVE 'Unusable    ' TO PP40-51
                   WHEN DFHVALUE(UPDATING)
                       MOVE 'Updating    ' TO PP40-51
                   WHEN DFHVALUE(DISCARDING)
                       MOVE 'Discarding  ' TO PP40-51
                   WHEN DFHVALUE(DISABLING)
                       MOVE 'Disabling   ' TO PP40-51
                   WHEN DFHVALUE(DISABLED)
                       MOVE 'Disabled    ' TO PP40-51
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT
               MOVE 'Last modified date and time . . . :' TO PP3-37
               IF WEBSERVICE-LAST-MOD-TIME NOT = 0 THEN
                   EXEC CICS FORMATTIME
                       ABSTIME(WEBSERVICE-LAST-MOD-TIME)
                       MMDDYYYY(WEBSERVICE-DATE) DATESEP('/')
                       TIME(WEBSERVICE-TIME) TIMESEP
                   END-EXEC
                   MOVE WEBSERVICE-DATE                TO PP40-49X
                   MOVE '/'                            TO PP51
                   MOVE WEBSERVICE-TIME                TO PP53-60
               END-IF
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'URIMAP name . . . . . . . . . . . :' TO PP3-37
               MOVE WEBSERVICE-URIMAP                     TO PP40-47
               PERFORM WRITE-OUTPUT
               MOVE 'PIPELINE name . . . . . . . . . . :' TO PP3-37
               MOVE WEBSERVICE-PIPELINE                   TO PP40-47
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Web service description (WSDL). . :' TO PP3-37
               MOVE WEBSERVICE-WSDL-FILE(1:85)            TO PP40-124
               PERFORM WRITE-OUTPUT
               IF WEBSERVICE-WSDL-FILE(86:85) NOT = SPACES THEN
                   MOVE ':' TO PP37
                   MOVE WEBSERVICE-WSDL-FILE(86:85)       TO PP40-124
                   PERFORM WRITE-OUTPUT
                   IF WEBSERVICE-WSDL-FILE(171:85) NOT = SPACES THEN
                       MOVE ':' TO PP37
                       MOVE WEBSERVICE-WSDL-FILE(171:85)  TO PP40-124
                       PERFORM WRITE-OUTPUT
                   END-IF
               END-IF

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Archive file. . . . . . . . . . . :' TO PP3-37
               MOVE WEBSERVICE-ARCHIVE-FILE(1:85)         TO PP40-124
               PERFORM WRITE-OUTPUT
               IF WEBSERVICE-ARCHIVE-FILE(86:85) NOT = SPACES THEN
                   MOVE ':' TO PP37
                   MOVE WEBSERVICE-ARCHIVE-FILE(86:85)     TO PP40-124
                   PERFORM WRITE-OUTPUT
                   IF WEBSERVICE-ARCHIVE-FILE(171:85) NOT = SPACES THEN
                       MOVE ':' TO PP37
                       MOVE WEBSERVICE-ARCHIVE-FILE(171:85) TO PP40-124
                       PERFORM WRITE-OUTPUT
                   END-IF
               END-IF

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Web service binding file. . . . . :' TO PP3-37
               MOVE WEBSERVICE-WSBIND-FILE(1:85)          TO PP40-124
               PERFORM WRITE-OUTPUT
               IF WEBSERVICE-WSBIND-FILE(86:85) NOT = SPACES THEN
                   MOVE ':' TO PP37
                   MOVE WEBSERVICE-WSBIND-FILE(86:85)     TO PP40-124
                   PERFORM WRITE-OUTPUT
                   IF WEBSERVICE-WSBIND-FILE(171:85) NOT = SPACES THEN
                       MOVE ':' TO PP37
                       MOVE WEBSERVICE-WSBIND-FILE(171:85) TO PP40-124
                       PERFORM WRITE-OUTPUT
                   END-IF
               END-IF

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Web service WSDL binding. . . . . :' TO PP3-37
               MOVE WEBSERVICE-WSDL-BINDING(1:85)         TO PP40-124
               PERFORM WRITE-OUTPUT
               IF WEBSERVICE-WSDL-BINDING(86:85) NOT = SPACES THEN
                   MOVE ':' TO PP37
                   MOVE WEBSERVICE-WSDL-BINDING(86:85)    TO PP40-124
                   PERFORM WRITE-OUTPUT
                   IF WEBSERVICE-WSDL-BINDING(171:85) NOT = SPACES THEN
                       MOVE ':' TO PP37
                       MOVE WEBSERVICE-WSDL-BINDING(171:85) TO PP40-124
                       PERFORM WRITE-OUTPUT
                   END-IF
               END-IF

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Endpoint. . . . . . . . . . . . . :' TO PP3-37
               MOVE WEBSERVICE-ENDPOINT-URI(1:85)         TO PP40-124
               PERFORM WRITE-OUTPUT
               IF WEBSERVICE-ENDPOINT-URI(86:85) NOT = SPACES THEN
                   MOVE ':' TO PP37
                   MOVE WEBSERVICE-ENDPOINT-URI(86:85)    TO PP40-124
                   PERFORM WRITE-OUTPUT
                   IF WEBSERVICE-ENDPOINT-URI(171:85) NOT = SPACES THEN
                       MOVE ':' TO PP37
                       MOVE WEBSERVICE-ENDPOINT-URI(171:85) TO PP40-124
                       PERFORM WRITE-OUTPUT
                   END-IF
               END-IF

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Validation. . . . . . . . . . . . :' TO PP3-37
               EVALUATE WEBSERVICE-MSG-VALIDATION
                   WHEN DFHVALUE(VALIDATION)
                       MOVE 'Yes' TO PP40-42
                   WHEN DFHVALUE(NOVALIDATION)
                       MOVE 'No ' TO PP40-42
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Program interface . . . . . . . . :' TO PP3-37
               EVALUATE WEBSERVICE-PGM-INTERFACE
                   WHEN DFHVALUE(CHANNEL)
                       MOVE 'Channel ' TO PP40-47
                   WHEN DFHVALUE(COMMAREA)
                       MOVE 'Commarea' TO PP40-47
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT
               MOVE 'Program name. . . . . . . . . . . :' TO PP3-37
               MOVE WEBSERVICE-PROGRAM-NAME               TO PP40-47
               PERFORM WRITE-OUTPUT
               MOVE 'Container . . . . . . . . . . . . :' TO PP3-37
               MOVE WEBSERVICE-CONTAINER                  TO PP40-55
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'WEBSERVICE use count. . . . . . . :' TO PP3-37
               MOVE PIW-WEBSERVICE-USE-COUNT              TO PP40-49
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE ALL '-' TO PP3-132
               PERFORM WRITE-OUTPUT

               EXEC CICS INQUIRE WEBSERVICE(WEBSERVICE-NAME) NEXT
                   ARCHIVEFILE(WEBSERVICE-ARCHIVE-FILE)
                   BINDING(WEBSERVICE-WSDL-BINDING)
                   CONTAINER(WEBSERVICE-CONTAINER)
                   ENDPOINT(WEBSERVICE-ENDPOINT-URI)
                   LASTMODTIME(WEBSERVICE-LAST-MOD-TIME)
                   PIPELINE(WEBSERVICE-PIPELINE)
                   PGMINTERFACE(WEBSERVICE-PGM-INTERFACE)
                   PROGRAM(WEBSERVICE-PROGRAM-NAME)
                   STATE(WEBSERVICE-STATUS)
                   URIMAP(WEBSERVICE-URIMAP)
                   VALIDATIONST(WEBSERVICE-MSG-VALIDATION)
                   WSBIND(WEBSERVICE-WSBIND-FILE)
                   WSDLFILE(WEBSERVICE-WSDL-FILE)
                   RESP(INQUIRE-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE WEBSERVICE END
           END-EXEC.

       WEBSERVICE-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** ATOMSERVICE-STATISTICS                                    ***
      ***                                                           ***
      *****************************************************************
       ATOMSERVICE-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO ATOMSERVICES-PAGENO.

      *****************************************************************
      *** Print the Atomservices headings                           ***
      *****************************************************************
           MOVE '-'            TO PRINT-CONTROL.
           MOVE 'ATOMSERVICEs' TO PP1-12.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-12.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      ***   Print the ATOMSERVICE statistics.                       ***
      *****************************************************************

           EXEC CICS INQUIRE ATOMSERVICE START
           END-EXEC.

           EXEC CICS INQUIRE ATOMSERVICE(ATOMSERVICE-NAME) NEXT
               ATOMTYPE(ATOMSERVICE-TYPE)
               ENABLESTATUS(ATOMSERVICE-ENABLE-STATUS)
               BINDFILE(ATOMSERVICE-BINDING-FILE)
               CONFIGFILE(ATOMSERVICE-CONFIG-FILE)
               RESOURCENAME(ATOMSERVICE-RESOURCE-NAME)
               RESOURCETYPE(ATOMSERVICE-RESOURCE-TYPE)
               URIMAP(ATOMSERVICE-URIMAP)
               XMLTRANSFORM(ATOMSERVICE-XMLTRANSFORM)
               RESP(INQUIRE-RESP)
           END-EXEC.


           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

               IF LINE-COUNT > (LINES-PER-PAGE - 27) THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               EXEC CICS EXTRACT STATISTICS ATOMSERVICE
                   RESID(ATOMSERVICE-NAME)
                   SET(ADDRESS OF DFHW2RDS)
                   NOHANDLE
               END-EXEC

               ADD 1    TO ATOMSERVICE-TOTAL

               MOVE '0' TO PRINT-CONTROL
               MOVE 'ATOMSERVICE Name. . . . . . . . . :' TO PP3-37
               MOVE ATOMSERVICE-NAME                      TO PP40-47
               PERFORM WRITE-OUTPUT
               MOVE 'ATOMSERVICE Enable Status . . . . :' TO PP3-37
               EVALUATE ATOMSERVICE-ENABLE-STATUS
                   WHEN DFHVALUE(ENABLED)
                       MOVE 'Enabled ' TO PP40-47
                   WHEN DFHVALUE(DISABLED)
                       MOVE 'Disabled' TO PP40-47
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               MOVE 'Atom document type. . . . . . . . :' TO PP3-37
               EVALUATE ATOMSERVICE-TYPE
                   WHEN DFHVALUE(FEED)
                       MOVE 'Feed      ' TO PP40-49X
                   WHEN DFHVALUE(COLLECTION)
                       MOVE 'Collection' TO PP40-49X
                   WHEN DFHVALUE(SERVICE)
                       MOVE 'Service   ' TO PP40-49X
                   WHEN DFHVALUE(CATEGORY)
                       MOVE 'Category  ' TO PP40-49X
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Atom configuration file . . . . . :' TO PP3-37
               MOVE ATOMSERVICE-CONFIG-FILE(1:85)         TO PP40-124
               PERFORM WRITE-OUTPUT
               IF ATOMSERVICE-CONFIG-FILE(86:85) NOT = SPACES THEN
                   MOVE ':' TO PP37
                   MOVE ATOMSERVICE-CONFIG-FILE(86:85)    TO PP40-124
                   PERFORM WRITE-OUTPUT
                   IF ATOMSERVICE-CONFIG-FILE(171:85) NOT = SPACES THEN
                       MOVE ':' TO PP37
                       MOVE ATOMSERVICE-CONFIG-FILE(171:85) TO PP40-124
                       PERFORM WRITE-OUTPUT
                   END-IF
               END-IF

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Atom binding file . . . . . . . . :' TO PP3-37
               MOVE ATOMSERVICE-BINDING-FILE(1:85)        TO PP40-124
               PERFORM WRITE-OUTPUT
               IF ATOMSERVICE-BINDING-FILE(86:85) NOT = SPACES THEN
                 MOVE ':' TO PP37
                 MOVE ATOMSERVICE-BINDING-FILE(86:85)     TO PP40-124
                 PERFORM WRITE-OUTPUT
                 IF ATOMSERVICE-BINDING-FILE(171:85) NOT = SPACES THEN
                   MOVE ':' TO PP37
                   MOVE ATOMSERVICE-BINDING-FILE(171:85)  TO PP40-124
                   PERFORM WRITE-OUTPUT
                 END-IF
               END-IF

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Resource type for Atom feed . . . :' TO PP3-37
               EVALUATE ATOMSERVICE-RESOURCE-TYPE
                   WHEN DFHVALUE(FILE)
                       PERFORM FILE-ATOMSERVICE
                   WHEN DFHVALUE(PROGRAM)
                       PERFORM PROGRAM-ATOMSERVICE
                   WHEN DFHVALUE(TSQUEUE)
                       PERFORM TSQUEUE-ATOMSERVICE
                   WHEN DFHVALUE(NOTAPPLIC)
                       PERFORM NOTAPPLIC-ATOMSERVICE
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE

               PERFORM WRITE-OUTPUT
               MOVE '0' TO PRINT-CONTROL
               MOVE 'URIMAP Name . . . . . . . . . . . :' TO PP3-37
               MOVE ATOMSERVICE-URIMAP                    TO PP40-47
               PERFORM WRITE-OUTPUT
               MOVE 'XMLTRANSFORM Name . . . . . . . . :' TO PP3-37
               MOVE ATOMSERVICE-XMLTRANSFORM              TO PP40-71

               PERFORM WRITE-OUTPUT
               MOVE '0' TO PRINT-CONTROL
               MOVE 'ATOMSERVICE reference count . . . :' TO PP3-37
               MOVE W2R-ATOMSERV-REF-COUNT                TO PP40-49
               PERFORM WRITE-OUTPUT
               MOVE 'Disabled. . . . . . . . . . . . . :' TO PP3-37
               MOVE W2R-ATOMSERV-REF-DISABLED             TO PP40-49
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'POST requests to the feed URL . . :' TO PP3-37
               MOVE W2R-ATOMSERV-POST-FEED-CNT            TO PP40-49
               PERFORM WRITE-OUTPUT
               MOVE 'GET requests to the feed URL. . . :' TO PP3-37
               MOVE W2R-ATOMSERV-GET-FEED-CNT             TO PP40-49
               PERFORM WRITE-OUTPUT
               MOVE 'GET requests to the entry URL . . :' TO PP3-37
               MOVE W2R-ATOMSERV-GET-ENTRY-CNT            TO PP40-49
               PERFORM WRITE-OUTPUT
               MOVE 'PUT requests to the entry URL . . :' TO PP3-37
               MOVE W2R-ATOMSERV-PUT-ENTRY-CNT            TO PP40-49
               PERFORM WRITE-OUTPUT
               MOVE 'DELETE requests to the entry URL. :' TO PP3-37
               MOVE W2R-ATOMSERV-DEL-ENTRY-CNT            TO PP40-49
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE ALL '-' TO PP3-132
               PERFORM WRITE-OUTPUT

               EXEC CICS INQUIRE ATOMSERVICE(ATOMSERVICE-NAME) NEXT
                   ATOMTYPE(ATOMSERVICE-TYPE)
                   ENABLESTATUS(ATOMSERVICE-ENABLE-STATUS)
                   BINDFILE(ATOMSERVICE-BINDING-FILE)
                   CONFIGFILE(ATOMSERVICE-CONFIG-FILE)
                   RESOURCENAME(ATOMSERVICE-RESOURCE-NAME)
                   RESOURCETYPE(ATOMSERVICE-RESOURCE-TYPE)
                   URIMAP(ATOMSERVICE-URIMAP)
                   XMLTRANSFORM(ATOMSERVICE-XMLTRANSFORM)
                   RESP(INQUIRE-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE ATOMSERVICE END
           END-EXEC.

       ATOMSERVICE-STATISTICS-EXIT.
           EXIT.
           EJECT
       FILE-ATOMSERVICE SECTION.

           MOVE 'File    '                            TO PP40-47.
           PERFORM WRITE-OUTPUT.

           MOVE 'Resource name for Atom feed . . . :' TO PP3-37.
           MOVE ATOMSERVICE-RESOURCE-NAME             TO PP40-47.
           PERFORM WRITE-OUTPUT.

           MOVE ATOMSERVICE-RESOURCE-NAME TO ATOMSERVICE-FILE-NAME.
           EXEC CICS INQUIRE FILE(ATOMSERVICE-FILE-NAME)
               DSNAME(ATOMSERVICE-FILE-DSNAME)
               RESP(INQUIRE-FILE-RESP)
           END-EXEC.

           IF INQUIRE-FILE-RESP = DFHRESP(NORMAL) THEN
               MOVE 'Dataset name. . . . . . . . . . . :' TO PP3-37
               MOVE ATOMSERVICE-FILE-DSNAME               TO PP40-83
               PERFORM WRITE-OUTPUT
           END-IF.

       FILE-ATOMSERVICE-EXIT.
           EXIT.

       PROGRAM-ATOMSERVICE SECTION.

           MOVE 'Program '                            TO PP40-47.
           PERFORM WRITE-OUTPUT.

           MOVE 'Resource name for Atom feed . . . :' TO PP3-37.
           MOVE ATOMSERVICE-RESOURCE-NAME             TO PP40-47.
           PERFORM WRITE-OUTPUT.

           MOVE ATOMSERVICE-RESOURCE-NAME TO ATOMSERVICE-PROGRAM-NAME.
           EXEC CICS INQUIRE PROGRAM(ATOMSERVICE-PROGRAM-NAME)
               CONCURRENCY(ATOMSERVICE-PROGRAM-CONCUR)
               RESP(INQUIRE-PROGRAM-RESP)
           END-EXEC.

      *    IF INQUIRE-PROGRAM-RESP = DFHRESP(NORMAL) THEN
      *        PERFORM WRITE-OUTPUT
      *    END-IF.

       PROGRAM-ATOMSERVICE-EXIT.
           EXIT.

       TSQUEUE-ATOMSERVICE SECTION.

           MOVE 'Tsqueue '                            TO PP40-47.
           PERFORM WRITE-OUTPUT.

           MOVE 'Resource name for Atom feed . . . :' TO PP3-37.
           MOVE ATOMSERVICE-RESOURCE-NAME             TO PP40-55.
           PERFORM WRITE-OUTPUT.

           MOVE ATOMSERVICE-RESOURCE-NAME TO ATOMSERVICE-TSQUEUE-NAME.
           EXEC CICS INQUIRE TSQNAME(ATOMSERVICE-TSQUEUE-NAME)
               LOCATION(ATOMSERVICE-TSQUEUE-LOC)
               RESP(INQUIRE-TSQNAME-RESP)
           END-EXEC.

      *    IF INQUIRE-TSQNAME-RESP = DFHRESP(NORMAL) THEN
      *        PERFORM WRITE-OUTPUT
      *    END-IF.

       TSQUEUE-ATOMSERVICE-EXIT.
           EXIT.

       NOTAPPLIC-ATOMSERVICE SECTION.

           MOVE 'NotAppl '                            TO PP40-47.
           PERFORM WRITE-OUTPUT.

       NOTAPPLIC-ATOMSERVICE-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** PIPELINE-STATISTICS                                       ***
      ***                                                           ***
      *****************************************************************
       PIPELINE-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO PIPELINES-PAGENO.

      *****************************************************************
      *** Print the Pipelines headings.                             ***
      *****************************************************************
           MOVE '-'         TO PRINT-CONTROL.
           MOVE 'PIPELINEs' TO PP1-9.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-9.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      ***   Print the Pipeline statistics.                          ***
      *****************************************************************

           EXEC CICS INQUIRE PIPELINE START
           END-EXEC.

           EXEC CICS INQUIRE PIPELINE(PIPELINE-NAME) NEXT
               CONFIGFILE(PIPELINE-CONFIG-FILE)
               MODE(PIPELINE-MODE)
               SHELF(PIPELINE-SHELF-DIRECTORY)
               WSDIR(PIPELINE-WSDIR-DIRECTORY)
               ENABLESTATUS(PIPELINE-ENABLE-STATUS)
               MSGFORMAT(PIPELINE-MSGFORMAT)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

               IF LINE-COUNT > (LINES-PER-PAGE - 24) THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               EXEC CICS EXTRACT STATISTICS PIPELINE
                   RESID(PIPELINE-NAME)
                   SET(ADDRESS OF DFHPIRDS)
                   NOHANDLE
               END-EXEC

               ADD 1    TO PIPELINE-TOTAL

               MOVE '0' TO PRINT-CONTROL
               MOVE 'PIPELINE Name. . . . . . . . . :' TO PP3-34
               MOVE PIPELINE-NAME                      TO PP37-44
               PERFORM WRITE-OUTPUT
               MOVE 'PIPELINE Mode. . . . . . . . . :' TO PP3-34
               EVALUATE PIPELINE-MODE
                   WHEN DFHVALUE(UNKNOWN)
                       MOVE 'Unknown  ' TO PP37-45
                   WHEN DFHVALUE(PROVIDER)
                       MOVE 'Provider ' TO PP37-45
                   WHEN DFHVALUE(REQUESTER)
                       MOVE 'Requester' TO PP37-45
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT
               MOVE 'PIPELINE Message Format. . . . :' TO PP3-34
               MOVE PIPELINE-MSGFORMAT                 TO PP37-44
               PERFORM WRITE-OUTPUT
               MOVE 'PIPELINE Enable Status . . . . :' TO PP3-34
               EVALUATE PIPELINE-ENABLE-STATUS
                   WHEN DFHVALUE(ENABLED)
                       MOVE 'Enabled ' TO PP37-44
                   WHEN DFHVALUE(DISABLED)
                       MOVE 'Disabled' TO PP37-44
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Configuration file . . . . . . :' TO PP3-34
               MOVE PIPELINE-CONFIG-FILE(1:85)         TO PP37-121
               PERFORM WRITE-OUTPUT
               IF PIPELINE-CONFIG-FILE(86:85) NOT = SPACES THEN
                   MOVE ':' TO PP34
                   MOVE PIPELINE-CONFIG-FILE(86:85)    TO PP37-121
                   PERFORM WRITE-OUTPUT
                   IF PIPELINE-CONFIG-FILE(171:85) NOT = SPACES THEN
                       MOVE ':' TO PP34
                       MOVE PIPELINE-CONFIG-FILE(171:85) TO PP37-121
                       PERFORM WRITE-OUTPUT
                   END-IF
               END-IF

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Shelf directory. . . . . . . . :' TO PP3-34
               MOVE PIPELINE-SHELF-DIRECTORY(1:85)     TO PP37-121
               PERFORM WRITE-OUTPUT
               IF PIPELINE-SHELF-DIRECTORY(86:85) NOT = SPACES THEN
                   MOVE ':' TO PP34
                   MOVE PIPELINE-SHELF-DIRECTORY(86:85) TO PP37-121
                   PERFORM WRITE-OUTPUT
                   IF PIPELINE-SHELF-DIRECTORY(171:85)
                                                    NOT = SPACES THEN
                       MOVE ':' TO PP34
                       MOVE PIPELINE-SHELF-DIRECTORY(171:85)
                                                       TO PP37-121
                       PERFORM WRITE-OUTPUT
                   END-IF
               END-IF

               MOVE '0' TO PRINT-CONTROL
               MOVE 'WSDIR pickup directory . . . . :' TO PP3-34
               MOVE PIPELINE-WSDIR-DIRECTORY(1:85)     TO PP37-121
               PERFORM WRITE-OUTPUT
               IF PIPELINE-WSDIR-DIRECTORY(86:85) NOT = SPACES THEN
                   MOVE ':' TO PP34
                   MOVE PIPELINE-WSDIR-DIRECTORY(86:85) TO PP37-121
                   PERFORM WRITE-OUTPUT
                   IF PIPELINE-WSDIR-DIRECTORY(171:85)
                                                    NOT = SPACES THEN
                       MOVE ':' TO PP34
                       MOVE PIPELINE-WSDIR-DIRECTORY(171:85)
                                                        TO PP37-121
                       PERFORM WRITE-OUTPUT
                   END-IF
               END-IF

               MOVE '0' TO PRINT-CONTROL
               MOVE 'PIPELINE use count . . . . . . :' TO PP3-34
               MOVE PIR-PIPELINE-USE-COUNT             TO PP37-46
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'JSON JAVA parser . . . . . . . :' TO PP3-34
               EVALUATE TRUE
                   WHEN PIR-JSON-JAVA-PARSER-NOTAPPLIC
                       MOVE 'Notapplic' TO PP37-45
                   WHEN PIR-JSON-JAVA-PARSER-YES
                       MOVE 'Yes      ' TO PP37-45
                   WHEN PIR-JSON-JAVA-PARSER-NO
                       MOVE 'No       ' TO PP37-45
               END-EVALUATE

               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE ALL '-' TO PP3-132
               PERFORM WRITE-OUTPUT

               EXEC CICS INQUIRE PIPELINE(PIPELINE-NAME) NEXT
                   CONFIGFILE(PIPELINE-CONFIG-FILE)
                   MODE(PIPELINE-MODE)
                   SHELF(PIPELINE-SHELF-DIRECTORY)
                   WSDIR(PIPELINE-WSDIR-DIRECTORY)
                   ENABLESTATUS(PIPELINE-ENABLE-STATUS)
                   MSGFORMAT(PIPELINE-MSGFORMAT)
                   RESP(INQUIRE-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE PIPELINE END
           END-EXEC.

       PIPELINE-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** DOCTEMPLATE-STATISTICS                                    ***
      ***                                                           ***
      *****************************************************************
       DOCTEMPLATE-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO DOCTEMPLATES-PAGENO.

      *****************************************************************
      *** Print the Document Template headings.                     ***
      *****************************************************************
           MOVE '-'                  TO PRINT-CONTROL.
           MOVE 'Document Templates' TO PP1-18.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-18.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      *** Get the Storage Manager DH Domain Subpool statistics.     ***
      *****************************************************************
           EXEC CICS EXTRACT STATISTICS SUBPOOL
               RESID(DHCACHE-SUBPOOL-NAME)
               SET(ADDRESS OF DFHSMDDS)
               NOHANDLE
           END-EXEC.

           IF EIBRESP NOT = DFHRESP(NORMAL) THEN
               GO TO SKIP-DOCTEMPLATE-DHCACHE
           END-IF.

           MOVE SMDSPN     TO DHCACHE-SUBPOOL-NAME.
           MOVE SMDDSANAME TO DHCACHE-SUBPOOL-DSANAME.
           MOVE SMDIFREE   TO DHCACHE-INITIAL-FREE.
           MOVE SMDGMREQ   TO DHCACHE-GETMAIN-REQUESTS.
           MOVE SMDFMREQ   TO DHCACHE-FREEMAIN-REQUESTS.
           MOVE SMDCELEM   TO DHCACHE-CURRENT-ELEMS.
           MOVE SMDCES     TO DHCACHE-CURRENT-ELEM-STG.
           MOVE SMDCPS     TO DHCACHE-CURRENT-PAGE-STG.
           MOVE SMDHWMPS   TO DHCACHE-PEAK-PAGE-STG.

       SKIP-DOCTEMPLATE-DHCACHE.

      *****************************************************************
      ***   Print the Document Template statistics.                 ***
      *****************************************************************

           EXEC CICS INQUIRE DOCTEMPLATE START
           END-EXEC.

           EXEC CICS INQUIRE DOCTEMPLATE(DOCTEMPLATE-NAME) NEXT
               APPENDCRLF(DOCTEMPLATE-APPEND-CRLF)
               DDNAME(DOCTEMPLATE-DDNAME)
               DSNAME(DOCTEMPLATE-DSNAME)
               EXITPGM(DOCTEMPLATE-EXIT-PROGRAM)
               FILE(DOCTEMPLATE-FILE-NAME)
               HFSFILE(DOCTEMPLATE-HFSFILE-NAME)
               MEMBER(DOCTEMPLATE-MEMBER-NAME)
               PROGRAM(DOCTEMPLATE-PROGRAM-NAME)
               TDQUEUE(DOCTEMPLATE-TDQUEUE-NAME)
               TEMPLATENAME(DOCTEMPLATE-TEMPLATE-NAME)
               TEMPLATETYPE(DOCTEMPLATE-TEMPLATE-TYPE)
               TSQUEUE(DOCTEMPLATE-TSQUEUE-NAME)
               TYPE(DOCTEMPLATE-TYPE)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)


               IF LINE-COUNT > (LINES-PER-PAGE - 20) THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               EXEC CICS EXTRACT STATISTICS DOCTEMPLATE
                   RESID(DOCTEMPLATE-NAME)
                   SET(ADDRESS OF DFHDHDDS)
                   NOHANDLE
               END-EXEC

               MOVE '0' TO PRINT-CONTROL

               ADD 1    TO DOCTEMPLATE-TOTAL

               MOVE 'DOCTEMPLATE name . . . . . . . :' TO PP3-34
               MOVE DOCTEMPLATE-NAME                   TO PP37-44
               PERFORM WRITE-OUTPUT

               MOVE 'Template name. . . . . . . . . :' TO PP3-34
               MOVE DOCTEMPLATE-TEMPLATE-NAME          TO PP37-84
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Append crlf. . . . . . . . . . :' TO PP3-34
               EVALUATE DOCTEMPLATE-APPEND-CRLF
                   WHEN DFHVALUE(APPEND)
                       MOVE 'Yes'                      TO PP37-39
                   WHEN DFHVALUE(NOAPPEND)
                       MOVE 'No '                      TO PP37-39
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               MOVE 'Template contents. . . . . . . :' TO PP3-34
               EVALUATE DOCTEMPLATE-TYPE
                   WHEN DFHVALUE(BINARY)
                       MOVE 'Binary'                   TO PP37-42
                   WHEN DFHVALUE(EBCDIC)
                       MOVE 'Ebcdic'                   TO PP37-42
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Template cache size. . . . . . :' TO PP3-34
               MOVE DHD-TEMPLATE-CACHE-SIZE            TO PP37-46
               PERFORM WRITE-OUTPUT

               MOVE 'Template type. . . . . . . . . :' TO PP3-34
               EVALUATE DOCTEMPLATE-TEMPLATE-TYPE
                   WHEN DFHVALUE(EXIT)
                       PERFORM EXIT-DOCTEMPLATE
                   WHEN DFHVALUE(FILE)
                       PERFORM FILE-DOCTEMPLATE
                   WHEN DFHVALUE(HFSFILE)
                       PERFORM HFSFILE-DOCTEMPLATE
                   WHEN DFHVALUE(PDSMEMBER)
                       PERFORM PDS-MEMBER-DOCTEMPLATE
                   WHEN DFHVALUE(PROGRAM)
                       PERFORM PROGRAM-DOCTEMPLATE
                   WHEN DFHVALUE(TDQ)
                       PERFORM TDQUEUE-DOCTEMPLATE
                   WHEN DFHVALUE(TSQ)
                       PERFORM TSQUEUE-DOCTEMPLATE
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Use count. . . . . . . . . . . :' TO PP3-34
               MOVE DHD-TEMPLATE-USE-COUNT             TO PP37-46
               PERFORM WRITE-OUTPUT
               MOVE 'Newcopy count. . . . . . . . . :' TO PP3-34
               MOVE DHD-TEMPLATE-NEWCOPIES             TO PP37-46
               PERFORM WRITE-OUTPUT
               MOVE 'Read count . . . . . . . . . . :' TO PP3-34
               MOVE DHD-TEMPLATE-READ-COUNT            TO PP37-46
               PERFORM WRITE-OUTPUT
               MOVE 'Cache copy used. . . . . . . . :' TO PP3-34
               MOVE DHD-TEMPLATE-CACHE-USED            TO PP37-46
               PERFORM WRITE-OUTPUT
               MOVE 'Cache copy deleted . . . . . . :' TO PP3-34
               MOVE DHD-TEMPLATE-CACHE-DELETED         TO PP37-46
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE ALL '-' TO PP3-132
               PERFORM WRITE-OUTPUT

               EXEC CICS INQUIRE DOCTEMPLATE(DOCTEMPLATE-NAME) NEXT
                   APPENDCRLF(DOCTEMPLATE-APPEND-CRLF)
                   DDNAME(DOCTEMPLATE-DDNAME)
                   DSNAME(DOCTEMPLATE-DSNAME)
                   EXITPGM(DOCTEMPLATE-EXIT-PROGRAM)
                   FILE(DOCTEMPLATE-FILE-NAME)
                   HFSFILE(DOCTEMPLATE-HFSFILE-NAME)
                   MEMBER(DOCTEMPLATE-MEMBER-NAME)
                   PROGRAM(DOCTEMPLATE-PROGRAM-NAME)
                   TDQUEUE(DOCTEMPLATE-TDQUEUE-NAME)
                   TEMPLATENAME(DOCTEMPLATE-TEMPLATE-NAME)
                   TEMPLATETYPE(DOCTEMPLATE-TEMPLATE-TYPE)
                   TSQUEUE(DOCTEMPLATE-TSQUEUE-NAME)
                   TYPE(DOCTEMPLATE-TYPE)
                   RESP(INQUIRE-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE DOCTEMPLATE END
           END-EXEC.

       DOCTEMPLATE-STATISTICS-EXIT.
           EXIT.
           EJECT
       EXIT-DOCTEMPLATE SECTION.

           MOVE 'Exit    '                         TO PP37-44.
           PERFORM WRITE-OUTPUT.
           MOVE 'Exit Program name. . . . . . . :' TO PP3-34.
           MOVE DOCTEMPLATE-EXIT-PROGRAM           TO PP37-44.

           EXEC CICS INQUIRE PROGRAM(DOCTEMPLATE-EXIT-PROGRAM)
               DATALOCATION(DOCTEMPLATE-PROGRAM-DATALOC)
               RESP(INQUIRE-PROGRAM-RESP)
           END-EXEC.

      *    IF INQUIRE-PROGRAM-RESP = DFHRESP(NORMAL) THEN
      *        PERFORM WRITE-OUTPUT
      *    END-IF.

       EXIT-DOCTEMPLATE-EXIT.
           EXIT.

       FILE-DOCTEMPLATE SECTION.

           MOVE 'File    '                         TO PP37-44.
           PERFORM WRITE-OUTPUT.
           MOVE 'File name. . . . . . . . . . . :' TO PP3-34.
           MOVE DOCTEMPLATE-FILE-NAME              TO PP37-44.
           PERFORM WRITE-OUTPUT.

           EXEC CICS INQUIRE FILE(DOCTEMPLATE-FILE-NAME)
               DSNAME(DOCTEMPLATE-FILE-DSNAME)
               RESP(INQUIRE-FILE-RESP)
           END-EXEC.

           IF INQUIRE-FILE-RESP = DFHRESP(NORMAL) THEN
               MOVE 'Dataset name . . . . . . . . . :' TO PP3-34
               MOVE DOCTEMPLATE-FILE-DSNAME            TO PP37-80
               PERFORM WRITE-OUTPUT
           END-IF.

       FILE-DOCTEMPLATE-EXIT.
           EXIT.

       PDS-MEMBER-DOCTEMPLATE SECTION.

           MOVE 'PDS     '                         TO PP37-44.
           PERFORM WRITE-OUTPUT.
           MOVE 'PDS Member name. . . . . . . . :' TO PP3-34.
           MOVE DOCTEMPLATE-MEMBER-NAME            TO PP37-44.
           PERFORM WRITE-OUTPUT.
           MOVE 'PDS Dataset name. .  . . . . . :' TO PP3-34.
           MOVE DOCTEMPLATE-DSNAME                 TO PP37-80.
           PERFORM WRITE-OUTPUT.

       PDS-MEMBER-DOCTEMPLATE-EXIT.
           EXIT.

       HFSFILE-DOCTEMPLATE SECTION.

           MOVE 'Hfsfile '              TO PP37-44.
           PERFORM WRITE-OUTPUT.

           MOVE 'HFS file name. . . . . . . . . :' TO PP3-34.
           MOVE DOCTEMPLATE-HFSFILE-NAME(1:85)     TO PP37-121.
           PERFORM WRITE-OUTPUT.
           IF DOCTEMPLATE-HFSFILE-NAME(86:85) NOT = SPACES THEN
               MOVE ':' TO PP34
               MOVE DOCTEMPLATE-HFSFILE-NAME(86:85) TO PP37-121
               PERFORM WRITE-OUTPUT
               IF DOCTEMPLATE-HFSFILE-NAME(171:85) NOT = SPACES THEN
                   MOVE ':' TO PP34
                   MOVE DOCTEMPLATE-HFSFILE-NAME(171:85) TO PP37-121
                   PERFORM WRITE-OUTPUT
               END-IF
           END-IF.

       HFSFILE-DOCTEMPLATE-EXIT.
           EXIT.

       PROGRAM-DOCTEMPLATE SECTION.

           MOVE 'Program '                         TO PP37-44.
           PERFORM WRITE-OUTPUT.
           MOVE 'Program name . . . . . . . . . :' TO PP3-34.
           MOVE DOCTEMPLATE-PROGRAM-NAME           TO PP37-44.
           PERFORM WRITE-OUTPUT.

           EXEC CICS INQUIRE PROGRAM(DOCTEMPLATE-PROGRAM-NAME)
               DATALOCATION(DOCTEMPLATE-PROGRAM-DATALOC)
               RESP(INQUIRE-PROGRAM-RESP)
           END-EXEC.

      *    IF INQUIRE-PROGRAM-RESP = DFHRESP(NORMAL) THEN
      *        PERFORM WRITE-OUTPUT
      *    END-IF.

       PROGRAM-DOCTEMPLATE-EXIT.
           EXIT.

       TDQUEUE-DOCTEMPLATE SECTION.

           MOVE 'Tdqueue '                         TO PP37-44.
           PERFORM WRITE-OUTPUT.
           MOVE 'Tdqueue name . . . . . . . . . :' TO PP3-34.
           MOVE DOCTEMPLATE-TDQUEUE-NAME           TO PP37-44.
           PERFORM WRITE-OUTPUT.

           EXEC CICS INQUIRE TDQUEUE(DOCTEMPLATE-TDQUEUE-NAME)
               DDNAME(DOCTEMPLATE-TDQUEUE-DDNAME)
               DSNAME(DOCTEMPLATE-TDQUEUE-DSNAME)
               TYPE(DOCTEMPLATE-TDQUEUE-TYPE)
               RESP(INQUIRE-TDQUEUE-RESP)
           END-EXEC.

      *    IF INQUIRE-TDQUEUE-RESP = DFHRESP(NORMAL) THEN
      *        PERFORM WRITE-OUTPUT
      *    END-IF.

       TDQUEUE-DOCTEMPLATE-EXIT.
           EXIT.

       TSQUEUE-DOCTEMPLATE SECTION.

           MOVE 'Tsqueue '                         TO PP37-44.
           PERFORM WRITE-OUTPUT.
           MOVE 'Tsqueue name . . . . . . . . . :' TO PP3-34.
           MOVE DOCTEMPLATE-TSQUEUE-NAME           TO PP37-52.

           EXEC CICS INQUIRE TSQNAME(DOCTEMPLATE-TSQUEUE-NAME)
               LOCATION(DOCTEMPLATE-TSQUEUE-LOC)
               RESP(INQUIRE-TSQNAME-RESP)
           END-EXEC.

      *    IF INQUIRE-TSQNAME-RESP = DFHRESP(NORMAL) THEN
      *        PERFORM WRITE-OUTPUT
      *    END-IF.

       TSQUEUE-DOCTEMPLATE-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** XMLTRANSFORM-STATISTICS                                   ***
      ***                                                           ***
      *****************************************************************
       XMLTRANSFORM-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO XMLTRANSFORMS-PAGENO.

      *****************************************************************
      *** Print the XMLTRANSFORM headings.                          ***
      *****************************************************************
           MOVE '-'             TO PRINT-CONTROL.
           MOVE 'XMLTRANSFORMs' TO PP1-13.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-13.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      ***   Print the XMLTRANSFORM statsistics.                     ***
      *****************************************************************

           EXEC CICS INQUIRE XMLTRANSFORM START
           END-EXEC.

           EXEC CICS INQUIRE XMLTRANSFORM(XMLTRANSFORM-NAME) NEXT
               ENABLESTATUS(XMLTRANSFORM-ENABLE-STATUS)
               VALIDATIONST(XMLTRANSFORM-VALIDATIONST)
               XSDBIND(XMLTRANSFORM-XSDBIND-FILE)
               XMLSCHEMA(XMLTRANSFORM-XMLSCHEMA-FILE)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

               IF LINE-COUNT > (LINES-PER-PAGE - 15) THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               EXEC CICS EXTRACT STATISTICS XMLTRANSFORM
                   RESID(XMLTRANSFORM-NAME)
                   SET(ADDRESS OF DFHMLRDS)
                   NOHANDLE
               END-EXEC

               ADD 1    TO XMLTRANSFORM-TOTAL

               MOVE '0' TO PRINT-CONTROL
               MOVE 'XMLTRANSFORM Name. . . . . . . :' TO PP3-34
               MOVE XMLTRANSFORM-NAME                  TO PP37-68
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'XMLTRANSFORM Enable Status . . :' TO PP3-34
               EVALUATE XMLTRANSFORM-ENABLE-STATUS
                   WHEN DFHVALUE(ENABLED)
                       MOVE 'Enabled   ' TO PP37-46X
                   WHEN DFHVALUE(DISABLED)
                       MOVE 'Disabled  ' TO PP37-46X
                   WHEN DFHVALUE(ENABLING)
                       MOVE 'Enabling  ' TO PP37-46X
                   WHEN DFHVALUE(DISABLING)
                       MOVE 'Disabling ' TO PP37-46X
                   WHEN DFHVALUE(DISCARDING)
                       MOVE 'Discarding' TO PP37-46X
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               MOVE 'XMLTRANSFORM XSDBIND File. . . :'  TO PP3-34
               MOVE XMLTRANSFORM-XSDBIND-FILE(1:85)     TO PP37-121
               PERFORM WRITE-OUTPUT
               IF XMLTRANSFORM-XSDBIND-FILE(86:85) NOT = SPACES THEN
                   MOVE ':' TO PP34
                   MOVE XMLTRANSFORM-XSDBIND-FILE(86:85) TO PP37-121
                   PERFORM WRITE-OUTPUT
                   IF XMLTRANSFORM-XSDBIND-FILE(171:85)
                                                    NOT = SPACES THEN
                       MOVE ':' TO PP34
                       MOVE XMLTRANSFORM-XSDBIND-FILE(171:85)
                                                        TO PP37-121
                       PERFORM WRITE-OUTPUT
                   END-IF
               END-IF

               IF XMLTRANSFORM-XMLSCHEMA-FILE(1:85) NOT = SPACES THEN
                 MOVE 'XMLTRANSFORM XML Schema File . :' TO PP3-34
                 MOVE XMLTRANSFORM-XMLSCHEMA-FILE(1:85)  TO PP37-121
                 PERFORM WRITE-OUTPUT
                 IF XMLTRANSFORM-XMLSCHEMA-FILE(86:85) NOT = SPACES
                   THEN MOVE ':' TO PP34
                   MOVE XMLTRANSFORM-XMLSCHEMA-FILE(86:85) TO PP37-121
                   PERFORM WRITE-OUTPUT
                   IF XMLTRANSFORM-XMLSCHEMA-FILE(171:85)
                                                    NOT = SPACES THEN
                     MOVE ':' TO PP34
                     MOVE XMLTRANSFORM-XMLSCHEMA-FILE(171:85)
                                                        TO PP37-121
                     PERFORM WRITE-OUTPUT
                   END-IF
                 END-IF

               END-IF

               MOVE 'XMLTRANSFORM Msg Validation. . :' TO PP3-34
               EVALUATE XMLTRANSFORM-VALIDATIONST
                   WHEN DFHVALUE(VALIDATION)
                       MOVE 'Yes'        TO PP37-39
                   WHEN DFHVALUE(NOVALIDATION)
                       MOVE 'No '        TO PP37-39
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE 'XMLTRANSFORM Use Count . . . . :' TO PP3-34
               MOVE MLR-XMLTRNFM-USE-COUNT             TO PP37-46
               PERFORM WRITE-OUTPUT

               MOVE '0' TO PRINT-CONTROL
               MOVE ALL '-' TO PP3-132
               PERFORM WRITE-OUTPUT

               EXEC CICS INQUIRE XMLTRANSFORM(XMLTRANSFORM-NAME) NEXT
                   ENABLESTATUS(XMLTRANSFORM-ENABLE-STATUS)
                   VALIDATIONST(XMLTRANSFORM-VALIDATIONST)
                   XSDBIND(XMLTRANSFORM-XSDBIND-FILE)
                   XMLSCHEMA(XMLTRANSFORM-XMLSCHEMA-FILE)
                   RESP(INQUIRE-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE XMLTRANSFORM END
           END-EXEC.

       XMLTRANSFORM-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** TOTALS-INITIALISE                                         ***
      *****************************************************************
       TOTALS-INITIALISE SECTION.

           MOVE 0 TO ATOMSERVICE-TOTAL BUNDLE-TOTAL
                     HOST-TOTAL
                     URIMAP-TOTAL PIPELINE-TOTAL
                     WEBSERVICE-TOTAL DOCTEMPLATE-TOTAL
                     XMLTRANSFORM-TOTAL.

       TOTALS-INITIALISE-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** PAGE-HEADINGS                                             ***
      ***   Increment page                                          ***
      ***   Write new heading line to spool                         ***
      *****************************************************************
       PAGE-HEADINGS SECTION.

           ADD 1       TO PAGENO.
           MOVE PAGENO TO PAGE-NUMBER.
           MOVE 1      TO LINE-COUNT.

           MOVE '1'          TO PRINT-CONTROL.
           MOVE HEADING-LINE TO PP1-132.
           PERFORM WRITE-OUTPUT.
           MOVE '+'          TO PRINT-CONTROL.
           MOVE ALL '_'      TO PP1-132.
           PERFORM WRITE-OUTPUT.

           MOVE 'Y' TO SUB-HEADINGS.

       PAGE-HEADINGS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** WRITE-OUTPUT                                              ***
      ***   Write current print line to report output destination.  ***
      *****************************************************************
       WRITE-OUTPUT SECTION.

           PERFORM WRITE-SPOOL.

       WRITE-OUTPUT-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** WRITE-SPOOL                                               ***
      *** Write current print line to spool                         ***
      *** Increment line-count                                      ***
      *** IF CICS exceptional condition is raised THEN              ***
      ***    call error routine                                     ***
      *****************************************************************
       WRITE-SPOOL SECTION.

           EXEC CICS SPOOLWRITE TOKEN(OUTPUT-TOKEN)
               FROM(PRINT-LINE) FLENGTH(LINE-LENGTH)
               LINE
               NOHANDLE
           END-EXEC.

           EVALUATE PRINT-CONTROL
               WHEN ' '
                   ADD 1 TO LINE-COUNT
               WHEN '0'
                   ADD 2 TO LINE-COUNT
               WHEN '-'
                   ADD 3 TO LINE-COUNT
               WHEN '+'
                   ADD 0 TO LINE-COUNT
               WHEN '1'
                   MOVE 1 TO LINE-COUNT
           END-EVALUATE.

      *                                       Check return code
           IF EIBRESP NOT = DFHRESP(NORMAL) THEN
               MOVE 'SPOOLWRITE' TO CICS-COMMAND
               PERFORM ERROR-ROUTINE
           ELSE
               MOVE ' '    TO PRINT-CONTROL
               MOVE SPACES TO PP1-132
           END-IF.

       WRITE-SPOOL-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  ERROR ROUTINE                                            ***
      ***  Error routine produces appropriate responses to CICS     ***
      ***  spool commands. To ensure this the resp and resp2 values ***
      ***  are returned to the main program DFH0STAT for appropriate***
      ***  outputing of the messages.                               ***
      *****************************************************************
       ERROR-ROUTINE SECTION.

      *  If an error occurs which is not due to the users input or if
      *  the program is not being run from a terminal then a general
      *  message is sent to the screen/console and the RESP and RESP2
      *  fields are given along with the spool command which failed.
           MOVE 'Y'      TO ERROR-FOUND.
           MOVE EIBRESP  TO STCM-RESP.
           MOVE EIBRESP2 TO STCM-RESP2.

      *  Whatever happens ensure we return to DFH0STAT quickly.
           EXEC CICS RETURN
           END-EXEC.

       ERROR-ROUTINE-EXIT.
           EXIT.
