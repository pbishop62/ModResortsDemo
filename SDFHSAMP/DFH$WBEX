***********************************************************************
*                                                                     *
*   MODULE NAME = DFH$WBEX                                            *
*                                                                     *
* DESCRIPTIVE NAME = CICS TS  ....                                    *
*        Sample user exit program for EXEC CICS WEB OPEN (XBAOPEN)    *
*                                                                     *
*                                                                     *
*                                                                     *
*        Licensed Materials - Property of IBM                         *
*                                                                     *
*        "Restricted Materials of IBM"                                *
*                                                                     *
*        5655-Y04                                                     *
*                                                                     *
*        (C) Copyright IBM Corp. 2004, 2014"                          *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
* STATUS = 7.2.0                                                      *
*                                                                     *
* FUNCTION =                                                          *
*        This is a sample user exit program to be invoked at the      *
*        XWBOPEN global user exit point when processing an EXEC       *
*        CICS WEB OPEN command.                                       *
*                                                                     *
*        If the host name addressed by uephost contains a             *
*        host name that matches the barred host name this exit        *
*        returns with return code uercbarr set.                       *
*                                                                     *
*        If the host name addressed by uephost contains a host        *
*        name that starts with string 'WWW' and the exits             *
*        Global Work Area contains a proxy value it's                 *
*        address is stored in the address field pointed to            *
*        by uepproxy, the halfword value that contains the            *
*        length of the proxy is stored in the field pointed           *
*        to by uepproxyl and this exit returns with return            *
*        code usrcprox set.                                           *
*                                                                     *
*                                                                     *
* NOTES :                                                             *
*    DEPENDENCIES = S/390                                             *
*    RESTRICTIONS = None                                              *
*    MODULE TYPE = Executable                                         *
*    PROCESSOR = Assembler                                            *
*    ATTRIBUTES = Read only, Serially Reusable                        *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
* ENTRY POINT = DFH$WBEX                                              *
*     PURPOSE = All Functions                                         *
*     LINKAGE = Invoked from the XWBOPEN user exit call.              *
*     INPUT = N/A                                                     *
*     OUTPUT = N/A                                                    *
*     EXIT-NORMAL = RETURN (14,12) with return code set.              *
*     EXIT-ERROR = None                                               *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
* EXTERNAL REFERENCES = None                                          *
*     ROUTINES = None                                                 *
*     DATA AREAS = N/A                                                *
*     CONTROL BLOCKS =                                                *
*         DFH$WBGA - Global Work Area mapping for this sample user    *
*                    exit program, DFH$WBEX.                          *
*     GLOBAL VARIABLES = None                                         *
* TABLES = None                                                       *
* MACROS = DFHUEXIT TYPE=EP,ID=(XWBOPEN)                              *
*         Generates the User Exit Paramter List for the XWBOPEN       *
*         global user exit point.                                     *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
* CHANGE ACTIVITY :                                                   *
*        $MOD(DFH$WBEX),COMP(CICSWEB),PROD(CICS TS ):                 *
*                                                                     *
*     PN= REASON REL YYMMDD HDXIII : REMARKS                          *
*    $L0= 781    640 040427 HDFXAB  : Sample exit                     *
*    $L1= 830    660 081006 HDISGPL: User Exits IPv6 awareness        *
*    $P1= D11268 640 040929 HDFXAB : Remove uppercasing code          *
*    $P2= D23301 660 081223 HDISGPL: Lowercase hostname constants     *
*         D82296 690 131212 HDHCPMC: length fix                       *
*                                                                     *
***********************************************************************
         SPACE
R0       EQU   0                   NOT USED
R1       EQU   1                   INITIAL USER EXIT PARAMETER LIST/
*                                  WORK REGISTER
R2       EQU   2                   WORK REGISTER
R3       EQU   3                   XWBOPEN GLOBAL WORK AREA ADDRESS
R4       EQU   4                   USER EXIT PARAMETER LIST
R5       EQU   5                   WORK REGISTER
R6       EQU   6                   NOT USED
R7       EQU   7                   NOT USED
R8       EQU   8                   NOT USED
R9       EQU   9                   NOT USED
R10      EQU   10                  NOT USED
R11      EQU   11                  NOT USED
R12      EQU   12                  PROGRAM BASE
R13      EQU   13                  SAVE AREA
R14      EQU   14                  RETURN ADDRESS
R15      EQU   15                  INITIAL PROGRAM BASE
         EJECT
         DFHUEXIT TYPE=EP,ID=(XWBOPEN)
         EJECT
         COPY  DFH$WBGA            XWBOPEN GWA DSECT
         EJECT
DFH$WBEX CSECT
DFH$WBEX AMODE 31
DFH$WBEX RMODE ANY
         SAVE  (14,12)             SAVE REGS
         LR    R12,R15             SET-UP BASE REGISTER
         USING DFH$WBEX,R12        ADDRESSABILITY
         LR    R4,R1               GET UEP PARAMETER LIST
         USING DFHUEPAR,R4         ADDRESSABILITY
         SPACE
***********************************************************************
* Check if the host matches the barred host.                          *
***********************************************************************
         SPACE
         L     R1,UEPHOSTT         Load address of host type field @L1A
         CLI   0(R1),UEPHSTNM      ?host name                      @L1A
         BNE   PROCPRX             N: must be an ip address        @L1A
         L     R1,UEPHOSTL         ADDRESS OF HOST LENGTH
         LH    R1,0(R1)            HOST LENGTH
         CL    R1,=AL4(BARHOSTL)   SAME LENGTH AS BARRED HOST?
         BNE   PROCPRX             NO, PROCESS PROXY
         L     R2,UEPHOST          ADDRESS OF HOST
         CLC   0(L'BARHOST,R2),BARHOST  BARRED HOST NAME
         BE    BARRET              YES, SO BARR HOST
         SPACE
***********************************************************************
* Pick up the address of the Global Work Area (GWA) for this exit     *
* program.                                                            *
***********************************************************************
         SPACE
PROCPRX  DS    0H
         ICM   R3,B'1111',UEPGAA   GWA EXISTS?
         BZ    NORRET              NO, NORMAL RETURN FROM EXIT
         USING DFH$WBGA,R3         ADDRESSABILITY
         EJECT
***********************************************************************
* If the Global Work Area contains a proxy then check if the host     *
* name begins with WWW.                                               *
***********************************************************************
         SPACE
         CLC   PROXYLEN,=H'0'      PROXY LENGTH ZERO?
         BE    NORRET              YES, NORMAL RETURN FROM EXIT
         L     R1,UEPHOSTT         Load address of host type field @L1A
         CLI   0(R1),UEPHSTNM      ?host name                      @L1A
         BNE   NORRET              N: must be an ip address        @L1A
         L     R1,UEPHOSTL         ADDRESS OF HOST LENGTH      @D82296A
         LH    R1,0(R1)            HOST LENGTH                 @D82296A
         CL    R1,=AL4(WWWHOSTL)   HOST LENGTH < WWW LENGTH?
         BL    NORRET              YES, NORMAL RETURN FROM EXIT
         L     R2,UEPHOST          ADDRESS OF HOST
         CLC   0(L'WWWHOST,R2),WWWHOST   WWW HOST?
         BNE   NORRET                    NO, NORMAL RETURN FROM EXIT
***********************************************************************
* The host name begins with WWW so store the halfword length          *
* of the proxy in the field pointed to by uepproxyl, store the        *
* address of the proxy in the address field pointed to by             *
* uepproxy and return with return code uercprox set.                  *
***********************************************************************
         L     R1,UEPPROXYL        ADDRESS OF PROXY LENGTH HALFWORD
         LH    R2,PROXYLEN         ACTUAL PROXY LENGTH
         STH   R2,0(R1)            SAVE ACTUAL PROXY LENGTH IN HALFWORD
         L     R1,UEPPROXY         ADDRESS OF ADDRESS FIELD THAT
*                                  WILL CONTAIN ADDRESS OF PROXY
         LA    R2,PROXYNAM         ADDRESS OF PROXY
         ST    R2,0(R1)            SAVE PROXY ADDRESS IN ADDRESS
*                                  FIELD
         DROP  R3                  DROP R3
         L     R13,UEPEPSA         ADDRESS OF EXIT SAVE AREA
         RETURN (14,12),RC=UERCPROX    RESTORE REGS AND RETURN
*                                      WITH RETURN CODE UERCPROX
*                                      SET
         SPACE
***********************************************************************
* Return with return code uercnorm set.                               *
***********************************************************************
         SPACE
NORRET   DS    0H                  RETURN TO THE CALLER
         L     R13,UEPEPSA         ADDRESS OF EXIT SAVE AREA
         RETURN (14,12),RC=UERCNORM    RESTORE REGS AND RETURN
         SPACE
***********************************************************************
* Return with return code uercbarr set.                               *
***********************************************************************
         SPACE
BARRET   DS    0H                  RETURN TO THE CALLER
         L     R13,UEPEPSA         ADDRESS OF EXIT SAVE AREA
         RETURN (14,12),RC=UERCBARR    RESTORE REGS AND RETURN
*                                      WITH RETURN CODE UERCBARR
*                                      SET
         EJECT
***********************************************************************
* Constants.                                                          *
***********************************************************************
         SPACE
BARHOST  DC    C'barred.host'
BARHOSTL EQU   *-BARHOST
WWWHOST  DC    C'www'
WWWHOSTL EQU   *-WWWHOST
WWWBLANK DC    C'   '
         LTORG
         SPACE
         END   DFH$WBEX
