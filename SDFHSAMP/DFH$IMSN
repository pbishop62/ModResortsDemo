         TITLE 'DFH$IMSN - INTERCOMMUNICATION SAMPLE - CICS TO IMS CONV*
               ERSATION'
***********************************************************************
*                                                                     *
* MODULE NAME = DFH$IMSN                                              *
*                                                                     *
* DESCRIPTIVE NAME = CICS to IMS Conversation Sample                  *
*                                                                     *
*                                                                     *
*                                                                     *
*     Licensed Materials - Property of IBM                            *
*                                                                     *
*     "Restricted Materials of IBM"                                   *
*                                                                     *
*     5655-Y04                                                        *
*                                                                     *
*     (C) Copyright IBM Corp. 1985, 1991"                             *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
* STATUS = 7.2.0                                                      *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
* CHANGE ACTIVITY :                                                   *
* $SEG(DFH$IMSN),COMP(SAMPLES),PROD(CICS TS ):                        *
*                                                                     *
*     PN= REASON REL YYMMDD HDXIII : REMARKS                          *
*    $P0= Mnnnnn 170 850808 HDZZUNK: Created.                         *
*    $P1= M90474 330 910807 HDBWSH : Prologue fixed.                  *
*                                                                     *
***********************************************************************
DFHEISTG DSECT
*
*        STORAGE AREA FOR EIB SESSION AND STATUS FLAGS
*
XDFEIFLG DS    0CL7
*
DFHSYNC  DS    C                   IF SET, SYNCPOINT MUST
*                                  BE EXECUTED
DFHFREE  DS    C                   IF SET, TERMINAL / LU
*                                  MUST BE FREED
DFHRECV  DS    C                   IF SET, RECEIVE MUST
*                                  BE EXECUTED
DFHSEND  DS    C                   RESERVED
*
DFHATT   DS    C                   IF SET, ATTACH HEADER
*                                  DATA EXISTS AND MAY BE
*                                  ACCESSED USING EXTRACT
DFHEOC   DS    C                   IF SET, END-OF-CHAIN
*                                  WAS RECEIVED WITH DATA
DFHFMH   DS    C                   IF SET, DATA PASSED TO
*                                  APPL'N CONTAINS FMH(S)
         COPY  DFH$IGS
         COPY  DFHBMSCA            BMS ATTRIBUTES
         COPY  DFHAID              ATTENTION IDS
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
MESSAGE  DS    CL32
REMSYS   DS    CL8
ATCHSESS DS    CL4
INLEN    DS    H
RESP     DS    F
         EJECT
DFH$IMSN CSECT
MAPFAIL  XC    MAPAI(MAPAE-MAPAI),MAPAI  CLEAR MAP
SENDMAP  DS    0H
         EXEC CICS SEND MAP('MAPA') MAPSET('DFH$IGS') ERASE WAIT       *
               RESP(RESP)
         CLC   RESP,DFHRESP(NORMAL)    CHECK FOR NORMAL RESPONSE
         BNE   ERROR1
         XC    DATAI,DATAI         RE-CLEAR THE DATA AREA
         EXEC CICS RECEIVE MAP('MAPA') MAPSET('DFH$IGS') RESP(RESP)
         CLI   EIBAID,DFHCLEAR         WAS CLEAR KEY PRESSED ?
         BE    EXIT                    YES, EXIT
         CLC   RESP,DFHRESP(NORMAL)    HAS MAP BEEN RECEIVED OK?
         BE    RESPOK1                 ... YES, GO RESPOK1
         CLC   RESP,DFHRESP(MAPFAIL)
         BE    MAPFAIL                 ... NO, GO MAPFAIL
         CLC   RESP,DFHRESP(EOC)       ... EOC OK
         BNE   ERROR1
RESPOK1  DS    0H
         CLI   SYSIDI,0            REMOTE SYSTEM NAME GIVEN ?
         BE    REMAP               ..NO, SEND MSG TO OPERATOR
         EXEC CICS ALLOCATE SYSID(SYSIDI) RESP(RESP)
         CLC   RESP,DFHRESP(SYSIDERR)  IS SYSTEM ID VALID?
         BE    SYSERR                  ... NO, GO TO SYSERR
         CLC   RESP,DFHRESP(NORMAL)
         BNE   ERROR1
         MVC   ATCHSESS,EIBRSRCE
         B     BUILD
REMAP    DS    0H
         MVC   ERROIO(L'SYSMSG),SYSMSG SET UP PROMPTING MSG
         MVI   ERROIA,DFHBMBRY     HIGHLIGHT MESSAGE
         B     SENDMAP             AND SEND IT.
BUILD    DS    0H
         EXEC CICS BUILD ATTACH ATTACHID('TIMS')                       *
               RESOURCE(TRANI) IUTYPE(=H'1') .
         EXEC CICS SEND SESSION(ATCHSESS) ATTACHID('TIMS') FROM(DATAI) *
               LENGTH(DATAL) INVITE RESP(RESP)
         CLC   RESP,DFHRESP(NORMAL)
         BNE   ERROR1
RECV     DS    0H
         EXEC CICS RECEIVE SESSION(ATCHSESS)                           *
               SET(R9) LENGTH(INLEN) RESP(RESP)
         CLC   RESP,DFHRESP(NORMAL)    HAS MAP BEEN RECEIVED OK?
         BE    DATASENT                ... YES, GO DATASENT
         CLC   RESP,DFHRESP(EOC)       ... EOC OK
         BNE   ERROR1
DATASENT DS    0H
         MVC   XDFEIFLG,EIBSYNC        SAVE EIB VALUES
*
         LA    R4,MAPBI                START OF OUTPUT MAP
         LR    R6,R4
         LA    R5,MAPBE-MAPBI          LENGTH OF MAP
         XR    R7,R7
         MVCL  R4,R6                   CLEAN UP THE MAP
*
         CLC   INLEN,=H'0'             IF NULL RU SENT THEN
         BE    TESTSYNC                NOTHING TO SEND TO TRM
*
         LA    R7,LINE1O               ADDRESS 1ST OUTPUT LINE
         LH    R4,INLEN                LENGTH OF RECEIVED DATA
LRECL    DS    0H
         LH    R5,0(R9)                LOGICAL RECORD LENGTH
         SR    R4,R5                   REDUCE BLOCK LENGTH
         SH    R5,=H'3'                PREPARE FOR EX INSTR.
         EX    R5,SETLINE              MOVE LREC TO MAP
         LTR   R4,R4                   END OF BLOCK REACHED ?
         BZ    SENDMAPB                ..YES, SEND THE MAP
         AH    R9,0(R9)                ADVANCE TO NEXT RECORD
         LA    R7,LINE2O-LINE1O(R7)    ADDR NEXT OUTPUT LINE
         B     LRECL                   GO TO MOVE NEXT REC
SENDMAPB DS    0H
         EXEC CICS SEND MAP('MAPB') MAPSET('DFH$IGS') ERASE WAIT       *
               CURSOR(=H'1840') RESP(RESP)
         CLC   RESP,DFHRESP(NORMAL)
         BNE   ERROR1
*
TESTSYNC DS    0H
         CLI   DFHSYNC,X'FF'
         BNE   TESTFREE
         EXEC CICS SYNCPOINT RESP(RESP)
         CLC   RESP,DFHRESP(NORMAL)
         BNE   ERROR1
TESTFREE DS    0H
         CLI   DFHFREE,X'FF'
         BE    EXIT
         EXEC CICS RECEIVE SET(R8) LENGTH(INLEN) RESP(RESP)
         CLI   EIBAID,DFHCLEAR         WAS CLEAR KEY PRESSED ?
         BE    EXIT                    YES, EXIT
         CLC   RESP,DFHRESP(NORMAL)    HAS MAP BEEN RECEIVED OK?
         BE    RESPOK2                 ... YES, GO RESPOK2
         CLC   RESP,DFHRESP(EOC)       ... EOC OK
         BNE   ERROR1
RESPOK2  DS    0H
         CLI   DFHRECV,X'FF'
         BE    RECV
         EXEC CICS CONVERSE FROMLENGTH(INLEN) SESSION(ATCHSESS)        *
               SET(R9) TOLENGTH(INLEN) FROM(0(R8))
         CLC   RESP,DFHRESP(NORMAL)    HAS MAP BEEN RECEIVED OK?
         BE    DATASENT                ... YES, GO DATASENT
         CLC   RESP,DFHRESP(EOC)       ... EOC OK
         BE    DATASENT
         B     ERROR1                  ... MUST BE ERROR, GO ERROR1
*
SETLINE  MVC   0(0,R7),2(R9)         MOVE INPUT RECORD TO MAP
SYSMSG   DC    C'MUST SPECIFY REMOTE SYSID'
*
ERROR1   DS    0H
         MVC   MESSAGE,ERRMSG
         B     EXPLAIN
*
ERRMSG   DC    CL32'ERROR - TRANSACTION TERMINATED'
SYSERR   DS    0H
         CLI   EIBRCODE+1,12
         BE    UNKNOWN
         CLI   EIBRCODE+1,8
         BE    OUTSERV
         CLI   EIBRCODE+1,4
         BE    NOTCTSE
*
UNKNOWN  DS    0H
         MVC   MESSAGE,UNKMSG
         MVC   MESSAGE+12(4),SYSIDI
         B     EXPLAIN
UNKMSG   DC    CL32'SYSTEM NAME      IS NOT KNOWN   '
*
OUTSERV  DS    0H
         MVC   MESSAGE,OUTSVMSG
         MVC   MESSAGE+8(4),SYSIDI
         B     EXPLAIN
OUTSVMSG DC    CL32'LINK TO      IS OUT OF SERVICE'
*
NOTCTSE  DS    0H
         MVC   MESSAGE,TCTMSG
         MVC   MESSAGE(4),SYSIDI
         B     EXPLAIN
TCTMSG   DC    CL32'     IS NOT A SYSTEM NAME'
*
NOLINK   DS    0H
         MVC   MESSAGE,LINKMSG
         MVC   MESSAGE+28(4),SYSIDI
         B     EXPLAIN
LINKMSG  DC    CL32'UNABLE TO ESTABLISH LINK TO     '
*
EXPLAIN  DS    0H
         EXEC CICS SEND FROM(MESSAGE) LENGTH(=H'32')                   *
               ERASE WAIT RESP(RESP)
EXIT     DS    0H
         EXEC CICS SEND CONTROL FREEKB RESP(RESP)
         EXEC  CICS RETURN
         END
