      CBL CICS('COBOL3') APOST
PROCESS MAP,XREF
      *****************************************************************
      *                                                               *
      * MODULE NAME = DFH0CNA3                                        *
      *                                                               *
      * DESCRIPTIVE NAME = CICS TS  (NACT) NACT SAMPLE                *
      *                                                               *
      *                                                               *
      *                                                               *
      *     Licensed Materials - Property of IBM                      *
      *                                                               *
      *     "Restricted Materials of IBM"                             *
      *                                                               *
      *     5655-Y04                                                  *
      *                                                               *
      *     (C) Copyright IBM Corp. 2001"                             *
      *                                                               *
      *                                                               *
      *                                                               *
      *                                                               *
      * STATUS = 7.2.0                                                *
      *                                                               *
      *****************************************************************
      *    DESCRIPTION                                                *
      *                                                               *
      * This program provides the convenience print function as part  *
      * of the CICS Application Design and Programming book sample    *
      * application. It employs Basic Mapping Support (BMS) services  *
      * to perform its function.                                      *
      *                                                               *
      *                                                               *
      * CHANGE ACTIVITY :                                             *
      *                                                               *
      * $MOD(DFH0CNA3),COMP(SAMPLES),PROD(CICS TS ):                  *
      *                                                               *
      * PN= REASON REL YYMMDD HDXXIII : REMARKS                       *
      * $P0= D02652 620 011003 HDFXAB  : Install Nact                 *
      *                                                               *
      *****************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID. DFH0CNA3.
       AUTHOR.       A PROGRAMMER.
       INSTALLATION. IBM HURSLEY.
       DATE-WRITTEN. AUGUST 1999.
       DATE-COMPILED.
      *
      ***************************************************************
      *     FILES
      *
      ***************************************************************
      *     CICS RESOURCES
      *
      ***************************************************************
      *     UTILITIES
      *
      ***************************************************************
      *     COPYBOOKS
      *
      *     DFH0CNAC - Common working storage.
      *     DFH0CNAR - Working storage layout of the data passed to
      *                the Error Handler program.
      *     DFH0CNAM - The mapsets.
      *
      ***************************************************************
      *
       ENVIRONMENT DIVISION.
       DATA DIVISION.
      *
       WORKING-STORAGE SECTION.
      *
      *    Store eye catcher details to aid dump reading
      *
       01  WS-DEBUG-DETAILS.
           05  FILLER                        PIC X(32)
                 VALUE 'DFH0CNA3-----WORKING STORAGE  '.
           05  DEBUG-EYE.
               10  DEBUG-TRANID              PIC X(4) VALUE SPACES.
               10  DEBUG-TERMID              PIC X(4) VALUE SPACES.
               10  DEBUG-TASKNO              PIC 9(7) VALUE ZERO.
           05  FILLER                        PIC X    VALUE SPACE.
           05  DEBUG-COMMAREA-ADDR           USAGE IS POINTER.
      *
       01  FILLER.
      *
      * This field is used for interfacing with the
      * ABEND/Error Handler program in the suite.
      *
           05  WS-PROGRAM-NAME               PIC X(8) VALUE SPACES.
           05  ABEND-PROGRAM.
               10  WS-ABEND-PROGRAM-PREFIX   PIC X(7) VALUE SPACES.
               10  FILLER                    PIC X(1) VALUE '4'.
      *
      * This field is used to control the input since there may
      * be more than one set of data to be printed queued up.
      *
           05  END-SWITCH                    PIC X VALUE 'Y'.
               88  NO-MORE                   VALUE 'N'.
      *
      * Store of EIBRESP and EIBRESP2 set up in each EXEC CICS statement
      *
           05  RESPONSE                      PIC S9(8) COMP-4 VALUE 0.
           05  REASON-CODE                   PIC S9(8) COMP-4 VALUE 0.
      *
      * The EIBFN is defined as character but the error interface
      * expects a numeric value.
      *
           05  WORK-FN                       PIC 9(4) COMP VALUE ZERO.
           05  WORK-FN-X REDEFINES WORK-FN   PIC X(2).
      *
      * Various values which you might wish to modify are placed in one
      * copy book in order to make those sorts of changes more easily.
      *
       01  FILLER.
           05  FILLER                        PIC X(36) VALUE
               '********  DFH0CNAC COPYBOOK  *******'.
           COPY DFH0CNAC.
      *
      * The interface to the error handler program is described
      * in a copy book in order to ensure consistency.
      *
       01  FILLER.
           05  FILLER                        PIC X(36) VALUE
              '********  DFH0CNAR COPYBOOK  *******'.
           COPY DFH0CNAR.
      *
      * The generated symbolic map must be included in the program.
      *
       01  FILLER.
           05  FILLER                        PIC X(36) VALUE
               '*******  DFH0CNAM COPYBOOK  ********'.
           COPY DFH0CNAM.
      *
       EJECT.
      *
       PROCEDURE DIVISION.
      *
      *
       NACT03-MAIN SECTION.
      *
      * First we establish the ABEND handler in case unexpected
      * errors arise, such as program interrupts. The technique
      * used here assumes a naming convention where the variable
      * part of the program names is in the 8th position.
      * This allows for changes to the names in case the provided
      * names conflict with existing applications.
      *
       NACT03-010.
           EXEC CICS ASSIGN
                     PROGRAM(WS-PROGRAM-NAME)
                     NOHANDLE
           END-EXEC.
      *
           MOVE WS-PROGRAM-NAME TO WS-ABEND-PROGRAM-PREFIX.
      *
           EXEC CICS HANDLE ABEND
                     PROGRAM(ABEND-PROGRAM)
                     RESP(RESPONSE)
                     RESP2(REASON-CODE)
           END-EXEC.
      *
           IF  RESPONSE NOT = DFHRESP(NORMAL)
               EXEC CICS ABEND
                         ABCODE(WS-LITS-ABEND-ERROR-ABEND)
               END-EXEC
           END-IF.
      *
       NACT03-020.
      *
      *  Set up values in the eye-catcher
      *
           MOVE EIBTRNID TO DEBUG-TRANID.
           MOVE EIBTRMID TO DEBUG-TERMID.
           MOVE EIBTASKN TO DEBUG-TASKNO.
      *
      *  Set up the commarea address
      *
           SET DEBUG-COMMAREA-ADDR TO ADDRESS OF DFHCOMMAREA.
      *
       NACT03-030.
      *
      * The main logic of the program is is performed as a loop since
      * there may be more than one set of data to be printed queued up.
      * Since the task on behalf of which this program runs must be
      * STARTed, the data is obtained via a CICS RETRIEVE command.
      *
           PERFORM UNTIL NO-MORE
               EXEC CICS RETRIEVE
                         INTO(ACCTDTLO)
                         RESP(RESPONSE)
                         RESP2(REASON-CODE)
               END-EXEC
               EVALUATE RESPONSE
      *
      * If data was returned, then it is to be in the BMS mapset form
      * so we simply need to send it to the terminal (printer) with
      * which this task is associated. The PRINT option is included
      * to ensure that printing is performed immediately rather
      * than simply placed into the buffer of the device.
      *
                   WHEN DFHRESP(NORMAL)
                       EXEC CICS SEND
                                 MAP('ACCTDTL')
                                 MAPSET(WS-LITS-MAPSET)
                                 PRINT
                                 ERASE
                                 NOHANDLE
                       END-EXEC
      *
      * If we have exhausted the data, then we set
      * the indicator to terminate the loop.
      *
                   WHEN DFHRESP(ENDDATA)
                       SET NO-MORE TO TRUE
      *
      * If any other condition arises, then a serious problem has
      * occurred, so the error handler is invoked.
      *
                   WHEN OTHER
                       SET WS-ERRH-CORRECT-VERSION TO TRUE
                       MOVE RESPONSE            TO WS-ERRH-ERROR
                       MOVE REASON-CODE         TO WS-ERRH-REASON
                       MOVE EIBFN               TO WORK-FN-X
                       MOVE WORK-FN             TO WS-ERRH-CICS-FUNCTION
                       MOVE WS-PROGRAM-NAME     TO WS-ERRH-PROGRAM
                       EXEC CICS XCTL
                                 PROGRAM(ABEND-PROGRAM)
                                 COMMAREA(WS-ERRH-ERROR-COMMAREA)
                                 NOHANDLE
                       END-EXEC
      *
      * The following will only be executed if the XCTL fails.
      * The primary reason that might happen is if the error
      * handling program has become unavailable for some reason.
      *
                       EXEC CICS ABEND
                                 ABCODE(WS-LITS-ABEND-ERROR-ABEND)
                       END-EXEC
               END-EVALUATE
           END-PERFORM.
      *
       NACT03-030.
      *
      * When all of the data is printed, we simply terminate the task.
      *
           EXEC CICS RETURN
           END-EXEC.
      *
       END-NACT03-MAIN.
           EXIT.
