      CBL CICS('COBOL3,SP') APOST
PROCESS MAP,XREF,TRUNC(BIN)
      *****************************************************************
      *                                                               *
      * MODULE NAME = DFH0STDB                                        *
      *                                                               *
      * DESCRIPTIVE NAME = Collect and Print Statistics Sample        *
      *                                                               *
      *                                                               *
      *                                                               *
      *     Licensed Materials - Property of IBM                      *
      *                                                               *
      *     "Restricted Materials of IBM"                             *
      *                                                               *
      *     5655-Y04                                                  *
      *                                                               *
      *     (C) Copyright IBM Corp. 2000, 2016"                       *
      *                                                               *
      *                                                               *
      *                                                               *
      *                                                               *
      * STATUS = 7.2.0                                                *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      * DESCRIPTION :                                                 *
      *                                                               *
      *     This set of programs illustrate ways of using the EXEC    *
      *     CICS INQUIRE, COLLECT and EXTRACT STATISTICS commands of  *
      *     CICS to produce information about a CICS system.          *
      *     The reports include a CICS and MVS storage analysis.      *
      *     Statistics obtained using these commands can be used to   *
      *     aid the specification of the DSA Size parameters.         *
      *     (See the CICS Performance Guide and CICS Data Areas       *
      *      manuals for more information, and the CICS System        *
      *      Programming Reference for use of commands)               *
      *                                                               *
      *     These programs can be invoked from the PLT at PLTSD or    *
      *     as a CICS transaction (conversationally).                 *
      *     The output is sent via the CICS JES SPOOL interface for   *
      *     which a number of default parameters can be changed by    *
      *     the user to specify the distribution of the report(s).    *
      *     These defaults are defined in the working-storage         *
      *     section of the DFH0STAT program under the 01 level        *
      *     "OUTPUT-DEFAULTS".                                        *
      *                                                               *
      *     Installation instructions:-                               *
      *     Require SIT parameters:-  SPOOL=YES                       *
      *     Required CICS definitions:-                               *
      *         Transactions:-  STAT -> DFH0STAT                      *
      *         Programs:-    DFH0STAT   IBM COBOL                    *
      *                       DFH0STDB       for OS/390 & VM          *
      *                       DFH0STEJ           2.1.1 or later       *
      *                       DFH0STEP       "   "   "                *
      *                       DFH0STGN       "   "   "                *
      *                       DFH0STLK       "   "   "                *
      *                       DFH0STPR       "   "   "                *
      *                       DFH0STSA       "   "   "                *
      *                       DFH0STSY       "   "   "                *
      *                       DFH0STTP       "   "   "                *
      *                       DFH0STTS       "   "   "                *
      *                       DFH0STWB       "   "   "                *
      *                       DFH$STAS   Assembler                    *
      *                       DFH$STCN   Assembler                    *
      *                       DFH$STTB   Assembler                    *
      *         Mapsets:-     DFH0STM                                 *
      *                       DFH0STS                                 *
      *                                                               *
      *     The CICS-supplied RDO group, DFH$STAT, contains all       *
      *     the necessary resource definitions for this sample.       *
      *                                                               *
      *     All programs are command level and run above the          *
      *     16Mb line. Note: that DFH0STAT, DFH0STDB, DFH0STEJ,       *
      *     DFH0STEP, DFH0STGN, DFH0STLK, DFH0STPR, DFH0STSA,         *
      *     DFH0STSY, DFH0STTP, DFH0STTS, and DFH0STWB must be        *
      *     translated with the Translator options "COBOL3 and SP".   *
      *                                                               *
      *   SPOOL errors.  If an EXEC CICS SPOOL .. command fails       *
      *   when the program is run as a transaction, an error message  *
      *   is displayed on the users screen and the transaction will   *
      *   continue.  If the program is not being run from a terminal  *
      *   then a message will be sent to the console using EXEC       *
      *   CICS WRITE OPERATOR commands and the transaction will be    *
      *   terminated normally.                                        *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      * CHANGE ACTIVITY :                                             *
      *  $SEG(DFH0STDB),COMP(SAMPLES),PROD(CICS TS ):                 *
      *                                                               *
      *  PN= REASON REL YYMMDD HDXIII : REMARKS                       *
      * $D1= I07099 620 000816 HD1VCJB : Enhanced DB2 Statistics      *
      * $D3= I07001 620 010511 HD2GJST : DB2 priority not applicable  *
      * $D5= I07001 620 010614 HD1VCJB : Tidy up DB2 Statistics Repor *
      * $D6= I07552 640 050426 HD0JADT : MQCONN statistics            *
      * $D7= I07610 650 050506 HD1VCJB : Clock/Timestamp Formatting   *
      * $D8= I07551 650 050920 HD4PALS : Change MQ Conn to WebSphere  *
      * $D9= I07551 650 050923 HD4PALS: 2 - Put MQCONN on EXTRACT STAT*
      * $DA= I07304 650 060104 HD1VCJB: Stats changes for Extended RBA*
      * $DB= I07766 650 060629 HD2GJST: Rename DFHa26ps to dfhmqgps   *
      * $DC= I07766 650 061116 HDFXAB : MQ Connection Stats           *
      * $L0= 773    620 000628 HD1VCJB : Module Creation              *
      * $L1= MIGRAT 620 001109 HD1VCJB : Correct DB2 Connection Name  *
      * $L2= MIGRAT 620 010120 HDNONEAB: Correct DFH0STAT LSR Pool Lo *
      * $L3= 852    640 040524 HD1VCJB : Statistics Updates for 3.1   *
      * $L4= 923    660 080515 HD1VCJB: Atomservice Statistics Support*
      * $L5= 927    660 080604 HD1VCJB: Event Processing Support      *
      * $L7= 918    660 081118 HD2GJST: CICS-MQ Group attach changes  *
      * $L8= R00771 670 100306 HD2GJST: Add Reuselimit                *
      * $LA= R12678 670 100630 HDHBIMS: Support 1-255 lsrpools        *
      * $LB= R22789 670 101215 HD2GJST: GROUPRESYNC on RESYNCMEMBER   *
      * $LC= R28245 680 110801 HD2GJST: Provide DT storage totals     *
      *      R38042 690 130319 HDAFDRB: Use EXTRACT STATISTICS        *
      *      R38042 690 130422 HDIDNCS: Use EXTRACT STATISTICS        *
      *      R38042 690 130404 HDAFDRB: More use of EXTRACT STATISTICS*
      * $P1= D02818 620 010919 HD1VCJB : Stats for TCB Attach Failure *
      * $P2= D02993 620 011004 HD1VCJB : Add the DB2 D2 TCB Mode      *
      * $P3= D03414 620 011105 HD1VCJB : Correct LSRPool Report       *
      * $P4= D04942 630 021104 HD1VCJB : Correct Page Number for LSRP *
      * $P5= D14868 650 051213 HDGRPNT: 2 MQCONN statistics should ret*
      * $01= A25021 650 060623 HDBGNRB: Migrate PK25021 from SPA R640 *
      * $02= A89844 670 091208 HDBGSLS: Migrate PK89844 from SPA R660 *
      * $P6= D14902 650 051219 HD1VCJB: Fix 0C9 abend in LSRPOOL stats*
      * $P7= D15724 650 060503 HD0PIHR: VSAM Data Set Type            *
      * $P8= D19591 660 070921 HD1VCJB: Update MQCONN and DB2 Reports *
      * $P9= D19862 660 071018 HD2GJST: Clarify connection stats      *
      * $PA= D23116 660 081128 HD1VCJB: Add thread create count       *
      * $PB= D17194 670 100719 HD2GJST: LSRpoolid > 8 for files       *
      *      D98593 700 150106 HD3BADW: Fix comments                  *
      *     R110014 710 160825 HDFVGMB: Add MQMONITOR statistics      *
      *     PI94527 710 180314 HD9RAJP: Fix AEIU on MQMONITOR stats   *
      *     R141674 720 180502 HDLHJJH: Connection for protected thred*
      *     D150786 720 181023 HDFVGMB: Add TRUNC(BIN) to PROCESS     *
      *                                                               *
      *****************************************************************
       ID DIVISION.
       PROGRAM-ID. DFH0STDB.

       ENVIRONMENT DIVISION.
           EJECT
       DATA DIVISION.
       WORKING-STORAGE SECTION.
       77  LSRPOOL-FIRST-PAGE          PIC X       VALUE 'Y'.

       77  STCN-COMMAREA               PIC S9(4)  COMP.
       77  INQUIRE-RESP                PIC S9(8)  COMP VALUE +0.

       77  SUB1                        PIC S9(4)  COMP VALUE +0.

       77  RESULT                      PIC S9(8)  COMP VALUE +0.
       77  LARGE-RESULT                PIC S9(18) COMP VALUE +0.
       77  LOOK-ASIDE-RATIO-WORK       PIC 999V9999    VALUE 0.
       77  LOOK-ASIDE-RATIO            PIC 999V9       VALUE 0.

       77  TIME-DAYS                   PIC S9(8)  COMP VALUE +0.
       77  TIME-HOURS                  PIC S9(8)  COMP VALUE +0.
       77  TIME-MINUTES                PIC S9(8)  COMP VALUE +0.
           EJECT
       77  ACCESS-METHOD               PIC S9(8)  COMP VALUE +0.
       77  DATA-TABLE                  PIC S9(8)  COMP VALUE +0.
       77  FILE-BASE-DSNAME            PIC X(44)       VALUE SPACES.
       77  FILE-NAME                   PIC X(8)        VALUE SPACES.
       77  FILE-TYPE                   PIC S9(8)  COMP VALUE +0.
       77  FILE-RBATYPE                PIC S9(8)  COMP VALUE +0.
       77  LSR-POOL-NUMBER             PIC S9(8)  COMP VALUE +0.
       77  RECOVERY-STATUS             PIC S9(8)  COMP VALUE +0.
       77  REMOTE-FILE-NAME            PIC X(8)        VALUE SPACES.
       77  REMOTE-FILE-SYSID           PIC X(4)        VALUE SPACES.
       77  REMOTE-TABLE                PIC S9(8)  COMP VALUE +0.

       77  CFDT-LOAD-TYPE              PIC S9(8)  COMP VALUE +0.
       77  CFDT-POOL-NAME              PIC X(8)        VALUE SPACES.
       77  CFDT-TABLE-NAME             PIC X(8)        VALUE SPACES.
       77  CFDT-UPDATE-MODEL           PIC S9(8)  COMP VALUE +0.

       77  CFDTPOOL-CONN-STATUS        PIC S9(8)  COMP VALUE +0.

       77  DATA-SET-NAME               PIC X(44)       VALUE SPACES.
       77  DSNAME-ACCESS-METHOD        PIC S9(8)  COMP VALUE +0.
       77  DSNAME-AVAILABILITY         PIC S9(8)  COMP VALUE +0.
       77  DSNAME-BASE-DSNAME          PIC X(44)       VALUE SPACES.
       77  DSNAME-FILE-COUNT           PIC S9(8)  COMP VALUE +0.
       77  DSNAME-OBJECT               PIC S9(8)  COMP VALUE +0.
       77  DSNAME-FWDRECOVLOG          PIC S9(8)  COMP VALUE +0.
       77  DSNAME-FWDRECOVLSN          PIC X(26)       VALUE SPACES.
       77  DSNAME-RECOVERY-STATUS      PIC S9(8)  COMP VALUE +0.
       77  DSNAME-VALIDITY             PIC S9(8)  COMP VALUE +0.

       77  DB2-CONNECTION-NAME         PIC X(8)        VALUE SPACES.
       77  DB2-GROUP-ID                PIC X(4)        VALUE SPACES.
       77  DB2-CONNECT-ERROR           PIC S9(8)  COMP VALUE +0.
       77  DB2-CONNECT-STATUS          PIC S9(8)  COMP VALUE +0.
       77  DB2-DROLLBACK               PIC S9(8)  COMP VALUE +0.
       77  DB2-MSG-QUEUE1              PIC X(4)        VALUE SPACES.
       77  DB2-MSG-QUEUE2              PIC X(4)        VALUE SPACES.
       77  DB2-MSG-QUEUE3              PIC X(4)        VALUE SPACES.
       77  DB2-NON-TERMREL             PIC S9(8)  COMP VALUE +0.
       77  DB2-PURGE-CYCLEM            PIC S9(8)  COMP VALUE +0.
       77  DB2-PURGE-CYCLES            PIC S9(8)  COMP VALUE +0.
       77  DB2-SIGNID                  PIC X(8)        VALUE SPACES.
       77  DB2-STANDBY-MODE            PIC S9(8)  COMP VALUE +0.
       77  DB2-STATS-QUEUE             PIC X(4)        VALUE SPACES.
       77  DB2-THREAD-ERROR            PIC S9(8)  COMP VALUE +0.
       77  DB2-RESYNCMEMBER            PIC S9(8)  COMP VALUE +0.

       77  DB2ENTRY-DISABLED-ACT       PIC S9(8)  COMP VALUE +0.
       77  DB2ENTRY-DROLLBACK          PIC S9(8)  COMP VALUE +0.
       77  DB2ENTRY-NAME               PIC X(8)        VALUE SPACES.
       77  DB2ENTRY-STATUS             PIC S9(8)  COMP VALUE +0.

       77  MQMONITOR-NAME              PIC X(8)        VALUE SPACES.
       77  MQMONITOR-AUTOSTATUS        PIC S9(8)  COMP VALUE +0.
       77  MQMONITOR-ENABLESTATUS      PIC S9(8)  COMP VALUE +0.
       77  MQMONITOR-MONSTATUS         PIC S9(8)  COMP VALUE +0.

       77  PROGRAM-CONCURRENCY         PIC S9(8)  COMP VALUE +0.
       77  PROGRAM-API-STATUS          PIC S9(8)  COMP VALUE +0.
       77  PROGRAM-LIBRARY-NAME        PIC X(8)        VALUE SPACES.
       77  PROGRAM-LIBRARY-DSNAME      PIC X(44)       VALUE SPACES.

      *****************************************************************
      ***  Total fields                                             ***
      *****************************************************************
       77  DATA-TABLES                 PIC S9(9)  COMP-3 VALUE +0.
       77  STORAGE-ALLOCATED-TOTAL     PIC S9(9)  COMP-3 VALUE +0.
       77  STORAGE-INUSE-TOTAL         PIC S9(9)  COMP-3 VALUE +0.
       77  STORAGE-ALLOCATED-ENTRIES   PIC S9(9)  COMP-3 VALUE +0.
       77  STORAGE-INUSE-ENTRIES       PIC S9(9)  COMP-3 VALUE +0.
       77  STORAGE-ALLOCATED-INDEX     PIC S9(9)  COMP-3 VALUE +0.
       77  STORAGE-INUSE-INDEX         PIC S9(9)  COMP-3 VALUE +0.
       77  STORAGE-ALLOCATED-DATA      PIC S9(9)  COMP-3 VALUE +0.
       77  STORAGE-INUSE-DATA          PIC S9(9)  COMP-3 VALUE +0.
           EJECT
      *****************************************************************
      ***  Report print line definitions                            ***
      *****************************************************************
       01  PRINT-LINE.
           03  PRINT-CONTROL           PIC X.
           03  PP1-132.
               05  FILLER              PIC XX.
               05  PP3-39              PIC X(37).
               05  FILLER              PIC X(93).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC XX.
               05  PP3-50              PIC X(48).
               05  FILLER              PIC XX.
               05  PP53-62             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X(70).
           03 FILLER REDEFINES PP1-132.
               05  PP1-18.
                   07  PP1-16          PIC X(16).
                   07  FILLER          PIC XX.
               05  FILLER REDEFINES PP1-18.
                   07  PP1-14          PIC X(14).
                   07  FILLER          PIC X(4).
               05  FILLER REDEFINES PP1-18.
                   07  PP1-11.
                       09  PP1-9       PIC X(9).
                       09  FILLER      PIC XX.
                   07  FILLER          PIC X(7).
               05  FILLER              PIC X(114).
           03 FILLER REDEFINES PP1-132.
               05  PP1-5               PIC X(5).
               05  FILLER              PIC X(41).
               05  PP47-59             PIC X(13).
               05  FILLER              PIC X(73).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(46).
               05  PP47-94             PIC X(48).
               05  FILLER              PIC X(38).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC XX.
               05  PP3-10              PIC X(8).
               05  FILLER              PIC X(38).
               05  PP49                PIC X.
               05  FILLER              PIC X(55).
               05  PP105-114           PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X(18).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(57).
               05  PP58-83             PIC X(26).
               05  FILLER              PIC XX.
               05  PP86-96             PIC X(11).
               05  FILLER              PIC X(36).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(12).
               05  PP13-22             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP25-34             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP37-46             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP49-58             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP61-70             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP73-82             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP85-94             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP97-106            PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP109-118           PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP121-130           PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
           03 FILLER REDEFINES PP1-132.
               05  PP1-22.
                   07  PP1-21          PIC X(21).
                   07  FILLER          PIC X.
               05  FILLER              PIC X(24).
               05  PP47-56             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X(76).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC XX.
               05  PP3-46.
                   07  PP3-44          PIC X(42).
                   07  FILLER          PIC XX.
               05  FILLER              PIC XXX.
               05  PP50-53             PIC X(4).
               05  FILLER              PIC X(4).
               05  PP58-61             PIC X(4).
               05  FILLER              PIC X(4).
               05  PP66-72             PIC X(7).
               05  FILLER              PIC XX.
               05  PP75-85             PIC X(11).
               05  FILLER              PIC X(4).
               05  PP90-92             PIC ZZ9.
               05  FILLER              PIC XXX.
               05  PP96-109            PIC X(14).
               05  FILLER              PIC X(23).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC XX.
               05  PP3-34.
                   07  PP3-25.
                       09  PP3-24      PIC X(22).
                       09  FILLER      PIC X.
                   07  FILLER          PIC X(9).
               05  FILLER              PIC X(11).
               05  PP46-51             PIC ZZ,ZZ9.
               05  FILLER              PIC X(81).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(5).
               05  PP6-39.
                   07  FILLER          PIC XX.
                   07  PP8-39.
                       09  FILLER      PIC X(18).
                       09  PP26-39     PIC X(14).
               05  FILLER              PIC XX.
               05  PP42-51             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X(17).
               05  PP69-102.
                   07  FILLER          PIC XX.
                   07  PP71-102        PIC X(32).
               05  FILLER              PIC X(30).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC XX.
               05  PP3-19.
                   07  PP3-15          PIC X(13).
                   07  FILLER          PIC XX.
                   07  PP18-20         PIC ZZ9.
               05  FILLER              PIC X(5).
               05  PP26-56.
                   07  FILLER          PIC X(17).
                   07  PP43-56         PIC X(14).
               05  FILLER              PIC X(11).
               05  PP68-132.
                   07  PP68-100        PIC X(33).
                   07  FILLER          PIC X(14).
                   07  PP115-124X.
                       09  PP115-117X  PIC XXX.
                       09  FILLER      PIC X(7).
                   07  FILLER          PIC X(8).
               05  FILLER REDEFINES PP68-132.
                   07  PP68-112.
                       09  FILLER      PIC X(35).
                       09  PP103-112   PIC X(10).
                   07  FILLER          PIC XX.
                   07  PP115-128.
                       09  PP115-124   PIC ZZ,ZZZ,ZZ9.
                       09  FILLER      PIC X(4).
                   07  FILLER          PIC X(4).
               05  FILLER REDEFINES PP68-132.
                   07  FILLER          PIC X(47).
                   07  PP115-122.
                       09  PP115-120   PIC X(6).
                       09  FILLER      PIC XX.
                   07  FILLER          PIC X(10).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(67).
               05  PP68-85             PIC X(18).
               05  FILLER              PIC X(2).
               05  PP88-97             PIC X(10).
               05  FILLER              PIC X(3).
               05  PP100-108           PIC X(8).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(4).
               05  PP5-123.
                   07  FILLER          PIC X.
                   07  PP6-10          PIC ZZZZ9.
                   07  FILLER          PIC XXX.
                   07  PP14-18         PIC ZZZZ9.
                   07  FILLER          PIC X(6).
                   07  PP25-29         PIC ZZZZ9.
                   07  FILLER          PIC X(4).
                   07  PP34-43         PIC ZZ,ZZZ,ZZ9.
                   07  FILLER          PIC XX.
                   07  PP46-55         PIC ZZ,ZZZ,ZZ9.
                   07  FILLER          PIC XX.
                   07  PP58-67         PIC ZZ,ZZZ,ZZ9.
                   07  FILLER          PIC XX.
                   07  PP70-79         PIC ZZ,ZZZ,ZZ9.
                   07  FILLER          PIC X(4).
                   07  PP84-88         PIC ZZ9.9.
                   07  PP89            PIC X.
                   07  FILLER          PIC X(6).
                   07  PP96-105        PIC ZZ,ZZZ,ZZ9.
                   07  FILLER          PIC X(6).
                   07  PP112-121       PIC ZZ,ZZZ,ZZ9.
                   07  FILLER          PIC XX.
               05  FILLER REDEFINES PP5-123.
                   07  PP5-68          PIC X(64).
                   07  PP69-123        PIC X(55).
               05  FILLER              PIC X(9).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(12).
               05  PP13-18             PIC X(6).
               05  FILLER              PIC XXX.
               05  PP22-29             PIC X(8).
               05  FILLER              PIC X(12).
               05  PP42-49             PIC X(8).
               05  FILLER              PIC X(65).
               05  PP115-118           PIC X(4).
               05  FILLER              PIC X(14).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(31).
               05  PP32-39             PIC X(8).
               05  FILLER              PIC XXX.
               05  PP43-46             PIC X(4).
               05  FILLER              PIC X(4).
               05  PP51-52.
                   07  FILLER          PIC X.
                   07  PP52            PIC ZZ9.
               05  FILLER              PIC X.
               05  PP56-58             PIC XXX.
               05  FILLER              PIC XX.
               05  PP61-64             PIC X(4).
               05  PP65                PIC X.
               05  PP66-75             PIC X(10).
               05  FILLER              PIC XX.
               05  PP78-85             PIC X(8).
               05  FILLER              PIC XX.
               05  PP88-95             PIC X(8).
               05  FILLER              PIC XX.
               05  PP98-111            PIC X(14).
               05  FILLER              PIC XXX.
               05  PP115-117           PIC ZZ9.
               05  FILLER              PIC XX.
               05  PP120-124           PIC ZZZZ9.
               05  FILLER              PIC XX.
               05  PP127-131           PIC ZZZZ9.
               05  FILLER              PIC X.
           03 FILLER REDEFINES PP1-132.
               05  PP1-34.
                   07  PP1-23          PIC X(23).
                   07  FILLER          PIC X(11).
               05  FILLER              PIC X(12).
               05  PP47-56X.
                   07  PP47-53.
                       09  PP47-52X.
                           11  PP47-48 PIC 99.
                           11  FILLER  PIC X.
                           11  PP50-51 PIC 99.
                           11  FILLER  PIC X.
                       09  FILLER      PIC X.
                   07  FILLER          PIC XXX.
               05  FILLER REDEFINES PP47-56X.
                   07  PP47-55.
                       09  PP47-51.
                           11  PP47-50 PIC X(4).
                           11  FILLER  PIC X.
                       09  FILLER      PIC X(4).
                   07  FILLER          PIC X.
               05  FILLER REDEFINES PP47-56X.
                   07  PP47-54.
                       09  PP47-52N    PIC 99B9B9.
                       09  FILLER      PIC XX.
                   07  FILLER          PIC XX.
               05  FILLER REDEFINES PP47-56X.
                   07  PP47-51N        PIC 9(5).
                   07  FILLER          PIC X(5).
               05  FILLER              PIC X(76).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC X(11).
               05  PP12-15             PIC X(4).
               05  FILLER              PIC X.
               05  PP17-26             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X.
               05  PP28-37             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC X.
               05  PP39-48             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP51-60             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP63-72             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP75-84             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP87-96             PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP99-108            PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP111-120           PIC ZZ,ZZZ,ZZ9.
               05  FILLER              PIC XX.
               05  PP123-132           PIC ZZ,ZZZ,ZZ9.
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC XX.
               05  PP3-132.
                   07  PP3-102.
                       09  PP3-72.
                           11  PP3-37  PIC X(35).
                           11  PP38-72 PIC X(35).
                       09  PP73-102    PIC X(30).
                   07  PP103-132       PIC X(30).
           03 FILLER REDEFINES PP1-132.
               05  FILLER              PIC XX.
               05  PP3-68              PIC X(66).
               05  FILLER              PIC X(64).
           EJECT
       LINKAGE SECTION.
           EJECT
      *****************************************************************
      ***  Statistics Sample Programs Commarea.                     ***
      *****************************************************************
       01  DFHCOMMAREA.
           COPY DFH0STCM.
           EJECT
      *****************************************************************
      *** Data structures for LSR Pool statistics                   ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHA08DS.
           EJECT
      *****************************************************************
      *** Data structures for File statistics                       ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHA17DS.
           EJECT
      *****************************************************************
      *** Data structures for Dispatcher Global statistics          ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHDSGDS.
           EJECT
      *****************************************************************
      *** Data structures for DB2 Connection Global statistics      ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHD2GDS.
           EJECT
      *****************************************************************
      *** Data structures for DB2 Entry Resource statistics         ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHD2RDS.
           EJECT
      *****************************************************************
      *** Data structures for WebSphere MQ Connection statistics    ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHMQGDS.
           EJECT
      *****************************************************************
      *** Data structures for WebSphere MQ Monitor statistics       ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHMQRDS.
           EJECT
      *****************************************************************
      ***                                                           ***
      ***           CICS Collect and print statistics               ***
      ***                                                           ***
      *****************************************************************
       PROCEDURE DIVISION.

      *****************************************************************
      *** MAIN-PROCESSING                                           ***
      ***     Initialise the totals fields                          ***
      ***     Get CICS system information                           ***
      ***     Print the statistics reports                          ***
      ***     Return to DFH0STLK                                    ***
      *****************************************************************

       MAIN-PROCESSING SECTION.

           PERFORM TOTALS-INITIALISE.

           PERFORM COLLECT-SYSTEM.

           PERFORM PRINT-STATISTICS.

           EXEC CICS RETURN
           END-EXEC.

       MAIN-PROCESSING-EXIT.
           GOBACK.
           EJECT
      *****************************************************************
      *** COLLECT-SYSTEM                                            ***
      ***   Get required CICS system information.                   ***
      *****************************************************************

       COLLECT-SYSTEM SECTION.

           EXEC CICS INQUIRE SYSTEM
               DB2CONN(DB2-CONNECTION-NAME)
           END-EXEC.

           IF DB2-CONNECTION-NAME NOT = SPACES THEN
               EXEC CICS INQUIRE DB2CONN
                   CONNECTST(DB2-CONNECT-STATUS)
               END-EXEC
           END-IF.

       COLLECT-SYSTEM-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *****************************************************************
      ***         Collect and Print the CICS Statistics             ***
      *****************************************************************
      *****************************************************************
      ***                                                           ***
      *** 1. Print the statistics in the following order:-          ***
      ***    - LSRPools                                             ***
      ***    - Files                                                ***
      ***    - Data Tables                                          ***
      ***    - Data Set Names                                       ***
      ***    - DB2 Connection                                       ***
      ***    - DB2 Entries                                          ***
      ***                                                           ***
      *****************************************************************
      *****************************************************************

       PRINT-STATISTICS SECTION.

           IF SELECT-LSRPOOLS = 'Y' OR 'y' THEN
               PERFORM LSR-POOL-STATISTICS
           END-IF.

           IF SELECT-FILES = 'Y' OR 'y' THEN
               PERFORM FILE-STATISTICS

               IF DATA-TABLES > 0 THEN
                   PERFORM DATA-TABLE-STATISTICS
               ELSE
                   PERFORM NO-DATA-TABLE-STATISTICS
               END-IF
           END-IF.

           IF SELECT-DSNAMES = 'Y' OR 'y' THEN
               PERFORM DSNAME-STATISTICS
           END-IF.

           IF SELECT-CFDT-POOLS = 'Y' OR 'y' THEN
               PERFORM CFDTPOOL-STATISTICS
           END-IF.

           IF SELECT-DB2 = 'Y' OR 'y' THEN
               PERFORM DB2-STATISTICS
           END-IF.

           IF SELECT-DB2ENTRIES = 'Y' OR 'y' THEN
               PERFORM DB2ENTRY-STATISTICS
           END-IF.

           IF SELECT-MQCONN = 'Y' OR 'y' THEN
               PERFORM MQCONN-STATISTICS
           END-IF.

           IF SELECT-MQMONITORS = 'Y' OR 'y' THEN
               PERFORM MQMONITOR-STATISTICS
           END-IF.

       PRINT-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** LSR-POOL-STATISTICS                                       ***
      ***   Print heading and lsr pool statistics                   ***
      *****************************************************************
       LSR-POOL-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO LSRPOOLS-PAGENO.

           MOVE '-'         TO PRINT-CONTROL.
           MOVE 'LSR Pools' TO PP1-9.
           PERFORM WRITE-OUTPUT.
           MOVE '+'         TO PRINT-CONTROL.
           MOVE ALL '_'     TO PP1-9.
           PERFORM WRITE-OUTPUT.

           MOVE 1 TO LSR-POOL-NUMBER.

           PERFORM WITH TEST BEFORE
               UNTIL LSR-POOL-NUMBER > 255

                   EXEC CICS EXTRACT STATISTICS LSRPOOL
                     RESID(LSR-POOL-NUMBER)
                     SET(ADDRESS OF DFHA08DS)
                     NOHANDLE
                   END-EXEC

                   EVALUATE EIBRESP
                       WHEN DFHRESP(NORMAL)
                           PERFORM PRINT-LSR-POOL-STATS
                       WHEN OTHER
                           CONTINUE
                   END-EVALUATE

                   ADD 1 TO LSR-POOL-NUMBER

           END-PERFORM.

       LSR-POOL-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** Print the statistics for an LSR Pool.                     ***
      *****************************************************************
       PRINT-LSR-POOL-STATS SECTION.

           IF LSRPOOL-FIRST-PAGE = 'Y' THEN
               MOVE 'N' TO LSRPOOL-FIRST-PAGE
               MOVE '0' TO PRINT-CONTROL
           ELSE
               PERFORM PAGE-HEADINGS
               MOVE '-' TO PRINT-CONTROL
           END-IF.

           MOVE 'Pool Number :'  TO PP3-15.
           MOVE LSR-POOL-NUMBER  TO PP18-20.
           MOVE 'Time Created :' TO PP26-39.
           MOVE A08LBKCD         TO CLOCK-FIELD.
           PERFORM TIMESTAMP-CONVERT.
           MOVE STOPWATCH        TO PP43-56.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP3-19.
           MOVE ALL '_' TO PP26-56.
           PERFORM WRITE-OUTPUT.

           MOVE '0'      TO PRINT-CONTROL.
           MOVE 'Maximum key length . . . . . . . :' TO PP6-39.
           MOVE A08BKKYL TO PP46-51.
           PERFORM WRITE-OUTPUT.
           MOVE 'Total number of strings  . . . . :' TO PP6-39.
           MOVE A08BKSTN TO PP46-51.
           PERFORM WRITE-OUTPUT.
           MOVE 'Peak concurrently active strings :' TO PP6-39.
           MOVE A08BKHAS TO PP46-51.
           PERFORM WRITE-OUTPUT.
           MOVE 'Total requests waited for string :' TO PP6-39.
           MOVE A08BKTSW TO PP46-51.
           PERFORM WRITE-OUTPUT.
           MOVE 'Peak requests waited for string. :' TO PP6-39.
           MOVE A08BKHSW TO PP46-51.
           PERFORM WRITE-OUTPUT.

           PERFORM BUFFER-TOTAL-STATISTICS.

           MOVE '0'       TO PRINT-CONTROL.

           IF A08IDSEP THEN
               MOVE '0' TO PRINT-CONTROL
               MOVE 'Data Buffer Statistics' TO PP3-24
               PERFORM WRITE-OUTPUT
               MOVE '+'     TO PRINT-CONTROL
               MOVE ALL '_' TO PP3-24
               PERFORM WRITE-OUTPUT

               PERFORM DATA-BUFFER-STATISTICS

               MOVE '0' TO PRINT-CONTROL
               MOVE 'Index Buffer Statistics' TO PP3-25
               PERFORM WRITE-OUTPUT
               MOVE '+'     TO PRINT-CONTROL
               MOVE ALL '_' TO PP3-25
               PERFORM WRITE-OUTPUT

               PERFORM INDEX-BUFFER-STATISTICS
           ELSE
               MOVE '0' TO PRINT-CONTROL
               MOVE 'Data and Index Buffer Statistics' TO PP3-34
               PERFORM WRITE-OUTPUT
               MOVE '+'     TO PRINT-CONTROL
               MOVE ALL '_' TO PP3-34
               PERFORM WRITE-OUTPUT

               PERFORM DATA-BUFFER-STATISTICS
           END-IF.

       PRINT-LSR-POOL-STATS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** Print the LSR Pool buffer total statistics.               ***
      *****************************************************************
       BUFFER-TOTAL-STATISTICS SECTION.

           MOVE '0'             TO PRINT-CONTROL.
           MOVE 'Buffer Totals' TO PP3-15.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP3-15.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Data Buffers . . . . . . . . . . :' TO PP6-39.
           MOVE A08TDBFN                             TO PP42-51.
           MOVE 'Index Buffers. . . . . . . . . . :' TO PP69-102.
           MOVE A08TIBFN                             TO PP105-114.
           PERFORM WRITE-OUTPUT.
           MOVE 'Hiperspace Data Buffers. . . . . :' TO PP6-39.
           MOVE A08TDHBN                             TO PP42-51.
           MOVE 'Hiperspace Index Buffers . . . . :' TO PP69-102.
           MOVE A08TIHBN                             TO PP105-114.
           PERFORM WRITE-OUTPUT.
           MOVE 'Successful look asides . . . . :' TO PP8-39.
           MOVE A08TDBFF                           TO PP42-51.
           MOVE 'Successful look asides . . . . :' TO PP71-102.
           MOVE A08TIBFF                           TO PP105-114.
           PERFORM WRITE-OUTPUT.
           MOVE 'Buffer reads . . . . . . . . . :' TO PP8-39.
           MOVE A08TDFRD                           TO PP42-51.
           MOVE 'Buffer reads . . . . . . . . . :' TO PP71-102.
           MOVE A08TIFRD                           TO PP105-114.
           PERFORM WRITE-OUTPUT.
           MOVE 'User initiated writes. . . . . :' TO PP8-39.
           MOVE A08TDUIW                           TO PP42-51.
           MOVE 'User initiated writes. . . . . :' TO PP71-102.
           MOVE A08TIUIW                           TO PP105-114.
           PERFORM WRITE-OUTPUT.
           MOVE 'Non-user initiated writes. . . :' TO PP8-39.
           MOVE A08TDNUW                           TO PP42-51.
           MOVE 'Non-user initiated writes. . . :' TO PP71-102.
           MOVE A08TINUW                           TO PP105-114.
           PERFORM WRITE-OUTPUT.
           MOVE 'Successful Hiperspace CREADS . :' TO PP8-39.
           MOVE A08TDCRS                           TO PP42-51.
           MOVE 'Successful Hiperspace CREADS . :' TO PP71-102.
           MOVE A08TICRS                           TO PP105-114.
           PERFORM WRITE-OUTPUT.
           MOVE 'Successful Hiperspace CWRITES. :' TO PP8-39.
           MOVE A08TDCWS                           TO PP42-51.
           MOVE 'Successful Hiperspace CWRITES. :' TO PP71-102.
           MOVE A08TICWS                           TO PP105-114.
           PERFORM WRITE-OUTPUT.
           MOVE 'Failing Hiperspace CREADS. . . :' TO PP8-39.
           MOVE A08TDCRF                           TO PP42-51.
           MOVE 'Failing Hiperspace CREADS. . . :' TO PP71-102.
           MOVE A08TICRF                           TO PP105-114.
           PERFORM WRITE-OUTPUT.
           MOVE 'Failing Hiperspace CWRITES . . :' TO PP8-39.
           MOVE A08TDCWF                           TO PP42-51.
           MOVE 'Failing Hiperspace CWRITES . . :' TO PP71-102.
           MOVE A08TICWF                           TO PP105-114.
           PERFORM WRITE-OUTPUT.

       BUFFER-TOTAL-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** Print the LSR Pool data buffer statistics.                ***
      *****************************************************************
       DATA-BUFFER-STATISTICS SECTION.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Buffer  No. of   Hiperspace     Look       Buffer
      -    ' User    ' TO PP5-68.
           MOVE '  Non-User   Look-Aside    Successful        Failing
      -    ''          TO PP69-123.
           PERFORM WRITE-OUTPUT.
           MOVE ' Size   Buffers    Buffers     Asides       Reads
      -    'Writes   ' TO PP5-68.
           MOVE '   Writes       Ratio    CREADS/CWRITES  CREADS/CWRITES
      -    ''          TO PP69-123.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP5-123.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 1   TO SUB1.

           PERFORM WITH TEST BEFORE
               UNTIL SUB1 > 11

                   IF A08BKBFN(SUB1) > 0
                       MOVE A08BKBSZ(SUB1) TO PP6-10
                       MOVE A08BKBFN(SUB1) TO PP14-18
                       MOVE A08BKHBN(SUB1) TO PP25-29
                       MOVE A08BKBFF(SUB1) TO PP34-43
                       MOVE A08BKFRD(SUB1) TO PP46-55
                       MOVE A08BKUIW(SUB1) TO PP58-67
                       MOVE A08BKNUW(SUB1) TO PP70-79
                       COMPUTE LARGE-RESULT =
                           (A08BKBFF(SUB1) + A08BKFRD(SUB1))
                       END-COMPUTE
                       IF LARGE-RESULT > 0 THEN
                           COMPUTE LOOK-ASIDE-RATIO-WORK =
                               (A08BKBFF(SUB1) / LARGE-RESULT)
                           END-COMPUTE
                           COMPUTE LOOK-ASIDE-RATIO =
                               (LOOK-ASIDE-RATIO-WORK * 100)
                           END-COMPUTE
                       ELSE
                           MOVE 0 TO LOOK-ASIDE-RATIO
                       END-IF
                       MOVE LOOK-ASIDE-RATIO TO PP84-88
                       MOVE '%'              TO PP89
                       COMPUTE RESULT =
                           (A08BKCRS(SUB1) + A08BKCWS(SUB1))
                       END-COMPUTE
                       MOVE RESULT           TO PP96-105
                       COMPUTE RESULT =
                           (A08BKCRF(SUB1) + A08BKCWF(SUB1))
                       END-COMPUTE
                       MOVE RESULT           TO PP112-121

                       PERFORM WRITE-OUTPUT
                   END-IF

                   ADD 1 TO SUB1

           END-PERFORM.

       DATA-BUFFER-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** Print the LSR Pool index buffer statistics.               ***
      *****************************************************************
       INDEX-BUFFER-STATISTICS SECTION.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 'Buffer  No. of   Hiperspace     Look       Buffer
      -    ' User    ' TO PP5-68.
           MOVE '  Non-User   Look-Aside    Successful        Failing
      -    ''          TO PP69-123.
           PERFORM WRITE-OUTPUT.
           MOVE ' Size   Buffers    Buffers     Asides       Reads
      -    'Writes   ' TO PP5-68.
           MOVE '   Writes       Ratio    CWRITES/CREADS  CREADS/CWRITES
      -    ''          TO PP69-123.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP5-123.
           PERFORM WRITE-OUTPUT.

           MOVE '0' TO PRINT-CONTROL.
           MOVE 1   TO SUB1.

           PERFORM WITH TEST BEFORE
               UNTIL SUB1 > 11

                   IF A08IKBFN(SUB1) > 0
                       MOVE A08IKBSZ(SUB1) TO PP6-10
                       MOVE A08IKBFN(SUB1) TO PP14-18
                       MOVE A08IKHBN(SUB1) TO PP25-29
                       MOVE A08IKBFF(SUB1) TO PP34-43
                       MOVE A08IKFRD(SUB1) TO PP46-55
                       MOVE A08IKUIW(SUB1) TO PP58-67
                       MOVE A08IKNUW(SUB1) TO PP70-79
                       COMPUTE LARGE-RESULT =
                           (A08IKBFF(SUB1) + A08IKFRD(SUB1))
                       END-COMPUTE
                       IF LARGE-RESULT > 0 THEN
                           COMPUTE LOOK-ASIDE-RATIO-WORK =
                               (A08IKBFF(SUB1) / LARGE-RESULT)
                           END-COMPUTE
                           COMPUTE LOOK-ASIDE-RATIO =
                               (LOOK-ASIDE-RATIO-WORK * 100)
                           END-COMPUTE
                       ELSE
                           MOVE 0 TO LOOK-ASIDE-RATIO
                       END-IF
                       MOVE LOOK-ASIDE-RATIO TO PP84-88
                       MOVE '%'              TO PP89
                       COMPUTE RESULT =
                           (A08IKCRS(SUB1) + A08IKCWS(SUB1))
                       END-COMPUTE
                       MOVE RESULT           TO PP96-105
                       COMPUTE RESULT =
                           (A08IKCRF(SUB1) + A08IKCWF(SUB1))
                       END-COMPUTE
                       MOVE RESULT           TO PP112-121

                       PERFORM WRITE-OUTPUT
                   END-IF

                   ADD 1 TO SUB1

           END-PERFORM.

       INDEX-BUFFER-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** FILE-STATISTICS                                           ***
      ***   Print heading and file statistics                       ***
      *****************************************************************
       FILE-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO FILES-PAGENO.

           MOVE '-'     TO PRINT-CONTROL.
           MOVE 'Files' TO PP1-5.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-5.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.

           EXEC CICS INQUIRE FILE START
           END-EXEC.

           EXEC CICS INQUIRE FILE(FILE-NAME) NEXT
               ACCESSMETHOD(ACCESS-METHOD)
               BASEDSNAME(FILE-BASE-DSNAME)
               CFDTPOOL(CFDT-POOL-NAME)
               LOADTYPE(CFDT-LOAD-TYPE)
               LSRPOOLNUM(LSR-POOL-NUMBER)
               RBATYPE(FILE-RBATYPE)
               RECOVSTATUS(RECOVERY-STATUS)
               REMOTENAME(REMOTE-FILE-NAME)
               REMOTESYSTEM(REMOTE-FILE-SYSID)
               REMOTETABLE(REMOTE-TABLE)
               TABLE(DATA-TABLE)
               TABLENAME(CFDT-TABLE-NAME)
               UPDATEMODEL(CFDT-UPDATE-MODEL)
               TYPE(FILE-TYPE)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

               IF LINE-COUNT > LINES-PER-PAGE THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               IF SUB-HEADINGS = 'Y' THEN
                   PERFORM FILE-SUB-HEADINGS-1
               END-IF

               EXEC CICS EXTRACT STATISTICS FILE
                   RESID(FILE-NAME)
                   SET(ADDRESS OF DFHA17DS)
               END-EXEC

               MOVE FILE-NAME TO PP3-10
               EVALUATE ACCESS-METHOD
                   WHEN DFHVALUE(BDAM)
                       PERFORM BDAM-FILE
                   WHEN DFHVALUE(VSAM)
                       PERFORM VSAM-FILE
                   WHEN DFHVALUE(REMOTE)
                       PERFORM REMOTE-FILE
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE

               PERFORM WRITE-OUTPUT

               EXEC CICS INQUIRE FILE(FILE-NAME) NEXT
                   ACCESSMETHOD(ACCESS-METHOD)
                   BASEDSNAME(FILE-BASE-DSNAME)
                   CFDTPOOL(CFDT-POOL-NAME)
                   LOADTYPE(CFDT-LOAD-TYPE)
                   LSRPOOLNUM(LSR-POOL-NUMBER)
                   RBATYPE(FILE-RBATYPE)
                   RECOVSTATUS(RECOVERY-STATUS)
                   REMOTENAME(REMOTE-FILE-NAME)
                   REMOTESYSTEM(REMOTE-FILE-SYSID)
                   REMOTETABLE(REMOTE-TABLE)
                   TABLE(DATA-TABLE)
                   TABLENAME(CFDT-TABLE-NAME)
                   TYPE(FILE-TYPE)
                   UPDATEMODEL(CFDT-UPDATE-MODEL)
                   RESP(INQUIRE-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE FILE END
           END-EXEC.

      *****************************************************************
      ***                                                           ***
      *****************************************************************
           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO FILES-PAGENO.

           MOVE '-'     TO PRINT-CONTROL.
           MOVE 'Files - Requests' TO PP1-16.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-16.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.

           EXEC CICS INQUIRE FILE START
           END-EXEC.

           EXEC CICS INQUIRE FILE(FILE-NAME) NEXT
               ACCESSMETHOD(ACCESS-METHOD)
               TYPE(FILE-TYPE)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

               IF LINE-COUNT > LINES-PER-PAGE THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               IF SUB-HEADINGS = 'Y' THEN
                   PERFORM FILE-SUB-HEADINGS-2
               END-IF

               EXEC CICS EXTRACT STATISTICS FILE
                   RESID(FILE-NAME)
                   SET(ADDRESS OF DFHA17DS)
               END-EXEC

               MOVE FILE-NAME TO PP3-10

               MOVE A17DSRD   TO PP13-22
               MOVE A17DSGU   TO PP25-34
               MOVE A17DSBR   TO PP37-46
               MOVE A17DSBRU  TO PP49-58
               MOVE A17DSWRA  TO PP61-70
               MOVE A17DSWRU  TO PP73-82
               MOVE A17DSDEL  TO PP85-94
               MOVE A17RLSWT  TO PP97-106
               EVALUATE ACCESS-METHOD
                   WHEN DFHVALUE(VSAM)
                       MOVE A17DSTSW  TO PP109-118
                       MOVE A17DSHSW  TO PP121-130
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE

               PERFORM WRITE-OUTPUT

               EXEC CICS INQUIRE FILE(FILE-NAME) NEXT
                   ACCESSMETHOD(ACCESS-METHOD)
                   TYPE(FILE-TYPE)
                   RESP(INQUIRE-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE FILE END
           END-EXEC.

       FILE-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** Print file type details.                                  ***
      *****************************************************************
       BDAM-FILE SECTION.

           MOVE ' BDAM '        TO PP13-18.
           EVALUATE FILE-TYPE
               WHEN DFHVALUE(KEYED)
                   MOVE 'KEYED   ' TO PP22-29
               WHEN DFHVALUE(NOTKEYED)
                   MOVE 'NOTKEYED' TO PP22-29
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.

       BDAM-FILE-EXIT.
           EXIT.

       VSAM-FILE SECTION.

           MOVE ' VSAM '        TO PP13-18.
           EVALUATE FILE-TYPE
               WHEN DFHVALUE(ESDS)
                   MOVE '  ESDS  ' TO PP22-29
               WHEN DFHVALUE(KSDS)
                   MOVE '  KSDS  ' TO PP22-29
               WHEN DFHVALUE(RRDS)
                   MOVE '  RRDS  ' TO PP22-29
               WHEN DFHVALUE(VRRDS)
                   MOVE ' VRRDS  ' TO PP22-29
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           IF A17RLS THEN
               MOVE 'Yes' TO PP56-58
           ELSE
               MOVE 'No ' TO PP56-58
               IF LSR-POOL-NUMBER > 0 THEN
                   MOVE LSR-POOL-NUMBER TO PP52
               ELSE
                   MOVE 'None'          TO PP51-52
               END-IF
               MOVE A17STRNO TO PP115-117
               MOVE A17DSINB TO PP120-124
               MOVE A17DSDNB TO PP127-131
           END-IF.
           EVALUATE RECOVERY-STATUS
               WHEN DFHVALUE(FWDRECOVABLE)
                   MOVE 'FwdRecoverable' TO PP98-111
               WHEN DFHVALUE(NOTRECOVABLE)
                   MOVE 'NotRecoverable' TO PP98-111
               WHEN DFHVALUE(RECOVERABLE)
                   MOVE 'Recoverable   ' TO PP98-111
               WHEN DFHVALUE(UNDETERMINED)
                   MOVE 'Undetermined  ' TO PP98-111
               WHEN OTHER
                   CONTINUE
           END-EVALUATE
           EVALUATE DATA-TABLE
               WHEN DFHVALUE(CFTABLE)
                   MOVE 'CF  '   TO PP61-64
                   PERFORM EVALUATE-TABLE-TYPE
                   PERFORM CF-DATA-TABLE
               WHEN DFHVALUE(CICSTABLE)
                   MOVE 'CICS'   TO PP61-64
                   PERFORM EVALUATE-TABLE-TYPE
               WHEN DFHVALUE(USERTABLE)
                   MOVE 'USER'   TO PP61-64
                   PERFORM EVALUATE-TABLE-TYPE
               WHEN DFHVALUE(NOTTABLE)
                   MOVE '    '   TO PP61-64
               WHEN OTHER
                   MOVE '????'   TO PP61-64
           END-EVALUATE.

       VSAM-FILE-EXIT.
           EXIT.

       REMOTE-FILE SECTION.

           MOVE 'REMOTE'          TO PP13-18.
           MOVE REMOTE-FILE-NAME  TO PP32-39.
           MOVE REMOTE-FILE-SYSID TO PP43-46.
           IF REMOTE-TABLE = DFHVALUE(REMTABLE) THEN
               MOVE ' REM'       TO PP61-64
               MOVE '/'          TO PP65
               MOVE 'TABLE     ' TO PP66-75
           END-IF.

       REMOTE-FILE-EXIT.
           EXIT.
           EJECT
       EVALUATE-TABLE-TYPE SECTION.

           ADD 1 TO DATA-TABLES.

           MOVE '/' TO PP65.
           EVALUATE TRUE
               WHEN A17DTRMT
                   MOVE 'REMOTE    ' TO PP66-75
               WHEN A17DTASS
                   MOVE 'ASSOCIATED' TO PP66-75
               WHEN A17DTPRS
                   MOVE 'TABLE     ' TO PP66-75
               WHEN A17DTCFL
                   MOVE 'LOCKING   ' TO PP66-75
               WHEN A17DTCFC
                   MOVE 'CONTENTION' TO PP66-75
               WHEN A17DTAIX
                   MOVE 'ALTERNATE ' TO PP66-75
               WHEN OTHER
                   MOVE 'UNKNOWN   ' TO PP66-75
           END-EVALUATE.

       EVALUATE-TABLE-TYPE-EXIT.
           EXIT.

       CF-DATA-TABLE SECTION.

           MOVE CFDT-POOL-NAME  TO PP78-85.
           MOVE CFDT-TABLE-NAME TO PP88-95.

       CF-DATA-TABLE-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** FILE-SUB-HEADINGS                                         ***
      *****************************************************************
       FILE-SUB-HEADINGS-1 SECTION.

           MOVE '0' TO PRINT-CONTROL.
           MOVE '          Access     File     Remot' TO PP3-37.
           MOVE 'e   Remote  LSR             Data   ' TO PP38-72.
           MOVE '       CFDT      Table      Re'      TO PP73-102.
           MOVE 'covery     Str-  <- Buffers-> '      TO PP103-132.
           PERFORM WRITE-OUTPUT.
           MOVE 'Filename  Method     Type    Filena' TO PP3-37.
           MOVE 'me  System  Pool  RLS    Table Type' TO PP38-72.
           MOVE '     Poolname    Name        S'      TO PP73-102.
           MOVE 'tatus      ings  Index   Data '      TO PP103-132.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP3-37
                           PP38-72
                           PP73-102.
           MOVE '_____________________________ '      TO PP103-132.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.
           MOVE 'N'     TO SUB-HEADINGS.

       FILE-SUB-HEADINGS-1-EXIT.
           EXIT.

      *****************************************************************
      *** FILE-SUB-HEADINGS                                         ***
      *****************************************************************
       FILE-SUB-HEADINGS-2 SECTION.

           MOVE '0' TO PRINT-CONTROL.
           MOVE '             Read     Get Update   ' TO PP3-37.
           MOVE ' Browse      Browse       Add      ' TO PP38-72.
           MOVE '  Update      Delete     RLS R'      TO PP73-102.
           MOVE 'eq.   <--- String Waits --->  '      TO PP103-132.
           PERFORM WRITE-OUTPUT.
           MOVE 'Filename   Requests    Requests    ' TO PP3-37.
           MOVE 'Requests     Updates    Requests   ' TO PP38-72.
           MOVE ' Requests    Requests    Timeo'      TO PP73-102.
           MOVE 'uts      Total        HWM     '      TO PP103-132.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP3-37
                           PP38-72
                           PP73-102.
           MOVE '____________________________  '      TO PP103-132.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.
           MOVE 'N'     TO SUB-HEADINGS.

       FILE-SUB-HEADINGS-2-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** DSNAME-STATISTICS                                         ***
      ***                                                           ***
      ***   Print heading and data set names.                       ***
      ***                                                           ***
      *****************************************************************
       DSNAME-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO DSNAMES-PAGENO.

           MOVE '-'     TO PRINT-CONTROL.
           MOVE 'Data Set Names' TO PP1-14.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-14.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.

           EXEC CICS INQUIRE DSNAME START
           END-EXEC.

           EXEC CICS INQUIRE DSNAME(DATA-SET-NAME) NEXT
               ACCESSMETHOD(DSNAME-ACCESS-METHOD)
               AVAILABILITY(DSNAME-AVAILABILITY)
               BASEDSNAME(DSNAME-BASE-DSNAME)
               FILECOUNT(DSNAME-FILE-COUNT)
               FWDRECOVLOG(DSNAME-FWDRECOVLOG)
               FWDRECOVLSN(DSNAME-FWDRECOVLSN)
               OBJECT(DSNAME-OBJECT)
               RECOVSTATUS(DSNAME-RECOVERY-STATUS)
               VALIDITY(DSNAME-VALIDITY)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

               IF LINE-COUNT > LINES-PER-PAGE THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               IF SUB-HEADINGS = 'Y' THEN
                   PERFORM DSNAME-SUB-HEADINGS
               END-IF

               MOVE DATA-SET-NAME TO PP3-46
               EVALUATE DSNAME-ACCESS-METHOD
                   WHEN DFHVALUE(BASE)
                       MOVE 'BDAM' TO PP50-53
                   WHEN DFHVALUE(PATH)
                       MOVE 'VSAM' TO PP50-53
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               EVALUATE DSNAME-OBJECT
                   WHEN DFHVALUE(BASE)
                       MOVE 'Base' TO PP58-61
                   WHEN DFHVALUE(PATH)
                       MOVE 'Path' TO PP58-61
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               EVALUATE DSNAME-VALIDITY
                   WHEN DFHVALUE(VALID)
                       MOVE 'Valid  ' TO PP66-72
                   WHEN DFHVALUE(INVALID)
                       MOVE 'Invalid' TO PP66-72
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               EVALUATE DSNAME-AVAILABILITY
                   WHEN DFHVALUE(AVAILABLE)
                       MOVE 'Available  ' TO PP75-85
                   WHEN DFHVALUE(UNAVAILABLE)
                       MOVE 'Unavailable' TO PP75-85
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               MOVE DSNAME-FILE-COUNT TO PP90-92
               EVALUATE DSNAME-RECOVERY-STATUS
                   WHEN DFHVALUE(FWDRECOVABLE)
                       MOVE 'FwdRecoverable' TO PP96-109
                   WHEN DFHVALUE(NOTRECOVABLE)
                       MOVE 'NotRecoverable' TO PP96-109
                   WHEN DFHVALUE(RECOVERABLE)
                       MOVE 'Recoverable   ' TO PP96-109
                   WHEN DFHVALUE(UNDETERMINED)
                       MOVE 'Undetermined  ' TO PP96-109
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
               PERFORM WRITE-OUTPUT

               EXEC CICS INQUIRE DSNAME(DATA-SET-NAME) NEXT
                   ACCESSMETHOD(DSNAME-ACCESS-METHOD)
                   AVAILABILITY(DSNAME-AVAILABILITY)
                   BASEDSNAME(DSNAME-BASE-DSNAME)
                   FILECOUNT(DSNAME-FILE-COUNT)
                   FWDRECOVLOG(DSNAME-FWDRECOVLOG)
                   FWDRECOVLSN(DSNAME-FWDRECOVLSN)
                   OBJECT(DSNAME-OBJECT)
                   RECOVSTATUS(DSNAME-RECOVERY-STATUS)
                   VALIDITY(DSNAME-VALIDITY)
                   RESP(INQUIRE-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE DSNAME END
           END-EXEC.

       DSNAME-STATISTICS-EXIT.
           EXIT.
           EJECT
       DSNAME-SUB-HEADINGS SECTION.

           MOVE '0' TO PRINT-CONTROL.
           MOVE '                                   ' TO PP3-37.
           MOVE '           Access  Dsname   Dsname ' TO PP38-72.
           MOVE '     Dsname     File      Reco'      TO PP73-102.
           MOVE 'very                          '      TO PP103-132.
           PERFORM WRITE-OUTPUT.
           MOVE 'Data Set Name                      ' TO PP3-37.
           MOVE '           Method  Object  Validity' TO PP38-72.
           MOVE '  Availability  Count      Sta'      TO PP73-102.
           MOVE 'tus                           '      TO PP103-132.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP3-37
                           PP38-72
                           PP73-102.
           MOVE '_______                       '      TO PP103-132.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.
           MOVE 'N'     TO SUB-HEADINGS.

       DSNAME-SUB-HEADINGS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** DATA-TABLE-STATISTICS                                     ***
      ***                                                           ***
      ***   Print heading and data table statistics                 ***
      ***                                                           ***
      *****************************************************************
       DATA-TABLE-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO DATA-TABLES-PAGENO.

           MOVE '-'     TO PRINT-CONTROL.
           MOVE 'Data Tables - Requests' TO PP1-22.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-22.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      ***   Print the data table statistics - pass 1.               ***
      *****************************************************************

           MOVE '0'     TO PRINT-CONTROL.

           EXEC CICS INQUIRE FILE START
           END-EXEC.

           EXEC CICS INQUIRE FILE(FILE-NAME) NEXT
               TABLE(DATA-TABLE) REMOTETABLE(REMOTE-TABLE)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

               IF LINE-COUNT > LINES-PER-PAGE THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               IF SUB-HEADINGS = 'Y' THEN
                   PERFORM DATA-TABLE-SUB-HEADINGS-1
               END-IF

               EVALUATE DATA-TABLE
                   WHEN DFHVALUE(NOTTABLE)
                       CONTINUE
                   WHEN DFHVALUE(CICSTABLE)
                       PERFORM PRINT-DATA-TABLE-STATS-1
                   WHEN DFHVALUE(USERTABLE)
                       PERFORM PRINT-DATA-TABLE-STATS-1
                   WHEN DFHVALUE(CFTABLE)
                       PERFORM PRINT-DATA-TABLE-STATS-1
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE

               IF REMOTE-TABLE = DFHVALUE(REMTABLE) THEN
                 PERFORM PRINT-DATA-TABLE-STATS-1
               END-IF

               EXEC CICS INQUIRE FILE(FILE-NAME) NEXT
                   TABLE(DATA-TABLE) REMOTETABLE(REMOTE-TABLE)
                   RESP(INQUIRE-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE FILE END
           END-EXEC.
           EJECT
      *****************************************************************
      *** Print heading and the data table storage statistics.      ***
      *****************************************************************

           PERFORM PAGE-HEADINGS.

           MOVE '-'     TO PRINT-CONTROL.
           MOVE 'Data Tables - Storage' TO PP1-21.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-21.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      ***   Print the data table statistics - pass 2.               ***
      *****************************************************************

           MOVE '0'     TO PRINT-CONTROL.

           EXEC CICS INQUIRE FILE START
           END-EXEC.

           EXEC CICS INQUIRE FILE(FILE-NAME) NEXT
               TABLE(DATA-TABLE)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

               IF LINE-COUNT > LINES-PER-PAGE THEN
                   PERFORM PAGE-HEADINGS
               END-IF

               IF SUB-HEADINGS = 'Y' THEN
                   PERFORM DATA-TABLE-SUB-HEADINGS-2
               END-IF

               EVALUATE DATA-TABLE
                   WHEN DFHVALUE(NOTTABLE)
                       CONTINUE
                   WHEN DFHVALUE(CICSTABLE)
                       PERFORM PRINT-DATA-TABLE-STATS-2
                   WHEN DFHVALUE(USERTABLE)
                       PERFORM PRINT-DATA-TABLE-STATS-2
                   WHEN DFHVALUE(CFTABLE)
                       PERFORM PRINT-DATA-TABLE-STATS-2
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE

               EXEC CICS INQUIRE FILE(FILE-NAME) NEXT
                   TABLE(DATA-TABLE)
                   RESP(INQUIRE-RESP)
               END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE FILE END
           END-EXEC.

           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_'      TO PP1-132.
           PERFORM WRITE-OUTPUT.
           MOVE 'Totals  '   TO PP3-10.
           MOVE STORAGE-ALLOCATED-TOTAL   TO PP39-48.
           MOVE STORAGE-INUSE-TOTAL       TO PP51-60.
           MOVE STORAGE-ALLOCATED-ENTRIES TO PP63-72.
           MOVE STORAGE-INUSE-ENTRIES     TO PP75-84.
           MOVE STORAGE-ALLOCATED-INDEX   TO PP87-96.
           MOVE STORAGE-INUSE-INDEX       TO PP99-108.
           MOVE STORAGE-ALLOCATED-DATA    TO PP111-120.
           MOVE STORAGE-INUSE-DATA        TO PP123-132.
           PERFORM WRITE-OUTPUT.

       DATA-TABLE-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** Print the data table request statistics.                  ***
      *****************************************************************
       PRINT-DATA-TABLE-STATS-1 SECTION.

           EXEC CICS EXTRACT STATISTICS FILE
               RESID(FILE-NAME)
               SET(ADDRESS OF DFHA17DS)
           END-EXEC.

           MOVE FILE-NAME      TO PP3-10.

           MOVE A17DTRDS       TO PP13-22.
           MOVE A17DTRNF       TO PP25-34.
           MOVE A17DTAVR       TO PP37-46.
           MOVE A17DTADS       TO PP49-58.
           MOVE A17DTARJ       TO PP61-70.
           MOVE A17DTATF       TO PP73-82.
           MOVE A17DTRWS       TO PP85-94.
           MOVE A17DTDLS       TO PP97-106.
           MOVE A17DTRRS       TO PP109-118.
           MOVE A17DTCON       TO PP121-130.
           PERFORM WRITE-OUTPUT.

       PRINT-DATA-TABLE-STATS-1-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** Print the data table storage statistics.                  ***
      *****************************************************************
       PRINT-DATA-TABLE-STATS-2 SECTION.

           EXEC CICS EXTRACT STATISTICS FILE
               RESID(FILE-NAME)
               SET(ADDRESS OF DFHA17DS)
           END-EXEC.

           MOVE FILE-NAME      TO PP3-10.
           EVALUATE DATA-TABLE
               WHEN DFHVALUE(CFTABLE)
                   MOVE '  CF' TO PP12-15
               WHEN DFHVALUE(CICSTABLE)
                   MOVE 'CICS' TO PP12-15
               WHEN DFHVALUE(USERTABLE)
                   MOVE 'USER' TO PP12-15
               WHEN OTHER
                   MOVE '????' TO PP12-15
           END-EVALUATE.

           MOVE A17DTSIZ       TO PP17-26.
           MOVE A17DTSHI       TO PP28-37.
           MOVE A17DTALT       TO PP39-48.
           MOVE A17DTUST       TO PP51-60.
           MOVE A17DTALE       TO PP63-72.
           MOVE A17DTUSE       TO PP75-84.
           MOVE A17DTALI       TO PP87-96.
           MOVE A17DTUSI       TO PP99-108.
           MOVE A17DTALD       TO PP111-120.
           MOVE A17DTUSD       TO PP123-132.
           PERFORM WRITE-OUTPUT.

           ADD A17DTALT        TO STORAGE-ALLOCATED-TOTAL.
           ADD A17DTUST        TO STORAGE-INUSE-TOTAL.
           ADD A17DTALE        TO STORAGE-ALLOCATED-ENTRIES.
           ADD A17DTUSE        TO STORAGE-INUSE-ENTRIES.
           ADD A17DTALI        TO STORAGE-ALLOCATED-INDEX.
           ADD A17DTUSI        TO STORAGE-INUSE-INDEX.
           ADD A17DTALD        TO STORAGE-ALLOCATED-DATA.
           ADD A17DTUSD        TO STORAGE-INUSE-DATA.
       PRINT-DATA-TABLE-STATS-2-EXIT.
           EXIT.
           EJECT
       DATA-TABLE-SUB-HEADINGS-1 SECTION.

           MOVE '0' TO PRINT-CONTROL.
           MOVE '          Successful    Records    ' TO PP3-37.
           MOVE 'Adds via    Adds via       Adds    ' TO PP38-72.
           MOVE '   Adds      Rewrite      Dele'      TO PP73-102.
           MOVE 'te       Read     Chng Resp/  '      TO PP103-132.
           PERFORM WRITE-OUTPUT.
           MOVE 'Filename     Reads     Not Found   ' TO PP3-37.
           MOVE '  Read         API       Rejected  ' TO PP38-72.
           MOVE '   Full      Requests    Reque'      TO PP73-102.
           MOVE 'sts     Retries   Lock Waits  '      TO PP103-132.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP3-132.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.
           MOVE 'N'     TO SUB-HEADINGS.

       DATA-TABLE-SUB-HEADINGS-1-EXIT.
           EXIT.

       DATA-TABLE-SUB-HEADINGS-2 SECTION.

           MOVE '0' TO PRINT-CONTROL.
           MOVE ' <------- Total ------>  <------ En' TO PP38-72.
           MOVE 'tries ----->  <------- Index -'      TO PP73-102.
           MOVE '----->  <------- Data ------->'      TO PP103-132.
           PERFORM WRITE-OUTPUT.
           MOVE '                Current      Peak  ' TO PP3-37.
           MOVE '   Storage    Storage      Storage ' TO PP38-72.
           MOVE '   Storage      Storage    Sto'      TO PP73-102.
           MOVE 'rage      Storage    Storage  '      TO PP103-132.
           PERFORM WRITE-OUTPUT.
           MOVE 'Filename Type   Records    Records ' TO PP3-37.
           MOVE '  Allocated    In-Use     Allocated' TO PP38-72.
           MOVE '    In-Use     Allocated    In'      TO PP73-102.
           MOVE '-Use     Allocated    In-Use  '      TO PP103-132.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP3-132.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.
           MOVE 'N'     TO SUB-HEADINGS.

       DATA-TABLE-SUB-HEADINGS-2-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** DATA-TABLE-STATISTICS                                     ***
      ***                                                           ***
      ***   Print heading and no data table statistics              ***
      ***                                                           ***
      *****************************************************************
       NO-DATA-TABLE-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO DATA-TABLES-PAGENO.

           MOVE '-'           TO PRINT-CONTROL.
           MOVE 'Data Tables' TO PP1-11.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-11.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.
           MOVE 'There are no Data Table statistics.' TO PP3-37.
           PERFORM WRITE-OUTPUT.

       NO-DATA-TABLE-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** CFDTPOOL-STATISTICS                                       ***
      ***                                                           ***
      ***   Print heading and Coupling Facility Data Table pools.   ***
      ***                                                           ***
      *****************************************************************
       CFDTPOOL-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO CFDT-POOLS-PAGENO.

           MOVE '-'     TO PRINT-CONTROL.
           MOVE 'Coupling Facility Data Table Pools' TO PP1-34.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-34.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      ***  Print the Coupling Facility Data Table Pools.            ***
      *****************************************************************
           MOVE '0'     TO PRINT-CONTROL.

           EXEC CICS INQUIRE CFDTPOOL START
           END-EXEC.

           EXEC CICS INQUIRE CFDTPOOL(CFDT-POOL-NAME) NEXT
               CONNSTATUS(CFDTPOOL-CONN-STATUS)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

                   IF LINE-COUNT > LINES-PER-PAGE THEN
                       PERFORM PAGE-HEADINGS
                   END-IF

                   MOVE '0' TO PRINT-CONTROL
                   MOVE 'Coupling Facility Data Table Pool . :'
                                       TO PP3-39
                   MOVE CFDT-POOL-NAME TO PP42-49
                   MOVE 'Connection Status. . . . :' TO PP58-83
                   EVALUATE CFDTPOOL-CONN-STATUS
                       WHEN DFHVALUE(CONNECTED)
                           MOVE 'CONNECTED  ' TO PP86-96
                       WHEN DFHVALUE(UNCONNECTED)
                           MOVE 'UNCONNECTED' TO PP86-96
                       WHEN DFHVALUE(UNAVAILABLE)
                           MOVE 'UNAVAILABLE' TO PP86-96
                       WHEN OTHER
                           CONTINUE
                   END-EVALUATE
                   PERFORM WRITE-OUTPUT

                   EXEC CICS INQUIRE CFDTPOOL(CFDT-POOL-NAME) NEXT
                       CONNSTATUS(CFDTPOOL-CONN-STATUS)
                       RESP(INQUIRE-RESP)
                   END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE CFDTPOOL END
           END-EXEC.

       CFDTPOOL-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** DB2-STATISTICS                                            ***
      ***                                                           ***
      *** Print heading and DB2 Connection statistics.              ***
      ***                                                           ***
      ***                                                           ***
      *****************************************************************
       DB2-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO DB2-CONNECTION-PAGENO.

      *****************************************************************
      *** Print heading and DB2 Connection statistics.              ***
      *****************************************************************
           MOVE '-'     TO PRINT-CONTROL.
           MOVE 'DB2 Connection' TO PP1-14.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-14.
           PERFORM WRITE-OUTPUT.

           EXEC CICS INQUIRE DB2CONN
               CONNECTERROR(DB2-CONNECT-ERROR)
               CONNECTST(DB2-CONNECT-STATUS)
               DROLLBACK(DB2-DROLLBACK)
               MSGQUEUE1(DB2-MSG-QUEUE1)
               MSGQUEUE2(DB2-MSG-QUEUE2)
               MSGQUEUE3(DB2-MSG-QUEUE3)
               NONTERMREL(DB2-NON-TERMREL)
               PURGECYCLEM(DB2-PURGE-CYCLEM)
               PURGECYCLES(DB2-PURGE-CYCLES)
               RESYNCMEMBER(DB2-RESYNCMEMBER)
               SIGNID(DB2-SIGNID)
               STANDBYMODE(DB2-STANDBY-MODE)
               STATSQUEUE(DB2-STATS-QUEUE)
               THREADERROR(DB2-THREAD-ERROR)
               NOHANDLE
           END-EXEC.

           IF EIBRESP NOT = DFHRESP(NORMAL) THEN
               GO TO DB2-STATISTICS-EXIT
           END-IF.

      *****************************************************************
      *** Collect the related Dispatcher statistics.                ***
      *****************************************************************
           EXEC CICS EXTRACT STATISTICS DISPATCHER
               SET(ADDRESS OF DFHDSGDS)
           END-EXEC.

      *****************************************************************
      *** We've got a DB2 Connection, so collect the DB2 Connection ***
      *** Global statistics and print the information collected.    ***
      *****************************************************************
           EXEC CICS EXTRACT STATISTICS DB2CONN
               SET(ADDRESS OF DFHD2GDS)
               NOHANDLE
           END-EXEC.

           MOVE '0'    TO PRINT-CONTROL.
           MOVE 'DB2 Connection Name. . . . . . . . . . . :' TO PP3-44.
           MOVE D2G-DB2CONN-NAME TO PP47-54.
           PERFORM WRITE-OUTPUT.
           MOVE 'DB2 Group Id . . . . . . . . . . . . . . :' TO PP3-44.
           MOVE D2G-DB2-GROUP-ID TO PP47-50.
           MOVE 'Resyncmember. . . . . . . . . . . . . . . . :'
                                 TO PP68-112.
           EVALUATE(DB2-RESYNCMEMBER)
               WHEN DFHVALUE(RESYNC)
                   MOVE 'YES' TO PP115-117X
               WHEN DFHVALUE(NORESYNC)
                   MOVE 'NO ' TO PP115-117X
               WHEN DFHVALUE(NOTAPPLIC)
                   MOVE 'N/A' TO PP115-117X
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           PERFORM WRITE-OUTPUT.

           MOVE 'DB2 Sysid. . . . . . . . . . . . . . . . :' TO PP3-44.
           MOVE D2G-DB2-ID TO PP47-50.
           PERFORM WRITE-OUTPUT.
           MOVE 'DB2 Release. . . . . . . . . . . . . . . :' TO PP3-44.
           IF D2G-DB2-RELEASE NOT = LOW-VALUES
               MOVE D2G-DB2-RELEASE TO PP47-52N
               INSPECT PP47-52N REPLACING ALL ' ' BY '.'
               INSPECT PP47-52N REPLACING LEADING '0' BY ' '
           END-IF.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.
           MOVE 'DB2 Connection Status. . . . . . . . . . :' TO PP3-44.
           EVALUATE DB2-CONNECT-STATUS
               WHEN DFHVALUE(CONNECTED)
                   MOVE 'CONNECTED    ' TO PP47-59
               WHEN DFHVALUE(NOTCONNECTED)
                   MOVE 'NOTCONNECTED ' TO PP47-59
               WHEN DFHVALUE(CONNECTING)
                   MOVE 'CONNECTING   ' TO PP47-59
               WHEN DFHVALUE(DISCONNING)
                   MOVE 'DISCONNECTING' TO PP47-59
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           IF DB2-CONNECT-STATUS = DFHVALUE(CONNECTED) OR
                                           DFHVALUE(DISCONNING) THEN
               MOVE 'DB2 Connect Date and Time . . . :' TO PP68-100
               MOVE D2G-CONNECT-TIME-LOCAL TO CLOCK-FIELD
               PERFORM TIMESTAMP-CONVERT
               MOVE MMDDYYYY  TO PP103-112
               MOVE STOPWATCH TO PP115-128
           END-IF.
           PERFORM WRITE-OUTPUT.
           MOVE 'DB2 Connection Error . . . . . . . . . . :' TO PP3-44.
           EVALUATE DB2-CONNECT-ERROR
               WHEN DFHVALUE(ABEND)
                   MOVE 'ABEND  ' TO PP47-53
               WHEN DFHVALUE(SQLCODE)
                   MOVE 'SQLCODE' TO PP47-53
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           PERFORM WRITE-OUTPUT.
           MOVE 'DB2 Standby Mode . . . . . . . . . . . . :' TO PP3-44.
           EVALUATE DB2-STANDBY-MODE
               WHEN DFHVALUE(NOCONNECT)
                   MOVE 'NOCONNECT' TO PP47-55
               WHEN DFHVALUE(CONNECT)
                   MOVE 'CONNECT  ' TO PP47-55
               WHEN DFHVALUE(RECONNECT)
                   MOVE 'RECONNECT' TO PP47-55
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.
           MOVE 'DB2 Pool Thread Plan Name. . . . . . . . :' TO PP3-44.
           IF D2G-POOL-PLAN-NAME NOT = LOW-VALUES THEN
             MOVE D2G-POOL-PLAN-NAME TO PP47-54
           END-IF.
           PERFORM WRITE-OUTPUT.
           MOVE 'DB2 Pool Thread Dynamic Plan Exit Name . :' TO PP3-44.
           IF D2G-POOL-PLANEXIT-NAME NOT = LOW-VALUES THEN
               MOVE D2G-POOL-PLANEXIT-NAME TO PP47-54

               EXEC CICS INQUIRE PROGRAM(D2G-POOL-PLANEXIT-NAME)
                   APIST(PROGRAM-API-STATUS)
                   CONCURRENCY(PROGRAM-CONCURRENCY)
                   LIBRARY(PROGRAM-LIBRARY-NAME)
                   LIBRARYDSN(PROGRAM-LIBRARY-DSNAME)
                   NOHANDLE
               END-EXEC

               IF EIBRESP = DFHRESP(NORMAL) THEN
                   MOVE 'Dynamic Plan Exit Concurrency Status. . . . :'
                                         TO PP68-112
                   EVALUATE PROGRAM-CONCURRENCY
                       WHEN DFHVALUE(QUASIRENT)
                           MOVE 'Quasirent ' TO PP115-124X
                       WHEN DFHVALUE(THREADSAFE)
                           MOVE 'Threadsafe' TO PP115-124X
                       WHEN OTHER
                           CONTINUE
                   END-EVALUATE
               END-IF
           END-IF.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.
           MOVE 'Pool Thread Authtype . . . . . . . . . . :' TO PP3-44.
           EVALUATE TRUE
               WHEN D2G-POOL-AUTHTYPE-NA
                   MOVE 'N/A   ' TO PP47-52X
               WHEN D2G-POOL-AUTHTYPE-USERID
                   MOVE 'USERID' TO PP47-52X
               WHEN D2G-POOL-AUTHTYPE-OPID
                   MOVE 'OPID  ' TO PP47-52X
               WHEN D2G-POOL-AUTHTYPE-GROUP
                   MOVE 'GROUP ' TO PP47-52X
               WHEN D2G-POOL-AUTHTYPE-SIGNID
                   MOVE 'SIGNID' TO PP47-52X
               WHEN D2G-POOL-AUTHTYPE-TERM
                   MOVE 'TERM  ' TO PP47-52X
               WHEN D2G-POOL-AUTHTYPE-TXID
                   MOVE 'TXID  ' TO PP47-52X
           END-EVALUATE.
           MOVE 'Command Thread Authtype . . . . . . . . . . :'
                                 TO PP68-112.
           EVALUATE TRUE
               WHEN D2G-COMD-AUTHTYPE-NA
                   MOVE 'N/A   ' TO PP115-120
               WHEN D2G-COMD-AUTHTYPE-USERID
                   MOVE 'USERID' TO PP115-120
               WHEN D2G-COMD-AUTHTYPE-OPID
                   MOVE 'OPID  ' TO PP115-120
               WHEN D2G-COMD-AUTHTYPE-GROUP
                   MOVE 'GROUP ' TO PP115-120
               WHEN D2G-COMD-AUTHTYPE-SIGNID
                   MOVE 'SIGNID' TO PP115-120
               WHEN D2G-COMD-AUTHTYPE-TERM
                   MOVE 'TERM  ' TO PP115-120
               WHEN D2G-COMD-AUTHTYPE-TXID
                   MOVE 'TXID  ' TO PP115-120
           END-EVALUATE.
           PERFORM WRITE-OUTPUT.

           MOVE 'Pool Thread Authid . . . . . . . . . . . :' TO PP3-44.
           IF D2G-POOL-AUTHID NOT = LOW-VALUES THEN
               MOVE D2G-POOL-AUTHID TO PP47-54
           END-IF.
           MOVE 'Command Thread Authid . . . . . . . . . . . :'
                                  TO PP68-112.
           IF D2G-COMD-AUTHID NOT = LOW-VALUES THEN
               MOVE D2G-COMD-AUTHID TO PP115-122
           END-IF.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.
           MOVE 'Signid for Pool/Entry/Command Threads. . :' TO PP3-44.
           MOVE DB2-SIGNID      TO PP47-54.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.
           MOVE 'Create Thread Error. . . . . . . . . . . :' TO PP3-44.
           EVALUATE DB2-THREAD-ERROR
               WHEN DFHVALUE(ABEND)
                   MOVE 'ABEND' TO PP47-51
               WHEN DFHVALUE(N906D)
                   MOVE 'N906D' TO PP47-51
               WHEN DFHVALUE(N906)
                   MOVE 'N906 ' TO PP47-51
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           MOVE 'Message TD Queue 1. . . . . . . . . . . . . :'
                                 TO PP68-112.
           MOVE DB2-MSG-QUEUE1   TO PP115-118.
           PERFORM WRITE-OUTPUT.
           MOVE 'Protected Thread Purge Cycle . . . . . . :' TO PP3-44.
           MOVE DB2-PURGE-CYCLEM TO PP47-48.
           MOVE '.'              TO PP49.
           MOVE DB2-PURGE-CYCLES TO PP50-51.
           MOVE 'Message TD Queue 2. . . . . . . . . . . . . :'
                                 TO PP68-112.
           MOVE DB2-MSG-QUEUE2   TO PP115-118.
           PERFORM WRITE-OUTPUT.
           MOVE 'Deadlock Resolution. . . . . . . . . . . :' TO PP3-44.
           EVALUATE DB2-DROLLBACK
               WHEN DFHVALUE(ROLLBACK)
                   MOVE 'ROLLBACK  ' TO PP47-56X
               WHEN DFHVALUE(NOROLLBACK)
                   MOVE 'NOROLLBACK' TO PP47-56X
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           MOVE 'Message TD Queue 3. . . . . . . . . . . . . :'
                                 TO PP68-112.
           MOVE DB2-MSG-QUEUE3   TO PP115-118.
           PERFORM WRITE-OUTPUT.
           MOVE 'Non-Terminal Intermediate Syncpoint. . . :' TO PP3-44.
           EVALUATE DB2-NON-TERMREL
               WHEN DFHVALUE(RELEASE)
                   MOVE 'RELEASE  ' TO PP47-55
               WHEN DFHVALUE(NORELEASE)
                   MOVE 'NORELEASE' TO PP47-55
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           PERFORM WRITE-OUTPUT.
           MOVE 'Pool Thread Wait Setting . . . . . . . . :' TO PP3-44.
           EVALUATE TRUE
               WHEN D2G-THREADWAIT-YES
                   MOVE 'WAIT ' TO PP47-51
               WHEN D2G-THREADWAIT-NO
                   MOVE 'ABEND' TO PP47-51
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           MOVE 'Statistics TD Queue . . . . . . . . . . . . :'
                                 TO PP68-112.
           MOVE DB2-STATS-QUEUE  TO PP115-118.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.
           MOVE 'Pool Thread Priority . . . . . . . . . . :' TO PP3-44.
           EVALUATE TRUE
               WHEN D2G-POOL-PRIORITY-NA
                   MOVE 'N/A  ' TO PP47-51
               WHEN D2G-POOL-PRIORITY-HIGH
                   MOVE 'HIGH ' TO PP47-51
               WHEN D2G-POOL-PRIORITY-EQUAL
                   MOVE 'EQUAL' TO PP47-51
               WHEN D2G-POOL-PRIORITY-LOW
                   MOVE 'LOW  ' TO PP47-51
           END-EVALUATE.
           MOVE 'DB2 Accounting records by . . . . . . . . . :'
                                 TO PP68-112.
           EVALUATE TRUE
               WHEN D2G-POOL-ACCOUNTREC-NONE
                   MOVE 'NONE' TO PP115-118
               WHEN D2G-POOL-ACCOUNTREC-TXID
                   MOVE 'TXID' TO PP115-118
               WHEN D2G-POOL-ACCOUNTREC-TASK
                   MOVE 'TASK' TO PP115-118
               WHEN D2G-POOL-ACCOUNTREC-UOW
                   MOVE 'UOW ' TO PP115-118
           END-EVALUATE.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.
           MOVE 'Current Connection Limit (TCB Limit) . . :' TO PP3-44.
           MOVE D2G-TCB-LIMIT   TO PP47-56.
           MOVE 'Thread Reuselimit . . . . . . . . . . . . . :'
                                 TO PP68-112.
           MOVE D2G-REUSELIMIT          TO PP115-124.
           PERFORM WRITE-OUTPUT.
           MOVE 'Current number of Connections with a TCB :' TO PP3-44.
           MOVE D2G-TCB-CURRENT TO PP47-56.
           MOVE 'Current number of Connections without a TCB :'
                                        TO PP68-112.
           MOVE D2G-TCB-FREE            TO PP115-124.
           PERFORM WRITE-OUTPUT.
           MOVE 'Peak number of Connections with a TCB. . :' TO PP3-44.
           MOVE D2G-TCB-HWM     TO PP47-56.
           MOVE 'Current number of Connections with pthreads :'
                                          TO PP68-112.
           MOVE D2G-TCB-PROTECTED-CURRENT TO PP115-124.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.
           MOVE 'Current number of tasks on Conn Readyq . :' TO PP3-44.
           MOVE D2G-TCB-READYQ-CURRENT   TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Peak number of tasks on Conn Readyq. . . :' TO PP3-44.
           MOVE D2G-TCB-READYQ-HWM       TO PP47-56.
           PERFORM WRITE-OUTPUT.

           MOVE '0'     TO PRINT-CONTROL.
           MOVE 'Pool Thread Limit. . . . . . . . . . . . :' TO PP3-44.
           MOVE D2G-POOL-THREAD-LIMIT   TO PP47-56.
           MOVE 'Number of Calls using Pool Threads. . . . . :'
                                         TO PP68-112.
           MOVE D2G-POOL-CALLS           TO PP115-124.
           PERFORM WRITE-OUTPUT.
           MOVE 'Current number of Pool Threads . . . . . :' TO PP3-44.
           MOVE D2G-POOL-THREAD-CURRENT  TO PP47-56.
           MOVE 'Number of Pool Thread Signons . . . . . . . :'
                                         TO PP68-112.
           MOVE D2G-POOL-SIGNONS         TO PP115-124.
           PERFORM WRITE-OUTPUT.
           MOVE 'Peak number of Pool Threads. . . . . . . :' TO PP3-44.
           MOVE D2G-POOL-THREAD-HWM      TO PP47-56.
           MOVE 'Number of Pool Thread Partial Signons . . . :'
                                         TO PP68-112.
           MOVE D2G-POOL-PARTIAL-SIGNONS TO PP115-124.
           PERFORM WRITE-OUTPUT.
           MOVE 'Number of Pool Thread Waits. . . . . . . :' TO PP3-44.
           MOVE D2G-POOL-THREAD-WAITS    TO PP47-56.
           MOVE 'Number of Pool Thread Commits . . . . . . . :'
                                         TO PP68-112.
           MOVE D2G-POOL-COMMITS         TO PP115-124.
           PERFORM WRITE-OUTPUT.
           MOVE 'Number of Pool Thread Aborts. . . . . . . . :'
                                         TO PP68-112.
           MOVE D2G-POOL-ABORTS          TO PP115-124.
           PERFORM WRITE-OUTPUT.
           MOVE 'Current number of Pool Tasks . . . . . . :' TO PP3-44.
           MOVE D2G-POOL-TASK-CURRENT    TO PP47-56.
           MOVE 'Number of Pool Thread Single Phase. . . . . :'
                                         TO PP68-112.
           MOVE D2G-POOL-SINGLE-PHASE    TO PP115-124.
           PERFORM WRITE-OUTPUT.
           MOVE 'Peak number of Pool Tasks. . . . . . . . :' TO PP3-44.
           MOVE D2G-POOL-TASK-HWM        TO PP47-56.
           MOVE 'Number of Pool Thread Creates . . . . . . . :'
                                         TO PP68-112.
           MOVE D2G-POOL-THREAD-CREATE   TO PP115-124.
           PERFORM WRITE-OUTPUT.
           MOVE 'Current Total number of Pool Tasks . . . :' TO PP3-44.
           COMPUTE RESULT =
                   (D2G-POOL-TASK-CURRENT + D2G-POOL-TASK-TOTAL)
           END-COMPUTE.
           MOVE RESULT                   TO PP47-56.
           MOVE 'Number of Pool Thread Reuses. . . . . . . . :'
                                         TO PP68-112.
           MOVE D2G-POOL-THREAD-REUSE    TO PP115-124.
           PERFORM WRITE-OUTPUT.

           MOVE 'Number of Pool Thread Terminates. . . . . . :'
                                         TO PP68-112.
           MOVE D2G-POOL-THREAD-TERM     TO PP115-124.
           PERFORM WRITE-OUTPUT.

           MOVE 'Current number of Tasks on Pool Readyq . :' TO PP3-44.
           MOVE D2G-POOL-READYQ-CURRENT  TO PP47-56.
           MOVE 'Times reuselimit hit by a pool thread . . . :'
                                 TO PP68-112.
           MOVE D2G-POOL-REUSELIMIT-COUNT TO PP115-124.
           PERFORM WRITE-OUTPUT.

           MOVE 'Peak number of Tasks on Pool Readyq. . . :' TO PP3-44.
           MOVE D2G-POOL-READYQ-HWM      TO PP47-56.
           MOVE 'Number of DSNC Command Calls. . . . . . . . :'
                                            TO PP68-112.
           MOVE D2G-DSNC-COMMAND-CALLS      TO PP115-124.
           PERFORM WRITE-OUTPUT.

           MOVE 'Number of DSNC Command Signons. . . . . . . :'
                                            TO PP68-112.
           MOVE D2G-COMMAND-SIGNONS         TO PP115-124.
           PERFORM WRITE-OUTPUT.

           MOVE 'Current number of DSNC Command threads . :' TO PP3-44.
           MOVE D2G-COMMAND-THREAD-CURRENT  TO PP47-56.
           MOVE 'Number of DSNC Command Thread Creates . . . :'
                                            TO PP68-112.
           MOVE D2G-COMMAND-THREAD-CREATE   TO PP115-124.
           PERFORM WRITE-OUTPUT.

           MOVE 'Peak number of DSNC Command threads. . . :' TO PP3-44.
           MOVE D2G-COMMAND-THREAD-HWM      TO PP47-56.
           MOVE 'Number of DSNC Command Thread Terminates. . :'
                                            TO PP68-112.
           MOVE D2G-COMMAND-THREAD-TERM     TO PP115-124.
           PERFORM WRITE-OUTPUT.

           MOVE 'DSNC Command Thread Limit. . . . . . . . :' TO PP3-44.
           MOVE D2G-COMMAND-THREAD-LIMIT    TO PP47-56.
           MOVE 'Number of DSNC Command Thread Overflows . . :'
                                            TO PP68-112.
           MOVE D2G-COMMAND-THREAD-OVERFLOW TO PP115-124.
           PERFORM WRITE-OUTPUT.

       DB2-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** DB2ENTRY-STATISTICS                                       ***
      ***                                                           ***
      *** Print heading and DB2 Entry statistics.                   ***
      ***                                                           ***
      ***                                                           ***
      *****************************************************************
       DB2ENTRY-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO DB2-ENTRIES-PAGENO.

      *****************************************************************
      *** Print DB2 Entry statistics.                               ***
      *****************************************************************

           EXEC CICS INQUIRE DB2ENTRY START
           END-EXEC.

           EXEC CICS INQUIRE DB2ENTRY(DB2ENTRY-NAME) NEXT
               DISABLEDACT(DB2ENTRY-DISABLED-ACT)
               DROLLBACK(DB2ENTRY-DROLLBACK)
               ENABLESTATUS(DB2ENTRY-STATUS)
               RESP(INQUIRE-RESP)
           END-EXEC.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

                   IF LINE-COUNT > (LINES-PER-PAGE - 25) THEN
                       PERFORM PAGE-HEADINGS
                   END-IF

                   IF SUB-HEADINGS = 'Y' THEN
                       MOVE '-' TO PRINT-CONTROL
                       MOVE 'DB2 Entries' TO PP1-11
                       PERFORM WRITE-OUTPUT
                       MOVE '+' TO PRINT-CONTROL
                       MOVE ALL '_' TO PP1-11
                       PERFORM WRITE-OUTPUT

                       MOVE 'N' TO SUB-HEADINGS
                   END-IF

                   EXEC CICS EXTRACT STATISTICS DB2ENTRY
                       RESID(DB2ENTRY-NAME)
                       SET(ADDRESS OF DFHD2RDS)
                       NOHANDLE
                   END-EXEC

                   IF EIBRESP NOT = DFHRESP(NORMAL) THEN
                       GO TO DB2-STATISTICS-EXIT
                   END-IF

                   MOVE '0' TO PRINT-CONTROL
                   MOVE 'DB2Entry Name. . . . . . . . . . . . . . :'
                                                 TO PP3-44
                   MOVE DB2ENTRY-NAME            TO PP47-54
                   MOVE 'DB2Entry Status . . . . . . . . . . . . . . :'
                                                 TO PP68-112
                   EVALUATE DB2ENTRY-STATUS
                       WHEN DFHVALUE(DISABLING)
                           MOVE 'DISABLING '     TO PP115-124X
                       WHEN DFHVALUE(DISABLED)
                           MOVE 'DISABLED'       TO PP115-122
                       WHEN DFHVALUE(ENABLED)
                           MOVE 'ENABLED '       TO PP115-122
                       WHEN OTHER
                           CONTINUE
                   END-EVALUATE
                   PERFORM WRITE-OUTPUT
                   MOVE 'DB2Entry Static Plan Name. . . . . . . . :'
                                                 TO PP3-44
                   IF D2R-PLAN-NAME NOT = LOW-VALUES THEN
                     MOVE D2R-PLAN-NAME          TO PP47-54
                   END-IF
                   MOVE 'DB2Entry Disabled Action. . . . . . . . . . :'
                                                 TO PP68-112
                   EVALUATE DB2ENTRY-DISABLED-ACT
                       WHEN DFHVALUE(ABEND)
                           MOVE 'ABEND   '       TO PP115-122
                       WHEN DFHVALUE(SQLCODE)
                           MOVE 'SQLCODE '       TO PP115-122
                       WHEN DFHVALUE(POOL)
                           MOVE 'POOL    '       TO PP115-122
                       WHEN OTHER
                           CONTINUE
                   END-EVALUATE
                   PERFORM WRITE-OUTPUT
                   MOVE 'DB2Entry Dynamic Plan Exit Name. . . . . :'
                                                 TO PP3-44
                   IF D2R-PLANEXIT-NAME NOT = LOW-VALUES THEN
                     MOVE D2R-PLANEXIT-NAME      TO PP47-54
                   END-IF
                   MOVE 'DB2Entry Deadlock Resolution. . . . . . . . :'
                                                 TO PP68-112
                   EVALUATE DB2ENTRY-DROLLBACK
                       WHEN DFHVALUE(ROLLBACK)
                           MOVE 'ROLLBACK  '     TO PP115-124X
                       WHEN DFHVALUE(NOROLLBACK)
                           MOVE 'NOROLLBACK'     TO PP115-124X
                       WHEN OTHER
                           CONTINUE
                   END-EVALUATE
                   PERFORM WRITE-OUTPUT

                   MOVE 'Dynamic Plan Exit Concurrency Status . . :'
                                                 TO PP3-44
                   IF D2R-PLANEXIT-NAME NOT = LOW-VALUES THEN
                       EXEC CICS INQUIRE PROGRAM(D2R-PLANEXIT-NAME)
                           APIST(PROGRAM-API-STATUS)
                           CONCURRENCY(PROGRAM-CONCURRENCY)
                           LIBRARY(PROGRAM-LIBRARY-NAME)
                           LIBRARYDSN(PROGRAM-LIBRARY-DSNAME)
                           NOHANDLE
                       END-EXEC

                       IF EIBRESP = DFHRESP(NORMAL) THEN
                           EVALUATE PROGRAM-CONCURRENCY
                               WHEN DFHVALUE(QUASIRENT)
                                   MOVE 'Quasirent ' TO PP47-56X
                               WHEN DFHVALUE(THREADSAFE)
                                   MOVE 'Threadsafe' TO PP47-56X
                               WHEN OTHER
                                   CONTINUE
                           END-EVALUATE
                       END-IF
                   END-IF
                   PERFORM WRITE-OUTPUT

                   MOVE '0' TO PRINT-CONTROL
                   MOVE 'DB2Entry Authtype. . . . . . . . . . . . :'
                                                 TO PP3-44
                   EVALUATE TRUE
                       WHEN D2R-AUTHTYPE-NA
                           MOVE 'N/A   ' TO PP47-52X
                       WHEN D2R-AUTHTYPE-USERID
                           MOVE 'USERID' TO PP47-52X
                       WHEN D2R-AUTHTYPE-OPID
                           MOVE 'OPID  ' TO PP47-52X
                       WHEN D2R-AUTHTYPE-GROUP
                           MOVE 'GROUP ' TO PP47-52X
                       WHEN D2R-AUTHTYPE-SIGNID
                           MOVE 'SIGNID' TO PP47-52X
                       WHEN D2R-AUTHTYPE-TERM
                           MOVE 'TERM  ' TO PP47-52X
                       WHEN D2R-AUTHTYPE-TXID
                           MOVE 'TXID  ' TO PP47-52X
                   END-EVALUATE
                   MOVE 'DB2Entry Accounting records by. . . . . . . :'
                                                 TO PP68-112
                   EVALUATE TRUE
                       WHEN D2R-ACCOUNTREC-NONE
                           MOVE 'NONE' TO PP115-118
                       WHEN D2R-ACCOUNTREC-TXID
                           MOVE 'TXID' TO PP115-118
                       WHEN D2R-ACCOUNTREC-TASK
                           MOVE 'TASK' TO PP115-118
                       WHEN D2R-ACCOUNTREC-UOW
                           MOVE 'UOW ' TO PP115-118
                   END-EVALUATE
                   PERFORM WRITE-OUTPUT
                   MOVE 'DB2Entry Authid. . . . . . . . . . . . . :'
                                                 TO PP3-44
                   IF D2R-AUTHID NOT = LOW-VALUES THEN
                       MOVE D2R-AUTHID           TO PP47-54
                   END-IF
                   PERFORM WRITE-OUTPUT

                   MOVE 'Number of Calls using DB2Entry. . . . . . . :'
                                                 TO PP68-112
                   MOVE D2R-CALLS                TO PP115-124
                   PERFORM WRITE-OUTPUT
                   MOVE 'DB2Entry Thread Wait Setting . . . . . . :'
                                                 TO PP3-44
                   EVALUATE TRUE
                       WHEN D2R-THREADWAIT-YES
                           MOVE 'WAIT ' TO PP47-51
                       WHEN D2R-THREADWAIT-NO
                           MOVE 'ABEND' TO PP47-51
                       WHEN D2R-THREADWAIT-POOL
                           MOVE 'POOL ' TO PP47-51
                       WHEN OTHER
                           CONTINUE
                   END-EVALUATE
                   MOVE 'Number of DB2Entry Signons. . . . . . . . . :'
                                                 TO PP68-112
                   MOVE D2R-SIGNONS              TO PP115-124
                   PERFORM WRITE-OUTPUT
                   MOVE 'Number of DB2Entry Partial Signons. . . . . :'
                                                 TO PP68-112
                   MOVE D2R-PARTIAL-SIGNONS      TO PP115-124
                   PERFORM WRITE-OUTPUT

                   MOVE 'DB2Entry Thread Priority . . . . . . . . :'
                                                 TO PP3-44
                   EVALUATE TRUE
                       WHEN D2R-PRIORITY-NA
                           MOVE 'N/A  ' TO PP47-51
                       WHEN D2R-PRIORITY-HIGH
                           MOVE 'HIGH ' TO PP47-51
                       WHEN D2R-PRIORITY-EQUAL
                           MOVE 'EQUAL' TO PP47-51
                       WHEN D2R-PRIORITY-LOW
                           MOVE 'LOW  ' TO PP47-51
                   END-EVALUATE
                   MOVE 'Number of DB2Entry Commits. . . . . . . . . :'
                                                 TO PP68-112
                   MOVE D2R-COMMITS              TO PP115-124
                   PERFORM WRITE-OUTPUT

                   MOVE 'DB2Entry Thread Limit. . . . . . . . . . :'
                                                 TO PP3-44
                   MOVE D2R-THREAD-LIMIT         TO PP47-56
                   MOVE 'Number of DB2Entry Aborts . . . . . . . . . :'
                                                 TO PP68-112
                   MOVE D2R-ABORTS               TO PP115-124
                   PERFORM WRITE-OUTPUT

                   MOVE 'Current number of DB2Entry Threads . . . :'
                                                 TO PP3-44
                   MOVE D2R-THREAD-CURRENT       TO PP47-56
                   MOVE 'Number of DB2Entry Single Phase . . . . . . :'
                                                 TO PP68-112
                   MOVE D2R-SINGLE-PHASE         TO PP115-124
                   PERFORM WRITE-OUTPUT
                   MOVE 'Peak number of DB2Entry Threads. . . . . :'
                                                 TO PP3-44
                   MOVE D2R-THREAD-HWM           TO PP47-56
                   MOVE 'Number of DB2Entry Thread Creates . . . . . :'
                                                 TO PP68-112
                   MOVE D2R-THREAD-CREATE        TO PP115-124
                   PERFORM WRITE-OUTPUT

                   MOVE 'Number of DB2Entry Thread Reuses. . . . . . :'
                                                 TO PP68-112
                   MOVE D2R-THREAD-REUSE         TO PP115-124
                   PERFORM WRITE-OUTPUT

                   MOVE 'DB2Entry Protected Thread Limit. . . . . . . .
      -        ':' TO PP3-50
                   MOVE D2R-PTHREAD-LIMIT   TO PP53-62
                   MOVE 'Number of DB2Entry Thread Terminates. . . . :'
                                                 TO PP68-112
                   MOVE D2R-THREAD-TERM          TO PP115-124
                   PERFORM WRITE-OUTPUT

                   MOVE 'Current number of DB2Entry Protected Threads .
      -        ':' TO PP3-50
                   MOVE D2R-PTHREAD-CURRENT TO PP53-62
                   MOVE 'Number of DB2Entry Thread Waits/Overflows . :'
                                                 TO PP68-112
                   MOVE D2R-THREAD-WAIT-OR-OVERF TO PP115-124
                   PERFORM WRITE-OUTPUT

                   MOVE 'Peak number of DB2Entry Protected Threads. . .
      -        ':' TO PP3-50
                   MOVE D2R-PTHREAD-HWM     TO PP53-62
                   MOVE 'Times reuselimit hit by DB2ENTRY thread . . :'
                                                 TO PP68-112
                   MOVE D2R-REUSELIMIT-COUNT TO PP115-124
                   PERFORM WRITE-OUTPUT

                   MOVE '0' TO PRINT-CONTROL
                   MOVE 'Current number of DB2Entry Tasks . . . . . . .
      -        ':' TO PP3-50
                   MOVE D2R-TASK-CURRENT         TO PP53-62
                   PERFORM WRITE-OUTPUT
                   MOVE 'Peak number of DB2Entry Tasks. . . . . . . . .
      -        ':' TO PP3-50
                   MOVE D2R-TASK-HWM             TO PP53-62
                   PERFORM WRITE-OUTPUT
                   MOVE 'Current Total number of DB2Entry Tasks . . . .
      -        ':' TO PP3-50
                   COMPUTE RESULT = (D2R-TASK-CURRENT + D2R-TASK-TOTAL)
                   END-COMPUTE
                   MOVE RESULT                   TO PP53-62
                   PERFORM WRITE-OUTPUT

                   MOVE '0' TO PRINT-CONTROL
                   MOVE 'Current number of Tasks on DB2Entry Readyq . .
      -        ':' TO PP3-50
                   MOVE D2R-READYQ-CURRENT       TO PP53-62
                   PERFORM WRITE-OUTPUT
                   MOVE 'Peak number of Tasks on DB2Entry Readyq. . . .
      -        ':' TO PP3-50
                   MOVE D2R-READYQ-HWM           TO PP53-62
                   PERFORM WRITE-OUTPUT

                   MOVE '0' TO PRINT-CONTROL
                   MOVE ALL '-' TO PP1-132
                   PERFORM WRITE-OUTPUT

                   EXEC CICS INQUIRE DB2ENTRY(DB2ENTRY-NAME) NEXT
                       DISABLEDACT(DB2ENTRY-DISABLED-ACT)
                       ENABLESTATUS(DB2ENTRY-STATUS)
                       DROLLBACK(DB2ENTRY-DROLLBACK)
                       RESP(INQUIRE-RESP)
                   END-EXEC

           END-PERFORM.

           EXEC CICS INQUIRE DB2ENTRY END
           END-EXEC.

       DB2ENTRY-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** MQCONN-STATISTICS                                         ***
      ***                                                           ***
      *** Print heading and WebSphere MQ Connection statistics.     ***
      ***                                                           ***
      ***                                                           ***
      *****************************************************************
       MQCONN-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO MQCONN-PAGENO.

      *****************************************************************
      *** Print heading and WebSphere MQ Connection statistics.     ***
      *****************************************************************
           MOVE '-'     TO PRINT-CONTROL.
           MOVE 'WebSphere MQ Connection' TO PP1-23.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-23.
           PERFORM WRITE-OUTPUT.


      *****************************************************************
      *** Extract the MQCONN statistics.                            ***
      *****************************************************************
           EXEC CICS EXTRACT STATISTICS MQCONN
               SET(ADDRESS OF DFHMQGDS)
               NOHANDLE
           END-EXEC.
      *                                                            @P5A
           IF EIBRESP NOT = DFHRESP(NORMAL) THEN
               MOVE '0'     TO PRINT-CONTROL
               MOVE 'There are no Websphere MQ Connection statistics.
      -    '                   ' TO PP3-72
               PERFORM WRITE-OUTPUT
               GO TO MQCONN-STATISTICS-EXIT
           END-IF.

           MOVE '0'    TO PRINT-CONTROL.
           MOVE 'MQCONN name. . . . . . . . . . . . . . . :' TO PP3-44.
           MOVE MQG-MQCONN-NAME TO PP47-54.
           PERFORM WRITE-OUTPUT.

           MOVE 'WebSphere MQ Connection Status . . . . . :' TO PP3-44.
           EVALUATE TRUE
               WHEN MQG-CONNECTED
                   MOVE 'CONNECTED    ' TO PP47-59
                   MOVE 'Websphere MQ connect date / time:' TO PP68-100
                   MOVE MQG-CONNECT-TIME-LOCAL TO CLOCK-FIELD
                   PERFORM TIMESTAMP-CONVERT
                   MOVE MMDDYYYY  TO PP103-112
                   MOVE STOPWATCH TO PP115-128
               WHEN MQG-NOT-CONNECTED
                   MOVE 'NOT CONNECTED' TO PP47-59
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           PERFORM WRITE-OUTPUT.

           MOVE '0'    TO PRINT-CONTROL.
           MOVE 'Mqname . . . . . . . . . . . . . . . . . :' TO PP3-44.
           MOVE MQG-MQNAME TO PP47-54.
           PERFORM WRITE-OUTPUT.

           MOVE 'WebSphere MQ Queue Manager Name. . . . . :' TO PP3-44.
           MOVE MQG-QMGR-NAME TO PP47-50.
           PERFORM WRITE-OUTPUT.

           MOVE 'Resyncmember . . . . . . . . . . . . . . :' TO PP3-44.
           EVALUATE TRUE
               WHEN MQG-RESYNCMEMBER-RESYNC
                   MOVE 'YES ' TO PP47-50
               WHEN MQG-RESYNCMEMBER-GROUPRESYNC
                   MOVE 'GROUPRESYNC  ' TO PP47-59
               WHEN MQG-RESYNCMEMBER-NORESYNC
                   MOVE 'NO  ' TO PP47-50
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           PERFORM WRITE-OUTPUT.

           MOVE 'WebSphere MQ Release . . . . . . . . . . :' TO PP3-44.
           IF MQG-MQ-RELEASE > '    ' THEN
               IF MQG-MQ-RELEASE(1:1) NOT = '0' THEN
                   MOVE MQG-MQ-RELEASE(1:2) TO PP47-59(1:2)
                   MOVE '.'                 TO PP47-59(3:1)
                   MOVE MQG-MQ-RELEASE(3:2) TO PP47-59(4:2)
               ELSE
                   MOVE MQG-MQ-RELEASE(2:1) TO PP47-59(1:1)
                   MOVE '.'                 TO PP47-59(2:1)
                   MOVE MQG-MQ-RELEASE(3:2) TO PP47-59(3:2)
               END-IF
           END-IF.
           PERFORM WRITE-OUTPUT.

           MOVE 'Initiation Queue Name. . . . . . . . . . :' TO PP3-44.
           MOVE MQG-INITIATION-QUEUE TO PP47-94.
           PERFORM WRITE-OUTPUT.
           MOVE 'Number of current tasks. . . . . . . . . :' TO PP3-44.
           MOVE MQG-Ttasks TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Number of futile attempts. . . . . . . . :' TO PP3-44.
           MOVE MQG-TFutileAtt    TO PP47-56.
           PERFORM WRITE-OUTPUT.

           MOVE '0'    TO PRINT-CONTROL.
           MOVE 'Total number of API calls. . . . . . . . :' TO PP3-44.
           MOVE MQG-TApi          TO PP47-56.
           MOVE 'API Crossing Exit Name. . . . . . . . . . . :'
                                  TO PP68-112.
           MOVE 'CSQCAPX '        TO PP115-122.
           PERFORM WRITE-OUTPUT.
           MOVE 'Number of API calls completed OK . . . . :' TO PP3-44.
           MOVE MQG-TApiOk        TO PP47-56.
           MOVE 'API Crossing Exit Concurrency Status. . . . :'
                                  TO PP68-112.
           EXEC CICS INQUIRE PROGRAM('CSQCAPX ')
               APIST(PROGRAM-API-STATUS)
               CONCURRENCY(PROGRAM-CONCURRENCY)
               LIBRARY(PROGRAM-LIBRARY-NAME)
               LIBRARYDSN(PROGRAM-LIBRARY-DSNAME)
               NOHANDLE
           END-EXEC.

           IF EIBRESP = DFHRESP(NORMAL) THEN
               EVALUATE PROGRAM-CONCURRENCY
                   WHEN DFHVALUE(QUASIRENT)
                       MOVE 'Quasirent ' TO PP115-124X
                   WHEN DFHVALUE(THREADSAFE)
                       MOVE 'Threadsafe' TO PP115-124X
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE
           END-IF.
           PERFORM WRITE-OUTPUT.

           MOVE '0'    TO PRINT-CONTROL.
           MOVE 'Number of OPEN requests. . . . . . . . . :' TO PP3-44.
           MOVE MQG-TOPEN         TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Number of CLOSE requests . . . . . . . . :' TO PP3-44.
           MOVE MQG-TCLOSE        TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Number of GET requests . . . . . . . . . :' TO PP3-44.
           MOVE MQG-TGET          TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE '  Number of GETWAIT requests . . . . . . :' TO PP3-44.
           MOVE MQG-TGETWAIT      TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE '  Number of GETWAITs that waited . . . . :' TO PP3-44.
           MOVE MQG-TWaitMsg      TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Number of PUT requests . . . . . . . . . :' TO PP3-44.
           MOVE MQG-TPUT          TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Number of PUT1 requests. . . . . . . . . :' TO PP3-44.
           MOVE MQG-TPUT1         TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Number of INQ requests . . . . . . . . . :' TO PP3-44.
           MOVE MQG-TINQ          TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Number of SET requests . . . . . . . . . :' TO PP3-44.
           MOVE MQG-TSET          TO PP47-56.
           PERFORM WRITE-OUTPUT.

           MOVE '0'    TO PRINT-CONTROL.
           MOVE 'Number of internal MQ calls. . . . . . . :' TO PP3-44.
           MOVE MQG-TCall         TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Number that completed synchronously. . . :' TO PP3-44.
           MOVE MQG-TCallSyncComp TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Number that needed I/O . . . . . . . . . :' TO PP3-44.
           MOVE MQG-TCallIO       TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Number of calls with TCB switch. . . . . :' TO PP3-44.
           MOVE MQG-TSubtasked    TO PP47-56.
           PERFORM WRITE-OUTPUT.

           MOVE '0'    TO PRINT-CONTROL.
           MOVE 'Number of indoubt units of work. . . . . :' TO PP3-44.
           MOVE MQG-IndoubtUOW    TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Number of unresolved units of work . . . :' TO PP3-44.
           MOVE MQG-UnResolvedUOW TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Number of resolved committed UOWs. . . . :' TO PP3-44.
           MOVE MQG-ResolveComm   TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Number of resolved backout UOWs. . . . . :' TO PP3-44.
           MOVE MQG-ResolveBack   TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Number of Backout UOWs . . . . . . . . . :' TO PP3-44.
           MOVE MQG-TBackUOW      TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Number of Committed UOWs . . . . . . . . :' TO PP3-44.
           MOVE MQG-TCommUOW      TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Number of tasks. . . . . . . . . . . . . :' TO PP3-44.
           MOVE MQG-TTaskend      TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Number of Single Phase Commits . . . . . :' TO PP3-44.
           MOVE MQG-TSPComm       TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Number of Two Phase Commits. . . . . . . :' TO PP3-44.
           MOVE MQG-T2PComm       TO PP47-56.
           PERFORM WRITE-OUTPUT.
           MOVE 'Number of CB requests. . . . . . . . . . :' TO PP3-44.
           MOVE MQG-TCB           TO PP47-56.
           PERFORM WRITE-SPOOL.
           MOVE 'Number of msgs consumed. . . . . . . . . :' TO PP3-44.
           MOVE MQG-TConsume      TO PP47-56.
           PERFORM WRITE-SPOOL.
           MOVE 'Number of CTL requests . . . . . . . . . :' TO PP3-44.
           MOVE MQG-TCTL          TO PP47-56.
           PERFORM WRITE-SPOOL.
           MOVE 'Number of SUB requests . . . . . . . . . :' TO PP3-44.
           MOVE MQG-TSUB          TO PP47-56.
           PERFORM WRITE-SPOOL.
           MOVE 'Number of SUBRQ requests . . . . . . . . :' TO PP3-44.
           MOVE MQG-TSUBRQ        TO PP47-56.
           PERFORM WRITE-SPOOL.
           MOVE 'Number of STAT requests. . . . . . . . . :' TO PP3-44.
           MOVE MQG-TSTAT         TO PP47-56.
           PERFORM WRITE-SPOOL.
           MOVE 'Number of CRTMH requests . . . . . . . . :' TO PP3-44.
           MOVE MQG-TCRTMH        TO PP47-56.
           PERFORM WRITE-SPOOL.
           MOVE 'Number of DLTMH requests . . . . . . . . :' TO PP3-44.
           MOVE MQG-TDLTMH        TO PP47-56.
           PERFORM WRITE-SPOOL.
           MOVE 'Number of SETMP requests . . . . . . . . :' TO PP3-44.
           MOVE MQG-TSETMP        TO PP47-56.
           PERFORM WRITE-SPOOL.
           MOVE 'Number of INQMP requests . . . . . . . . :' TO PP3-44.
           MOVE MQG-TINQMP        TO PP47-56.
           PERFORM WRITE-SPOOL.
           MOVE 'Number of DLTMP requests . . . . . . . . :' TO PP3-44.
           MOVE MQG-TDLTMP        TO PP47-56.
           PERFORM WRITE-SPOOL.
           MOVE 'Number of MHBUF requests . . . . . . . . :' TO PP3-44.
           MOVE MQG-TMHBUF        TO PP47-56.
           PERFORM WRITE-SPOOL.
           MOVE 'Number of BUFMH requests . . . . . . . . :' TO PP3-44.
           MOVE MQG-TBUFMH        TO PP47-56.
           PERFORM WRITE-SPOOL.


       MQCONN-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** MQMONITOR-STATISTICS                                      ***
      ***                                                           ***
      *** Print heading and MQMONITOR statistics.                   ***
      ***                                                           ***
      ***                                                           ***
      *****************************************************************
       MQMONITOR-STATISTICS SECTION.

           PERFORM PAGE-HEADINGS.
           MOVE PAGENO TO MQMONITORS-PAGENO.

           MOVE SPACES  TO PP1-132.
           MOVE '-'     TO PRINT-CONTROL.
           MOVE 'WebSphere MQ Monitors'  TO PP1-21.
           PERFORM WRITE-OUTPUT.
           MOVE '+'     TO PRINT-CONTROL.
           MOVE ALL '_' TO PP1-21.
           PERFORM WRITE-OUTPUT.

      *****************************************************************
      *** Print MQMONITOR statistics.                               ***
      *** If none, then report that there are none.                 ***
      *** Make sure that browse ends.                      @PI94527C***
      *****************************************************************

           EXEC CICS INQUIRE MQMONITOR START
           END-EXEC.

           EXEC CICS INQUIRE MQMONITOR(MQMONITOR-NAME) NEXT
                     AUTOSTATUS(MQMONITOR-AUTOSTATUS)
                     ENABLESTATUS(MQMONITOR-ENABLESTATUS)
                     MONSTATUS(MQMONITOR-MONSTATUS)
                     RESP(INQUIRE-RESP)
           END-EXEC.

           IF EIBRESP NOT = DFHRESP(NORMAL) THEN
               MOVE '0'     TO PRINT-CONTROL
               MOVE 'There are no Websphere MQ Monitor statistics.
      -    '                   ' TO PP3-72
               PERFORM WRITE-OUTPUT
               GO TO MQMONITOR-STATISTICS-ENDBR
           END-IF.

           PERFORM WITH TEST BEFORE
               UNTIL INQUIRE-RESP = DFHRESP(END)

                   EXEC CICS EXTRACT STATISTICS MQMONITOR
                             RESID(MQMONITOR-NAME)
                             SET(ADDRESS OF DFHMQRDS)
                             NOHANDLE
                   END-EXEC

                   IF EIBRESP NOT = DFHRESP(NORMAL) THEN
                       GO TO MQMONITOR-STATISTICS-EXIT
                   END-IF

                   IF LINE-COUNT > (LINES-PER-PAGE - 25) THEN
                      PERFORM PAGE-HEADINGS
                   END-IF

                   MOVE '0' TO PRINT-CONTROL
                   MOVE 'Monitor name . . . . . . . . . . . . . . :'
                                                  TO PP3-44
                   MOVE MQMONITOR-NAME            TO PP47-54
                   PERFORM WRITE-OUTPUT
                   MOVE 'Autostatus . . . . . . . . . . . . . . . :'
                                                 TO PP3-44
                   EVALUATE MQMONITOR-AUTOSTATUS
                       WHEN DFHVALUE(AUTOSTART)
                           MOVE 'YES'            TO PP47-51
                       WHEN OTHER
                           MOVE 'NO '            TO PP47-51
                   END-EVALUATE
                   PERFORM WRITE-OUTPUT
                   MOVE 'Enablestatus . . . . . . . . . . . . . . :'
                                                 TO PP3-44
                   EVALUATE MQMONITOR-ENABLESTATUS
                       WHEN DFHVALUE(ENABLED)
                           MOVE 'ENABLED  '      TO PP47-55
                       WHEN DFHVALUE(ENABLING)
                           MOVE 'ENABLING '      TO PP47-55
                       WHEN DFHVALUE(DISABLED)
                           MOVE 'DISABLED '      TO PP47-56X
                       WHEN DFHVALUE(DISABLING)
                           MOVE 'DISABLING'      TO PP47-56X
                       WHEN DFHVALUE(DISCARDING)
                           MOVE 'DISCARDING'     TO PP47-56X
                       WHEN OTHER
                           CONTINUE
                   END-EVALUATE
                   PERFORM WRITE-OUTPUT

                   MOVE 'Monitor status . . . . . . . . . . . . . :'
                                                 TO PP3-44
                   EVALUATE MQMONITOR-MONSTATUS
                       WHEN DFHVALUE(STARTED)
                           MOVE 'STARTED      '      TO PP47-55
                           MOVE 'Start date / time:' TO PP68-85
                           MOVE MQR-START-TIME-LOCAL TO CLOCK-FIELD
                           PERFORM TIMESTAMP-CONVERT
                            MOVE MMDDYYYY  TO PP88-97
                            MOVE STOPWATCH TO PP100-108
                       WHEN DFHVALUE(STARTING)
                           MOVE 'STARTING'       TO PP47-55
                       WHEN DFHVALUE(STOPPED)
                           MOVE 'STOPPED '       TO PP47-55
                           IF  MQR-STOP-TIME-LOCAL NOT EQUAL LOW-VALUES
                               MOVE 'Stop date / time: '
                                                 TO PP68-85
                               MOVE MQR-STOP-TIME-LOCAL
                                                TO CLOCK-FIELD
                               PERFORM TIMESTAMP-CONVERT
                               MOVE MMDDYYYY  TO PP88-97
                               MOVE STOPWATCH TO PP100-108
                          END-IF
                       WHEN DFHVALUE(STOPPING)
                           MOVE 'STOPPING'       TO PP47-55
                       WHEN OTHER
                           CONTINUE
                   END-EVALUATE
                   PERFORM WRITE-OUTPUT

                   MOVE 'MQ Queue name. . . . . . . . . . . . . . :'
                                                 TO PP3-44
                   MOVE MQR-QNAME                TO PP47-94
                   PERFORM WRITE-OUTPUT

                   MOVE 'Monitor userid . . . . . . . . . . . . . :'
                                                 TO PP3-44
                   MOVE MQR-MONUSERID            TO PP47-55
                   PERFORM WRITE-OUTPUT
                   MOVE 'Userid . . . . . . . . . . . . . . . . . :'
                                                 TO PP3-44
                   MOVE MQR-USERID               TO PP47-55
                   PERFORM WRITE-OUTPUT

                   MOVE 'Transaction Id . . . . . . . . . . . . . :'
                                                 TO PP3-44
                   MOVE MQR-TRANID               TO PP47-55
                   PERFORM WRITE-OUTPUT

                   MOVE 'Task number .  . . . . . . . . . . . . . :'
                                                 TO PP3-44
                   IF MQR-TASKNUM NOT NUMERIC
                       MOVE 0                    TO MQR-TASKNUM
                   END-IF
                   MOVE MQR-TASKNUM              TO PP47-51N
                   PERFORM WRITE-OUTPUT

                   MOVE '0'    TO PRINT-CONTROL
                   MOVE 'Number of OPEN requests. . . . . . . . . :'
                                          TO PP3-44
                   MOVE MQR-TOPEN         TO PP47-56
                   PERFORM WRITE-OUTPUT
                   MOVE 'Number of CLOSE requests . . . . . . . . :'
                                          TO PP3-44
                   MOVE MQR-TCLOSE        TO PP47-56
                   PERFORM WRITE-OUTPUT
                   MOVE 'Number of GET requests . . . . . . . . . :'
                                          TO PP3-44
                   MOVE MQR-TGET          TO PP47-56
                   PERFORM WRITE-OUTPUT
                   MOVE 'Number of GETWAIT requests . . . . . . . :'
                                          TO PP3-44
                   MOVE MQR-TGETWAIT      TO PP47-56
                   PERFORM WRITE-OUTPUT
                   MOVE 'Number of PUT requests . . . . . . . . . :'
                                          TO PP3-44
                   MOVE MQR-TPUT          TO PP47-56
                   PERFORM WRITE-OUTPUT
                   MOVE 'Number of PUT1 requests. . . . . . . . . :'
                                          TO PP3-44
                   MOVE MQR-TPUT1         TO PP47-56
                   PERFORM WRITE-OUTPUT
                   MOVE 'Number of INQ requests . . . . . . . . . :'
                                          TO PP3-44
                   MOVE MQR-TINQ          TO PP47-56
                   PERFORM WRITE-OUTPUT
                   MOVE 'Number of INQL requests. . . . . . . . . :'
                                          TO PP3-44
                   MOVE MQR-TINQL         TO PP47-56
                   PERFORM WRITE-OUTPUT
                   MOVE 'Number of SET requests . . . . . . . . . :'
                                          TO PP3-44
                   MOVE MQR-TSET          TO PP47-56
                   PERFORM WRITE-OUTPUT
                   MOVE 'Number of Committed UOWs . . . . . . . . :'
                                          TO PP3-44
                   MOVE MQR-TCommUOW      TO PP47-56
                   PERFORM WRITE-OUTPUT
                   MOVE 'Number of Backout UOWs . . . . . . . . . :'
                                          TO PP3-44
                   MOVE MQR-TBackUOW      TO PP47-56
                   PERFORM WRITE-OUTPUT
                   MOVE 'Number of OTHER requests . . . . . . . . :'
                                          TO PP3-44
                   MOVE MQR-TOTHER        TO PP47-56
                   PERFORM WRITE-SPOOL

                   EXEC CICS INQUIRE MQMONITOR(MQMONITOR-NAME) NEXT
                             AUTOSTATUS(MQMONITOR-AUTOSTATUS)
                             ENABLESTATUS(MQMONITOR-ENABLESTATUS)
                             MONSTATUS(MQMONITOR-MONSTATUS)
                             RESP(INQUIRE-RESP)
                   END-EXEC

           END-PERFORM.

       MQMONITOR-STATISTICS-ENDBR.

           EXEC CICS INQUIRE MQMONITOR END
           END-EXEC.

       MQMONITOR-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** TOTALS-INITIALISE                                         ***
      *****************************************************************
       TOTALS-INITIALISE SECTION.

           MOVE 0 TO DATA-TABLES.
           MOVE 0 TO STORAGE-ALLOCATED-TOTAL STORAGE-INUSE-TOTAL
                     STORAGE-ALLOCATED-ENTRIES STORAGE-INUSE-ENTRIES
                     STORAGE-ALLOCATED-INDEX STORAGE-INUSE-INDEX
                     STORAGE-ALLOCATED-DATA STORAGE-INUSE-DATA.

           MOVE 'Y' TO LSRPOOL-FIRST-PAGE.

       TOTALS-INITIALISE-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** PAGE-HEADINGS                                             ***
      ***   Increment page                                          ***
      ***   Write new heading line to spool                         ***
      *****************************************************************
       PAGE-HEADINGS SECTION.

           ADD 1       TO PAGENO.
           MOVE PAGENO TO PAGE-NUMBER.
           MOVE 1      TO LINE-COUNT.

           MOVE '1'          TO PRINT-CONTROL.
           MOVE HEADING-LINE TO PP1-132.
           PERFORM WRITE-OUTPUT.
           MOVE '+'          TO PRINT-CONTROL.
           MOVE ALL '_'      TO PP1-132.
           PERFORM WRITE-OUTPUT.

           MOVE 'Y' TO SUB-HEADINGS.

       PAGE-HEADINGS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** TIMESTAMP-CONVERT                                         ***
      *** Call program DFH$STCN to convert S/370 format TOD to      ***
      ***   EBCDIC date and time                                    ***
      *****************************************************************
       TIMESTAMP-CONVERT SECTION.

           MOVE 'T'    TO CLOCK-TYPE.
           MOVE SPACES TO CLOCK-OUTPUT.

           MOVE LENGTH OF CLOCK-FIELDS TO STCN-COMMAREA.

           EXEC CICS LINK PROGRAM('DFH$STCN')
               COMMAREA(CLOCK-FIELDS)
               LENGTH(STCN-COMMAREA)
           END-EXEC.

           MOVE LOW-VALUES TO CLOCK-FIELD.

       TIMESTAMP-CONVERT-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** WRITE-OUTPUT                                              ***
      ***   Write current print line to report output destination.  ***
      *****************************************************************
       WRITE-OUTPUT SECTION.

           PERFORM WRITE-SPOOL.

       WRITE-OUTPUT-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** WRITE-SPOOL                                               ***
      *** Write current print line to spool                         ***
      *** Increment line-count                                      ***
      *** IF CICS exceptional condition is raised THEN              ***
      ***    call error routine                                     ***
      *****************************************************************
       WRITE-SPOOL SECTION.

           EXEC CICS SPOOLWRITE TOKEN(OUTPUT-TOKEN)
               FROM(PRINT-LINE) FLENGTH(LINE-LENGTH)
               LINE
               NOHANDLE
           END-EXEC.

           EVALUATE PRINT-CONTROL
               WHEN ' '
                   ADD 1 TO LINE-COUNT
               WHEN '0'
                   ADD 2 TO LINE-COUNT
               WHEN '-'
                   ADD 3 TO LINE-COUNT
               WHEN '+'
                   ADD 0 TO LINE-COUNT
               WHEN '1'
                   MOVE 1 TO LINE-COUNT
           END-EVALUATE.

      *                                       Check return code
           IF EIBRESP NOT = DFHRESP(NORMAL) THEN
               MOVE 'SPOOLWRITE' TO CICS-COMMAND
               PERFORM ERROR-ROUTINE
           ELSE
               MOVE ' '    TO PRINT-CONTROL
               MOVE SPACES TO PP1-132
           END-IF.

       WRITE-SPOOL-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  ERROR ROUTINE                                            ***
      ***  Error routine produces appropriate responses to CICS     ***
      ***  spool commands. To ensure this the resp and resp2 values ***
      ***  are returned to the main program DFH0STAT for appropriate***
      ***  outputing of the messages.                               ***
      *****************************************************************
       ERROR-ROUTINE SECTION.

      *  If an error occurs which is not due to the users input or if
      *  the program is not being run from a terminal then a general
      *  message is sent to the screen/console and the RESP and RESP2
      *  fields are given along with the spool command which failed.
           MOVE 'Y'      TO ERROR-FOUND.
           MOVE EIBRESP  TO STCM-RESP.
           MOVE EIBRESP2 TO STCM-RESP2.

      *  Whatever happens ensure we return to DFH0STAT quickly.
           EXEC CICS RETURN
           END-EXEC.

       ERROR-ROUTINE-EXIT.
           EXIT.
