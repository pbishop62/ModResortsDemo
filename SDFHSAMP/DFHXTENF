         TITLE 'CUSTOMER INFORMATION CONTROL - SAMPLE XICTENF/XALTENF G*
               LOBAL USER EXIT PROGRAM'
***********************************************************************
*                                                                     *
* MODULE NAME = DFHXTENF                                              *
*                                                                     *
* DESCRIPTIVE NAME = C.I.C.S./VS Sample XICTENF/XALTENF global exit.  *
*                                                                     *
*                                                                     *
*                                                                     *
*     Licensed Materials - Property of IBM                            *
*                                                                     *
*     "Restricted Materials of IBM"                                   *
*                                                                     *
*     5655-Y04                                                        *
*                                                                     *
*     (C) Copyright IBM Corp. 1988, 2001"                             *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
* STATUS = 7.2.0                                                      *
*                                                                     *
* FUNCTION =                                                          *
*        DFHXTENF acts as an exit program for the XALTENF and         *
*        XICTENF exits which deal with 'terminal not known'           *
*        conditions. Its purpose is to tell CICS whether the          *
*        terminal exists on another system and, if so, which one.     *
*                                                                     *
*        It is called by the User Exit Handler but only if required   *
*        and only if the user has enabled it as an exit program       *
*        for the XALTENF exit in DFHALP or XICTENF exit in DFHICP.    *
*        When a START command involving a terminal is issued,         *
*        DFHICP checks that the terminal exists. If the locate        *
*        fails to find it, DFHICP drives the XICTENF exit.            *
*        When DFHALP is asked by DFHICP or Transient Data to          *
*        start an ATI task on a given terminal, it checks that        *
*        the terminal exists. If the locate fails to find it,         *
*        DFHALP drives the XALTENF exit.                              *
*                                                                     *
*        DFHALP and DFHICP pass a common set of parameters to their   *
*        respective exits to help the exit program make its           *
*        decision.                                                    *
*                                                                     *
*        DFHXTENF does the following:-                                *
*                                                                     *
*           It decides whether it is dealing with a START request     *
*        or a Transient Data request.                                 *
*           For START requests with a netname passed in, it returns   *
*        the same netname and a return code of UERCNETN.              *
*        For START requests with no netname, it constructs a netname  *
*        by taking the first character of the terminal name and       *
*        appending it to the characters 'CICS'.                       *
*           For Transient Data requests it rejects the request.       *
*                                                                     *
*           Note that the XALTENF exit may need to be driven during   *
*        warm or emergency startup. This happens if                   *
*        expired ICEs are found from a previous run or Transient      *
*        Data queues from previous runs need processing. To get       *
*        an exit program enabled for the XALTENF exit for such        *
*        processing, the exit program must be ENABLEd from a PLTPI    *
*        program.                                                     *
*                                                                     *
*                                                                     *
* NOTES :                                                             *
*    DEPENDENCIES = S/370                                             *
*        DFHXTENF must be defined on the CSD as a PROGRAM.            *
*    RESTRICTIONS =                                                   *
*        None                                                         *
*    REGISTER CONVENTIONS =                                           *
*        See the code                                                 *
*    MODULE TYPE = Executable                                         *
*    PROCESSOR = Assembler                                            *
*    ATTRIBUTES = Read only, Re-entrant                               *
*                                                                     *
*------------------------------------------------------------------ *
*                                                                     *
* ENTRY POINT = DFHXTENA                                              *
*                                                                     *
*     PURPOSE = see above                                             *
*                                                                     *
*     LINKAGE =                                                       *
*        Called by the User Exit Handler.                             *
*                                                                     *
*     INPUT =                                                         *
*        On entry register 1 addresses a parameter list described     *
*        by the DFHUEPAR DSECT.                                       *
*                                                                     *
*     OUTPUT =                                                        *
*        A return code is placed in register 15. If the return code   *
*        is 4 ('terminal exists'), a netname is passed back. If the   *
*        return code is 8 ('terminal exists'), a sysid is returned.   *
*                                                                     *
*     EXIT-NORMAL =                                                   *
*        DFHXTENF returns to the address that was in register 14      *
*        when it was called. Possible return codes in register 15     *
*        are:-                                                        *
*          0 - terminal does not exist                                *
*          4 - terminal exists, netname returned in parameter list    *
*          8 - terminal exists, sysid returned in parameter list      *
*                                                                     *
*     EXIT-ERROR = none                                               *
*                                                                     *
*------------------------------------------------------------------ *
*                                                                     *
* EXTERNAL REFERENCES = none                                          *
*                                                                     *
*------------------------------------------------------------------ *
*                                                                     *
* DESCRIPTION - see above                                             *
*                                                                     *
*------------------------------------------------------------------ *
*                                                                     *
* CHANGE ACTIVITY :                                                   *
*                                                                     *
*     PN= REASON REL YYMMDD HDXIII : REMARKS                          *
*    $MOD(DFHXTENF),COMP(TERMSHR),PROD(CICS TS ):                     *
*    $P0= Mnnnnn 170 880527 HD6NBW : Created.                         *
*    $P1= M90474 330 910807 HDBWSH : Prologue fixed.                  *
*    $P2= Mnnnnn 610 010114 HD7OPJW: Binder error messages            *
*                                                                     *
***********************************************************************
DFHXTENF  CSECT
DFHXTENF  AMODE 31                                                 @P2A
DFHXTENF  RMODE ANY                                                @P2A
         DFHEJECT
***********************************************************************
*        REGISTER DEFINITIONS                                         *
***********************************************************************
R0       EQU   0                   not used
R1       EQU   1                   base for parameter list built       *
                                   by DFHUEH
R2       EQU   2                   used to address data
R3       EQU   3                   used to address data
R4       EQU   4                   not used
R5       EQU   5                   not used
R6       EQU   6                   not used
R7       EQU   7                   not used
R8       EQU   8                   not used
R11      EQU   11                  base register
R12      EQU   12                  not used
R13      EQU   13                  standard save area base
R14      EQU   14                  return address
R15      EQU   15                  return code
         DFHEJECT
         DFHUEXIT TYPE=EP,         generate parameter list DSECTs      *
               ID=(XICTENF,XALTENF) for XICTENF and XALTENF exits
         DFHEJECT
DFHXTENF CSECT
         DFHVM XTENF
         ENTRY DFHXTENA
DFHXTENA DS    0H
         STM   R14,R12,12(R13)     save registers
         BALR  R11,0               set up base register
         USING *,R11
*
         USING DFHUEPAR,R1         DFHUEH parameter list
*
*        Could check the terminal id at this point. In this
*        program we assume it is valid. We also choose to accept
*        START requests and reject Transient Data trigger level
*        events.
*
         L     R2,UEPICEVT         access type of request
         CLC   0(2,R2),START       START command?
         BE    STARTCMD            yes
*
         CLC   0(2,R2),STARTDAT    START command with data?
         BNE   NOTSTART            no, must be Transient Data
*
STARTCMD DS    0H
*
*        Accept the default netname if we are Function Shipping.
*        Otherwise build a netname.
*
         L     R2,UEPICFS          access FS information
         CLI   0(R2),UEPICFY       Function Shipping?
         BNE   BLDNETNM            no, build a netname
*
         LH    R15,NETNAME         accept the default netname
         B     EXIT
*
BLDNETNM DS    0H
*
*        Build a netname by taking the first character of the
*        terminal id and appending it to the characters 'CICS'.
*
         L     R2,UEPICNTO         access the output netname field
         L     R3,UEPICRTR         access id of requested terminal
         MVC   0(8,R2),=C'CICS    '
         MVC   4(1,R2),0(R3)       first character of terminal id
         LH    R15,NETNAME         netname returned
         B     EXIT
*
NOTSTART DS    0H
         LH    R15,UNKNOWN         reject Transient Data trigger       *
                                   level events
*
EXIT     DS    0H
         L     R14,12(R13)         restore registers except 15
         LM    R0,R12,20(R13)      which contains the return code
         BR    R14
*
***********************************************************************
*        Local constants
***********************************************************************
START    DC    AL2(UEPICES)
STARTDAT DC    AL2(UEPICESD)
NETNAME  DC    AL2(UERCNETN)
UNKNOWN  DC    AL2(UERCTEUN)
*
         DFHEND DFHXTENF
