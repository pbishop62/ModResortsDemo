LDSP     TITLE 'DFH$LDSP - CREATE FILEA TEST FILE FOR TESTING OF CICS S*
               AMPLES'
***********************************************************************
*                                                                     *
* MODULE NAME = DFH$LDSP                                              *
*                                                                     *
* DESCRIPTIVE NAME = Test File Creation for Sample Application        *
*                                                                     *
*                                                                     *
*                                                                     *
*     Licensed Materials - Property of IBM                            *
*                                                                     *
*     "Restricted Materials of IBM"                                   *
*                                                                     *
*     5655-Y04                                                        *
*                                                                     *
*     (C) Copyright IBM Corp. 1984, 1991"                             *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
* STATUS = 7.2.0                                                      *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
* CHANGE ACTIVITY :                                                   *
* $SEG(DFH$LDSP),COMP(SAMPLES),PROD(CICS TS ):                        *
*                                                                     *
*     PN= REASON REL YYMMDD HDXIII : REMARKS                          *
*    $P0= Mnnnnn 170 841016 HDZZUNK: Created.                         *
*    $P1= M90474 330 910807 HDBWSH : Prologue fixed.                  *
*                                                                     *
***********************************************************************
DFH$LDSP CSECT
         STM   14,12,12(13)
         BALR  10,0
         USING *,10
         ST    13,SAVEAREA+4
         LA    13,SAVEAREA
         OPEN  (SYSIN,(INPUT),SYSOUT,(OUTPUT),VSAMACB,)
         C     15,=F'4'
         BH    OPENERR
WTLOOP   EQU   *
         GET   SYSIN,INAREA
         MVC   INAREA+71(9),COMMENT
         PUT   RPL=OUTRPL
         LTR   15,15
         BNZ   ERROR
         PUT   SYSOUT,OUTLEN
         B     WTLOOP
OPENERR  EQU   *
         ST    15,RETCODE
         PUT   SYSOUT,OPENRLEN
         B     CLOSE
ERROR    EQU   *
         SHOWCB RPL=OUTRPL,AREA=RETCODE,LENGTH=4,FIELDS=(FDBK)
         PUT   SYSOUT,ERRORLEN
         B     CLOSE
EOF      EQU   *
* END OF INPUT FILE
         PUT SYSOUT,NORMALEN
CLOSE    EQU   *
         PUT   SYSOUT,TERMILEN
         CLOSE (SYSIN,,SYSOUT,,VSAMACB)
         L     13,SAVEAREA+4
         L     15,RETCODE
         RETURN (14,12),RC=(15)
         BR    14
SAVEAREA DS    9D
RETCODE  DC    F'0'
OUTLEN   DC    H'84'
         DC    H'0'
INAREA   DS    0CL80
OUTAREA  DS    CL80
COMMENT  DC    C'*********'
OPENRLEN DS    0H
         DC    AL2(OPENREND-OPENRLEN)
         DC    XL2'0'
         DC    C'*** ERROR IN OPEN - COND CODE = R15 VALUE AFTER OPEN **
               **'
OPENREND EQU   *
ERRORLEN DS    0H
         DC    AL2(ERROREND-ERRORLEN)
         DC    XL2'0'
         DC    C'*** ERROR IN LOADING FILEA - COND CODE = RPL FDBK FIEL*
               D ***'
ERROREND EQU   *
NORMALEN DS    0H
         DC    AL2(NORMAEND-NORMALEN)
         DC    XL2'0'
         DC    C'*** TEST FILEA LOADED SUCCESSFULLY ***'
NORMAEND EQU   *
TERMILEN DS    0H
         DC    AL2(TERMIEND-TERMILEN)
         DC    XL2'0'
         DC    C'*** PROGRAM DFH$LDSP TERMINATED ***'
TERMIEND EQU   *
SYSIN    DCB   DDNAME=SYSIN,                                           *
               LRECL=80,RECFM=FB,                                      *
               DSORG=PS,                                               *
               EODAD=EOF,                                              *
               MACRF=(GM)
SYSOUT   DCB   DDNAME=SYSOUT,                                          *
               LRECL=132,BLKSIZE=660,                                  *
               DSORG=PS,                                               *
               MACRF=(PM),                                             *
               RECFM=VB
VSAMACB  ACB   DDNAME=FILEA,                                           *
               MACRF=(KEY,SEQ,OUT),                                    *
               BUFND=2,                                                *
               BUFNI=1,                                                *
               STRNO=1
OUTRPL   RPL   ACB=VSAMACB,                                            *
               AREA=OUTAREA,                                           *
               AREALEN=80,                                             *
               OPTCD=(KEY,SEQ,SYN,MVE,NUP),                            *
               RECLEN=80
         END
