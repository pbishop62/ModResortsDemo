*ASM XOPTS(SP)
* Line above passes parameters to CICS TRANSLATOR
         TITLE 'DFH$AXVS - CICS SAMPLE EXCI SERVER'
***********************************************************************
*                                                                     *
* MODULE NAME = DFH$AXVS                                              *
*                                                                     *
* DESCRIPTIVE NAME = CICS TS  (component) ....                        *
*                                                                     *
*     CICS SAMPLE EXCI SERVER                                         *
**                                                                    *
*                                                                     *
*                                                                     *
*        Licensed Materials - Property of IBM                         *
*                                                                     *
*        "Restricted Materials of IBM"                                *
*                                                                     *
*        5655-Y04                                                     *
*                                                                     *
*        (C) Copyright IBM Corp. 1998, 2014"                          *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
**                                                                    *
*  STATUS = 7.2.0                                                     *
**      Global symbol for the CICS level. Leave set to the level at   *
**      which the object was created; it is replaced with the last    *
**      updated level automatically at Build time.                    *
*                                                                     *
* TRANSACTION NAME = Cxxx                                             *
**       Only applicable to modules which are CICS transactions       *
*                                                                     *
* FUNCTION = Sample                                                   *
*                                                                     *
* NOTES :                                                             *
*    DEPENDENCIES = S/390                                             *
**         List any dependencies on the CICS tables, bring-up,        *
**         or SYSGEN.                                                 *
*    RESTRICTIONS =                                                   *
**         List any restrictions, such as the maximum record length   *
*    REGISTER CONVENTIONS =                                           *
**         These can be listed here, or a named section of code       *
**         defines the registers can be referenced.                   *
*    MODULE TYPE = Executable | Table                                 *
*    PROCESSOR = Assembler                                            *
*    ATTRIBUTES = Read only, Serially Reusable, <Authorized>          *
*                                                                     *
*------------------------------------------------------------------   *
*                                                                     *
* ENTRY POINT = DFH[AXVS                                              *
*                                                                     *
*     OUTPUT = messages on CSMT                                       *
*                                                                     *
*                                                                     *
*------------------------------------------------------------------   *
*                                                                     *
* EXTERNAL REFERENCES =                                               *
*                                                                     *
*     CONTROL BLOCKS = Common area communicating with DFH$DXVC        *
*                                                                     *
*                                                                     *
*------------------------------------------------------------------   *
*                                                                     *
* DESCRIPTION                                                         *
*                                                                     *
*    External interface server, see EXCI programme DFH[DXVC           *
*             (JCKCEMT)                                               *
*                                                                     *
*------------------------------------------------------------------   *
*                                                                     *
* CHANGE ACTIVITY :                                                   *
**                                                                    *
*        $MOD(DFH$AXVS),COMP(SAMPLES),PROD(CICS TS ):                 *
*                                                                     *
*     PN= REASON REL YYMMDD HDXXIII : REMARKS                         *
*    $D1= I07991 660 081223 HD3YIJM : Unprintable chars in comments   *
*    $L0= 753    530 981113 HD5LLD  : Samples DFH$DXVC DFH$AXVS       *
*    $P1= M15766 610 000927 HD7OPJW : Binder warning messages         *
*         This PN= line ensures that when this object is ALTERed      *
*         within a non-test MPU, a change flag is allocated and a     *
*         change descriptor line added.                               *
*         DO NOT DELETE THE PN= line                                  *
*                                                                     *
*         All code changes, after initial creation of the object,     *
*         should be flagged using the allocated change flag.          *
*                                                                     *
*         See HELP LCSFLAG for an explanation of the change           *
*         descriptor fields.                                          *
*                                                                     *
***********************************************************************
* MODULE NAME = DFH$AXVS                                              *
*                                                                     *
* DESCRIPTION                                                         *
*                                                                     *
*           CICS SAMPLE EXCI SERVER                                   *
* to issue :-                                                         *
*       EXEC CICS SET PROGRAM(PROGNAME) PHASEIN                       *
*                                                                     *
***********************************************************************
         SPACE 1
R0       EQU   0                   REGISTER EQUATES
R1       EQU   1
R2       EQU   2
R5       EQU   5
R7       EQU   7
RE       EQU   14
RF       EQU   15
         SPACE 1
*
*        Commarea Structure
*
COMMAREA DSECT
COMM_START DS 0F
KEY      DS    CL8
RETCODE  DS    F
COUNT    DS    F
STRING   DS    CL256               parms. null term. strings
COMLEN   EQU   *-COMM_START        Length of COMMAREA
*
*
         EJECT ,
         DFHEISTG ,
*
*        Working Storage
*
PINNAME  DS   A                    Address of name
NAMELN   DS   F                    Name length
PROGNAME DS   CL8
PROGNMLN DS   CL8                  Name length
TRACEMSG DS   CL80
*WTODMP   WTODMP TITLE='==[AXVS==',LENGTH=RE,ADDRESS=R2,MF=L
         EJECT ,
DFH$AXVS CSECT
DFH$AXVS AMODE 31                                                  @P1A
DFH$AXVS RMODE ANY                                                 @P1A
***********************************************************************
*        If we get here, we have been linked to from another program  *
*        and we should always be called with a commarea.              *
***********************************************************************
         EXEC CICS ADDRESS COMMAREA(R5)
         USING COMMAREA,R5
         LTR   R5,R5
         BZ    RETURN
         LH    R7,EIBCALEN
         LTR   R7,R7
         BZ    RETURN              .. Exit
         SPACE 1
         LA    R0,1
         LCR   R0,R0               -1
         ST    R0,RETCODE          Set Bad return code
         SPACE 1
*         LR    RE,R7               length
*         SRL   RE,2                words
*         LR    R2,R5               commarea
*         MVC   WTODMP(WTOE-WTOS),WTOS Prime area
*         WTODMP TITLE='==[AXVS==',LENGTH=RE,ADDRESS=R2,MF=(E,WTODMP)
         SPACE 1
*  printf("eibtrnid = %s\n",dfheiptr->eibtrnid);
         MVC   TRACEMSG(11),=C'eibtrnid = '
         MVC   TRACEMSG+11(4),EIBTRNID Tran Id.
    EXEC CICS WRITEQ TD QUEUE(CSMT) FROM (TRACEMSG) LENGTH(15)
*  inputName =  ((comms*)commarea)->args.string;
         LA    R1,STRING
         ST    R1,PINNAME          Set Add. of Input name
         LR    R0,R1               Save start
*  programNameLen = strlen(inputName);
*  printf("programNameLen = %i\n",programNameLen);
*  if (programNameLen > 8) programNameLen=8;
*  memcpy(programName,inputName,programNameLen);
         MVC   PROGNAME(8),=CL8' ' clear field to blanks
         LA    R2,8
LOOP     DS    0H                  Find length of name
         CLI   0(R1),0
         BE    FOUND               .. found end
         LA    R1,1(,R1)           Next character
         BCT   R2,LOOP
         SPACE 1
FOUND    DS    0H
         SR    R1,R0               Name length
         ST    R1,NAMELN           save length
         MVC   TRACEMSG(17),=C'programNameLen = '
         CVD   R1,PROGNMLN         Length as decimal
         OI    PROGNMLN+7,X'0F'    Set sign nibble
         UNPK  TRACEMSG+17(2),PROGNMLN+6(2)
         LR    R2,R0               Address of Input name
         MVC   TRACEMSG+19(2),=C', '
         MVC   TRACEMSG+21(8),0(R2) Copy programme name
    EXEC CICS WRITEQ TD QUEUE(CSMT) FROM (TRACEMSG) LENGTH(29)
         L     R1,NAMELN           restore name length
         BCTR  R1,0                -1 for execute
         C     R1,=F'0'            Greater than null ?
         BM    RETURN              .. no
         SPACE 1
         EX    R1,MVC              Copy name to PROGNAME
*MVC     MVC   PROGNAME(0),0(R2)
      EXEC CICS SET PROGRAM(PROGNAME) PHASEIN
*  ((comms*)commarea)->retcode=dfheiptr->eibresp;
         MVC   RETCODE,EIBRESP     Set return code
         SPACE 1
*  printf("eibresp = %i\n",dfheiptr->eibresp);
         MVC   TRACEMSG(10),=C'eibresp = '
         L     R1,EIBRESP
         CVD   R1,PROGNMLN         Length as decimal
         OI    PROGNMLN+7,X'0F'    Set sign nibble
         UNPK  TRACEMSG+10(4),PROGNMLN+6(2)
    EXEC CICS WRITEQ TD QUEUE(CSMT) FROM (TRACEMSG) LENGTH(14)
         SPACE 1
RETURN   DS    0H
         EXEC CICS RETURN
         EJECT ,
MVC      MVC   PROGNAME(0),0(R2)
CSMT     DC    C'CSMT'
*WTOS     WTODMP TITLE='==[AXVS==',LENGTH=RE,ADDRESS=R2,MF=L
*WTOE     EQU   *
         LTORG
         END
