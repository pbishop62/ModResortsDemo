***********************************************************************
*                                                                     *
*   MODULE NAME = DFH$SXP1                                            *
*                                                                     *
* DESCRIPTIVE NAME = CICS TS  (RDO) Sample User Exit Program 1        *
*                                                                     *
*                                                                     *
*                                                                     *
*        Licensed Materials - Property of IBM                         *
*                                                                     *
*        "Restricted Materials of IBM"                                *
*                                                                     *
*        5655-Y04                                                     *
*                                                                     *
*        (C) Copyright IBM Corp. 1989, 2014"                          *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
* STATUS = 7.2.0                                                      *
*                                                                     *
* FUNCTION =                                                          *
*        Provide sample user exit for suppressing a message by number.*
*                                                                     *
*        This sample shows how to suppress a message with a           *
*        particular message number, from all destinations.            *
*                                                                     *
*        The message used is:                                         *
*                                                                     *
*        DFHDX8320I applid DBCTL Restart processing completed after   *
*        DBCTL failure.                                               *
*                                                                     *
*        The message is suppressed if it has a message number 8320,   *
*        product id 'DFH' (for CICS message),                         *
*        and domain id 'DX'. Otherwise, the message is issued as      *
*        normal.                                                      *
*        The exit has product id added now in order to avoid the      *
*        suppression of a CPSM message unintentionally. CPSM product  *
*        id is 'EYU'.                                                 *
*                                                                     *
* NOTES :                                                             *
*    DEPENDENCIES = S/370                                             *
*        None.                                                        *
*                                                                     *
*    RESTRICTIONS =                                                   *
*        None.                                                        *
*                                                                     *
*    MODULE TYPE = Executable | Table                                 *
*    PROCESSOR = Assembler                                            *
*    ATTRIBUTES = Read only, Serially Reusable, <Authorized>          *
*                                                                     *
*------------------------------------------------------------------ *
*                                                                     *
* DESCRIPTION                                                         *
*        See Title and Comments in code.                              *
*                                                                     *
*------------------------------------------------------------------ *
*                                                                     *
* CHANGE ACTIVITY :                                                   *
**                                                                    *
*        $MOD(DFH$SXP1),COMP(SAMPLES),PROD(CICS TS ):                 *
*                                                                     *
*     PN= REASON REL YYMMDD HDXIII : REMARKS                          *
*    $L0= 507    320 890811 HD5EISR: Implicit flag.                   *
*    $L1= 807    650 050720 HD4PALS: Pass product id on XMEOUT exit   *
*    $P1= M60568 320 900116 HD6ISS: Change Message Number DFHCD8320   *
*                                 : to DFHDX8320.                     *
*    $P2= M62307 320 900602 HD3BADW: Use UERCNORM and UERCBYP rc's    *
*    $P3= M96433 510 960205 HD4PALS : Change to RMODE ANY             *
*        R132499 710 161010 HD2GJST: Set rc in UEPCRCA if highest     *
*                                                                     *
***********************************************************************
**********************************************************
* This instruction sets up the Sample user exit point.   *
**********************************************************
*
         DFHUEXIT TYPE=EP,ID=XMEOUT
*
**********************************************************
* The following DSECT maps a storage area you can use to *
* make the exit program reentrant by storing the address *
* of the storage you acquire in the first four bytes of  *
* the 260-byte area provided by the user exit handler    *
* (DFHUEH) and addressed by UEPXSTOR.                    *
**********************************************************
*
TRANSTOR DSECT
*
**********************************************************
* Register Equates                                       *
**********************************************************
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
**********************************************************
* The next seven instructions form the normal start of a *
* Sample user exit program, setting the addressing mode  *
* to 31-bit, saving the calling program's registers,     *
* establishing base addressing, and establishing the     *
* addressing of the user exit parameter list             *
**********************************************************
*
DFH$SXP1 CSECT
DFH$SXP1 AMODE 31
DFH$SXP1 RMODE ANY                                                 @P3A
         SAVE  (14,12)             SAVE REGISTERS
         LR    R11,R15
         USING DFH$SXP1,R11        SET UP PROGRAM BASE REGISTER
         LR    R2,R1
         USING DFHUEPAR,R2         ADDRESS USER EXIT PARAMETER LIST
*
**********************************************************
* <<<<<< Section to be Modified by the Users.     >>>>>> *
*                        START.                          *
**********************************************************
         L     R3,UEPCPID                                          @L1A
         CLC   0(3,R3),=C'DFH'     Check Product Id                @L1A
         BNE   RCNORMAL            If not DFH, then ret code normal@L1A
*
         L     R3,UEPCPNUM                                         @L1C
         CLC   0(4,R3),=F'8320'    Check Message number            @L1C
         BNE   RCNORMAL            If not 8320, then ret code normal
*
         L     R3,UEPCPDOM                                         @L1C
         CLC   0(2,R3),=C'DX'      Check Domain                    @L1C
         BE    RCBYPASS            If DX domain, then ret code Bypass  F
                                                                   @P1C
**********************************************************
*                         END.                           *
* <<<<<< Section to be Modified by the Users.     >>>>>> *
**********************************************************
*
**********************************************************
*   RCNORMAL will set the return code to UERCNORM        *
**********************************************************
*
RCNORMAL DS    0H
         LA    R15,UERCNORM            Set Normal RC          @R132499C
         B     STEND
*
**********************************************************
*   RCBYPASS will set the return code to UERCBYP         *
**********************************************************
*
RCBYPASS DS    0H
         LA    R15,UERCBYP             Set Bypass RC          @R132499C
         B     STEND
*
STEND    DS    0H
**********************************************************
* Compare my RC (R15) with previous RC.                  *
* IF my RC = previous, send it.                          *
* IF my RC < previous, send previous.                    *
* IF my RC > previous, send mine, and update previous    *
* to mine.                                               *
**********************************************************
         ICM   R14,15,UEPCRCA          Load previous RC addr  @R132499A
         BZ    STEND2                  None - send my rc      @R132499A
         CH    R15,0(,R14)             Compare RC with prev   @R132499A
         BE    STEND2                  Equal - send mine      @R132499A
         BL    STEND1                  Mine low - send prev   @R132499A
         STH   R15,0(,R14)             Mine high set previous @R132499A
         B     STEND2                  Go exit                @R132499A
STEND1   DS    0H
         LH    R15,0(,R14)             Get previous RC        @R132499A
STEND2   DS    0H
**********************************************************
* Restore registers, set return code, and return to user *
* exit handler.                                          *
**********************************************************
         L     R13,UEPEPSA
         RETURN (14,12),RC=(15)
         LTORG
         END   DFH$SXP1
