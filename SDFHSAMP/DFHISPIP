*PROCESS XOPTS(CICS) PP(CICS) SYSTEM(CICS) CSECT LIST LANGLVL(SPROG) ;
 /*===================================================================*/
 /*                                                                   */
 /* MODULE NAME = DFHISPIP                                            */
 /*                                                                   */
 /* DESCRIPTIVE NAME = CICS TS  (IS) ISC over TCP/IP                  */
 /*                    IPCONN autoinstall user program (AUP)          */
 /*                                                                   */
 /*                                                                   */
 /*                                                                   */
 /*        Licensed Materials - Property of IBM                       */
 /*                                                                   */
 /*        "Restricted Materials of IBM"                              */
 /*                                                                   */
 /*        5655-Y04                                                   */
 /*                                                                   */
 /*        (C) Copyright IBM Corp. 2006"                              */
 /*                                                                   */
 /*                                                                   */
 /*                                                                   */
 /*                                                                   */
 /*  STATUS = 7.2.0                                                   */
 /*                                                                   */
 /* FUNCTION =                                                        */
 /*  Autoinstall user program to allow tailoring of autoinstalled     */
 /*  IPCONNs.                                                         */
 /*                                                                   */
 /*  See the comments in DFHISAIC, the AUP commarea, for details of   */
 /*  the expected/allowed processing.                                 */
 /*                                                                   */
 /*  The equivalent Assembler program is DFHISAIP.                    */
 /*  The equivalent COBOL     program is DFHISCIP.                    */
 /*  The equivalent C         program is DFHISDIP.                    */
 /*                                                                   */
 /*-------------------------------------------------------------------*/
 /*                                                                   */
 /* CHANGE ACTIVITY :                                                 */
 /*        $MOD(DFHISPIP),COMP(ISCIP),PROD(CICS TS ):                 */
 /*                                                                   */
 /*  PN= REASON REL YYMMDD HDXXIII : REMARKS                          */
 /* $L0= 710    650 060804 HD0JADT : First coded                      */
 /*                                                                   */
 /*===================================================================*/
 ISPIP: PROCEDURE OPTIONS(MAIN,NOEXECOPS,REENTRANT) REORDER ;

 /*-------------------------------------------------------------------*/
 /* Autoinstall User Program commarea format                          */
 /*-------------------------------------------------------------------*/
  %include dfhisaic ;             /* AUP commarea                     */

 /*-------------------------------------------------------------------*/
 /* Miscellaneous declarations                                        */
 /*-------------------------------------------------------------------*/
  dcl substr builtin ;
  dcl isaicp ptr ;                /* Pointer to AUP commarea          */
  dcl blank8 char(8) init('        ') ; /* Eight blanks               */
  dcl i bin(32) ;                 /* Work variable                    */

 /*===================================================================*/
 /*                     I N I T I A L I S A T I O N                   */
 /*===================================================================*/
  EXEC CICS ADDRESS EIB(dfheiptr);
  if eibcalen = 0 then                /* Just return if no commarea.. */
    EXEC CICS RETURN ;                /* ..because nowhere to set rc  */

  EXEC CICS ADDRESS COMMAREA(isaicp); /* Address of parameters        */
  isaicp->isaic_response = '00'x ;    /* Assume all OK                */

 /*===================================================================*/
 /*    User specific code below                                       */
 /*===================================================================*/

 /*-------------------------------------------------------------------*/
 /* Select function required                                          */
 /*-------------------------------------------------------------------*/
  select ( isaicp->isaic_function ) ;
    when ( isaic_install ) call ispip_install ;
    when ( isaic_delete  ) call ispip_delete  ;
    otherwise ;
  end ;
  EXEC CICS RETURN ;

 /*===================================================================*/
 /*                                                                   */
 /*                     I N S T A L L                                 */
 /*                                                                   */
 /* If the connecting system did not specify an applid, then use      */
 /* isaic_suggested_applid for both the ipconn name and the applid.   */
 /* If there is an applid, then use the last four non-blank           */
 /* characters as the ipconn name.                                    */
 /*                                                                   */
 /* All other parameters are unchanged.                               */
 /*                                                                   */
 /*===================================================================*/
  ispip_install: proc reorder ;

   if isaicp->isaic_applid = blank8 then
     do ;
       isaicp->isaic_ipconn = isaicp->isaic_suggested_applid ;
       isaicp->isaic_applid = isaicp->isaic_suggested_applid ;
     end ;
   else
     do ;
       isaicp->isaic_ipconn = blank8 ;  /* Ensure clear               */
       do i = 8 to 4 by -1
        while(substr(isaicp->isaic_applid,i,1)=' ');
       end ;
       i = i - 3 ;
       substr(isaicp->isaic_ipconn,1,4) =
        substr(isaicp->isaic_applid,i,4) ;
     end ;

  end ispip_install ;

 /*===================================================================*/
 /*                                                                   */
 /*                       D E L E T E                                 */
 /*                                                                   */
 /*===================================================================*/
  ispip_delete: proc reorder ;

   /* >>> Code for delete processing can be added here <<< */

  end ispip_delete ;

 END ISPIP;
