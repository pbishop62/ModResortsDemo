      CBL CICS('COBOL3,SP') APOST
PROCESS MAP,XREF,TRUNC(BIN)
      *****************************************************************
      *                                                               *
      * MODULE NAME = DFH0STAT                                        *
      *                                                               *
      * DESCRIPTIVE NAME = Collect and Print Statistics Sample        *
      *                                                               *
      *                                                               *
      *                                                               *
      *     Licensed Materials - Property of IBM                      *
      *                                                               *
      *     "Restricted Materials of IBM"                             *
      *                                                               *
      *     5655-Y04                                                  *
      *                                                               *
      *     (C) Copyright IBM Corp. 1990, 2016"                       *
      *                                                               *
      *                                                               *
      *                                                               *
      *                                                               *
      * STATUS = 7.2.0                                                *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      * DESCRIPTION :                                                 *
      *                                                               *
      *     This set of programs illustrate ways of using the EXEC    *
      *     CICS INQUIRE, COLLECT and EXTRACT STATISTICS commands of  *
      *     CICS to produce information about a CICS system.          *
      *     The reports include a CICS and MVS storage analysis.      *
      *     Statistics obtained using these commands can be used to   *
      *     aid the specification of the DSA Size parameters.         *
      *     (See the CICS Performance Guide and CICS Data Areas       *
      *      manuals for more information, and the CICS System        *
      *      Programming Reference for use of commands)               *
      *                                                               *
      *     These programs can be invoked from the PLT at PLTSD or    *
      *     as a CICS transaction (conversationally).                 *
      *     The output is sent via the CICS JES SPOOL interface for   *
      *     which a number of default parameters can be changed by    *
      *     the user to specify the distribution of the report(s).    *
      *     These defaults are defined in the working-storage         *
      *     section of the DFH0STAT program under the 01 level        *
      *     "OUTPUT-DEFAULTS".                                        *
      *                                                               *
      *     Installation instructions:-                               *
      *     Require SIT parameters:-  SPOOL=YES                       *
      *     Required CICS definitions:-                               *
      *         Transactions:-  STAT -> DFH0STAT                      *
      *         Programs:-    DFH0STAT   IBM COBOL                    *
      *                       DFH0STDB       for OS/390 & VM          *
      *                       DFH0STEJ           2.1.1 or later       *
      *                       DFH0STEP       "   "   "                *
      *                       DFH0STGN       "   "   "                *
      *                       DFH0STLK       "   "   "                *
      *                       DFH0STPR       "   "   "                *
      *                       DFH0STSA       "   "   "                *
      *                       DFH0STSY       "   "   "                *
      *                       DFH0STTP       "   "   "                *
      *                       DFH0STTS       "   "   "                *
      *                       DFH0STWB       "   "   "                *
      *                       DFH$STAS   Assembler                    *
      *                       DFH$STCN   Assembler                    *
      *                       DFH$STTB   Assembler                    *
      *         Mapsets:-     DFH0STM                                 *
      *                       DFH0STS                                 *
      *                                                               *
      *     The CICS-supplied RDO group, DFH$STAT, contains all       *
      *     the necessary resource definitions for this sample.       *
      *                                                               *
      *     All programs are command level and run above the          *
      *     16Mb line. Note: that DFH0STAT, DFH0STDB, DFH0STEJ,       *
      *     DFH0STEP, DFH0STGN, DFH0STLK, DFH0STPR, DFH0STSA,         *
      *     DFH0STSY, DFH0STTP, DFH0STTS, and DFH0STWB must be        *
      *     translated with the Translator options "COBOL3 and SP".   *
      *                                                               *
      *   SPOOL errors.  If an EXEC CICS SPOOL .. command fails       *
      *   when the program is run as a transaction, an error message  *
      *   is displayed on the users screen and the transaction will   *
      *   continue.  If the program is not being run from a terminal  *
      *   then a message will be sent to the console using EXEC       *
      *   CICS WRITE OPERATOR commands and the transaction will be    *
      *   terminated normally.                                        *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      * CHANGE ACTIVITY :                                             *
      * $SEG(DFH0STAT), COMP(SAMPLES) PROD(CICS)                      *
      *                                                               *
      *     PN= REASON REL YYMMDD HDXIII : REMARKS                    *
      *    $D1= I06720 520 970103 HD1VCJB: DB2 Connection/Entry stats *
      *    $D2= I07799 650 070111 HD1VCJB: change use of ip connection*
      *    $D3= I07991 670 100820 HDIGPG : Translate unprintable char *
      *    $L1= 710    650 060112 HD1VCJB: 3 - IPCONN statistics      *
      *    $L2= 897    650 061010 HD1VCJB: LIBRARY Stats Infrastructur*
      *    $01= A26376 670 101122 HDBGSLS: Migrate PM26376 from SPA R6*
      *    $L3= 907    660 071005 HD1VCJB: Part 3 RDO audit for PROGRA*
      *    $L5= 923    660 080513 HD1VCJB: Atomservice Statistics Supp*
      *    $L6= 927    660 080604 HD1VCJB: Event Processing Support   *
      *    $L7= 937    660 080724 HD1VCJB: JVMSERVER Statistics Suppor*
      *    $L8= 936    660 081104 HD1VCJB: XMLTRANSFORM Statistics Sup*
      *    $L9= 927    660 081119 HD1VCJB: CAPTURESPEC Statistics Supp*
      *    $LA= R00256 670 100722 HD4IAEC: EPADAPTER statistics       *
      *         R38170 680 111205 HD1VCJB: Remove EJB Statistics      *
      *         R38170 680 111220 HD1VCJB: Remove JVM Statistics      *
      *         R38042 690 130319 HDAFDRB: Use EXTRACT STATISTICS     *
      *    $P1= D18343 650 070326 HD1VCJB: Large Lastused Interval val*
      *    $P2= D18439 650 070405 HD1VCJB: Max DFHRPL dataset printing*
      *    $P3= D19533 660 080115 HD1VCJB: Program/DFHRPL Report Selec*
      *    $D2= I07551 650 050517 HD1VCJB: MQCONN statistics          *
      *    $D2= I07610 650 050506 HD1VCJB: Clock/Timestamp Formatting *
      *    $L1= 613    410 920527 HD1VCJB: Add Files/LSR/Data Tables  *
      *    $P0= .      320 900612        : Created.                   *
      *    $P1= M90474 330 910807 HDBWSH : Prologue fixed.            *
      *    $P2= M90707 330 910829 HD1VCJB: Correct Temp Storage report*
      *    $P3= M91386 330 911120 HD1VCJB: Correct Region size estab. *
      *    $P4= M91514 330 911202 HD1VCJB: Add DSA High-Water-Mark    *
      *    $P5= M84511 410 931111 HD4OGB : Ignore Inactive TCBs       *
      *    $P6= M95242 510 950829 HD4OGB : Fix prologue               *
      *    $01= A76702 630 030812 HDBGNRB: Migrate PQ76702 from SPA   *
      *         D98593 700 150106 HD3BADW: Fix comments               *
      *        R116308 710 160411 HDGVJDT : Use LASTRESETABS          *
      *        R110014 710 160825 HDFVGMB : Add MQMONITOR statistics  *
      *        R131367 710 161025 HDLHJJH : Add AS statistics         *
      *        R148775 720 180718 HDLHJJH : Add NODEJSAPP statistics  *
      *        D150786 720 181023 HDFVGMB:  Add TRUNC(BIN) to PROCESS *
      *                                                               *
      *****************************************************************
       ID DIVISION.
       PROGRAM-ID. DFH0STAT.

       ENVIRONMENT DIVISION.
           EJECT
       DATA DIVISION.
       WORKING-STORAGE SECTION.


      *****************************************************************
      *  FACILITY is set to X'01' - value for a terminal facility     *
      *****************************************************************
       77  FACILITY                    PIC X       VALUE LOW-VALUE.
       88  TERM-FACILITY                           VALUE X'01'.

      *****************************************************************
      *  DATA-STREAM is set to X'FF' - value for a 3270 data stream   *
      *****************************************************************
       77  DATA-STREAM                 PIC X       VALUE LOW-VALUE.
       88  3270-DATA-STREAM                        VALUE X'FF'.
       77  START-CODE                  PIC XX      VALUE SPACES.

       77  END-PRINT                   PIC X       VALUE 'N'.
       77  END-SELECTION               PIC X       VALUE 'N'.
       77  END-HELP                    PIC X       VALUE 'N'.
       77  TERM-MSG-REQUIRED           PIC X       VALUE 'N'.
       77  REPORT-SELECTIONS-FOUND     PIC X       VALUE 'N'.
       77  REPORT-SELECTIONS-MODIFIED  PIC X       VALUE 'N'.
       77  DEFAULT-SELECTIONS-RESTORED PIC X       VALUE 'N'.

       77  STCM-COMMAREA               PIC S9(4)  COMP.
       77  PMSG2O-LEN                  PIC S9(8)  COMP.

       77  UTIME                       PIC S9(15) COMP-3 VALUE +0.
       77  MAP-DATE                    PIC X(10)       VALUE SPACES.
       77  MAP-TIME                    PIC X(8)        VALUE SPACES.

       77  MAP-NAME                    PIC X(8)    VALUE 'DFH0SS1 '.

       77  RESULT                      PIC S9(8)  COMP VALUE +0.

       77  CURRENT-SECONDS             PIC S9(8)  COMP VALUE +0.

       77  CICS-STARTUP                PIC S9(8)  COMP VALUE +0.
       77  CICS-STATUS                 PIC S9(8)  COMP VALUE +0.
       77  INIT-STATUS                 PIC S9(8)  COMP VALUE +0.

       77  EOD-COLLECTION-TIME         PIC S9(7)  COMP-3 VALUE +0.
       77  COLLECTION-INTERVAL         PIC S9(7)  COMP-3 VALUE +0.
       77  NEXT-COLLECTION-TIME        PIC S9(7)  COMP-3 VALUE +0.
       77  RECORDING-STATUS            PIC S9(8)  COMP VALUE +0.
       77  LAST-RESET-TIME-ABS         PIC S9(15) COMP-3 VALUE +0.
       77  ELAPSED-ABS                 PIC S9(15) COMP-3 VALUE +0.
       77  LAST-RESET-MAP-TIME         PIC X(8)   VALUE SPACES.
       77  ELAPSED-MAP-TIME            PIC X(8)   VALUE SPACES.
           EJECT
      *****************************************************************
      ***         Error messages                                    ***
      *****************************************************************
       77  ABBREV-ERROR-MSG            PIC X(79) VALUE 'Invalid report a
      -    'bbreviation.  Please correct and re-enter.          '.
       77  SELECT-ERROR-MSG            PIC X(79) VALUE 'Invalid report s
      -    'election.  Please correct and re-enter.             '.
       77  SELECT-VALID-MSG1           PIC X(79) VALUE 'Report selection
      -    's are valid.                                        '.
       77  SELECT-VALID-MSG2           PIC X(79) VALUE 'Return to the St
      -    'atistics Print screen to print the selected reports.'.
       77  NODE-MISSING-MSG            PIC X(79) VALUE 'Please enter a v
      -    'alid Node'.
       77  NODE-IDERR-MSG              PIC X(79) VALUE 'The Node specifi
      -    'ed is not valid.  Please re-enter this field.'.
       77  USERID-MSG                  PIC X(79) VALUE 'Please enter a v
      -    'alid Userid.'.
       77  CLASS-MSG                   PIC X(79) VALUE 'The Class specif
      -    'ied is not valid.  Please re-enter this field.'.
       77  OPEN-ERROR-MSG              PIC X(79) VALUE 'A CICS spool ope
      -    'n error has occurred. No statistics print produced.'.
       77  WRITE-ERROR-MSG             PIC X(79) VALUE 'A CICS spool wri
      -    'te error has occurred. Print may not be complete.'.
       77  CLOSE-ERROR-MSG             PIC X(79) VALUE 'A CICS spool clo
      -    'se error has occurred. No statistics print produced.'.
       77  OPER-MSG                    PIC X(79) VALUE 'PROGRAM DFH0STAT
      -    ' HAS FAILED DUE TO A SPOOL ERROR.  NO PRINT WILL BE PRODUCED
      -    '.'.
       77  SELECTIONS-SAVED-MSG        PIC X(79) VALUE 'The statistics r
      -    'eport selections have been saved.                   '.
       77  SELECTIONS-UPDATED-MSG      PIC X(79) VALUE 'The saved statis
      -    'tics report selections have been updated.           '.
       77  SAVED-SELECTIONS-USED-MSG   PIC X(79) VALUE 'Saved statistics
      -    ' report selections are being used.                  '.
       77  UPDATED-SELECTIONS-USED-MSG PIC X(79) VALUE 'Updated statisti
      -    'cs report selections are being used.                '.
       77  DEFAULTS-RESTORED-MSG       PIC X(79) VALUE 'The default stat
      -    'istics report selections have been restored.        '.

       77  OPER-MSG-LEN                PIC S9(8) COMP.

       77  END-MESSAGE                 PIC X(22)
               VALUE 'TRANSACTION TERMINATED'.
       77  SUCCESS-MESSAGE             PIC X(39)
               VALUE 'STATISTICS PRINT SUCCESSFULLY COMPLETED'.

       77  PF12-MSG                    PIC X(20)
                               VALUE 'F12=Restore Defaults'.

       77  MESSAGE-LENGTH              PIC S9(4) COMP.

       77  SELECTION-TSQUEUE-ITEM      PIC S9(4) COMP  VALUE +1.
       77  SELECTION-TSQUEUE-ITEMS     PIC S9(4) COMP  VALUE +0.
       77  SELECTION-TSQUEUE-LENGTH    PIC S9(4) COMP  VALUE +0.

       01  STAT-TSQUEUE.
           03  STAT-TSQUEUE-PREFIX     PIC X(4)    VALUE SPACES.
           03  STAT-TSQUEUE-SUFFIX     PIC X(4)    VALUE SPACES.
           03  STAT-TSQUEUE-USERID     PIC X(8)    VALUE SPACES.
           EJECT
      *****************************************************************
      ***  Defaults - Output Destination and Report Selections.     ***
      *****************************************************************
       01  REPORT-SELECTION-DEFAULTS.
           03  OUTPUT-DEFAULTS.
               05  DEFAULT-NODE        PIC X(8)    VALUE '*       '.
               05  DEFAULT-USERID      PIC X(8)    VALUE '*       '.
               05  DEFAULT-CLASS       PIC X       VALUE 'A'.
               05  DEFAULT-ABBREV      PIC X       VALUE 'B'.
               05  FILLER              PIC X(6)    VALUE SPACES.
           03  SELECTION-DEFAULTS.
               05  DEFAULT-PAGE-INDEX          PIC X   VALUE 'N'.
               05  DEFAULT-TRANMGR             PIC X   VALUE 'Y'.
               05  DEFAULT-DISPATCHER          PIC X   VALUE 'Y'.
               05  DEFAULT-MVS-TCBS            PIC X   VALUE 'N'.
               05  DEFAULT-STORAGE             PIC X   VALUE 'Y'.
               05  DEFAULT-STORAGE-SUBPOOLS    PIC X   VALUE 'Y'.
               05  DEFAULT-LIBRARY             PIC X   VALUE 'N'.
               05  DEFAULT-LOADER              PIC X   VALUE 'Y'.
               05  DEFAULT-TRANCLASS           PIC X   VALUE 'N'.
               05  DEFAULT-TRANSIDS            PIC X   VALUE 'N'.
               05  DEFAULT-PROGRAMDEFS         PIC X   VALUE 'N'.
               05  DEFAULT-PROGRAMS            PIC X   VALUE 'N'.
               05  DEFAULT-DFHRPL-ANALYSIS     PIC X   VALUE 'N'.
               05  DEFAULT-PROGRAMS-BY-DSA     PIC X   VALUE 'N'.
               05  DEFAULT-TEMPSTOR            PIC X   VALUE 'Y'.
               05  DEFAULT-TSMODELS            PIC X   VALUE 'N'.
               05  DEFAULT-TSQUEUES            PIC X   VALUE 'N'.
               05  DEFAULT-TSQUEUES-BY-POOL    PIC X   VALUE 'N'.
               05  DEFAULT-TRANDATA            PIC X   VALUE 'Y'.
               05  DEFAULT-TDQUEUES            PIC X   VALUE 'N'.
               05  DEFAULT-JOURNALS            PIC X   VALUE 'N'.
               05  DEFAULT-LOGSTREAMS          PIC X   VALUE 'N'.
               05  DEFAULT-LOGSTREAM-GLOBAL    PIC X   VALUE 'Y'.
               05  DEFAULT-IRC-ISC             PIC X   VALUE 'N'.
               05  DEFAULT-PGM-AUTOINST        PIC X   VALUE 'N'.
               05  DEFAULT-AUTO-VTAM           PIC X   VALUE 'N'.
               05  DEFAULT-TCPIP               PIC X   VALUE 'N'.
               05  DEFAULT-TCPIPSERVICES       PIC X   VALUE 'N'.
               05  DEFAULT-IPCONNS             PIC X   VALUE 'N'.
               05  DEFAULT-JVM-PROGRAMS        PIC X   VALUE 'N'.
               05  DEFAULT-LSRPOOLS            PIC X   VALUE 'N'.
               05  DEFAULT-FILES               PIC X   VALUE 'N'.
               05  DEFAULT-DSNAMES             PIC X   VALUE 'N'.
               05  DEFAULT-CFDT-POOLS          PIC X   VALUE 'N'.
               05  DEFAULT-USER-EXIT-PGMS      PIC X   VALUE 'N'.
               05  DEFAULT-USER-EXITS          PIC X   VALUE 'N'.
               05  DEFAULT-DB2                 PIC X   VALUE 'N'.
               05  DEFAULT-DB2ENTRIES          PIC X   VALUE 'N'.
               05  DEFAULT-ENQUEUE             PIC X   VALUE 'N'.
               05  DEFAULT-ENQMODELS           PIC X   VALUE 'N'.
               05  DEFAULT-RECOVERY            PIC X   VALUE 'N'.
               05  DEFAULT-MQCONN              PIC X   VALUE 'N'.
               05  DEFAULT-MQMONITORS          PIC X   VALUE 'N'.
               05  DEFAULT-TRACE-SETTINGS      PIC X   VALUE 'N'.
               05  DEFAULT-DOCTEMPLATES        PIC X   VALUE 'N'.
               05  DEFAULT-URIMAPS             PIC X   VALUE 'N'.
               05  DEFAULT-HOSTS               PIC X   VALUE 'N'.
               05  DEFAULT-WEBSERVICES         PIC X   VALUE 'N'.
               05  DEFAULT-PIPELINES           PIC X   VALUE 'N'.
               05  DEFAULT-ATOMSERVICES        PIC X   VALUE 'N'.
               05  DEFAULT-BUNDLES             PIC X   VALUE 'N'.
               05  DEFAULT-EVENT-PROCESS       PIC X   VALUE 'N'.
               05  DEFAULT-EVENTBINDINGS       PIC X   VALUE 'N'.
               05  DEFAULT-JVMSERVERS          PIC X   VALUE 'N'.
               05  DEFAULT-XMLTRANSFORMS       PIC X   VALUE 'N'.
               05  DEFAULT-CAPTURESPECS        PIC X   VALUE 'N'.
               05  DEFAULT-EPADAPTERS          PIC X   VALUE 'N'.
               05  DEFAULT-ASYNCSERVICE        PIC X   VALUE 'N'.
               05  DEFAULT-NODEJSAPP           PIC X   VALUE 'N'.
           EJECT
      *****************************************************************
      ***  Statistics Sample Programs Commarea.                     ***
      *****************************************************************
       01  DFH0STCM.
           COPY DFH0STCM.
           EJECT
      *****************************************************************
      ***  Report heading line definition                           ***
      *****************************************************************
       01  INITIAL-HEADING.
           03  FILLER                  PIC X(7)    VALUE 'Applid '.
           03  FILLER                  PIC X(8)    VALUE SPACES.
           03  FILLER                  PIC XX      VALUE SPACES.
           03  FILLER                  PIC X(6)    VALUE 'Sysid '.
           03  FILLER                  PIC X(4)    VALUE SPACES.
           03  FILLER                  PIC XX      VALUE SPACES.
           03  FILLER                  PIC X(8)    VALUE 'Jobname '.
           03  FILLER                  PIC X(8)    VALUE SPACES.
           03  FILLER                  PIC X(4)    VALUE SPACES.
           03  FILLER                  PIC X(5)    VALUE 'Date '.
           03  FILLER                  PIC X(10)   VALUE SPACES.
           03  FILLER                  PIC XX      VALUE SPACES.
           03  FILLER                  PIC X(5)    VALUE 'Time '.
           03  FILLER                  PIC X(8)    VALUE SPACES.
           03  FILLER                  PIC XX      VALUE SPACES.
           03  FILLER                  PIC X(14)   VALUE SPACES.
           03  FILLER                  PIC X(6)    VALUE ' CICS '.
           03  FILLER                  PIC 9B9B9.
           03  FILLER                  PIC X(14)   VALUE SPACES.
           03  FILLER                  PIC XX      VALUE SPACES.
           03  FILLER                  PIC X(5)    VALUE 'PAGE'.
           03  FILLER                  PIC Z,ZZ9.
           EJECT
      *****************************************************************
      *** Data structures for using mapset DFH0STM                  ***
      *****************************************************************
           COPY DFH0STMD.
           EJECT
      *****************************************************************
      *** Data structures for using mapset DFH0STS                  ***
      *****************************************************************
           COPY DFH0STSD.
           EJECT
      *****************************************************************
      *** Data structures for using EIBAID and BMS Attributes.      ***
      *****************************************************************
           COPY DFHAID.
           EJECT
           COPY DFHBMSCA.
           EJECT
       LINKAGE SECTION.
           EJECT
      *****************************************************************
      *** Data structures for Statistics Global statistics          ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHSTGDS.
           EJECT
      *****************************************************************
      *** Data structures for Transaction Manager Global statistics ***
      *** (See the CICS Data Areas manual)                          ***
      *****************************************************************
           COPY DFHXMGDS.
           EJECT
      *****************************************************************
      ***                                                           ***
      ***           CICS Collect and print statistics               ***
      ***                                                           ***
      *****************************************************************
       PROCEDURE DIVISION.

      *****************************************************************
      *** MAIN-PROCESSING                                           ***
      ***     Set default destination fields for output             ***
      ***     Get initial CICS information                          ***
      ***     SELECT on CICS status                                 ***
      ***        WHEN startup                                       ***
      ***            process statistics print                       ***
      ***        WHEN active                                        ***
      ***            IF primary facility is a terminal THEN         ***
      ***                IF terminal is a 3270 THEN                 ***
      ***                    process terminal input                 ***
      ***                ELSE                                       ***
      ***                    process statistics print               ***
      ***            ELSE                                           ***
      ***                process statistics print                   ***
      ***        WHEN first stage quiesce                           ***
      ***            process statistics print                       ***
      ***        WHEN final quiesce no action                       ***
      ***        WHEN other no action                               ***
      ***     END SELECT                                            ***
      ***     RETURN                                                ***
      *****************************************************************

       MAIN-PROCESSING SECTION.

      *****************************************************************
      ***   Perform the initial setup routine and get the required  ***
      *** CISC system information.                                  ***
      *****************************************************************
           PERFORM SETUP-ROUTINE.

           EXEC CICS INQUIRE SYSTEM
               CICSSTATUS(CICS-STATUS) INITSTATUS(INIT-STATUS)
               JOBNAME(JOBNAME) RELEASE(CICS-VERSION-RELEASE)
               STARTUP(CICS-STARTUP)
           END-EXEC.

           MOVE CICS-RELEASE TO RELEASE-NO.
           INSPECT RELEASE-NO REPLACING ALL ' ' BY '.'.

           EXEC CICS ASSIGN
               APPLID(APPLID) SYSID(SYSID)
               STARTCODE(START-CODE) USERID(STAT-TSQUEUE-USERID)
           END-EXEC.

      *****************************************************************

           IF START-CODE NOT = 'D ' AND 'DS' THEN
               EXEC CICS ASSIGN FCI(FACILITY)
               END-EXEC
               IF TERM-FACILITY THEN
                   EXEC CICS ASSIGN DS3270(DATA-STREAM)
                   END-EXEC
               END-IF
           END-IF.

      *****************************************************************
      * Determine from what stage DFH0STAT is being invoked; during   *
      * startup, active, or shutdown and process the type of          *
      * invocation appropriately. For example, only use the BMS maps  *
      * if DFH0STAT is being invoked from a terminal.                 *
      *****************************************************************

           EVALUATE CICS-STATUS
               WHEN DFHVALUE(STARTUP)
                   IF INIT-STATUS = DFHVALUE(THIRDINIT)
                     OR INIT-STATUS = DFHVALUE(INITCOMPLETE) THEN
                       PERFORM PROCESS-FROM-NON-TERMINAL
                   ELSE
                       CONTINUE
                   END-IF
               WHEN DFHVALUE(ACTIVE)
                   IF START-CODE = 'D ' OR 'DS' THEN
                       PERFORM PROCESS-FROM-NON-TERMINAL
                   ELSE
                       IF TERM-FACILITY THEN
                           IF 3270-DATA-STREAM THEN
                               MOVE 'Y' TO TERM-MSG-REQUIRED
                               PERFORM PROCESS-FROM-TERMINAL
                           ELSE
                               PERFORM PROCESS-FROM-NON-TERMINAL
                           END-IF
                       ELSE
                           PERFORM PROCESS-FROM-NON-TERMINAL
                       END-IF
                   END-IF
               WHEN DFHVALUE(FIRSTQUIESCE)
                   PERFORM PROCESS-FROM-NON-TERMINAL
               WHEN DFHVALUE(FINALQUIESCE)
                   CONTINUE
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.

           EXEC CICS RETURN
           END-EXEC.

       MAIN-PROCESSING-EXIT.
           GOBACK.
           EJECT
      *****************************************************************
      *** PROCESS-FROM-TERMINAL                                     ***
      ***     LOOP while not end of transaction                     ***
      ***        build screen for user panel                        ***
      ***        send user panel (with alarm if any error)          ***
      ***        receive user input                                 ***
      ***        SELECT on key pressed by user                      ***
      ***           WHEN enter                                      ***
      ***               update any fields changed by user           ***
      ***               process statistics print                    ***
      ***           WHEN clear no action                            ***
      ***           WHEN  PF3                                       ***
      ***               flag end of transaction                     ***
      ***           WHEN  PF4                                       ***
      ***               process report selection                    ***
      ***           WHEN other                                      ***
      ***               put error message in panels error field     ***
      ***     END SELECT                                            ***
      ***     send transaction ended message                        ***
      *****************************************************************

       PROCESS-FROM-TERMINAL SECTION.

           PERFORM ESTABLISH-SELECTIONS.

           MOVE LOW-VALUES TO DFH0STMO.

           PERFORM WITH TEST BEFORE
               UNTIL END-PRINT = 'Y'

               PERFORM BUILD-SCREEN

               IF ERROR-FOUND = 'N' THEN
                   EXEC CICS SEND MAP('DFH0STM') MAPSET('DFH0STM')
                       ERASE FREEKB
                   END-EXEC
               ELSE
                   EXEC CICS SEND MAP('DFH0STM') MAPSET('DFH0STM')
                       ALARM ERASE FREEKB CURSOR
                   END-EXEC
               END-IF
      *                                  Reset attribute bytes for map
               MOVE 'N' TO ERROR-FOUND
               MOVE DFHNEUTR   TO PMSG1C
               MOVE DFHNEUTR   TO PMSG2C
               MOVE DFHDFHI    TO PMSG1H
               MOVE DFHDFHI    TO PMSG2H
               MOVE LOW-VALUES TO PMSG1O
               MOVE LOW-VALUES TO PMSG2O

               EXEC CICS RECEIVE MAP('DFH0STM') MAPSET('DFH0STM')
                   NOHANDLE
               END-EXEC
      *                                  Act on users input
               EVALUATE EIBAID
                   WHEN DFHENTER
                       PERFORM PROCESS-INPUT

                       PERFORM PRINT-STATISTICS
                   WHEN DFHCLEAR
                       CONTINUE
                   WHEN DFHPF1
                       PERFORM SEND-DFH0STM-HELP
                   WHEN DFHPF2
                       CONTINUE
                   WHEN DFHPF3
                       MOVE 'Y' TO END-PRINT
                   WHEN DFHPF4
                       PERFORM REPORT-SELECTION
                   WHEN DFHPF5
                       PERFORM PROCESS-INPUT

                       PERFORM PRINT-STATISTICS
                   WHEN DFHPF12
                       PERFORM RESTORE-REPORT-DEFAULTS
                       IF DEFAULT-SELECTIONS-RESTORED = 'Y' THEN
                           MOVE DEFAULTS-RESTORED-MSG TO PMSG1O
                           MOVE LOW-VALUES            TO PPF12O
                       END-IF
                   WHEN OTHER
                       MOVE 'Y'      TO ERROR-FOUND
                       MOVE DFHYELLO TO PMSG2C
                       MOVE -1       TO PNODEL
                       MOVE 'Please use PF3 to exit or Enter to print'
                                     TO PMSG2O
               END-EVALUATE

           END-PERFORM.

           MOVE LENGTH OF END-MESSAGE TO MESSAGE-LENGTH.

           EXEC CICS SEND TEXT FROM(END-MESSAGE) LENGTH(MESSAGE-LENGTH)
               ERASE FREEKB
           END-EXEC.

       PROCESS-FROM-TERMINAL-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** PROCESS-FROM-NON-TERMINAL                                 ***
      ***     process statistics print not from a terminal.         ***
      *****************************************************************

       PROCESS-FROM-NON-TERMINAL SECTION.

           PERFORM PRINT-STATISTICS.

       PROCESS-FROM-NON-TERMINAL-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** BUILD-SCREEN                                              ***
      ***     build the DFH0STM print statistics map                ***
      ***     get and format the date and time (using CICS)         ***
      ***     put information in all map output fields              ***
      *****************************************************************

       BUILD-SCREEN SECTION.

           EXEC CICS ASKTIME ABSTIME(UTIME)
           END-EXEC.

           EXEC CICS FORMATTIME ABSTIME(UTIME)
               MMDDYYYY(MAP-DATE) DATESEP('/') TIME(MAP-TIME) TIMESEP
           END-EXEC.

      *****************************************************************
      *** Get the statistics information for the print screen.      ***
      *****************************************************************
           EXEC CICS INQUIRE STATISTICS
               ENDOFDAY(EOD-COLLECTION-TIME)
               INTERVAL(COLLECTION-INTERVAL)
               NEXTTIME(NEXT-COLLECTION-TIME)
               RECORDING(RECORDING-STATUS)
           END-EXEC.

           EXEC CICS EXTRACT STATISTICS TRANSACTION
               SET(ADDRESS OF DFHXMGDS)
               LASTRESETABS(LAST-RESET-TIME-ABS)
           END-EXEC.

      *    Calculate the elapsed time (in milliseconds) since statistics
      *    last reset.
           COMPUTE ELAPSED-ABS = UTIME - LAST-RESET-TIME-ABS.
           COMPUTE ELAPSED-SECONDS = (ELAPSED-ABS / 1000).

      *****************************************************************

           MOVE MAP-DATE        TO PDATEO.
           MOVE MAP-TIME        TO PTIMEO.
           MOVE JOBNAME         TO PJOBNMO.
           MOVE APPLID          TO PAPPLIDO.
           MOVE SYSID           TO PSYSIDO.

           MOVE OUTPUT-NODE     TO PNODEO.
           MOVE OUTPUT-USERID   TO PUSERIDO.
           MOVE OUTPUT-CLASS    TO PCLASSO.
           MOVE OUTPUT-ABBREV   TO PABBREVO.

           EVALUATE RECORDING-STATUS
               WHEN DFHVALUE(OFF)
                   MOVE 'OFF'   TO PSTRCDO
               WHEN DFHVALUE(ON)
                   MOVE 'ON '   TO PSTRCDO
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.
           MOVE COLLECTION-INTERVAL  TO PSTINTTO.
           INSPECT PSTINTTO REPLACING ALL ' ' BY ':'.

           EXEC CICS FORMATTIME ABSTIME(LAST-RESET-TIME-ABS)
               TIME(PSTLRSTO) TIMESEP
           END-EXEC.
           EXEC CICS FORMATTIME ABSTIME(ELAPSED-ABS)
               TIME(PETSRSTO) TIMESEP
           END-EXEC.

           MOVE NEXT-COLLECTION-TIME TO PNXTSTCO.
           INSPECT PNXTSTCO REPLACING ALL ' ' BY ':'.
           MOVE EOD-COLLECTION-TIME  TO PSTEODTO.
           INSPECT PSTEODTO REPLACING ALL ' ' BY ':'.

           IF REPORT-SELECTIONS-FOUND = 'Y' AND
             REPORT-SELECTIONS-MODIFIED = 'N' THEN
               MOVE SAVED-SELECTIONS-USED-MSG TO PMSG1O
               MOVE PF12-MSG                  TO PPF12O
           ELSE
               IF REPORT-SELECTIONS-MODIFIED = 'Y' THEN
                   MOVE UPDATED-SELECTIONS-USED-MSG TO PMSG1O
                   MOVE PF12-MSG                    TO PPF12O
               ELSE
                   NEXT SENTENCE
           END-IF.

       BUILD-SCREEN-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** SEND-DFH0STM-HELP                                         ***
      ***  Help screen requisted.                                   ***
      *****************************************************************
       SEND-DFH0STM-HELP SECTION.

           MOVE 'N' TO END-HELP.

           PERFORM WITH TEST BEFORE
               UNTIL END-HELP = 'Y'

               EXEC CICS SEND MAP('DFH0STH') MAPSET('DFH0STM')
                   ERASE FREEKB
               END-EXEC

               EXEC CICS RECEIVE MAP('DFH0STH') MAPSET('DFH0STM')
                   NOHANDLE
               END-EXEC
      *                                  Act on users input
               EVALUATE EIBAID
                   WHEN DFHPF3
                       MOVE 'Y' TO END-HELP
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE

           END-PERFORM.

       SEND-DFH0STM-HELP-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** PROCESS-INPUT                                             ***
      ***   Update any fields which the user has changed            ***
      ***     (include check for Erase EOF)                         ***
      *****************************************************************

       PROCESS-INPUT SECTION.

           IF (PNODEI NOT = LOW-VALUES) OR
                         (PNODEF NOT = LOW-VALUES)
               MOVE PNODEI TO OUTPUT-NODE
           END-IF.
           IF (PUSERIDI NOT = LOW-VALUES) OR
                         (PUSERIDF NOT = LOW-VALUES)
               MOVE PUSERIDI TO OUTPUT-USERID
           END-IF.
           IF (PCLASSI NOT = LOW-VALUES) OR
                         (PCLASSF NOT = LOW-VALUES)
               MOVE PCLASSI TO OUTPUT-CLASS
           END-IF.
           IF (PABBREVI NOT = LOW-VALUES) OR
                         (PABBREVF NOT = LOW-VALUES)
               IF PABBREVI = 'B' OR 'b' OR 'U' OR 'u'
                          OR 'N' OR 'n' OR ' ' THEN
                   MOVE PABBREVI TO OUTPUT-ABBREV
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO PMSG2C
                   MOVE ABBREV-ERROR-MSG TO PMSG2O
                   MOVE DFHYELLO         TO PABBREVC
                   MOVE DFHREVRS         TO PABBREVH
                   MOVE DFHBMBRY         TO PABBREVA
                   MOVE -1               TO PABBREVL
               END-IF
           END-IF.

       PROCESS-INPUT-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** REPORT-SELECTION                                          ***
      ***                                                           ***
      *****************************************************************
       REPORT-SELECTION SECTION.

      *    Initialise the report selection maps and set the initial
      *    map name to the first report selection screen.
           MOVE LOW-VALUES TO DFH0SS1O.
           MOVE 'DFH0SS1 ' TO MAP-NAME.

           MOVE 'N'        TO END-SELECTION.

           PERFORM WITH TEST BEFORE
               UNTIL END-SELECTION = 'Y'

      *        Determine the map and build the selection screen
               EVALUATE MAP-NAME
                   WHEN 'DFH0SS3 '
                       PERFORM BUILD-SELECTION-SCREEN-3
                   WHEN 'DFH0SS2 '
                       PERFORM BUILD-SELECTION-SCREEN-2
                   WHEN OTHER
                       PERFORM BUILD-SELECTION-SCREEN-1
               END-EVALUATE

      *        Determine the map and Send the selection screen
               EVALUATE MAP-NAME
                 WHEN 'DFH0SS3 '
                   IF ERROR-FOUND = 'N' THEN
                       EXEC CICS SEND MAP('DFH0SS3') MAPSET('DFH0STS')
                           ERASE FREEKB
                       END-EXEC
                   ELSE
                       EXEC CICS SEND MAP('DFH0SS3') MAPSET('DFH0STS')
                           ALARM ERASE FREEKB CURSOR
                       END-EXEC
                   END-IF
                 WHEN 'DFH0SS2 '
                   IF ERROR-FOUND = 'N' THEN
                       EXEC CICS SEND MAP('DFH0SS2') MAPSET('DFH0STS')
                           ERASE FREEKB
                       END-EXEC
                   ELSE
                       EXEC CICS SEND MAP('DFH0SS2') MAPSET('DFH0STS')
                           ALARM ERASE FREEKB CURSOR
                       END-EXEC
                   END-IF
                 WHEN OTHER
                   IF ERROR-FOUND = 'N' THEN
                       EXEC CICS SEND MAP('DFH0SS1') MAPSET('DFH0STS')
                           ERASE FREEKB
                       END-EXEC
                   ELSE
                       EXEC CICS SEND MAP('DFH0SS1') MAPSET('DFH0STS')
                           ALARM ERASE FREEKB CURSOR
                       END-EXEC
                   END-IF
               END-EVALUATE
      *                                Reset to no errors found yet
               MOVE 'N' TO ERROR-FOUND

               EVALUATE MAP-NAME
                   WHEN 'DFH0SS3 '
      *                                Reset attribute bytes for map 3
                       MOVE DFHNEUTR TO SMSG31C
                       MOVE DFHNEUTR TO SMSG32C
                       MOVE DFHDFHI  TO SMSG31H
                       MOVE DFHDFHI  TO SMSG32H
                       MOVE LOW-VALUES TO SMSG31O
                       MOVE LOW-VALUES TO SMSG32O
                   WHEN 'DFH0SS2 '
      *                                Reset attribute bytes for map 2
                       MOVE DFHNEUTR TO SMSG21C
                       MOVE DFHNEUTR TO SMSG22C
                       MOVE DFHDFHI  TO SMSG21H
                       MOVE DFHDFHI  TO SMSG22H
                       MOVE LOW-VALUES TO SMSG21O
                       MOVE LOW-VALUES TO SMSG22O
                   WHEN OTHER
      *                                Reset attribute bytes for map 1
                       MOVE DFHNEUTR TO SMSG11C
                       MOVE DFHNEUTR TO SMSG12C
                       MOVE DFHDFHI  TO SMSG11H
                       MOVE DFHDFHI  TO SMSG12H
                       MOVE LOW-VALUES TO SMSG11O
                       MOVE LOW-VALUES TO SMSG12O
               END-EVALUATE

      *       Determine the map and Receive the users input
               EVALUATE MAP-NAME
                   WHEN 'DFH0SS3 '
                       EXEC CICS RECEIVE MAP('DFH0SS3')
                           MAPSET('DFH0STS')
                           NOHANDLE
                       END-EXEC
                   WHEN 'DFH0SS2 '
                       EXEC CICS RECEIVE MAP('DFH0SS2')
                           MAPSET('DFH0STS')
                           NOHANDLE
                       END-EXEC
                   WHEN OTHER
                       EXEC CICS RECEIVE MAP('DFH0SS1')
                           MAPSET('DFH0STS')
                           NOHANDLE
                       END-EXEC
               END-EVALUATE
      *                                  Act on the users input
               EVALUATE EIBAID
                   WHEN DFHENTER
                       PERFORM PROCESS-SELECTION-INPUT
                   WHEN DFHCLEAR
                       CONTINUE
                   WHEN DFHPF1
                       PERFORM SEND-DFH0STS-HELP
                   WHEN DFHPF3
                       MOVE 'Y' TO END-SELECTION
                   WHEN DFHPF7
                       EVALUATE MAP-NAME
                           WHEN 'DFH0SS3 '
                               MOVE LOW-VALUES TO DFH0SS2O
                               MOVE 'DFH0SS2 ' TO MAP-NAME
                           WHEN 'DFH0SS2 '
                               MOVE LOW-VALUES TO DFH0SS1O
                               MOVE 'DFH0SS1 ' TO MAP-NAME
                           WHEN OTHER
                               MOVE LOW-VALUES TO DFH0SS1O
                               MOVE 'DFH0SS1 ' TO MAP-NAME
                       END-EVALUATE
                   WHEN DFHPF8
                       EVALUATE MAP-NAME
                           WHEN 'DFH0SS3 '
                               MOVE LOW-VALUES TO DFH0SS3O
                               MOVE 'DFH0SS3 ' TO MAP-NAME
                           WHEN 'DFH0SS2 '
                               MOVE LOW-VALUES TO DFH0SS3O
                               MOVE 'DFH0SS3 ' TO MAP-NAME
                           WHEN OTHER
                               MOVE LOW-VALUES TO DFH0SS2O
                               MOVE 'DFH0SS2 ' TO MAP-NAME
                       END-EVALUATE
                   WHEN DFHPF10
                       PERFORM PROCESS-SELECTION-INPUT
                       IF ERROR-FOUND NOT = 'Y' THEN
                           PERFORM SAVE-REPORT-SELECTIONS
                       END-IF
                   WHEN DFHPF12
                       PERFORM RESTORE-REPORT-DEFAULTS
                       EVALUATE MAP-NAME
                           WHEN 'DFH0SS3 '
                               MOVE DEFAULTS-RESTORED-MSG TO SMSG31O
                               MOVE SELECT-VALID-MSG2     TO SMSG32O
                           WHEN 'DFH0SS2 '
                               MOVE DEFAULTS-RESTORED-MSG TO SMSG21O
                               MOVE SELECT-VALID-MSG2     TO SMSG22O
                           WHEN OTHER
                               MOVE DEFAULTS-RESTORED-MSG TO SMSG11O
                               MOVE SELECT-VALID-MSG2     TO SMSG12O
                       END-EVALUATE
                   WHEN OTHER
                       MOVE 'Y' TO ERROR-FOUND
                       EVALUATE MAP-NAME
                         WHEN 'DFH0SS3 '
                           MOVE -1 TO SRCVRYL
                           MOVE DFHYELLO                   TO SMSG32C
                           MOVE 'Please use PF3 to return' TO SMSG32O
                         WHEN 'DFH0SS2 '
                           MOVE -1 TO SDB2CL
                           MOVE DFHYELLO                   TO SMSG22C
                           MOVE 'Please use PF3 to return' TO SMSG22O
                         WHEN OTHER
                           MOVE -1 TO SPGIDXL
                           MOVE DFHYELLO                   TO SMSG12C
                           MOVE 'Please use PF3 to return' TO SMSG12O
                       END-EVALUATE
               END-EVALUATE

           END-PERFORM.

       REPORT-SELECTION-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** BUILD-SELECTION-SCREEN-1                                  ***
      ***     get and format time (using CICS)                      ***
      ***     put information in all map output fields              ***
      *****************************************************************

       BUILD-SELECTION-SCREEN-1 SECTION.

           EXEC CICS ASKTIME ABSTIME(UTIME)
           END-EXEC.

           EXEC CICS FORMATTIME ABSTIME(UTIME)
               MMDDYYYY(MAP-DATE) DATESEP('/') TIME(MAP-TIME) TIMESEP
           END-EXEC.

           MOVE 'DFH0SS1 '                 TO MAP-NAME.

           MOVE MAP-DATE                   TO SDATE1O.
           MOVE MAP-TIME                   TO STIME1O.

           MOVE SELECT-PAGE-INDEX          TO SPGIDXO.

           MOVE SELECT-TRANMGR             TO SXMO.
           MOVE SELECT-DISPATCHER          TO SDSO.
           MOVE SELECT-MVS-TCBS            TO SDSTCBO.
           MOVE SELECT-STORAGE             TO SSTGO.
           MOVE SELECT-LOADER              TO SLDO.
           MOVE SELECT-STORAGE-SUBPOOLS    TO SSTGSPO.
           MOVE SELECT-LIBRARY             TO SLBRYO.
           MOVE SELECT-TRANSIDS            TO STRANO.
           MOVE SELECT-TRANCLASS           TO STCLSO.
           MOVE SELECT-PROGRAMDEFS         TO SPGMDFO.
           MOVE SELECT-PROGRAMS            TO SPGMO.
           MOVE SELECT-DFHRPL-ANALYSIS     TO SRPLAO.
           MOVE SELECT-PROGRAMS-BY-DSA     TO SPGDSAO.
           MOVE SELECT-TRANDATA            TO STDATAO.
           MOVE SELECT-TDQUEUES            TO STDQO.
           MOVE SELECT-TEMPSTOR            TO STEMPO.
           MOVE SELECT-TSMODELS            TO STSMDLSO.
           MOVE SELECT-TSQUEUES            TO STSQO.
           MOVE SELECT-TSQUEUES-BY-POOL    TO STSQBYPO.
           MOVE SELECT-JOURNALS            TO SJNLO.
           MOVE SELECT-LOGSTREAM-GLOBAL    TO SLGGBLO.
           MOVE SELECT-LOGSTREAMS          TO SLGSTRMO.
           MOVE SELECT-LSRPOOLS            TO SLSRPO.

           MOVE SELECT-FILES               TO SFILEO.
           MOVE SELECT-DSNAMES             TO SDSNAMEO.
           MOVE SELECT-CFDT-POOLS          TO SCFDTPSO.

       BUILD-SELECTION-SCREEN-1-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** BUILD-SELECTION-SCREEN-2                                  ***
      ***     get and format time (using CICS)                      ***
      ***     put information in all map output fields              ***
      *****************************************************************

       BUILD-SELECTION-SCREEN-2 SECTION.

           EXEC CICS ASKTIME ABSTIME(UTIME)
           END-EXEC.

           EXEC CICS FORMATTIME ABSTIME(UTIME)
               MMDDYYYY(MAP-DATE) DATESEP('/') TIME(MAP-TIME) TIMESEP
           END-EXEC.

           MOVE 'DFH0SS2 '                 TO MAP-NAME.

           MOVE MAP-DATE                   TO SDATE2O.
           MOVE MAP-TIME                   TO STIME2O.

           MOVE SELECT-DB2                 TO SDB2CO.
           MOVE SELECT-DB2ENTRIES          TO SDB2ENTO.
           MOVE SELECT-MQCONN              TO SMQCONNO.
           MOVE SELECT-MQMONITORS          TO SMQMONO.

           MOVE SELECT-PGM-AUTOINST        TO SPGAUTOO.
           MOVE SELECT-AUTO-VTAM           TO SAUTOVTO.

           MOVE SELECT-IRC-ISC             TO SIRCISCO.
           MOVE SELECT-JVM-PROGRAMS        TO SJVMPRGO.
           MOVE SELECT-JVMSERVERS          TO SJVMSVRO.
           MOVE SELECT-NODEJSAPP           TO SNODEJSO.

           MOVE SELECT-ASYNCSERVICE        TO SASYNCSO.

           MOVE SELECT-TCPIP               TO STCPIPO.
           MOVE SELECT-TCPIPSERVICES       TO STCPIPSO.
           MOVE SELECT-IPCONNS             TO SIPCONNO.

           MOVE SELECT-BUNDLES             TO SBNDLEO.
           MOVE SELECT-EVENT-PROCESS       TO SEVENTPO.
           MOVE SELECT-EPADAPTERS          TO SEPADAPO.
           MOVE SELECT-EVENTBINDINGS       TO SEVNTBSO.
           MOVE SELECT-CAPTURESPECS        TO SCSPECSO.

           MOVE SELECT-URIMAPS             TO SURIMAPO.
           MOVE SELECT-HOSTS               TO SHOSTSO.
           MOVE SELECT-ATOMSERVICES        TO SASVCESO.
           MOVE SELECT-PIPELINES           TO SPILNESO.
           MOVE SELECT-WEBSERVICES         TO SWEBSVSO.
           MOVE SELECT-DOCTEMPLATES        TO SDOCTMPO.
           MOVE SELECT-XMLTRANSFORMS       TO SXMLTRFO.

       BUILD-SELECTION-SCREEN-2-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** BUILD-SELECTION-SCREEN-3                                  ***
      ***     get and format time (using CICS)                      ***
      ***     put information in all map output fields              ***
      *****************************************************************

       BUILD-SELECTION-SCREEN-3 SECTION.

           EXEC CICS ASKTIME ABSTIME(UTIME)
           END-EXEC.

           EXEC CICS FORMATTIME ABSTIME(UTIME)
               MMDDYYYY(MAP-DATE) DATESEP('/') TIME(MAP-TIME) TIMESEP
           END-EXEC.

           MOVE 'DFH0SS3 '                 TO MAP-NAME.

           MOVE MAP-DATE                   TO SDATE3O.
           MOVE MAP-TIME                   TO STIME3O.

           MOVE SELECT-TRACE-SETTINGS      TO STROPTSO.
           MOVE SELECT-USER-EXIT-PGMS      TO SUEXPGMO.
           MOVE SELECT-USER-EXITS          TO SGLUESO.

           MOVE SELECT-RECOVERY            TO SRCVRYO.
           MOVE SELECT-ENQUEUE             TO SENQMGRO.
           MOVE SELECT-ENQMODELS           TO SENQMDLO.

       BUILD-SELECTION-SCREEN-3-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** SEND-DFH0STS-HELP                                         ***
      ***  Help screen requisted.                                   ***
      *****************************************************************
       SEND-DFH0STS-HELP SECTION.

           MOVE 'N' TO END-HELP.

           PERFORM WITH TEST BEFORE
               UNTIL END-HELP = 'Y'

               EXEC CICS SEND MAP('DFH0SSH') MAPSET('DFH0STS')
                   ERASE FREEKB
               END-EXEC

               EXEC CICS RECEIVE MAP('DFH0SSH') MAPSET('DFH0STS')
                   NOHANDLE
               END-EXEC
      *                                  Act on users input
               EVALUATE EIBAID
                   WHEN DFHPF3
                       MOVE 'Y' TO END-HELP
                   WHEN OTHER
                       CONTINUE
               END-EVALUATE

           END-PERFORM.

       SEND-DFH0STS-HELP-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** PROCESS-SELECTION-INPUT                                   ***
      ***                                                           ***
      *****************************************************************
       PROCESS-SELECTION-INPUT SECTION.

           EVALUATE MAP-NAME
               WHEN 'DFH0SS1 '
                   PERFORM PROCESS-SELECTION-INPUT-1
               WHEN 'DFH0SS2 '
                   PERFORM PROCESS-SELECTION-INPUT-2
               WHEN 'DFH0SS3 '
                   PERFORM PROCESS-SELECTION-INPUT-3
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.

       PROCESS-SELECTION-INPUT-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** PROCESS-SELECTION-INPUT-1                                 ***
      ***   Update any fields which the user has changed            ***
      ***     (include check for Erase EOF)                         ***
      *****************************************************************

       PROCESS-SELECTION-INPUT-1 SECTION.

           MOVE 'N' TO REPORT-SELECTIONS-MODIFIED.

           IF (SPGIDXI NOT = LOW-VALUES) OR
                         (SPGIDXF NOT = LOW-VALUES)
               IF SPGIDXI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SPGIDXI TO SELECT-PAGE-INDEX
                   MOVE 'Y'     TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO SPGIDXC
                   MOVE DFHREVRS         TO SPGIDXH
                   MOVE DFHBMBRY         TO SPGIDXA
                   MOVE -1               TO SPGIDXL
               END-IF
           END-IF.

           IF (SXMI NOT = LOW-VALUES) OR
                         (SXMF NOT = LOW-VALUES)
               IF SXMI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SXMI TO SELECT-TRANMGR
                   MOVE 'Y'  TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO SXMC
                   MOVE DFHREVRS         TO SXMH
                   MOVE DFHBMBRY         TO SXMA
                   MOVE -1               TO SXML
               END-IF
           END-IF.

           IF (SDSI NOT = LOW-VALUES) OR
                         (SDSF NOT = LOW-VALUES)
               IF SDSI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SDSI TO SELECT-DISPATCHER
                   MOVE 'Y'  TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO SDSC
                   MOVE DFHREVRS         TO SDSH
                   MOVE DFHBMBRY         TO SDSA
                   MOVE -1               TO SDSL
               END-IF
           END-IF.

           IF (SDSTCBI NOT = LOW-VALUES) OR
                         (SDSTCBF NOT = LOW-VALUES)
               IF SDSTCBI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SDSTCBI TO SELECT-MVS-TCBS
                   MOVE 'Y'  TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO SDSTCBC
                   MOVE DFHREVRS         TO SDSTCBH
                   MOVE DFHBMBRY         TO SDSTCBA
                   MOVE -1               TO SDSTCBL
               END-IF
           END-IF.

           IF (SSTGI NOT = LOW-VALUES) OR
                         (SSTGF NOT = LOW-VALUES)
               IF SSTGI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SSTGI TO SELECT-STORAGE
                   MOVE 'Y'   TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO SSTGC
                   MOVE DFHREVRS         TO SSTGH
                   MOVE DFHBMBRY         TO SSTGA
                   MOVE -1               TO SSTGL
               END-IF
           END-IF.

           IF (SLDI NOT = LOW-VALUES) OR
                         (SLDF NOT = LOW-VALUES)
               IF SLDI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SLDI TO SELECT-LOADER
                   MOVE 'Y'  TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO SLDC
                   MOVE DFHREVRS         TO SLDH
                   MOVE DFHBMBRY         TO SLDA
                   MOVE -1               TO SLDL
               END-IF
           END-IF.

           IF (SLBRYI NOT = LOW-VALUES) OR
                         (SLBRYF NOT = LOW-VALUES)
               IF SLBRYI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SLBRYI TO SELECT-LIBRARY
                   MOVE 'Y'    TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO SLBRYC
                   MOVE DFHREVRS         TO SLBRYH
                   MOVE DFHBMBRY         TO SLBRYA
                   MOVE -1               TO SLBRYL
               END-IF
           END-IF.

           IF (SSTGSPI NOT = LOW-VALUES) OR
                         (SSTGF NOT = LOW-VALUES)
               IF SSTGSPI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SSTGSPI TO SELECT-STORAGE-SUBPOOLS
                   MOVE 'Y'     TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO SSTGSPC
                   MOVE DFHREVRS         TO SSTGSPH
                   MOVE DFHBMBRY         TO SSTGSPA
                   MOVE -1               TO SSTGSPL
               END-IF
           END-IF.

           IF (STRANI NOT = LOW-VALUES) OR
                         (STRANF NOT = LOW-VALUES)
               IF STRANI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE STRANI TO SELECT-TRANSIDS
                   MOVE 'Y'    TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO STRANC
                   MOVE DFHREVRS         TO STRANH
                   MOVE DFHBMBRY         TO STRANA
                   MOVE -1               TO STRANL
               END-IF
           END-IF.

           IF (SPGMDFI NOT = LOW-VALUES) OR
                         (SPGMDFF NOT = LOW-VALUES)
               IF SPGMDFI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SPGMDFI TO SELECT-PROGRAMDEFS
                   MOVE 'Y'     TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO SPGMDFC
                   MOVE DFHREVRS         TO SPGMDFH
                   MOVE DFHBMBRY         TO SPGMDFA
                   MOVE -1               TO SPGMDFL
               END-IF
           END-IF.

           IF (STCLSI NOT = LOW-VALUES) OR
                         (STCLSF NOT = LOW-VALUES)
               IF STCLSI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE STCLSI TO SELECT-TRANCLASS
                   MOVE 'Y'    TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO STCLSC
                   MOVE DFHREVRS         TO STCLSH
                   MOVE DFHBMBRY         TO STCLSA
                   MOVE -1               TO STCLSL
               END-IF
           END-IF.

           IF (SPGMI NOT = LOW-VALUES) OR
                         (SPGMF NOT = LOW-VALUES)
               IF SPGMI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SPGMI TO SELECT-PROGRAMS
                   MOVE 'Y'   TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO SPGMC
                   MOVE DFHREVRS         TO SPGMH
                   MOVE DFHBMBRY         TO SPGMA
                   MOVE -1               TO SPGML
               END-IF
           END-IF.

           IF (SRPLAI NOT = LOW-VALUES) OR
                         (SRPLAF NOT = LOW-VALUES)
               IF SRPLAI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SRPLAI TO SELECT-DFHRPL-ANALYSIS
                   MOVE 'Y'    TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO SRPLAC
                   MOVE DFHREVRS         TO SRPLAH
                   MOVE DFHBMBRY         TO SRPLAA
                   MOVE -1               TO SRPLAL
               END-IF
           END-IF.

           IF (SPGDSAI NOT = LOW-VALUES) OR
                         (SPGDSAF NOT = LOW-VALUES)
               IF SPGDSAI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SPGDSAI TO SELECT-PROGRAMS-BY-DSA
                   MOVE 'Y'     TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO SPGDSAC
                   MOVE DFHREVRS         TO SPGDSAH
                   MOVE DFHBMBRY         TO SPGDSAA
                   MOVE -1               TO SPGDSAL
               END-IF
           END-IF.

           IF (STDATAI NOT = LOW-VALUES) OR
                         (STDATAF NOT = LOW-VALUES)
               IF STDATAI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE STDATAI TO SELECT-TRANDATA
                   MOVE 'Y'     TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO STDATAC
                   MOVE DFHREVRS         TO STDATAH
                   MOVE DFHBMBRY         TO STDATAA
                   MOVE -1               TO STDATAL
               END-IF
           END-IF.

           IF (STDQI NOT = LOW-VALUES) OR
                         (STDQF NOT = LOW-VALUES)
               IF STDQI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE STDQI TO SELECT-TDQUEUES
                   MOVE 'Y'   TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO STDQC
                   MOVE DFHREVRS         TO STDQH
                   MOVE DFHBMBRY         TO STDQA
                   MOVE -1               TO STDQL
               END-IF
           END-IF.

           IF (STEMPI NOT = LOW-VALUES) OR
                         (STEMPF NOT = LOW-VALUES)
               IF STEMPI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE STEMPI TO SELECT-TEMPSTOR
                   MOVE 'Y'    TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO STEMPC
                   MOVE DFHREVRS         TO STEMPH
                   MOVE DFHBMBRY         TO STEMPA
                   MOVE -1               TO STEMPL
               END-IF
           END-IF.

           IF (STSMDLSI NOT = LOW-VALUES) OR
                         (STSMDLSF NOT = LOW-VALUES)
               IF STSMDLSI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE STSMDLSI TO SELECT-TSMODELS
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO STSMDLSC
                   MOVE DFHREVRS         TO STSMDLSH
                   MOVE DFHBMBRY         TO STSMDLSA
                   MOVE -1               TO STSMDLSL
               END-IF
           END-IF.

           IF (STSQI NOT = LOW-VALUES) OR
                         (STSQF NOT = LOW-VALUES)
               IF STSQI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE STSQI TO SELECT-TSQUEUES
                   MOVE 'Y'   TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO STSQC
                   MOVE DFHREVRS         TO STSQH
                   MOVE DFHBMBRY         TO STSQA
                   MOVE -1               TO STSQL
               END-IF
           END-IF.

           IF (STSQBYPI NOT = LOW-VALUES) OR
                         (STSQBYPF NOT = LOW-VALUES)
               IF STSQBYPI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE STSQBYPI TO SELECT-TSQUEUES-BY-POOL
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO STSQBYPC
                   MOVE DFHREVRS         TO STSQBYPH
                   MOVE DFHBMBRY         TO STSQBYPA
                   MOVE -1               TO STSQBYPL
               END-IF
           END-IF.

           IF (SLGGBLI NOT = LOW-VALUES) OR
                         (SLGGBLF NOT = LOW-VALUES)
               IF SLGGBLI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SLGGBLI TO SELECT-LOGSTREAM-GLOBAL
                   MOVE 'Y'     TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO SLGGBLC
                   MOVE DFHREVRS         TO SLGGBLH
                   MOVE DFHBMBRY         TO SLGGBLA
                   MOVE -1               TO SLGGBLL
               END-IF
           END-IF.

           IF (SJNLI NOT = LOW-VALUES) OR
                         (SJNLF NOT = LOW-VALUES)
               IF SJNLI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SJNLI TO SELECT-JOURNALS
                   MOVE 'Y'   TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO SJNLC
                   MOVE DFHREVRS         TO SJNLH
                   MOVE DFHBMBRY         TO SJNLA
                   MOVE -1               TO SJNLL
               END-IF
           END-IF.

           IF (SLGSTRMI NOT = LOW-VALUES) OR
                         (SLGSTRMF NOT = LOW-VALUES)
               IF SLGSTRMI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SLGSTRMI TO SELECT-LOGSTREAMS
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO SLGSTRMC
                   MOVE DFHREVRS         TO SLGSTRMH
                   MOVE DFHBMBRY         TO SLGSTRMA
                   MOVE -1               TO SLGSTRML
               END-IF
           END-IF.

           IF (SLSRPI NOT = LOW-VALUES) OR
                         (SLSRPF NOT = LOW-VALUES)
               IF SLSRPI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SLSRPI TO SELECT-LSRPOOLS
                   MOVE 'Y'    TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO SLSRPC
                   MOVE DFHREVRS         TO SLSRPH
                   MOVE DFHBMBRY         TO SLSRPA
                   MOVE -1               TO SLSRPL
               END-IF
           END-IF.

           IF (SFILEI NOT = LOW-VALUES) OR
                         (SFILEF NOT = LOW-VALUES)
               IF SFILEI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SFILEI TO SELECT-FILES
                   MOVE 'Y'    TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO SFILEC
                   MOVE DFHREVRS         TO SFILEH
                   MOVE DFHBMBRY         TO SFILEA
                   MOVE -1               TO SFILEL
               END-IF
           END-IF.

           IF (SDSNAMEI NOT = LOW-VALUES) OR
                         (SDSNAMEF NOT = LOW-VALUES)
               IF SDSNAMEI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SDSNAMEI TO SELECT-DSNAMES
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO SDSNAMEC
                   MOVE DFHREVRS         TO SDSNAMEH
                   MOVE DFHBMBRY         TO SDSNAMEA
                   MOVE -1               TO SDSNAMEL
               END-IF
           END-IF.

           IF (SCFDTPSI NOT = LOW-VALUES) OR
                         (SCFDTPSF NOT = LOW-VALUES)
               IF SCFDTPSI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SCFDTPSI TO SELECT-CFDT-POOLS
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG12C
                   MOVE SELECT-ERROR-MSG TO SMSG12O
                   MOVE DFHYELLO         TO SCFDTPSC
                   MOVE DFHREVRS         TO SCFDTPSH
                   MOVE DFHBMBRY         TO SCFDTPSA
                   MOVE -1               TO SCFDTPSL
               END-IF
           END-IF.

      *****************************************************************
      *** If no error has been found during validation add the      ***
      *** appropriate messages to the screen.                       ***
      *****************************************************************
           IF ERROR-FOUND = 'N' THEN
               MOVE SELECT-VALID-MSG1 TO SMSG11O
               MOVE SELECT-VALID-MSG2 TO SMSG12O
           END-IF.

       PROCESS-SELECTION-INPUT-1-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** PROCESS-SELECTION-INPUT-2                                 ***
      ***   Update any fields which the user has changed            ***
      ***     (include check for Erase EOF)                         ***
      *****************************************************************

       PROCESS-SELECTION-INPUT-2 SECTION.

           IF (SDB2CI NOT = LOW-VALUES) OR
                         (SDB2CF NOT = LOW-VALUES)
               IF SDB2CI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SDB2CI TO SELECT-DB2
                   MOVE 'Y'    TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG22C
                   MOVE SELECT-ERROR-MSG TO SMSG22O
                   MOVE DFHYELLO         TO SDB2CC
                   MOVE DFHREVRS         TO SDB2CH
                   MOVE DFHBMBRY         TO SDB2CA
                   MOVE -1               TO SDB2CL
               END-IF
           END-IF.

           IF (SDB2ENTI NOT = LOW-VALUES) OR
                         (SDB2ENTF NOT = LOW-VALUES)
               IF SDB2ENTI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SDB2ENTI TO SELECT-DB2ENTRIES
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG22C
                   MOVE SELECT-ERROR-MSG TO SMSG22O
                   MOVE DFHYELLO         TO SDB2ENTC
                   MOVE DFHREVRS         TO SDB2ENTH
                   MOVE DFHBMBRY         TO SDB2ENTA
                   MOVE -1               TO SDB2ENTL
               END-IF
           END-IF.

           IF (SMQCONNI NOT = LOW-VALUES) OR
                         (SMQCONNF NOT = LOW-VALUES)
               IF SMQCONNI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SMQCONNI TO SELECT-MQCONN
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG22C
                   MOVE SELECT-ERROR-MSG TO SMSG22O
                   MOVE DFHYELLO         TO SMQCONNC
                   MOVE DFHREVRS         TO SMQCONNH
                   MOVE DFHBMBRY         TO SMQCONNA
                   MOVE -1               TO SMQCONNL
               END-IF
           END-IF.

           IF (SMQMONI NOT = LOW-VALUES) OR
                         (SMQMONF NOT = LOW-VALUES)
               IF SMQMONI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SMQMONI TO SELECT-MQMONITORS
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG22C
                   MOVE SELECT-ERROR-MSG TO SMSG22O
                   MOVE DFHYELLO         TO SMQMONC
                   MOVE DFHREVRS         TO SMQMONH
                   MOVE DFHBMBRY         TO SMQMONA
                   MOVE -1               TO SMQMONL
               END-IF
           END-IF.

           IF (SPGAUTOI NOT = LOW-VALUES) OR
                         (SPGAUTOF NOT = LOW-VALUES)
               IF SPGAUTOI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SPGAUTOI TO SELECT-PGM-AUTOINST
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG22C
                   MOVE SELECT-ERROR-MSG TO SMSG22O
                   MOVE DFHYELLO         TO SPGAUTOC
                   MOVE DFHREVRS         TO SPGAUTOH
                   MOVE DFHBMBRY         TO SPGAUTOA
                   MOVE -1               TO SPGAUTOL
               END-IF
           END-IF.

           IF (SAUTOVTI NOT = LOW-VALUES) OR
                         (SAUTOVTF NOT = LOW-VALUES)
               IF SAUTOVTI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SAUTOVTI TO SELECT-AUTO-VTAM
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG22C
                   MOVE SELECT-ERROR-MSG TO SMSG22O
                   MOVE DFHYELLO         TO SAUTOVTC
                   MOVE DFHREVRS         TO SAUTOVTH
                   MOVE DFHBMBRY         TO SAUTOVTA
                   MOVE -1               TO SAUTOVTL
               END-IF
           END-IF.

           IF (SIRCISCI NOT = LOW-VALUES) OR
                         (SIRCISCF NOT = LOW-VALUES)
               IF SIRCISCI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SIRCISCI TO SELECT-IRC-ISC
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG22C
                   MOVE SELECT-ERROR-MSG TO SMSG22O
                   MOVE DFHYELLO         TO SIRCISCC
                   MOVE DFHREVRS         TO SIRCISCH
                   MOVE DFHBMBRY         TO SIRCISCA
                   MOVE -1               TO SIRCISCL
               END-IF
           END-IF.

           IF (SJVMPRGI NOT = LOW-VALUES) OR
                         (SJVMPRGF NOT = LOW-VALUES)
               IF SJVMPRGI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SJVMPRGI TO SELECT-JVM-PROGRAMS
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG22C
                   MOVE SELECT-ERROR-MSG TO SMSG22O
                   MOVE DFHYELLO         TO SJVMPRGC
                   MOVE DFHREVRS         TO SJVMPRGH
                   MOVE DFHBMBRY         TO SJVMPRGA
                   MOVE -1               TO SJVMPRGL
               END-IF
           END-IF.

           IF (SJVMSVRI NOT = LOW-VALUES) OR
                         (SJVMSVRF NOT = LOW-VALUES)
               IF SJVMSVRI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SJVMSVRI TO SELECT-JVMSERVERS
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG22C
                   MOVE SELECT-ERROR-MSG TO SMSG22O
                   MOVE DFHYELLO         TO SJVMSVRC
                   MOVE DFHREVRS         TO SJVMSVRH
                   MOVE DFHBMBRY         TO SJVMSVRA
                   MOVE -1               TO SJVMSVRL
               END-IF
           END-IF.

           IF (SNODEJSI NOT = LOW-VALUES) OR
                         (SNODEJSF NOT = LOW-VALUES)
               IF SNODEJSI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SNODEJSI TO SELECT-NODEJSAPP
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG22C
                   MOVE SELECT-ERROR-MSG TO SMSG22O
                   MOVE DFHYELLO         TO SNODEJSC
                   MOVE DFHREVRS         TO SNODEJSH
                   MOVE DFHBMBRY         TO SNODEJSA
                   MOVE -1               TO SNODEJSL
               END-IF
           END-IF.

           IF (SASYNCSI NOT = LOW-VALUES) OR
                         (SASYNCSF NOT = LOW-VALUES)
               IF SASYNCSI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SASYNCSI TO SELECT-ASYNCSERVICE
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG22C
                   MOVE SELECT-ERROR-MSG TO SMSG22O
                   MOVE DFHYELLO         TO SASYNCSC
                   MOVE DFHREVRS         TO SASYNCSH
                   MOVE DFHBMBRY         TO SASYNCSA
                   MOVE -1               TO SASYNCSL
               END-IF
           END-IF.

           IF (STCPIPI NOT = LOW-VALUES) OR
                         (STCPIPF NOT = LOW-VALUES)
               IF STCPIPI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE STCPIPI TO SELECT-TCPIP
                   MOVE 'Y'     TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG22C
                   MOVE SELECT-ERROR-MSG TO SMSG22O
                   MOVE DFHYELLO         TO STCPIPC
                   MOVE DFHREVRS         TO STCPIPH
                   MOVE DFHBMBRY         TO STCPIPA
                   MOVE -1               TO STCPIPL
               END-IF
           END-IF.

           IF (STCPIPSI NOT = LOW-VALUES) OR
                         (STCPIPSF NOT = LOW-VALUES)
               IF STCPIPSI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE STCPIPSI TO SELECT-TCPIPSERVICES
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG22C
                   MOVE SELECT-ERROR-MSG TO SMSG22O
                   MOVE DFHYELLO         TO STCPIPSC
                   MOVE DFHREVRS         TO STCPIPSH
                   MOVE DFHBMBRY         TO STCPIPSA
                   MOVE -1               TO STCPIPSL
               END-IF
           END-IF.

           IF (SIPCONNI NOT = LOW-VALUES) OR
                         (SIPCONNF NOT = LOW-VALUES)
               IF SIPCONNI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SIPCONNI TO SELECT-IPCONNS
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG22C
                   MOVE SELECT-ERROR-MSG TO SMSG22O
                   MOVE DFHYELLO         TO SIPCONNC
                   MOVE DFHREVRS         TO SIPCONNH
                   MOVE DFHBMBRY         TO SIPCONNA
                   MOVE -1               TO SIPCONNL
               END-IF
           END-IF.

           IF (SBNDLEI NOT = LOW-VALUES) OR
                         (SBNDLEF NOT = LOW-VALUES)
               IF SBNDLEI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SBNDLEI TO SELECT-BUNDLES
                   MOVE 'Y'     TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG22C
                   MOVE SELECT-ERROR-MSG TO SMSG22O
                   MOVE DFHYELLO         TO SBNDLEC
                   MOVE DFHREVRS         TO SBNDLEH
                   MOVE DFHBMBRY         TO SBNDLEA
                   MOVE -1               TO SBNDLEL
               END-IF
           END-IF.

           IF (SEVENTPI NOT = LOW-VALUES) OR
                         (SEVENTPF NOT = LOW-VALUES)
               IF SEVENTPI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SEVENTPI TO SELECT-EVENT-PROCESS
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG22C
                   MOVE SELECT-ERROR-MSG TO SMSG22O
                   MOVE DFHYELLO         TO SEVENTPC
                   MOVE DFHREVRS         TO SEVENTPH
                   MOVE DFHBMBRY         TO SEVENTPA
                   MOVE -1               TO SEVENTPL
               END-IF
           END-IF.

           IF (SEPADAPI NOT = LOW-VALUES) OR
                         (SEPADAPF NOT = LOW-VALUES)
               IF SEPADAPI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SEPADAPI TO SELECT-EPADAPTERS
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG22C
                   MOVE SELECT-ERROR-MSG TO SMSG22O
                   MOVE DFHYELLO         TO SEPADAPC
                   MOVE DFHREVRS         TO SEPADAPH
                   MOVE DFHBMBRY         TO SEPADAPA
                   MOVE -1               TO SEPADAPL
               END-IF
           END-IF.

           IF (SEVNTBSI NOT = LOW-VALUES) OR
                         (SEVNTBSF NOT = LOW-VALUES)
               IF SEVNTBSI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SEVNTBSI TO SELECT-EVENTBINDINGS
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG22C
                   MOVE SELECT-ERROR-MSG TO SMSG22O
                   MOVE DFHYELLO         TO SEVNTBSC
                   MOVE DFHREVRS         TO SEVNTBSH
                   MOVE DFHBMBRY         TO SEVNTBSA
                   MOVE -1               TO SEVNTBSL
               END-IF
           END-IF.

           IF (SCSPECSI NOT = LOW-VALUES) OR
                         (SCSPECSF NOT = LOW-VALUES)
               IF SCSPECSI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SCSPECSI TO SELECT-CAPTURESPECS
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG22C
                   MOVE SELECT-ERROR-MSG TO SMSG22O
                   MOVE DFHYELLO         TO SCSPECSC
                   MOVE DFHREVRS         TO SCSPECSH
                   MOVE DFHBMBRY         TO SCSPECSA
                   MOVE -1               TO SCSPECSL
               END-IF
           END-IF.

           IF (SURIMAPI NOT = LOW-VALUES) OR
                         (SURIMAPF NOT = LOW-VALUES)
               IF SURIMAPI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SURIMAPI TO SELECT-URIMAPS
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG22C
                   MOVE SELECT-ERROR-MSG TO SMSG22O
                   MOVE DFHYELLO         TO SURIMAPC
                   MOVE DFHREVRS         TO SURIMAPH
                   MOVE DFHBMBRY         TO SURIMAPA
                   MOVE -1               TO SURIMAPL
               END-IF
           END-IF.

           IF (SHOSTSI NOT = LOW-VALUES) OR
                         (SHOSTSF NOT = LOW-VALUES)
               IF SHOSTSI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SHOSTSI TO SELECT-HOSTS
                   MOVE 'Y'     TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG22C
                   MOVE SELECT-ERROR-MSG TO SMSG22O
                   MOVE DFHYELLO         TO SHOSTSC
                   MOVE DFHREVRS         TO SHOSTSH
                   MOVE DFHBMBRY         TO SHOSTSA
                   MOVE -1               TO SHOSTSL
               END-IF
           END-IF.

           IF (SASVCESI NOT = LOW-VALUES) OR
                         (SASVCESF NOT = LOW-VALUES)
               IF SASVCESI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SASVCESI TO SELECT-ATOMSERVICES
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG22C
                   MOVE SELECT-ERROR-MSG TO SMSG22O
                   MOVE DFHYELLO         TO SASVCESC
                   MOVE DFHREVRS         TO SASVCESH
                   MOVE DFHBMBRY         TO SASVCESA
                   MOVE -1               TO SASVCESL
               END-IF
           END-IF.

           IF (SPILNESI NOT = LOW-VALUES) OR
                         (SPILNESF NOT = LOW-VALUES)
               IF SPILNESI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SPILNESI TO SELECT-PIPELINES
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG22C
                   MOVE SELECT-ERROR-MSG TO SMSG22O
                   MOVE DFHYELLO         TO SPILNESC
                   MOVE DFHREVRS         TO SPILNESH
                   MOVE DFHBMBRY         TO SPILNESA
                   MOVE -1               TO SPILNESL
               END-IF
           END-IF.

           IF (SWEBSVSI NOT = LOW-VALUES) OR
                         (SWEBSVSF NOT = LOW-VALUES)
               IF SWEBSVSI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SWEBSVSI TO SELECT-WEBSERVICES
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG22C
                   MOVE SELECT-ERROR-MSG TO SMSG22O
                   MOVE DFHYELLO         TO SWEBSVSC
                   MOVE DFHREVRS         TO SWEBSVSH
                   MOVE DFHBMBRY         TO SWEBSVSA
                   MOVE -1               TO SWEBSVSL
               END-IF
           END-IF.

           IF (SDOCTMPI NOT = LOW-VALUES) OR
                         (SDOCTMPF NOT = LOW-VALUES)
               IF SDOCTMPI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SDOCTMPI TO SELECT-DOCTEMPLATES
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG22C
                   MOVE SELECT-ERROR-MSG TO SMSG22O
                   MOVE DFHYELLO         TO SDOCTMPC
                   MOVE DFHREVRS         TO SDOCTMPH
                   MOVE DFHBMBRY         TO SDOCTMPA
                   MOVE -1               TO SDOCTMPL
               END-IF
           END-IF.

           IF (SXMLTRFI NOT = LOW-VALUES) OR
                         (SXMLTRFF NOT = LOW-VALUES)
               IF SXMLTRFI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SXMLTRFI TO SELECT-XMLTRANSFORMS
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG22C
                   MOVE SELECT-ERROR-MSG TO SMSG22O
                   MOVE DFHYELLO         TO SXMLTRFC
                   MOVE DFHREVRS         TO SXMLTRFH
                   MOVE DFHBMBRY         TO SXMLTRFA
                   MOVE -1               TO SXMLTRFL
               END-IF
           END-IF.

      *****************************************************************
      *** If no error has been found during validation add the      ***
      *** appropriate messages to the screen.                       ***
      *****************************************************************
           IF ERROR-FOUND = 'N' THEN
               MOVE SELECT-VALID-MSG1 TO SMSG21O
               MOVE SELECT-VALID-MSG2 TO SMSG22O
           END-IF.

       PROCESS-SELECTION-INPUT-2-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** PROCESS-SELECTION-INPUT-3                                 ***
      ***   Update any fields which the user has changed            ***
      ***     (include check for Erase EOF)                         ***
      *****************************************************************

       PROCESS-SELECTION-INPUT-3 SECTION.

           IF (STROPTSI NOT = LOW-VALUES) OR
                         (STROPTSF NOT = LOW-VALUES)
               IF STROPTSI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE STROPTSI TO SELECT-TRACE-SETTINGS
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG32C
                   MOVE SELECT-ERROR-MSG TO SMSG32O
                   MOVE DFHYELLO         TO STROPTSC
                   MOVE DFHREVRS         TO STROPTSH
                   MOVE DFHBMBRY         TO STROPTSA
                   MOVE -1               TO STROPTSL
               END-IF
           END-IF.

           IF (SUEXPGMI NOT = LOW-VALUES) OR
                         (SUEXPGMF NOT = LOW-VALUES)
               IF SUEXPGMI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SUEXPGMI TO SELECT-USER-EXIT-PGMS
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG32C
                   MOVE SELECT-ERROR-MSG TO SMSG32O
                   MOVE DFHYELLO         TO SUEXPGMC
                   MOVE DFHREVRS         TO SUEXPGMH
                   MOVE DFHBMBRY         TO SUEXPGMA
                   MOVE -1               TO SUEXPGML
               END-IF
           END-IF.

           IF (SGLUESI NOT = LOW-VALUES) OR
                         (SGLUESF NOT = LOW-VALUES)
               IF SGLUESI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SGLUESI TO SELECT-USER-EXITS
                   MOVE 'Y'    TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG32C
                   MOVE SELECT-ERROR-MSG TO SMSG32O
                   MOVE DFHYELLO         TO SGLUESC
                   MOVE DFHREVRS         TO SGLUESH
                   MOVE DFHBMBRY         TO SGLUESA
                   MOVE -1               TO SGLUESL
               END-IF
           END-IF.

           IF (SRCVRYI NOT = LOW-VALUES) OR
                         (SRCVRYF NOT = LOW-VALUES)
               IF SRCVRYI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SRCVRYI TO SELECT-RECOVERY
                   MOVE 'Y'     TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG32C
                   MOVE SELECT-ERROR-MSG TO SMSG32O
                   MOVE DFHYELLO         TO SRCVRYC
                   MOVE DFHREVRS         TO SRCVRYH
                   MOVE DFHBMBRY         TO SRCVRYA
                   MOVE -1               TO SRCVRYL
               END-IF
           END-IF.

           IF (SENQMGRI NOT = LOW-VALUES) OR
                         (SENQMGRF NOT = LOW-VALUES)
               IF SENQMGRI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SENQMGRI TO SELECT-ENQUEUE
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG32C
                   MOVE SELECT-ERROR-MSG TO SMSG32O
                   MOVE DFHYELLO         TO SENQMGRC
                   MOVE DFHREVRS         TO SENQMGRH
                   MOVE DFHBMBRY         TO SENQMGRA
                   MOVE -1               TO SENQMGRL
               END-IF
           END-IF.

           IF (SENQMDLI NOT = LOW-VALUES) OR
                         (SENQMDLF NOT = LOW-VALUES)
               IF SENQMDLI = 'N' OR 'n' OR 'Y' OR 'y' OR ' ' THEN
                   MOVE SENQMDLI TO SELECT-ENQMODELS
                   MOVE 'Y'      TO REPORT-SELECTIONS-MODIFIED
               ELSE
                   MOVE 'Y'              TO ERROR-FOUND
                   MOVE DFHRED           TO SMSG32C
                   MOVE SELECT-ERROR-MSG TO SMSG32O
                   MOVE DFHYELLO         TO SENQMDLC
                   MOVE DFHREVRS         TO SENQMDLH
                   MOVE DFHBMBRY         TO SENQMDLA
                   MOVE -1               TO SENQMDLL
               END-IF
           END-IF.

      *****************************************************************
      *** If no error has been found during validation add the      ***
      *** appropriate messages to the screen.                       ***
      *****************************************************************
           IF ERROR-FOUND = 'N' THEN
               MOVE SELECT-VALID-MSG1 TO SMSG31O
               MOVE SELECT-VALID-MSG2 TO SMSG32O
           END-IF.

       PROCESS-SELECTION-INPUT-3-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** Establish the statistics report selections. This can be   ***
      *** either those saved in a ts queue or the defaults.         ***
      *****************************************************************
       ESTABLISH-SELECTIONS SECTION.

           MOVE 'N' TO REPORT-SELECTIONS-FOUND.
           MOVE LENGTH OF REPORT-SELECTION-FIELDS
                               TO SELECTION-TSQUEUE-LENGTH.

           EXEC CICS READQ TS QNAME(STAT-TSQUEUE)
               INTO(REPORT-SELECTION-FIELDS)
               LENGTH(SELECTION-TSQUEUE-LENGTH)
               ITEM(SELECTION-TSQUEUE-ITEM)
               NUMITEMS(SELECTION-TSQUEUE-ITEMS)
               NOHANDLE
           END-EXEC.

           IF EIBRESP NOT = DFHRESP(NORMAL) THEN
               MOVE 'N' TO REPORT-SELECTIONS-FOUND
               MOVE REPORT-SELECTION-DEFAULTS
                                   TO REPORT-SELECTION-FIELDS
           ELSE
               MOVE 'Y' TO REPORT-SELECTIONS-FOUND
           END-IF.

       ESTABLISH-SELECTIONS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** Save the statistics print reports selected.               ***
      *****************************************************************
       SAVE-REPORT-SELECTIONS SECTION.

           MOVE LENGTH OF REPORT-SELECTION-FIELDS
                                   TO SELECTION-TSQUEUE-LENGTH.

           EXEC CICS WRITEQ TS QNAME(STAT-TSQUEUE)
               FROM(REPORT-SELECTION-FIELDS)
               LENGTH(SELECTION-TSQUEUE-LENGTH)
               ITEM(SELECTION-TSQUEUE-ITEM)
               REWRITE
               NOHANDLE
           END-EXEC.

           EVALUATE EIBRESP
               WHEN DFHRESP(NORMAL)
                   EVALUATE MAP-NAME
                       WHEN 'DFH0SS3 '
                           MOVE SELECTIONS-UPDATED-MSG TO SMSG31O
                       WHEN 'DFH0SS2 '
                           MOVE SELECTIONS-UPDATED-MSG TO SMSG21O
                       WHEN OTHER
                           MOVE SELECTIONS-UPDATED-MSG TO SMSG11O
                   END-EVALUATE
               WHEN DFHRESP(QIDERR)
                   EXEC CICS WRITEQ TS QNAME(STAT-TSQUEUE)
                       FROM(REPORT-SELECTION-FIELDS)
                       LENGTH(SELECTION-TSQUEUE-LENGTH)
                       ITEM(SELECTION-TSQUEUE-ITEM)
                   END-EXEC
                   EVALUATE MAP-NAME
                       WHEN 'DFH0SS3 '
                           MOVE SELECTIONS-SAVED-MSG TO SMSG31O
                       WHEN 'DFH0SS2 '
                           MOVE SELECTIONS-SAVED-MSG TO SMSG21O
                       WHEN OTHER
                           MOVE SELECTIONS-SAVED-MSG TO SMSG11O
                   END-EVALUATE
               WHEN OTHER
                   CONTINUE
           END-EVALUATE.

           MOVE 'Y' TO REPORT-SELECTIONS-FOUND.
           EVALUATE MAP-NAME
               WHEN 'DFH0SS3 '
                   MOVE SELECT-VALID-MSG2    TO SMSG32O
               WHEN 'DFH0SS2 '
                   MOVE SELECT-VALID-MSG2    TO SMSG22O
               WHEN OTHER
                   MOVE SELECT-VALID-MSG2    TO SMSG12O
           END-EVALUATE.

       SAVE-REPORT-SELECTIONS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** Restore the statistics print defaults.                    ***
      *****************************************************************
       RESTORE-REPORT-DEFAULTS SECTION.

           MOVE 'N' TO DEFAULT-SELECTIONS-RESTORED.

           EXEC CICS DELETEQ TS QNAME(STAT-TSQUEUE)
               NOHANDLE
           END-EXEC.

           MOVE 'Y' TO DEFAULT-SELECTIONS-RESTORED
           MOVE 'N' TO REPORT-SELECTIONS-FOUND
           MOVE 'N' TO REPORT-SELECTIONS-MODIFIED

           MOVE REPORT-SELECTION-DEFAULTS TO REPORT-SELECTION-FIELDS.

       RESTORE-REPORT-DEFAULTS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** COLLECT-SYSTEM                                            ***
      *****************************************************************

       COLLECT-SYSTEM SECTION.

      *****************************************************************
      *** Update the date and time.                                 ***
      *****************************************************************
           PERFORM UPDATE-TIME.

      *****************************************************************
      *** Get the statistics information.                           ***
      *****************************************************************
           EXEC CICS INQUIRE STATISTICS
               ENDOFDAY(EOD-COLLECTION-TIME)
               INTERVAL(COLLECTION-INTERVAL)
               NEXTTIME(NEXT-COLLECTION-TIME)
               RECORDING(RECORDING-STATUS)
           END-EXEC.

      *****************************************************************
      *** Get the statistics last reset time and format it.         ***
      *****************************************************************
      *** The following code changes are made by APAR PI99047.      ***
      *** Use LASTRESETABS time on the EXTRACT STATISTICS call.     ***
      *** LAST_RESET_TIME_ABS is then used to calculate             ***
      *** ELAPSED-SECONDS.                                          ***
      *****************************************************************

           EXEC CICS EXTRACT STATISTICS TRANSACTION
               SET(ADDRESS OF DFHXMGDS)
               LASTRESETABS(LAST-RESET-TIME-ABS)
           END-EXEC.

           COMPUTE ELAPSED-ABS = UTIME - LAST-RESET-TIME-ABS.
           COMPUTE ELAPSED-SECONDS = (ELAPSED-ABS / 1000).

       COLLECT-SYSTEM-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *****************************************************************
      ***         Collect and Print the CICS/ESA Statistics         ***
      *****************************************************************
      *****************************************************************
      ***                                                           ***
      *** 1. Collect system information                             ***
      *** 2. Open JES Spool thread                                  ***
      *** 3. Link to 'DFH0STLK' to print the statistics reports:-   ***
      ***    - DFH0STLK links to each on the statistics print       ***
      ***      programs in turn and also prints the statistics      ***
      ***      report page index (if selected).                     ***
      *** 4. Output completion message                              ***
      *** 5. Close JES Spool thread                                 ***
      ***                                                           ***
      *****************************************************************
      *****************************************************************

       PRINT-STATISTICS SECTION.

           PERFORM COLLECT-SYSTEM.

           PERFORM OPEN-SPOOL.

           IF ERROR-FOUND = 'Y'
               GO TO PRINT-STATISTICS-EXIT
           ELSE
               NEXT SENTENCE
           END-IF.

           PERFORM RESET-ROUTINE.

      *****************************************************************
      *** Setup commarea length. Initialise resp/resp2 values and   ***
      *** link to 'DFH0STLK' to print the statistics reports        ***
      *** selected by the user.                                     ***
      *****************************************************************
           MOVE LENGTH OF DFH0STCM TO STCM-COMMAREA.
           MOVE DFHRESP(NORMAL)    TO STCM-RESP.
           MOVE +0                 TO STCM-RESP2.

           EXEC CICS LINK PROGRAM('DFH0STLK')
               COMMAREA(DFH0STCM)
               LENGTH(STCM-COMMAREA)
           END-EXEC.

      *****************************************************************
      *** If other than a RESP of NORMAL is returned something very ***
      *** nasty occurred in DFH0STLK. We need to copy the resp and  ***
      *** resp2 values into the EIB and perform the common error    ***
      *** routine to issue the appropriate message to the user.     ***
      *****************************************************************
           IF STCM-RESP NOT = DFHRESP(NORMAL) THEN
               MOVE STCM-RESP  TO EIBRESP
               MOVE STCM-RESP2 TO EIBRESP2
               PERFORM ERROR-ROUTINE
               GO TO PRINT-STATISTICS-EXIT
           END-IF.

      *****************************************************************
      *** Continue with completion processing - Close Spool File.   ***
      *****************************************************************
           PERFORM CLOSE-SPOOL.

      *****************************************************************
      *  If there's not been any error, determine how we were started *
      *  and output the print completion message as appropriate.      *
      *****************************************************************
           IF ERROR-FOUND = 'N' THEN
               IF START-CODE = 'D ' OR 'DS' THEN
                   NEXT SENTENCE
               ELSE
                   IF TERM-FACILITY THEN
                       IF 3270-DATA-STREAM THEN
                         MOVE LOW-VALUES TO PMSG1O
                         MOVE 'Statistics print successfully completed'
                               TO PMSG2O
                       ELSE
                           MOVE LENGTH OF SUCCESS-MESSAGE TO
                                                 MESSAGE-LENGTH

                           EXEC CICS SEND TEXT
                               FROM(SUCCESS-MESSAGE)
                               LENGTH(MESSAGE-LENGTH)
                           END-EXEC
                       END-IF
                   ELSE
                       NEXT SENTENCE
                   END-IF
               END-IF
           END-IF.

       PRINT-STATISTICS-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** UPDATE-TIME SECTION                                       ***
      ***   Get and format the time from CICS.                      ***
      *****************************************************************

       UPDATE-TIME SECTION.

           EXEC CICS ASKTIME ABSTIME(UTIME)
           END-EXEC.

           EXEC CICS FORMATTIME ABSTIME(UTIME)
               MMDDYYYY(PRINT-DATE) DATESEP('/')
               TIME(PRINT-TIME) TIMESEP
           END-EXEC.

       UPDATE-TIME-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  Setup various defaults and initial setting values.       ***
      *****************************************************************

       SETUP-ROUTINE SECTION.

           MOVE EIBTRNID TO STAT-TSQUEUE-PREFIX.
           MOVE EIBTRMID TO STAT-TSQUEUE-SUFFIX.

           MOVE REPORT-SELECTION-DEFAULTS TO REPORT-SELECTION-FIELDS.

           MOVE LOW-VALUES       TO OUTPUT-TOKEN.

           MOVE DEFAULT-NODE     TO OUTPUT-NODE.
           MOVE DEFAULT-USERID   TO OUTPUT-USERID.
           MOVE DEFAULT-CLASS    TO OUTPUT-CLASS.
           MOVE DEFAULT-ABBREV   TO OUTPUT-ABBREV.
           MOVE 133              TO OUTPUT-RECORD-LENGTH.

           MOVE 133              TO LINE-LENGTH.
           MOVE 99               TO LINE-COUNT.
           MOVE 58               TO LINES-PER-PAGE.
           MOVE 0                TO PAGENO.
           MOVE 'N'              TO ERROR-FOUND.
           MOVE 'Y'              TO SUB-HEADINGS.

           MOVE LOW-VALUES       TO CLOCK-FIELDS
                                    CLOCK-WORK-FIELD
                                    TIME-WORK-FIELDS
                                    STCK-WORK-FIELDS.
           MOVE '-'              TO INTERVAL-DAYS-DELIM.
           MOVE ':'              TO INTERVAL-HOURS-DELIM
                                    INTERVAL-MINUTES-DELIM.

           MOVE INITIAL-HEADING  TO HEADING-LINE.

       SETUP-ROUTINE-EXIT.
           EXIT.

       RESET-ROUTINE SECTION.

           MOVE 99  TO LINE-COUNT.
           MOVE 0   TO PAGENO.
           MOVE 'Y' TO SUB-HEADINGS.

       RESET-ROUTINE-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      *** OPEN-SPOOL                                                ***
      *** IF CICS exceptional condition is raised THEN              ***
      ***    call error routine                                     ***
      *****************************************************************
       OPEN-SPOOL SECTION.

           EXEC CICS SPOOLOPEN OUTPUT TOKEN(OUTPUT-TOKEN)
               NODE(OUTPUT-NODE) USERID(OUTPUT-USERID)
               CLASS(OUTPUT-CLASS) RECORDLENGTH(OUTPUT-RECORD-LENGTH)
               ASA PRINT
               NOHANDLE
           END-EXEC.

      *                                       Check return code
           IF EIBRESP NOT = DFHRESP(NORMAL) THEN
               MOVE 'SPOOLOPEN ' TO CICS-COMMAND
               PERFORM ERROR-ROUTINE
           END-IF.

       OPEN-SPOOL-EXIT.
           EXIT.

      *****************************************************************
      *** CLOSE-SPOOL                                               ***
      *** IF CICS exceptional condition is raised THEN              ***
      ***    call error routine                                     ***
      *****************************************************************
       CLOSE-SPOOL SECTION.

           EXEC CICS SPOOLCLOSE TOKEN(OUTPUT-TOKEN)
               NOHANDLE
           END-EXEC.
      *                                       Check return code
           IF EIBRESP NOT = DFHRESP(NORMAL) THEN
               MOVE 'SPOOLCLOSE' TO CICS-COMMAND
               PERFORM ERROR-ROUTINE
           END-IF.

       CLOSE-SPOOL-EXIT.
           EXIT.
           EJECT
      *****************************************************************
      ***  ERROR ROUTINE                                            ***
      ***  Error routine produces appropriate responses to CICS     ***
      ***  spool commands. For print requests from a terminal an    ***
      ***  error message is selected to return to the user.  If the ***
      ***  program is being run from the PLT an operator msg is sent***
      ***  then a spool close is attempted and the tran is ended    ***
      *****************************************************************
       ERROR-ROUTINE SECTION.

           EVALUATE EIBRESP ALSO EIBRESP2 ALSO TERM-MSG-REQUIRED
      *  If the user has typed in an invalid node, userid or class
      *  this code gives the user a diagnostic message
               WHEN DFHRESP(INVREQ) ALSO 20 ALSO 'Y'
      *                                        NODE missing message and
      *                                        set attribute bytes- CUA
                   MOVE 'Y' TO ERROR-FOUND
                   MOVE DFHRED           TO PMSG2C
                   MOVE NODE-MISSING-MSG TO PMSG2O
                   MOVE DFHYELLO         TO PNODEC
                   MOVE DFHREVRS         TO PNODEH
                   MOVE DFHBMBRY         TO PNODEA
                   MOVE -1               TO PNODEL
               WHEN DFHRESP(NODEIDERR) ALSO ANY ALSO 'Y'
      *                                        NODE does not exist msg
      *                                        and set attributes -CUA
                   MOVE 'Y' TO ERROR-FOUND
                   MOVE DFHRED         TO PMSG2C
                   MOVE NODE-IDERR-MSG TO PMSG2O
                   MOVE DFHYELLO       TO PNODEC
                   MOVE DFHREVRS       TO PNODEH
                   MOVE DFHBMBRY       TO PNODEA
                   MOVE -1             TO PNODEL
               WHEN DFHRESP(INVREQ) ALSO 16 ALSO 'Y'
      *                                        USERID missing message
      *                                        and set attributes - CUA
                   MOVE 'Y' TO ERROR-FOUND
                   MOVE DFHRED     TO PMSG2C
                   MOVE USERID-MSG TO PMSG2O
                   MOVE DFHYELLO   TO PUSERIDC
                   MOVE DFHREVRS   TO PUSERIDH
                   MOVE DFHBMBRY   TO PUSERIDA
                   MOVE -1         TO PUSERIDL
               WHEN DFHRESP(ILLOGIC) ALSO ANY ALSO 'Y'
      *                                        Invalid CLASS value
      *                                        and set attributes - CUA
                   MOVE 'Y' TO ERROR-FOUND
                   MOVE DFHRED    TO PMSG2C
                   MOVE CLASS-MSG TO PMSG2O
                   MOVE DFHYELLO  TO PCLASSC
                   MOVE DFHREVRS  TO PCLASSH
                   MOVE DFHBMBRY  TO PCLASSA
                   MOVE -1        TO PCLASSL
               WHEN OTHER
      *  If an error occurs which is not due to the users input or if
      *  the program is not being run from a terminal then a general
      *  message is sent to the screen/console and the RESP and RESP2
      *  fields are given along with the spool command which failed.
                   MOVE 'Y' TO ERROR-FOUND
                       IF CICS-COMMAND = 'SPOOLOPEN ' THEN
                            MOVE OPEN-ERROR-MSG TO PMSG1O
                       END-IF
                       IF CICS-COMMAND = 'SPOOLWRITE' THEN
                            MOVE WRITE-ERROR-MSG TO PMSG1O
                       END-IF
                       IF CICS-COMMAND = 'SPOOLCLOSE' THEN
                            MOVE CLOSE-ERROR-MSG TO PMSG1O
                       END-IF
                       MOVE EIBRESP  TO TXT-RESP
                       MOVE EIBRESP2 TO TXT-RESP2
                       STRING 'CICS Command was ' CICS-COMMAND
                           '. RESP FIELD = ' TXT-RESP
                           '. RESP2 FIELD = ' TXT-RESP2 '.'
                           DELIMITED BY SIZE INTO PMSG2O
                       END-STRING
                   IF TERM-MSG-REQUIRED IS EQUAL TO 'Y'
                       MOVE DFHRED TO PMSG1C
                       MOVE DFHRED TO PMSG2C
                   ELSE
                       MOVE LENGTH OF OPER-MSG TO OPER-MSG-LEN
                       MOVE LENGTH OF PMSG2O TO PMSG2O-LEN

      * If this is not run from a terminal - output message to console
                       EXEC CICS WRITE OPERATOR
                           TEXT(OPER-MSG)
                           TEXTLENGTH(OPER-MSG-LEN)
                       END-EXEC

                       EXEC CICS WRITE OPERATOR
                           TEXT(PMSG2O)
                           TEXTLENGTH(PMSG2O-LEN)
                       END-EXEC

      *  Try to close the spool. If this fails no action
                       EXEC CICS SPOOLCLOSE
                           TOKEN(OUTPUT-TOKEN)
                           NOHANDLE
                       END-EXEC

      *  Terminate the transaction
                       EXEC CICS RETURN
                       END-EXEC

                   END-IF

           END-EVALUATE.

       ERROR-ROUTINE-EXIT.
           EXIT.
