* ASM CICS ( NOPROLOG NOEPILOG)
99VH     TITLE 'DFH99VH - CICS DYNAMIC ALLOCATION SAMPLE PROGRAM (MVS) *
               - LISTS DESCRIPTION, FOR HELP'
***********************************************************************
*                                                                     *
* MODULE NAME = DFH99VH                                               *
*                                                                     *
* DESCRIPTIVE NAME = Dynamic Allocation Sample Program                *
*                    Lists Description, for Help                      *
*                                                                     *
*                                                                     *
*                                                                     *
*     Licensed Materials - Property of IBM                            *
*                                                                     *
*     "Restricted Materials of IBM"                                   *
*                                                                     *
*     5655-Y04                                                        *
*                                                                     *
*     (C) Copyright IBM Corp. 1980, 1991"                             *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
* STATUS = 7.2.0                                                      *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
* GIVEN AN INPUT DESCRIPTION, CREATES MESSAGES FOR ITS CONTENTS.      *
*                                                                     *
* INPUT:      DESCR IN COMM ADDRESSES THE DESCRIPTION.                *
* OUTPUT:     MESSAGES FILED.                                         *
*             IF KEYWORD VALUES, CALLS KEYHELP TO LIST THEM.          *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
* CHANGE ACTIVITY :                                                   *
* $MOD(DFH99VH),COMP(SAMPLES),PROD(CICS TS ):                         *
*                                                                     *
*     PN= REASON REL YYMMDD HDXIII : REMARKS                          *
*    $P0= Mnnnnn 170 800925 HDZZUNK: Created.                         *
*    $P1= M90474 330 910807 HDBWSH : Prologue fixed.                  *
*                                                                     *
***********************************************************************
DFH99VH  CSECT
DFH99VH  DFH99M PROLOG,REGS=(D,I1,I2,I3,R)
* ADDRESS DESCRIPTION
         L     D,DESCR
         USING DESCRIPT,D
* IF RETURNED VALUE, A MSG HAS ALREADY BEEN ISSUED SAYING VALUE
* IS NOT ALLOWED. HERE WE JUST SAY WHAT THE RESULT IS LIKE.
         TM    DSFLAG,DSRET
         BZ    NOTRET              BR IF NOT A RETURNED VALUE
         LA    I1,DRET             POINT AT 'RETURNED-VALUE'
         BAL   R,M1                PUT OUT MESSAGE
NOTRET   TM    DSFLAG,DSVAL        IF NOT A VALUE OPERAND ..
         BZ    ISKW                .. BR TO KEYWORD SECTION.
         TM    DSFLAG,DSRET        IF RETURNED VALUE ..
         BO    QEXPL               .. SKIP TESTS ON OTHER BITS
         TM    DSFLAG,DSALLOW
         BO    ALLOWED             BR IF VALUE ALLOWED.
         LA    I1,DNALLOW          POINT AT 'NOT-ALLOWED'
         BAL   R,M1
         B     QEXPL               SKIP TESTS ON OTHER BITS
ALLOWED  LA    I1,DREQ             POINT AT 'REQUIRED'
         TM    DSFLAG,DSREQ        REQUIRED?
         BO    REQ                 BR IF SO
         LA    I1,DOPT             POINT AT 'OPTIONAL'
REQ      LA    I2,DMULT            POINT AT 'MULTIPLE'
         TM    DSFLAG,DSMULT       MULTIPLE?
         BO    MULT                BR IF SO
         LA    I2,DSIN             POINT AT 'SINGLE'
MULT     BAL   R,M2                PUT OUT MESSAGE
QEXPL    TM    DSFLAG,DSRTN        ROUTINE?
         BZ    NORTN               BR IF NOT
         LA    I1,DRTN             POINT AT 'EXPLICIT....'
         BAL   R,M1                PUT OUT MESSAGE
NORTN    DS    0H
* ANALYSE THE TYPE AND LENGTH FOR VALUES
         CLI   DSVTYPE,TKW         IS IT CHAR?
         BNE   QNUM                BR IF NOT
         LA    I1,VCHAR            POINT AT 'CHARACTER'
         B     STRING              GO TO COMMON STRING SECTION
QNUM     CLI   DSVTYPE,TNUM        IS IT NUMERIC?
         BNE   QBIN                BR IF NOT
         LA    I1,VNUM             POINT AT 'NUMERIC STRING'
STRING   SR    I2,I2               CLEAR MIN LENGTH REG
         IC    I2,DSMIN            PICK UP MIN LENGTH
         SR    I3,I3               CLEAR MAX LENGTH REG
         IC    I3,DSMAX            PICK UP MAX LENGTH
         DFH99M ID,1998
         DFH99M INSERT,(I1),FORMAT=TOKEN
         DFH99M TEXT,(,MIN,LENGTH)
         DFH99M INSERT,(I2),FORMAT=BIN
         DFH99M TEXT,(,MAX,LENGTH)
         DFH99M INSERT,(I3),FORMAT=BIN
         DFH99M FILE
         B     DONE                END OF HELP INFO
QBIN     DS    0H
         LA    I1,B1               POINT AT '1-BYTE'
         CLI   DSVTYPE,TBIN1
         BE    ISBIN
         LA    I1,B2               POINT AT '2-BYTE'
         CLI   DSVTYPE,TBIN2
         BE    ISBIN
         LA    I1,B3               POINT AT '3-BYTE'
         CLI   DSVTYPE,TBIN3
         BE    ISBIN
         LA    I1,B4               POINT AT '4-BYTE'
         CLI   DSVTYPE,TBIN4
         BE    ISBIN
         B     DONE                NOT BINARY. WHAT IS IT?
ISBIN    BAL   R,M1                PUT OUT MSG FOR BINARY
         B     DONE
ISKW     LA    I1,DKW              POINT AT 'KEYWORD'
         TM    DSFLAG,DSKEYSTR     KEYSTRING?
         BZ    KWM1
         LA    I1,DKSTR            POINT AT 'KEYSTRING'
KWM1     BAL   R,M1
         DFH99M INVOKE,DFH99KH,(DSKEYSET) KEYHELP
         B     DONE
M1       DFH99M ID,1998
         DFH99M INSERT,(I1),FORMAT=TOKEN
         DFH99M FILE
         BR    R
M2       DFH99M ID,1998
         DFH99M INSERT,(I1),FORMAT=TOKEN
         DFH99M INSERT,(I2),FORMAT=TOKEN
         DFH99M FILE
         BR    R
DONE     DFH99M EPILOG
         EJECT
* TOKENS TO BE USED IN MESSAGES WHICH LIST PARTS OF THE DESCRIPTION
DKW      DFH99M TOKEN,(ONE,OF,THE,KEYWORDS:)
DKSTR    DFH99M TOKEN,(A,STRING,OF,THE,KEYS:)
DRET     DFH99M TOKEN,(RETURNED,VALUE)
DNALLOW  DFH99M TOKEN,(NOT,ALLOWED)
DREQ     DFH99M TOKEN,REQUIRED
DOPT     DFH99M TOKEN,OPTIONAL
DMULT    DFH99M TOKEN,MULTIPLE
DSIN     DFH99M TOKEN,SINGLE
DRTN     DFH99M TOKEN,(LOCAL,SPECIAL,ROUTINE)
VCHAR    DFH99M TOKEN,(CHARACTER,STRING)
VNUM     DFH99M TOKEN,(NUMERIC,STRING)
B1       DFH99M TOKEN,(CONVERTIBLE,TO,1,BYTE,BINARY)
B2       DFH99M TOKEN,(CONVERTIBLE,TO,2,BYTE,BINARY)
B3       DFH99M TOKEN,(CONVERTIBLE,TO,3,BYTE,BINARY)
B4       DFH99M TOKEN,(CONVERTIBLE,TO,4,BYTE,BINARY)
         END
